<!doctype html>
<html lang="ru">

<head>
  <meta charset="utf-8">
  <title>HTML Document Template</title>
  <link href="styles.css" rel="stylesheet">
</head>

<body>
  <div class="header__menu">
    <ul>
      <li><a href="str/1">1</a></li>
      <li><a href="str/2">2</a></li>
      <li><a href="str/3">3</a></li>
      <li><a href="str/4">4</a></li>
      <li><a href="str/5">5</a></li>
      <li><a href="str/6">6</a></li>
      <li><a href="str/7">7</a></li>
      <li><a href="str/8">8</a></li>
      <li><a href="str/9">9</a></li>
      <li><a href="str/10">10</a></li>
      <li><a href="str/11">11</a></li>
      <li><a href="str/12">12</a></li>
      <li><a href="str/13">13</a></li>
      <li><a href="str/14">14</a></li>
      <li><a href="str/15">15</a></li>
      <li><a href="str/16">16</a></li>
    </ul>
  </div>
  <h1>6</h1>
  <H2 class="OU">
    Оперативный учет
  </H2>
  <pre>
    <p>///// Общий модуль ОбработкаТабличнойЧасти</p>
    Процедура РассчитатьСуммуВСтроке(ТекСтрока) Экспорт
      ТекСтрока.Сумма = ТекСтрока.Цена * ТекСтрока.Количество;
    КонецПроцедуры  
    
    <p>///// Документ ПриходнаяНакладная</p>
    Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
      СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
    КонецПроцедуры
    
    Процедура ОбработкаПроведения(Отказ, Режим)
    
      Движения.ОстаткиНоменклатуры.Записывать = Истина;
      
      Запрос = Новый Запрос;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
        |	СУММА(ПриходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество
        |ИЗ
        |	Документ.ПриходнаяНакладная.СписокНоменклатуры КАК ПриходнаяНакладнаяСписокНоменклатуры
        |ГДЕ
        |	ПриходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
        |
        |СГРУППИРОВАТЬ ПО
        |	ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура";
      
      Запрос.УстановитьПараметр("Ссылка", Ссылка);
      
      РезультатЗапроса = Запрос.Выполнить();
      
      Выборка = РезультатЗапроса.Выбрать();
      
      Пока Выборка.Следующий() Цикл
        Движение = Движения.ОстаткиНоменклатуры.Добавить();
        Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
        Движение.Период = Дата;
        Движение.Номенклатура = Выборка.Номенклатура;
        Движение.Склад = Склад;
        Движение.Количество = Выборка.Количество;		
      КонецЦикла;
       
    КонецПроцедуры
    
    <p>///// Документ РасходнаяНакладная</p> 
    Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
      СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
    КонецПроцедуры
    
    Процедура ОбработкаПроведения(Отказ, Режим)
    
      Запрос = Новый Запрос;
      МенеджерВТ = Новый МенеджерВременныхТаблиц;
      Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
        |	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
        |	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуры КАК ВидНоменклатуры
        |ПОМЕСТИТЬ втТЧТовары
        |ИЗ
        |	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
        |ГДЕ
        |	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
        |
        |СГРУППИРОВАТЬ ПО
        |	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура,
        |	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуры
        |
        |ИНДЕКСИРОВАТЬ ПО
        |	Номенклатура
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	втТЧТовары.Номенклатура КАК НоменклатураПродажи,
        |	ЕСТЬNULL(СоставСтеллажей.Комплектующая, ВЫБОР
        |			КОГДА втТЧТовары.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Комплектующая)
        |				ТОГДА втТЧТовары.Номенклатура
        |		КОНЕЦ) КАК Номенклатура,
        |	ЕСТЬNULL(втТЧТовары.Количество * СоставСтеллажей.Количество, ВЫБОР
        |			КОГДА втТЧТовары.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Комплектующая)
        |				ТОГДА втТЧТовары.Количество
        |		КОНЕЦ) КАК Количество
        |ПОМЕСТИТЬ втКомплектующие
        |ИЗ
        |	втТЧТовары КАК втТЧТовары
        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоставСтеллажей КАК СоставСтеллажей
        |		ПО втТЧТовары.Номенклатура = СоставСтеллажей.Стеллаж
        |
        |ИНДЕКСИРОВАТЬ ПО
        |	Номенклатура
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	втКомплектующие.НоменклатураПродажи.Представление КАК НоменклатураПредставление
        |ИЗ
        |	втКомплектующие КАК втКомплектующие
        |ГДЕ
        |	втКомплектующие.Номенклатура ЕСТЬ NULL
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	втКомплектующие.Номенклатура КАК Номенклатура,
        |	СУММА(втКомплектующие.Количество) КАК Количество,
        |	&Период КАК Период,
        |	&Склад КАК Склад
        |ИЗ
        |	втКомплектующие КАК втКомплектующие
        |
        |СГРУППИРОВАТЬ ПО
        |	втКомплектующие.Номенклатура";
        
      Запрос.УстановитьПараметр("Период", Дата);
      Запрос.УстановитьПараметр("Склад",  Склад);
      Запрос.УстановитьПараметр("Ссылка", Ссылка);
      
      РезультатыЗапросов = Запрос.ВыполнитьПакет();
      
      РезультатПроверка = РезультатыЗапросов[2];
      Если Не РезультатПроверка.Пустой() Тогда
        Отказ = Истина;
        
        Выборка = РезультатПроверка.Выбрать();
        Пока Выборка.Следующий() Цикл
          Сообщение = Новый СообщениеПользователю;
          Сообщение.Текст = "Не определен состав стеллажа " + Выборка.НоменклатураПредставление;
          Сообщение.Сообщить();
        КонецЦикла;
        
        Возврат;
      КонецЕсли;
      
      Результат = РезультатыЗапросов[3];
      Выборка = Результат.Выбрать();
      Пока Выборка.Следующий() Цикл
        Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
        ЗаполнитьЗначенияСвойств(Движение, Выборка);
      КонецЦикла;
      
      Движения.ОстаткиНоменклатуры.Записывать = Истина;
      Движения.ОстаткиНоменклатуры.БлокироватьДляИзменения = Истина;
      Движения.Записать();
      
      Запрос = Новый Запрос;
      Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	ОстаткиНоменклатурыОстатки.Номенклатура.Представление КАК НоменклатураПредставление,
        |	-ОстаткиНоменклатурыОстатки.КоличествоОстаток КАК КоличествоНехватки,
        |	СоставСтеллажей.Стеллаж.Представление КАК СтеллажПредставление
        |ИЗ
        |	РегистрНакопления.ОстаткиНоменклатуры.Остатки(
        |			&Граница,
        |			Склад = &Склад
        |				И Номенклатура В
        |					(ВЫБРАТЬ
        |						Т.Номенклатура
        |					ИЗ
        |						втКомплектующие КАК Т)) КАК ОстаткиНоменклатурыОстатки
        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоставСтеллажей КАК СоставСтеллажей
        |		ПО ОстаткиНоменклатурыОстатки.Номенклатура = СоставСтеллажей.Комплектующая
        |ГДЕ
        |	ОстаткиНоменклатурыОстатки.КоличествоОстаток < 0";
      
      Запрос.УстановитьПараметр("Граница", Новый Граница(МоментВремени(), ВидГраницы.Включая));
      Запрос.УстановитьПараметр("Склад", Склад);
      
      РезультатЗапроса = Запрос.Выполнить();
      
      Если Не РезультатЗапроса.Пустой() Тогда
        Отказ = Истина;
        
        Выборка = РезультатЗапроса.Выбрать();	
        Пока Выборка.Следующий() Цикл
          Сообщение = Новый СообщениеПользователю;
          
          Если Выборка.СтеллажПредставление = NULL Тогда
            Сообщение.Текст = СтрШаблон("Недостаточно комплектующей %1 в количестве %2",
            Выборка.НоменклатураПредставление, Выборка.КоличествоНехватки);
          Иначе	
            Сообщение.Текст = СтрШаблон("Недостаточно комплектующей %1 (входит в состав %2) в количестве %3",
            Выборка.НоменклатураПредставление, Выборка.СтеллажПредставление, Выборка.КоличествоНехватки);				
          КонецЕсли;
          
          Сообщение.Сообщить();	
        КонецЦикла;
      КонецЕсли;
      
    КонецПроцедуры
    
    <p>///// Модуль формы</p>
    &НаКлиенте
    Процедура СписокНоменклатурыКоличествоПриИзменении(Элемент)
      ТекСтрока = Элементы.СписокНоменклатуры.ТекущиеДанные;
      ОбработкаТабличнойЧасти.РассчитатьСуммуВСтроке(ТекСтрока);
    КонецПроцедуры
    
    &НаКлиенте
    Процедура СписокНоменклатурыЦенаПриИзменении(Элемент)
      ТекСтрока = Элементы.СписокНоменклатуры.ТекущиеДанные;
      ОбработкаТабличнойЧасти.РассчитатьСуммуВСтроке(ТекСтрока);
    КонецПроцедуры    
    <embed src="pdf/ОбщееОУ6.pdf" type="application/pdf" width="1200" height="800">
      <img src="pdf/ОУ6.jpg">
    <H2 class="OU">
      Бухгалтерский учет
    </H2>
    <p>///// Общий модуль ОбработкаТабличнойЧастиКлиент</p>
    Процедура РассчитатьСуммуВСтроке(ТекСтрока) Экспорт
      ТекСтрока.Сумма = ТекСтрока.Цена * ТекСтрока.Количество;
    КонецПроцедуры 
    
    <p>///// Документ Операция</p>   
    Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
      Если Не Движения.Управленческий.Записывать Тогда
        Движения.Управленческий.Прочитать();
        Движения.Управленческий.Записывать = Истина;
      КонецЕсли;
      
      Для Каждого Движение Из Движения.Управленческий Цикл
        Движение.Период = Дата;
        Движение.Активность = Не ПометкаУдаления;
      КонецЦикла;
    КонецПроцедуры
    
    Процедура ПриКопировании(ОбъектКопирования)	
      Выборка = РегистрыБухгалтерии.Управленческий.ВыбратьПоРегистратору(ОбъектКопирования.Ссылка);
      Пока Выборка.Следующий() Цикл
        Движение = Движения.Управленческий.Добавить();
        ЗаполнитьЗначенияСвойств(Движение, Выборка);
        ЗаполнитьЗначенияСвойств(Движение.СубконтоДт, Выборка.СубконтоДт);
        ЗаполнитьЗначенияСвойств(Движение.СубконтоКт, Выборка.СубконтоКт);
      КонецЦикла;
    КонецПроцедуры
     
    <p>///// Документ ПриходнаяНакладная</p>
    Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
      СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
    КонецПроцедуры
    
    Процедура ОбработкаПроведения(Отказ, РежимПроведения)
      
      Движения.Управленческий.Записывать = Истина;
      
      Запрос = Новый Запрос;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
        |	ПриходнаяНакладнаяСписокНоменклатуры.Склад КАК Склад,
        |	СУММА(ПриходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
        |	СУММА(ПриходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма
        |ИЗ
        |	Документ.ПриходнаяНакладная.СписокНоменклатуры КАК ПриходнаяНакладнаяСписокНоменклатуры
        |ГДЕ
        |	ПриходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
        |
        |СГРУППИРОВАТЬ ПО
        |	ПриходнаяНакладнаяСписокНоменклатуры.Склад,
        |	ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура";
      
      Запрос.УстановитьПараметр("Ссылка", Ссылка);
      
      РезультатЗапроса = Запрос.Выполнить();
      
      Выборка = РезультатЗапроса.Выбрать();
      
      Пока Выборка.Следующий() Цикл
        
        Движение = Движения.Управленческий.Добавить();
        Движение.Период = Дата;
        
        Движение.СчетДт = ПланыСчетов.Управленческий.Товары;
        Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = Выборка.Номенклатура;
        Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Склады] = Выборка.Склад;
        Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Партии] = Ссылка;
        Движение.КоличествоДт = Выборка.Количество;
        Движение.КоличествоСкладДт = Выборка.Количество;
        
        Движение.СчетКт = ПланыСчетов.Управленческий.Поставщики;
        
        Движение.Сумма = Выборка.Сумма;
        
      КонецЦикла;
      
    КонецПроцедуры
    
    <p>///// Документ РасходнаяНакладная</p>
    Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
      СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
    КонецПроцедуры
    
    Процедура ОбработкаПроведения(Отказ, РежимПроведения)
      
      МетодСписанияСебестоимости = РегистрыСведений.УчетнаяПолитика.ПолучитьПоследнее(Дата).МетодСписанияСебестоимости;
      Если МетодСписанияСебестоимости.Пустая() Тогда
        Отказ = Истина;
        Сообщение = Новый СообщениеПользователю;
        Сообщение.Текст = "Не указана учетная политика";
        Сообщение.Сообщить();		
        Возврат;
      КонецЕсли;
      
      Движения.Управленческий.Записывать = Истина;
      Движения.Записать();
      
      Движения.Управленческий.Записывать = Истина;
      
      Блокировка = Новый БлокировкаДанных;
      ЭлементБлокировки = Блокировка.Добавить("РегистрБухгалтерии.Управленческий");
      ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
      ЭлементБлокировки.УстановитьЗначение("Счет", ПланыСчетов.Управленческий.Товары);
      ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
      ЭлементБлокировки.ИспользоватьИзИсточникаДанных(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура, "Номенклатура");
      Блокировка.Заблокировать();
      
      Запрос = Новый Запрос;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
        |	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество
        |ПОМЕСТИТЬ втТЧТовары
        |ИЗ
        |	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
        |ГДЕ
        |	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
        |
        |СГРУППИРОВАТЬ ПО
        |	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура
        |
        |ИНДЕКСИРОВАТЬ ПО
        |	Номенклатура
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	втТЧТовары.Номенклатура КАК Номенклатура,
        |	втТЧТовары.Номенклатура.Представление КАК НоменклатураПредставление,
        |	втТЧТовары.Количество КАК Количество,
        |	ЕСТЬNULL(УправленческийОстаткиПоСкладу.КоличествоСкладОстаток, 0) КАК КоличествоОстатокНаСкладе,
        |	УправленческийОстаткиПоПартиям.Субконто2 КАК Партия,
        |	ЕСТЬNULL(УправленческийОстаткиПоПартиям.КоличествоОстаток, 0) КАК КоличествоОстаток,
        |	ЕСТЬNULL(УправленческийОстаткиПоПартиям.СуммаОстаток, 0) КАК СуммаОстаток
        |ИЗ
        |	втТЧТовары КАК втТЧТовары
        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(
        |				&МоментВремени,
        |				Счет = &СчетТовары,
        |				&ВидыСубконтоПоСкладам,
        |				Субконто1 В
        |						(ВЫБРАТЬ
        |							втТЧТовары.Номенклатура КАК Номенклатура
        |						ИЗ
        |							втТЧТовары КАК втТЧТовары)
        |					И Субконто2 = &Склад) КАК УправленческийОстаткиПоСкладу
        |		ПО втТЧТовары.Номенклатура = УправленческийОстаткиПоСкладу.Субконто1
        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(
        |				&МоментВремени,
        |				Счет = &СчетТовары,
        |				&ВидыСубконтоПоПартиям,
        |				Субконто1 В
        |					(ВЫБРАТЬ
        |						втТЧТовары.Номенклатура КАК Номенклатура
        |					ИЗ
        |						втТЧТовары КАК втТЧТовары)) КАК УправленческийОстаткиПоПартиям
        |		ПО втТЧТовары.Номенклатура = УправленческийОстаткиПоПартиям.Субконто1
        |
        |УПОРЯДОЧИТЬ ПО
        |	УправленческийОстаткиПоПартиям.Субконто2.МоментВремени
        |ИТОГИ
        |	МАКСИМУМ(Количество),
        |	МАКСИМУМ(КоличествоОстатокНаСкладе)
        |ПО
        |	Номенклатура";
      
      Если МетодСписанияСебестоимости = Перечисления.УчетнаяПолитика.ФИФО Тогда
        Запрос.Текст = СтрЗаменить(Запрос.Текст, "УБЫВ", "ВОЗР");
      КонецЕсли;
      
      ВидыСубконтоПоСкладам = Новый Массив;
      ВидыСубконтоПоСкладам.Добавить(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура);
      ВидыСубконтоПоСкладам.Добавить(ПланыВидовХарактеристик.ВидыСубконто.Склады);
      
      ВидыСубконтоПоПартиям = Новый Массив;
      ВидыСубконтоПоПартиям.Добавить(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура);
      ВидыСубконтоПоПартиям.Добавить(ПланыВидовХарактеристик.ВидыСубконто.Партии);	
      
      Запрос.УстановитьПараметр("ВидыСубконтоПоСкладам", ВидыСубконтоПоСкладам);
      Запрос.УстановитьПараметр("ВидыСубконтоПоПартиям", ВидыСубконтоПоПартиям);
      Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
      Запрос.УстановитьПараметр("Склад", Склад);
      Запрос.УстановитьПараметр("Ссылка", Ссылка);
      Запрос.УстановитьПараметр("СчетТовары", ПланыСчетов.Управленческий.Товары);
      
      РезультатЗапроса = Запрос.Выполнить();
      
      ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
      
      Пока ВыборкаНоменклатура.Следующий() Цикл
        
        Если ВыборкаНоменклатура.Количество > ВыборкаНоменклатура.КоличествоОстатокНаСкладе Тогда
          Отказ = Истина;
          
          Сообщение = Новый СообщениеПользователю;
          Сообщение.Текст = СтрШаблон("Недостаточно товара %1 в количестве %2",
          ВыборкаНоменклатура.НоменклатураПредставление, 
          ВыборкаНоменклатура.Количество - ВыборкаНоменклатура.КоличествоОстатокНаСкладе);
          Сообщение.Сообщить();
          
        КонецЕсли;
        
        Если Отказ Тогда
          Продолжить;
        КонецЕсли;
        
        КоличествоСписать = ВыборкаНоменклатура.Количество;
        
        ВыборкаДетальныеЗаписи = ВыборкаНоменклатура.Выбрать();
      
        Пока ВыборкаДетальныеЗаписи.Следующий() И КоличествоСписать > 0 Цикл
          
          Количество = Мин(КоличествоСписать, ВыборкаДетальныеЗаписи.КоличествоОстаток);
          
          Если Количество = ВыборкаДетальныеЗаписи.КоличествоОстаток Тогда
            Себестоимость = ВыборкаДетальныеЗаписи.СуммаОстаток;
          Иначе
            Себестоимость = ВыборкаДетальныеЗаписи.СуммаОстаток / ВыборкаДетальныеЗаписи.КоличествоОстаток * Количество;
          КонецЕсли;
          
          Движение = Движения.Управленческий.Добавить();
          Движение.Период = Дата;
          
          Движение.СчетДт = ПланыСчетов.Управленческий.ПрибылиУбытки;
          
          Движение.СчетКт = ПланыСчетов.Управленческий.Товары;
          Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = ВыборкаДетальныеЗаписи.Номенклатура;
          Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Склады] = Склад;
          Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Партии] = ВыборкаДетальныеЗаписи.Партия;
          Движение.КоличествоКт = Количество;
          Движение.КоличествоСкладКт = Количество;
          
          Движение.Сумма = Себестоимость;
          
          КоличествоСписать = КоличествоСписать - Количество;
          
        КонецЦикла;
      КонецЦикла;
      
      Движение = Движения.Управленческий.Добавить();
      Движение.Период = Дата;	
      Движение.СчетДт = ПланыСчетов.Управленческий.Покупатели;
      Движение.СчетКт = ПланыСчетов.Управленческий.ПрибылиУбытки;	
      Движение.Сумма = СуммаПоДокументу;	
    КонецПроцедуры
    
    <p>///// Модуль формы</p>
    &НаКлиенте
    Процедура СписокНоменклатурыКоличествоПриИзменении(Элемент)
      ТекСтрока = Элементы.СписокНоменклатуры.ТекущиеДанные;
      ОбработкаТабличнойЧасти.РассчитатьСуммуВСтроке(ТекСтрока);
    КонецПроцедуры
    
    &НаКлиенте
    Процедура СписокНоменклатурыЦенаПриИзменении(Элемент)
      ТекСтрока = Элементы.СписокНоменклатуры.ТекущиеДанные;
      ОбработкаТабличнойЧасти.РассчитатьСуммуВСтроке(ТекСтрока);
    КонецПроцедуры    
    <embed src="pdf/ОбщееБУ6.pdf" type="application/pdf" width="1200" height="800">
      <img src="pdf/БУ6.jpg">
    <H2 class="OU">
      Расчеты
    </H2>
    <p>///// Документ НачислениеЗарплаты</p>   
    Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
      
      Если ОсновныеНачисления.Количество() = 0 И ДополнительныеНачисления.Количество() = 0 Тогда
        Сообщение = Новый СообщениеПользователю;
        Сообщение.Текст = "Документ пустой!";
        Сообщение.Сообщить();              
        Отказ = Истина;
      КонецЕсли;
      
    КонецПроцедуры
    
    Процедура ОбработкаПроведения(Отказ, РежимПроведения)
      
      ПериодРегистрации = НачалоМесяца(Дата);
      НачалоБазовогоПериода = ДобавитьМесяц(ПериодРегистрации, -3);
      КонецБазовогоПериода  = ПериодРегистрации - 1;
      
      Движения.ОсновныеНачисления.Записывать = Истина;
      Для Каждого Строка Из ОсновныеНачисления Цикл
        Движение = Движения.ОсновныеНачисления.Добавить();
        ЗаполнитьЗначенияСвойств(Движение, Строка);
        Движение.ПериодРегистрации = ПериодРегистрации;
        Движение.ПериодДействияНачало = Строка.ДатаНачала;
        Движение.ПериодДействияКонец = КонецДня(Строка.ДатаОкончания);
        
        Если Строка.ВидРасчета = ПланыВидовРасчета.ОсновныеНачисления.Отпуск Тогда
          Движение.БазовыйПериодНачало = НачалоБазовогоПериода;
          Движение.БазовыйПериодКонец  = КонецБазовогоПериода;
        КонецЕсли;
      КонецЦикла;
      
      Движения.ДополнительныеНачисления.Записывать = Истина;
      Для Каждого Строка Из ДополнительныеНачисления Цикл
        Движение = Движения.ДополнительныеНачисления.Добавить();
        ЗаполнитьЗначенияСвойств(Движение, Строка);
        Движение.ПериодРегистрации = ПериодРегистрации;
      КонецЦикла;                                        
      Движения.Записать();
        
      Запрос = Новый Запрос;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
        |	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ДнейПятидневкиФактическийПериодДействия, 0) КАК ДнейОтпуска,
        |	ЕСТЬNULL(ОсновныеНачисленияБазаОсновныеНачисления.РезультатБаза, 0) + ЕСТЬNULL(ОсновныеНачисленияБазаДополнительныеНачисления.РезультатБаза, 0) КАК БазаНачислений,
        |	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ДнейБазовыйПериод, 0) КАК БазаРабочихДней
        |ИЗ
        |	РегистрРасчета.ОсновныеНачисления.ДанныеГрафика(
        |			Регистратор = &Регистратор
        |				И ВидРасчета В (&ВидыРасчета)) КАК ОсновныеНачисленияДанныеГрафика
        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ОсновныеНачисления.БазаОсновныеНачисления(
        |				&Измерения,
        |				&Измерения,
        |				,
        |				Регистратор = &Регистратор
        |					И ВидРасчета В (&ВидыРасчета)) КАК ОсновныеНачисленияБазаОсновныеНачисления
        |		ПО ОсновныеНачисленияДанныеГрафика.НомерСтроки = ОсновныеНачисленияБазаОсновныеНачисления.НомерСтроки
        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ОсновныеНачисления.БазаДополнительныеНачисления(
        |				&Измерения,
        |				&Измерения,
        |				,
        |				Регистратор = &Регистратор
        |					И ВидРасчета В (&ВидыРасчета)) КАК ОсновныеНачисленияБазаДополнительныеНачисления
        |		ПО ОсновныеНачисленияДанныеГрафика.НомерСтроки = ОсновныеНачисленияБазаДополнительныеНачисления.НомерСтроки";
      
      ВидыРасчета = Новый Массив;
      ВидыРасчета.Добавить(ПланыВидовРасчета.ОсновныеНачисления.Отпуск);
        Запрос.УстановитьПараметр("ВидыРасчета", ВидыРасчета);
      
      Измерения = Новый Массив;
      Измерения.Добавить("Сотрудник");	
      Запрос.УстановитьПараметр("Измерения", Измерения);
      
      Запрос.УстановитьПараметр("Регистратор", Ссылка);
      
      РезультатЗапроса = Запрос.Выполнить();
      
      Выборка = РезультатЗапроса.Выбрать();
    
      Для Каждого Движение Из Движения.ОсновныеНачисления Цикл
        
        Если ВидыРасчета.Найти(Движение.ВидРасчета) = Неопределено Тогда
          Продолжить;
        КонецЕсли;
        
        Выборка.Сбросить();
        Выборка.НайтиСледующий(Движение.НомерСтроки, "НомерСтроки");
        
        Движение.Результат = ?(Выборка.БазаРабочихДней <> 0,
                     Выборка.ДнейОтпуска * Выборка.БазаНачислений / Выборка.БазаРабочихДней,
                     0);
                     
        ЗаполнитьЗначенияСвойств(Движение, Выборка, "ДнейОтпуска, БазаНачислений, БазаРабочихДней");
        
      КонецЦикла;
      
      Движения.ОсновныеНачисления.Записать(, Истина);
      
      Запрос = Новый Запрос;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	ДополнительныеНачисления.НомерСтроки КАК НомерСтроки,
        |	ДополнительныеНачисления.Сотрудник КАК Сотрудник
        |ПОМЕСТИТЬ втНачисления
        |ИЗ
        |	РегистрРасчета.ДополнительныеНачисления КАК ДополнительныеНачисления
        |ГДЕ
        |	ДополнительныеНачисления.Регистратор = &Регистратор
        |	И ДополнительныеНачисления.ВидРасчета В (&ВидыРасчета)
        |
        |ИНДЕКСИРОВАТЬ ПО
        |	Сотрудник
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	втНачисления.НомерСтроки КАК НомерСтроки,
        |	ЕСТЬNULL(ОплатыПассажировОбороты.СуммаОборот, 0) КАК СуммаОплат,
        |	ЕСТЬNULL(ПоказателиРасчетаЗарплатыСрезПоследних.ФиксированнаяСумма, 0) КАК ФиксированнаяСумма,
        |	ЕСТЬNULL(ПоказателиРасчетаЗарплатыСрезПоследних.ПроцентНадбавки, 0) КАК ПроцентНадбавки
        |ИЗ
        |	втНачисления КАК втНачисления
        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОплатыПассажиров.Обороты(
        |				&НачалоПрошлогоМесяца,
        |				&КонецПрошлогоМесяца,
        |				,
        |				Сотрудник В
        |					(ВЫБРАТЬ
        |						втНачисления.Сотрудник КАК Сотрудник
        |					ИЗ
        |						втНачисления КАК втНачисления)) КАК ОплатыПассажировОбороты
        |		ПО втНачисления.Сотрудник = ОплатыПассажировОбороты.Сотрудник
        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПоказателиРасчетаЗарплаты.СрезПоследних(&ПериодРегистрации, ) КАК ПоказателиРасчетаЗарплатыСрезПоследних
        |		ПО (ИСТИНА)";
      
      ВидыРасчета = Новый Массив;
      ВидыРасчета.Добавить(ПланыВидовРасчета.ДополнительныеНачисления.ФиксированнаяСумма);
      ВидыРасчета.Добавить(ПланыВидовРасчета.ДополнительныеНачисления.Надбавка);
    
      Запрос.УстановитьПараметр("ВидыРасчета", ВидыРасчета);
      Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
      Запрос.УстановитьПараметр("Регистратор", Ссылка);
      Запрос.УстановитьПараметр("НачалоПрошлогоМесяца", ДобавитьМесяц(ПериодРегистрации, -1));
      Запрос.УстановитьПараметр("КонецПрошлогоМесяца", ПериодРегистрации - 1);
      
      РезультатЗапроса = Запрос.Выполнить();
      
      Выборка = РезультатЗапроса.Выбрать();
      
      Для Каждого Движение Из Движения.ДополнительныеНачисления Цикл
        
        Если ВидыРасчета.Найти(Движение.ВидРасчета) = Неопределено Тогда
          Продолжить;	
        КонецЕсли;
        
        Выборка.Сбросить();
        Выборка.НайтиСледующий(Движение.НомерСтроки, "НомерСтроки");
        Если Движение.ВидРасчета = ПланыВидовРасчета.ДополнительныеНачисления.ФиксированнаяСумма Тогда
          Движение.Результат = Выборка.ФиксированнаяСумма;
          ЗаполнитьЗначенияСвойств(Движение, Выборка, "ФиксированнаяСумма");
        ИначеЕсли Движение.ВидРасчета = ПланыВидовРасчета.ДополнительныеНачисления.Надбавка Тогда
          Движение.Результат = Выборка.СуммаОплат * Выборка.ПроцентНадбавки / 100;
          ЗаполнитьЗначенияСвойств(Движение, Выборка, "СуммаОплат, ПроцентНадбавки")
        КонецЕсли;
            
      КонецЦикла;
        
        Движения.ДополнительныеНачисления.Записать();
      
    КонецПроцедуры
    
    <p>///// Документ ПолучениеОплатОтПассажиров</p>
    Процедура ОбработкаПроведения(Отказ, Режим)
    
      // регистр ОплатыПассажиров 
      Движения.ОплатыПассажиров.Записывать = Истина;
      Движение = Движения.ОплатыПассажиров.Добавить();
      Движение.Период = Дата;
      Движение.Сотрудник = Сотрудник;
      Движение.Сумма = Сумма;
      
      //ВидРасчета = ПланыВидовРасчета.ДополнительныеНачисления.Надбавка;
      //ПериодРегистрации = КонецМесяца(Дата) + 1;
      //
      //Запрос = Новый Запрос;
      //Запрос.Текст = 
      //	"ВЫБРАТЬ РАЗЛИЧНЫЕ
      //	|	ДополнительныеНачисления.Сотрудник КАК Сотрудник,
      //	|	ДополнительныеНачисления.ВидРасчета КАК ВидРасчета,
      //	|	ДополнительныеНачисления.Регистратор КАК ОбъектПерерасчета
      //	|ПОМЕСТИТЬ втНачисления
      //	|ИЗ
      //	|	РегистрРасчета.ДополнительныеНачисления КАК ДополнительныеНачисления
      //	|ГДЕ
      //	|	ДополнительныеНачисления.Сотрудник = &Сотрудник
      //	|	И ДополнительныеНачисления.ПериодРегистрации = &ПериодРегистрации
      //	|	И ДополнительныеНачисления.ВидРасчета = &ВидРасчета
      //	|;
      //	|
      //	|////////////////////////////////////////////////////////////////////////////////
      //	|ВЫБРАТЬ
      //	|	втНачисления.Сотрудник КАК Сотрудник,
      //	|	втНачисления.ВидРасчета КАК ВидРасчета,
      //	|	втНачисления.ОбъектПерерасчета КАК ОбъектПерерасчета
      //	|ИЗ
      //	|	втНачисления КАК втНачисления
      //	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ДополнительныеНачисления.ПоСотруднику КАК ПоСотруднику
      //	|		ПО втНачисления.Сотрудник = ПоСотруднику.Сотрудник
      //	|			И втНачисления.ВидРасчета = ПоСотруднику.ВидРасчета
      //	|			И втНачисления.ОбъектПерерасчета = ПоСотруднику.ОбъектПерерасчета
      //	|ГДЕ
      //	|	ПоСотруднику.ОбъектПерерасчета ЕСТЬ NULL";
      //
      //Запрос.УстановитьПараметр("ВидРасчета", ВидРасчета);
      //Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
      //Запрос.УстановитьПараметр("Сотрудник", Сотрудник);
      //
      //РезультатЗапроса = Запрос.Выполнить();
      //
      //Выборка = РезультатЗапроса.Выбрать();	
      //
      //Пока Выборка.Следующий() Цикл
      //	
      //	НаборЗаписей = РегистрыРасчета.ДополнительныеНачисления.Перерасчеты.ПоСотруднику.СоздатьНаборЗаписей();
      //	НаборЗаписей.Отбор.ОбъектПерерасчета.Установить(Выборка.ОбъектПерерасчета);
      //	//НаборЗаписей.Прочитать();
      //	
      //	НоваяЗапись = НаборЗаписей.Добавить();
      //	ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
      //	
      //	//ТЗ = НаборЗаписей.Выгрузить();
      //	//ТЗ.Свернуть("ОбъектПерерасчета, ВидРасчета, Сотрудник");
      //	//НаборЗаписей.Загрузить(ТЗ);
      //	
      //	НаборЗаписей.Записать(Ложь);
      //КонецЦикла;
      
    КонецПроцедуры
    
    <p>///// Модуль объекта ЗаполнениеГрафика</p>
    Процедура ЗаполнитьГрафик(ДатаНачала, ДатаОкончания, ВыходныеДни, Автомобиль) Экспорт 
      
      Набор = РегистрыСведений.ГрафикиРаботы.СоздатьНаборЗаписей();
      
      Набор.Отбор.Автомобиль.Установить(Автомобиль);
        
      ЧислоСекундВСутках = 86400;
      
      Дат = ДатаНачала;	
      Пока Дат <= ДатаОкончания Цикл
        Запись = Набор.Добавить();
        Запись.Автомобиль = Автомобиль;
        Запись.Дата = Дат;
        Если Найти(ВыходныеДни, Строка(ДеньНедели(Дат))) Тогда
          Запись.Дней = 0;
        Иначе	          
          Запись.Дней = 1;
        КонецЕсли;
        
        Если ДеньНедели(Дат) > 5 Тогда
          Запись.ДнейПятидневки = 0;
        Иначе
          Запись.ДнейПятидневки = 1;
        КонецЕсли;
        
        Дат = Дат + ЧислоСекундВСутках;
      КонецЦикла; 
      Набор.Записать();
    КонецПроцедуры
    
    <p>///// Модуль формы</p>
    &НаКлиенте
    Процедура Заполнить(Команда)
      ВыполнитьОбработку();
      Сообщить("Обработка завершена!");
    КонецПроцедуры
    
    &НаСервере
    Процедура ВыполнитьОбработку()
      ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
        ОбработкаОбъект.ЗаполнитьГрафик(ВыборПериода.ДатаНачала, ВыборПериода.ДатаОкончания, ВыходныеДни, ГрафикРаботы);
    КонецПроцедуры 
    <embed src="pdf/ОбщееСПР6.pdf" type="application/pdf" width="1200" height="800">
      <img src="pdf/СПР6.jpg">
  </pre>
</body>

</html>