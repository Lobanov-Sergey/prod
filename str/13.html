<!doctype html>
<html lang="ru">

<head>
  <meta charset="utf-8">
  <title>HTML Document Template</title>
  <link href="styles.css" rel="stylesheet">
</head>

<body>
  <div class="header__menu">
    <ul>
      <li><a href="1">1</a></li>
      <li><a href="2">2</a></li>
      <li><a href="3">3</a></li>
      <li><a href="4">4</a></li>
      <li><a href="5">5</a></li>
      <li><a href="6">6</a></li>
      <li><a href="7">7</a></li>
      <li><a href="8">8</a></li>
      <li><a href="9">9</a></li>
      <li><a href="10">10</a></li>
      <li><a href="11">11</a></li>
      <li><a href="12">12</a></li>
      <li><a href="13">13</a></li>
      <li><a href="14">14</a></li>
      <li><a href="15">15</a></li>
      <li><a href="16">16</a></li>
    </ul>
  </div>
  <h1>13</h1>
  <H2 class="OU">
    Оперативный учет
  </H2>
  <pre>
    <p>///// Общий модуль ОбработкаТабличнойЧасти</p>
    Процедура РассчитатьСуммуВСтроке(ТекСтрока) Экспорт
      ТекСтрока.Сумма = ТекСтрока.Цена * ТекСтрока.Количество;
    КонецПроцедуры 
    
    <p>///// Документ ВводВЭксплуатацию</p>   
    Процедура ОбработкаПроведения(Отказ, РежимПроведения)
      
      Движения.ОстаткиНоменклатуры.Записывать = Истина;
      Движения.Себестоимость.Записывать = Истина;
      Движения.Записать();
      
      Движения.ОстаткиНоменклатуры.Записывать = Истина;
      Движения.ОборудованиеВЭксплуатации.Записывать = Истина;
      Движения.Себестоимость.Записывать = Истина;
      
      ДатаНачалаДокумента = НачалоДня(Дата);
      
      Блокировка = Новый БлокировкаДанных;
      
      ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
      ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
      ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
      ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
      ЭлементБлокировки.УстановитьЗначение("СрокГодности", Новый Диапазон(ДатаНачалаДокумента,));
      
      ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.Себестоимость");
      ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
      ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
      ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
      
      Блокировка.Заблокировать();
      
      Запрос = Новый Запрос;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	ВводВЭксплуатациюСписокНоменклатуры.Номенклатура КАК Номенклатура,
        |	СУММА(ВводВЭксплуатациюСписокНоменклатуры.Количество) КАК Количество,
        |	ВводВЭксплуатациюСписокНоменклатуры.Номенклатура.СрокЭксплуатацииДней КАК СрокЭксплуатацииДней
        |ПОМЕСТИТЬ втТЧТовары
        |ИЗ
        |	Документ.ВводВЭксплуатацию.СписокНоменклатуры КАК ВводВЭксплуатациюСписокНоменклатуры
        |ГДЕ
        |	ВводВЭксплуатациюСписокНоменклатуры.Ссылка = &Ссылка
        |
        |СГРУППИРОВАТЬ ПО
        |	ВводВЭксплуатациюСписокНоменклатуры.Номенклатура,
        |	ВводВЭксплуатациюСписокНоменклатуры.Номенклатура.СрокЭксплуатацииДней
        |
        |ИНДЕКСИРОВАТЬ ПО
        |	Номенклатура
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	втТЧТовары.Номенклатура КАК Номенклатура,
        |	втТЧТовары.Номенклатура.Представление КАК НоменклатураПредставление,
        |	втТЧТовары.Количество КАК Количество,
        |	ОстаткиНоменклатурыОстатки.СрокГодности КАК СрокГодности,
        |	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) КАК КоличествоОстатокПоСрокуГодности,
        |	ЕСТЬNULL(СебестоимостьОстатки.КоличествоОстаток, 0) КАК ИтоговоеКоличествоОстаток,
        |	ЕСТЬNULL(СебестоимостьОстатки.СтоимостьОстаток, 0) КАК ИтоговаяСтоимостьОстаток,
        |	ДОБАВИТЬКДАТЕ(&ДатаНачалаДокумента, ДЕНЬ, втТЧТовары.СрокЭксплуатацииДней) КАК СрокЭксплуатации,
        |	&Период КАК Период,
        |	втТЧТовары.СрокЭксплуатацииДней КАК СрокЭксплуатацииДней
        |ИЗ
        |	втТЧТовары КАК втТЧТовары
        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
        |				&МоментВремени,
        |				Номенклатура В
        |						(ВЫБРАТЬ
        |							втТЧТовары.Номенклатура КАК Номенклатура
        |						ИЗ
        |							втТЧТовары КАК втТЧТовары)
        |					И СрокГодности >= &ДатаНачалаДокумента) КАК ОстаткиНоменклатурыОстатки
        |		ПО втТЧТовары.Номенклатура = ОстаткиНоменклатурыОстатки.Номенклатура
        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.Себестоимость.Остатки(
        |				&МоментВремени,
        |				Номенклатура В
        |					(ВЫБРАТЬ
        |						втТЧТовары.Номенклатура КАК Номенклатура
        |					ИЗ
        |						втТЧТовары КАК втТЧТовары)) КАК СебестоимостьОстатки
        |		ПО втТЧТовары.Номенклатура = СебестоимостьОстатки.Номенклатура
        |
        |УПОРЯДОЧИТЬ ПО
        |	СрокГодности
        |ИТОГИ
        |	МАКСИМУМ(Количество),
        |	СУММА(КоличествоОстатокПоСрокуГодности),
        |	МАКСИМУМ(ИтоговоеКоличествоОстаток),
        |	МАКСИМУМ(ИтоговаяСтоимостьОстаток),
        |	МАКСИМУМ(Период),
        |	МАКСИМУМ(СрокЭксплуатацииДней)
        |ПО
        |	Номенклатура";
      
      Запрос.УстановитьПараметр("ДатаНачалаДокумента", ДатаНачалаДокумента);
      Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
      Запрос.УстановитьПараметр("Ссылка", Ссылка);
      Запрос.УстановитьПараметр("Период", Дата);
      
      РезультатЗапроса = Запрос.Выполнить();
      
      ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
      
      Пока ВыборкаНоменклатура.Следующий() Цикл
        
        Если ВыборкаНоменклатура.СрокЭксплуатацииДней = 0 Тогда
          Отказ = Истина;
          
          Сообщение = Новый СообщениеПользователю;
          Сообщение.Текст = СтрШаблон("Не указан срок эксплуатации по номенклатуре %1",
          ВыборкаНоменклатура.НоменклатураПредставление);
          Сообщение.Сообщить();
        КонецЕсли;
        
        Если ВыборкаНоменклатура.Количество > ВыборкаНоменклатура.КоличествоОстатокПоСрокуГодности Тогда
          
          Отказ = Истина;
          
          Сообщение = Новый СообщениеПользователю;
          Сообщение.Текст = СтрШаблон("Недостаточно товара %1 в количестве %2",
          ВыборкаНоменклатура.НоменклатураПредставление, ВыборкаНоменклатура.Количество - ВыборкаНоменклатура.КоличествоОстатокПоСрокуГодности);
          Сообщение.Сообщить();
          
        КонецЕсли;
        
        Если Отказ Тогда
          Продолжить;
        КонецЕсли;
        
        СебестоимостьЕдиницы = ВыборкаНоменклатура.ИтоговаяСтоимостьОстаток / ВыборкаНоменклатура.ИтоговоеКоличествоОстаток;
        
        Если ВыборкаНоменклатура.Количество = ВыборкаНоменклатура.ИтоговоеКоличествоОстаток Тогда
          Себестоимость = ВыборкаНоменклатура.ИтоговаяСтоимостьОстаток;
        Иначе
          Себестоимость = Окр(СебестоимостьЕдиницы * ВыборкаНоменклатура.Количество, 2);
        КонецЕсли;
        
        Движение = Движения.Себестоимость.ДобавитьРасход();
        ЗаполнитьЗначенияСвойств(Движение, ВыборкаНоменклатура);
        Движение.Стоимость = Себестоимость;
        
        КоличествоКСписанию  = ВыборкаНоменклатура.Количество;
        
        ВыборкаДетальныеЗаписи = ВыборкаНоменклатура.Выбрать();
        
        СебестоимостьИтого = 0;
        Пока ВыборкаДетальныеЗаписи.Следующий() И КоличествоКСписанию > 0 Цикл
          
          Количество = Мин(ВыборкаДетальныеЗаписи.КоличествоОстатокПоСрокуГодности, КоличествоКСписанию);
          
          Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
          ЗаполнитьЗначенияСвойств(Движение, ВыборкаДетальныеЗаписи);
          Движение.Количество = Количество;
          
          Движение = Движения.ОборудованиеВЭксплуатации.ДобавитьПриход();
          ЗаполнитьЗначенияСвойств(Движение, ВыборкаДетальныеЗаписи);
          Движение.Количество = Количество;
          Движение.Стоимость  = СебестоимостьЕдиницы * Количество;
          
          СебестоимостьИтого = СебестоимостьИтого + Движение.Стоимость;
          
          КоличествоКСписанию = КоличествоКСписанию - Количество;
          
        КонецЦикла;
        
        Погрешность = Себестоимость - СебестоимостьИтого;
        Если Погрешность <> 0 Тогда
          Движение.Стоимость = Движение.Стоимость + Погрешность;
        КонецЕсли;
        
      КонецЦикла;
      
    КонецПроцедуры
    
    <p>///// Документ ВыбытиеОборудования</p> 
    Процедура ОбработкаПроведения(Отказ, РежимПроведения)
      
      Движения.ОстаткиНоменклатуры.Записывать = Истина;
      Движения.ОборудованиеВЭксплуатации.Записывать = Истина;
      Движения.Себестоимость.Записывать = Истина;
      Движения.Записать();
      
      Движения.ОстаткиНоменклатуры.Записывать = Истина;
      Движения.ОборудованиеВЭксплуатации.Записывать = Истина;
      Движения.Себестоимость.Записывать = Истина;
      
      НачалоДняДокумента = НачалоДня(Дата);
      
      Блокировка = Новый БлокировкаДанных;
      
      ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
      ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
      ЭлементБлокировки.УстановитьЗначение("СрокГодности", Новый Диапазон(,НачалоДняДокумента - 1));
      
      ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.Себестоимость");
      ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
      
      Блокировка.Заблокировать();
        
      Запрос = Новый Запрос;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	ОстаткиНоменклатурыОстатки.Номенклатура КАК Номенклатура,
        |	ОстаткиНоменклатурыОстатки.СрокГодности КАК СрокГодности,
        |	ОстаткиНоменклатурыОстатки.КоличествоОстаток КАК КоличествоОстатокПоСрокуГодности,
        |	СебестоимостьОстатки.КоличествоОстаток КАК ИтоговоеКоличествоОстаток,
        |	СебестоимостьОстатки.СтоимостьОстаток КАК ИтоговаяСтоимостьОстаток,
        |	&Период КАК Период
        |ИЗ
        |	РегистрНакопления.ОстаткиНоменклатуры.Остатки(&МоментВремени, СрокГодности < &НачалоДня) КАК ОстаткиНоменклатурыОстатки
        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.Себестоимость.Остатки(&МоментВремени, ) КАК СебестоимостьОстатки
        |		ПО ОстаткиНоменклатурыОстатки.Номенклатура = СебестоимостьОстатки.Номенклатура
        |ИТОГИ
        |	СУММА(КоличествоОстатокПоСрокуГодности),
        |	МАКСИМУМ(ИтоговоеКоличествоОстаток),
        |	МАКСИМУМ(ИтоговаяСтоимостьОстаток),
        |	МАКСИМУМ(Период)
        |ПО
        |	Номенклатура";
      
      Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
      Запрос.УстановитьПараметр("НачалоДня", НачалоДняДокумента);
      Запрос.УстановитьПараметр("Период", Дата);
      
      РезультатЗапроса = Запрос.ВыполнитьПакет();
      
      ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
      
      Пока ВыборкаНоменклатура.Следующий() Цикл
        
        Если ВыборкаНоменклатура.КоличествоОстатокПоСрокуГодности = ВыборкаНоменклатура.ИтоговоеКоличествоОстаток Тогда
          Себестоимость = ВыборкаНоменклатура.ИтоговаяСтоимостьОстаток;
        Иначе
          Себестоимость = ВыборкаНоменклатура.ИтоговаяСтоимостьОстаток / ВыборкаНоменклатура.ИтоговоеКоличествоОстаток 
          * ВыборкаНоменклатура.КоличествоОстатокПоСрокуГодности; 
        КонецЕсли;
        
        Движение = Движения.Себестоимость.ДобавитьРасход();
        ЗаполнитьЗначенияСвойств(Движение, ВыборкаНоменклатура);
        Движение.Количество = ВыборкаНоменклатура.КоличествоОстатокПоСрокуГодности;
        Движение.Стоимость = Себестоимость;
        
        ВыборкаДетальныеЗаписи = ВыборкаНоменклатура.Выбрать();
        Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
          Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
          ЗаполнитьЗначенияСвойств(Движение, ВыборкаДетальныеЗаписи);
          Движение.Количество = ВыборкаДетальныеЗаписи.КоличествоОстатокПоСрокуГодности;
        КонецЦикла;
        
      КонецЦикла;	
      
      Блокировка = Новый БлокировкаДанных;
      ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОборудованиеВЭксплуатации");
      ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
      ЭлементБлокировки.УстановитьЗначение("СрокГодности", Новый Диапазон(,НачалоДняДокумента - 1));	
      ЭлементБлокировки.УстановитьЗначение("СрокЭксплуатации", Новый Диапазон(,НачалоДняДокумента - 1));
      Блокировка.Заблокировать();
      
      Запрос = Новый Запрос;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	ОборудованиеВЭксплуатацииОстатки.Номенклатура КАК Номенклатура,
        |	ОборудованиеВЭксплуатацииОстатки.СрокГодности КАК СрокГодности,
        |	ОборудованиеВЭксплуатацииОстатки.СрокЭксплуатации КАК СрокЭксплуатации,
        |	ОборудованиеВЭксплуатацииОстатки.КоличествоОстаток КАК Количество,
        |	ОборудованиеВЭксплуатацииОстатки.СтоимостьОстаток КАК Стоимость,
        |	&Период КАК Период
        |ИЗ
        |	РегистрНакопления.ОборудованиеВЭксплуатации.Остатки(
        |			&МоментВремени,
        |			СрокГодности < &НачалоДня
        |				ИЛИ СрокЭксплуатации < &НачалоДня) КАК ОборудованиеВЭксплуатацииОстатки";
      
      Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
      Запрос.УстановитьПараметр("НачалоДня", НачалоДняДокумента);
      Запрос.УстановитьПараметр("Период", Дата);
      
      РезультатЗапроса = Запрос.Выполнить();
      
      Выборка = РезультатЗапроса.Выбрать();
      
      Пока Выборка.Следующий() Цикл
        Движение = Движения.ОборудованиеВЭксплуатации.ДобавитьРасход();
        ЗаполнитьЗначенияСвойств(Движение, Выборка);
      КонецЦикла;
          
    КонецПроцедуры
    
    <p>///// Документ ПриходнаяНакладная</p>
    Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
      СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
    КонецПроцедуры
    
    Процедура ОбработкаПроведения(Отказ, Режим)
    
      Движения.ОстаткиНоменклатуры.Записывать = Истина;
      Движения.Себестоимость.Записывать = Истина;
      
      Запрос = Новый Запрос;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
        |	СУММА(ПриходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
        |	СУММА(ПриходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Стоимость,
        |	ДОБАВИТЬКДАТЕ(&Период, ДЕНЬ, ПриходнаяНакладнаяСписокНоменклатуры.СрокГодностиДней) КАК СрокГодности,
        |	&Период КАК Период
        |ИЗ
        |	Документ.ПриходнаяНакладная.СписокНоменклатуры КАК ПриходнаяНакладнаяСписокНоменклатуры
        |ГДЕ
        |	ПриходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
        |
        |СГРУППИРОВАТЬ ПО
        |	ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура,
        |	ДОБАВИТЬКДАТЕ(&Период, ДЕНЬ, ПриходнаяНакладнаяСписокНоменклатуры.СрокГодностиДней)
        |ИТОГИ
        |	СУММА(Количество),
        |	СУММА(Стоимость),
        |	МАКСИМУМ(Период)
        |ПО
        |	Номенклатура";
      
      Запрос.УстановитьПараметр("Период", Дата);
      Запрос.УстановитьПараметр("Ссылка", Ссылка);
      
      РезультатЗапроса = Запрос.Выполнить();
      
      ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
      
      Пока ВыборкаНоменклатура.Следующий() Цикл
        
        Движение = Движения.Себестоимость.ДобавитьПриход();
        ЗаполнитьЗначенияСвойств(Движение, ВыборкаНоменклатура);
      
        ВыборкаДетальныеЗаписи = ВыборкаНоменклатура.Выбрать();
      
        Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
          Движение = Движения.ОстаткиНоменклатуры.ДобавитьПриход();
          ЗаполнитьЗначенияСвойств(Движение, ВыборкаДетальныеЗаписи);
        КонецЦикла;
      КонецЦикла;
        
    КонецПроцедуры
    
    <p>///// Документ РасходнаяНакладная</p> 
    Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
      СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
    КонецПроцедуры
    
    <p>///// Модуль формы</p>
    &НаКлиенте
    Процедура СписокНоменклатурыКоличествоПриИзменении(Элемент)
      ТекСтрока = Элементы.СписокНоменклатуры.ТекущиеДанные;
      ОбработкаТабличнойЧасти.РассчитатьСуммуВСтроке(ТекСтрока);
    КонецПроцедуры
    
    &НаКлиенте
    Процедура СписокНоменклатурыЦенаПриИзменении(Элемент)
      ТекСтрока = Элементы.СписокНоменклатуры.ТекущиеДанные;
      ОбработкаТабличнойЧасти.РассчитатьСуммуВСтроке(ТекСтрока);
    КонецПроцедуры
    <embed src="pdf/ОбщееОУ13.pdf" type="application/pdf" width="1200" height="800">
      <img src="pdf/ОУ13.jpg">
    <H2 class="OU">
      Бухгалтерский учет
    </H2>
    <p>///// Общий модуль ОбработкаТабличнойЧастиКлиент</p>
    Процедура РассчитатьСуммуВСтроке(ТекСтрока) Экспорт
      ТекСтрока.Сумма = ТекСтрока.Цена * ТекСтрока.Количество;
    КонецПроцедуры 
    
    <p>///// Документ Операция</p>   
    Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
      Если Не Движения.Управленческий.Записывать Тогда
        Движения.Управленческий.Прочитать();
        Движения.Управленческий.Записывать = Истина;
      КонецЕсли;
      
      Для Каждого Движение Из Движения.Управленческий Цикл
        Движение.Период = Дата;
        Движение.Активность = Не ПометкаУдаления;
      КонецЦикла;
    КонецПроцедуры
    
    Процедура ПриКопировании(ОбъектКопирования)	
      Выборка = РегистрыБухгалтерии.Управленческий.ВыбратьПоРегистратору(ОбъектКопирования.Ссылка);
      Пока Выборка.Следующий() Цикл
        Движение = Движения.Управленческий.Добавить();
        ЗаполнитьЗначенияСвойств(Движение, Выборка);
        ЗаполнитьЗначенияСвойств(Движение.СубконтоДт, Выборка.СубконтоДт);
        ЗаполнитьЗначенияСвойств(Движение.СубконтоКт, Выборка.СубконтоКт);
      КонецЦикла;
    КонецПроцедуры
     
    <p>///// Документ ПриходнаяНакладная</p>
    Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
      СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
    КонецПроцедуры
    
    <p>///// Документ РасходнаяНакладная</p> 
    Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
      СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
    КонецПроцедуры
    
    Процедура ОбработкаПроведения(Отказ, РежимПроведения)
      
      Движения.Управленческий.Записывать = Истина;
      Движения.Записать();
      
      Движения.Управленческий.Записывать = Истина;
      
      Запрос = Новый Запрос;
      МенеджерВТ = Новый МенеджерВременныхТаблиц;
      Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
        |	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
        |	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма
        |ПОМЕСТИТЬ втТЧТовары
        |ИЗ
        |	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
        |ГДЕ
        |	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
        |
        |СГРУППИРОВАТЬ ПО
        |	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура
        |
        |ИНДЕКСИРОВАТЬ ПО
        |	Номенклатура
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	втТЧТовары.Номенклатура КАК Комплект,
        |	СоставКомплекта.Комплектующая КАК Комплектующая,
        |	втТЧТовары.Количество * СоставКомплекта.Количество КАК Количество,
        |	втТЧТовары.Сумма КАК Сумма
        |ПОМЕСТИТЬ втКомплектующие
        |ИЗ
        |	втТЧТовары КАК втТЧТовары
        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоставКомплекта КАК СоставКомплекта
        |		ПО втТЧТовары.Номенклатура = СоставКомплекта.Комплект
        |
        |ИНДЕКСИРОВАТЬ ПО
        |	Комплектующая
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	втКомплектующие.Комплект.Представление КАК КомплектПредставление
        |ИЗ
        |	втКомплектующие КАК втКомплектующие
        |ГДЕ
        |	втКомплектующие.Комплектующая ЕСТЬ NULL
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	втКомплектующие.Комплектующая КАК Комплектующая,
        |	втКомплектующие.Количество КАК Количество
        |ИЗ
        |	втКомплектующие КАК втКомплектующие";
      
      Запрос.УстановитьПараметр("Ссылка", Ссылка);
      
      РезультатыЗапроса = Запрос.ВыполнитьПакет();
      
      Если Не РезультатыЗапроса[2].Пустой() Тогда		
        Отказ = Истина;
        
        Выборка = РезультатыЗапроса[2].Выбрать();
        Пока Выборка.Следующий() Цикл
          Сообщение = Новый СообщениеПользователю;
          Сообщение.Текст = "Не задан состав комплекта " + Выборка.КомплектПредставление;
          Сообщение.Сообщить();
        КонецЦикла;
        
        Возврат;		
      КонецЕсли;
      
      Блокировка = Новый БлокировкаДанных;
      ЭлементБлокировки = Блокировка.Добавить("РегистрБухгалтерии.Управленческий");
      ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
      ЭлементБлокировки.УстановитьЗначение("Счет", ПланыСчетов.Управленческий.Товары);
      ЭлементБлокировки.УстановитьЗначение(ПланыВидовХарактеристик.ВидыСубконто.Склады, Склад);
      ЭлементБлокировки.ИсточникДанных = РезультатыЗапроса[3];
      ЭлементБлокировки.ИспользоватьИзИсточникаДанных(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура, "Комплектующая");
      Блокировка.Заблокировать();
      
      Запрос = Новый Запрос;
      Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	втКомплектующие.Комплект КАК Комплект,
        |	втКомплектующие.Комплект.Представление КАК КомплектПредставление,
        |	втКомплектующие.Комплектующая КАК Комплектующая,
        |	втКомплектующие.Комплектующая.Представление КАК КомплектующаяПредставление,
        |	втКомплектующие.Количество КАК Количество,
        |	ЕСТЬNULL(УправленческийОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
        |	ЕСТЬNULL(УправленческийОстатки.СуммаОстаток, 0) КАК СуммаОстаток,
        |	втКомплектующие.Сумма КАК Сумма
        |ИЗ
        |	втКомплектующие КАК втКомплектующие
        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(
        |				&МоментВремени,
        |				Счет = &СчетТовары,
        |				&ВидыСубконто,
        |				Субконто1 В
        |						(ВЫБРАТЬ
        |							Т.Комплектующая
        |						ИЗ
        |							втКомплектующие КАК Т)
        |					И Субконто2 = &Склад) КАК УправленческийОстатки
        |		ПО втКомплектующие.Комплектующая = УправленческийОстатки.Субконто1
        |ИТОГИ
        |	МАКСИМУМ(Сумма)
        |ПО
        |	Комплект";
      
      ВидыСубконто = Новый Массив;
      ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура);
      ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконто.Склады);
      
      Запрос.УстановитьПараметр("ВидыСубконто", ВидыСубконто);
      Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
      Запрос.УстановитьПараметр("Склад", Склад);
      Запрос.УстановитьПараметр("СчетТовары", ПланыСчетов.Управленческий.Товары);
      
      РезультатЗапроса = Запрос.Выполнить();
      
      ВыборкаКомплект = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
      
      Пока ВыборкаКомплект.Следующий() Цикл
      
        Выборка = ВыборкаКомплект.Выбрать();
      
        Пока Выборка.Следующий() Цикл
          
          Если Выборка.Количество > Выборка.КоличествоОстаток Тогда
            Отказ = Истина;
            Сообщение = Новый СообщениеПользователю;
            Сообщение.Текст = СтрШаблон("Недостаточно остатков товара %1 (комплект %2) в количестве %3",
            Выборка.КомплектующаяПредставление, Выборка.КомплектПредставление, Выборка.Количество - Выборка.КоличествоОстаток);
            Сообщение.Сообщить();
          КонецЕсли;
          
          Если Отказ Тогда
            Продолжить;
          КонецЕсли;
          
          Если Выборка.Количество = Выборка.КоличествоОстаток Тогда
            Себестоимость = Выборка.СуммаОстаток;
          Иначе
            Себестоимость = Выборка.СуммаОстаток / Выборка.КоличествоОстаток * Выборка.Количество;
          КонецЕсли;
          
          Движение = Движения.Управленческий.Добавить();
          Движение.Период = Дата;
          Движение.СчетДт = ПланыСчетов.Управленческий.ПрибылиУбытки;
          Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = Выборка.Комплект;
          
          Движение.СчетКт = ПланыСчетов.Управленческий.Товары;
          Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = Выборка.Комплектующая;
          Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Склады] = Склад;
          Движение.КоличествоКт = Выборка.Количество;
          
          Движение.Сумма = Себестоимость;
          
        КонецЦикла;
        
        Если Не Отказ Тогда
          Движение = Движения.Управленческий.Добавить();
          Движение.Период = Дата;
          Движение.СчетДт = ПланыСчетов.Управленческий.Покупатели;
          Движение.СчетКт = ПланыСчетов.Управленческий.ПрибылиУбытки;
          Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = ВыборкаКомплект.Комплект;
          Движение.Сумма = ВыборкаКомплект.Сумма;
        КонецЕсли;
        
      КонецЦикла;
      
    КонецПроцедуры
    
    <p>///// Модуль формы</p>
    &НаКлиенте
    Процедура СписокНоменклатурыКоличествоПриИзменении(Элемент)
      ТекСтрока = Элементы.СписокНоменклатуры.ТекущиеДанные;
      ОбработкаТабличнойЧасти.РассчитатьСуммуВСтроке(ТекСтрока);
    КонецПроцедуры
    
    &НаКлиенте
    Процедура СписокНоменклатурыЦенаПриИзменении(Элемент)
      ТекСтрока = Элементы.СписокНоменклатуры.ТекущиеДанные;
      ОбработкаТабличнойЧасти.РассчитатьСуммуВСтроке(ТекСтрока);
    КонецПроцедуры
    <embed src="pdf/ОбщееБУ13.pdf" type="application/pdf" width="1200" height="800">
      <img src="pdf/БУ13.jpg">
    <H2 class="OU">
      Расчеты
    </H2>
    <p>///// Документ НачислениеЗарплаты</p>   
    Процедура ОбработкаПроведения(Отказ, РежимПроведения)
      
      ПериодРегистрации = НачалоМесяца(Дата);
      КонецПериодаРегистрации = КонецМесяца(ПериодРегистрации);
      Движения.ОсновныеНачисления.Записывать = Истина;
      Для Каждого Строка Из ОсновныеНачисления Цикл
        Движение = Движения.ОсновныеНачисления.Добавить();
        ЗаполнитьЗначенияСвойств(Движение, Строка);
        Движение.ПериодРегистрации = ПериодРегистрации;
        Движение.ПериодДействияНачало = Строка.ДатаНачала;
        Движение.ПериодДействияКонец = КонецДня(Строка.ДатаОкончания);
      КонецЦикла;
      
      Движения.ДополнительныеНачисления.Записывать = Истина;
      Для Каждого Строка Из ДополнительныеНачисления Цикл
        Движение = Движения.ДополнительныеНачисления.Добавить();
        ЗаполнитьЗначенияСвойств(Движение, Строка);
        Движение.ПериодРегистрации = ПериодРегистрации;
        Если Движение.ВидРасчета = ПланыВидовРасчета.ДополнительныеНачисления.ПремияНачальникуБригады Тогда
          Движение.БазовыйПериодНачало = ПериодРегистрации;
          Движение.БазовыйПериодКонец = КонецПериодаРегистрации;
        КонецЕсли;
      КонецЦикла;	
      
      Движения.Записать();
      
      Запрос = Новый Запрос;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
        |	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ЗначениеФактическийПериодДействия, 0) КАК ОтработаноЧасов
        |ИЗ
        |	РегистрРасчета.ОсновныеНачисления.ДанныеГрафика(
        |			Регистратор = &Ссылка
        |				И ВидРасчета В (&ВидыРасчета)) КАК ОсновныеНачисленияДанныеГрафика";
      
      ВидыРасчета = Новый Массив;
      ВидыРасчета.Добавить(ПланыВидовРасчета.ОсновныеНачисления.ОплатаПоЧасовомуТарифу);
      Запрос.УстановитьПараметр("ВидыРасчета", ВидыРасчета);
      
      Запрос.УстановитьПараметр("Ссылка", Ссылка);
      
      РезультатЗапроса = Запрос.Выполнить();
      
      Выборка = РезультатЗапроса.Выбрать();
      
      Для Каждого Движение Из Движения.ОсновныеНачисления Цикл
        
        Если ВидыРасчета.Найти(Движение.ВидРасчета) = Неопределено Тогда
          Продолжить;
        КонецЕсли;
        
        Выборка.Сбросить();
        Выборка.НайтиСледующий(Движение.НомерСтроки, "НомерСтроки");
        
        Если Движение.ВидРасчета = ПланыВидовРасчета.ОсновныеНачисления.ОплатаПоЧасовомуТарифу Тогда
          Движение.Результат = Выборка.ОтработаноЧасов * Движение.Параметр;
          ЗаполнитьЗначенияСвойств(Движение, Выборка, "ОтработаноЧасов");
        КонецЕсли;
        
      КонецЦикла;
      
      Движения.ОсновныеНачисления.Записать(, Истина);
      
      Запрос = Новый Запрос;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	ДополнительныеНачисления.НомерСтроки КАК НомерСтроки,
        |	ДополнительныеНачисления.Подразделение КАК Подразделение
        |ПОМЕСТИТЬ втДанныеНачислений
        |ИЗ
        |	РегистрРасчета.ДополнительныеНачисления КАК ДополнительныеНачисления
        |ГДЕ
        |	ДополнительныеНачисления.Регистратор = &Ссылка
        |	И ДополнительныеНачисления.ВидРасчета В(&ВидыРасчета)
        |
        |ИНДЕКСИРОВАТЬ ПО
        |	Подразделение
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	втДанныеНачислений.НомерСтроки КАК НомерСтроки,
        |	ЕСТЬNULL(ПродажиОбороты.СуммаОборот, 0) КАК СуммаПродаж
        |ИЗ
        |	втДанныеНачислений КАК втДанныеНачислений
        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.Продажи.Обороты(
        |				&НачалоПредыдущегоКвартала,
        |				&КонецПредыдущегоКвартала,
        |				,
        |				Подразделение В
        |					(ВЫБРАТЬ
        |						втДанныеНачислений.Подразделение КАК Подразделение
        |					ИЗ
        |						втДанныеНачислений КАК втДанныеНачислений)) КАК ПродажиОбороты
        |		ПО втДанныеНачислений.Подразделение = ПродажиОбороты.Подразделение";
      
      ВидыРасчета = Новый Массив;
      ВидыРасчета.Добавить(ПланыВидовРасчета.ДополнительныеНачисления.Надбавка);
      Запрос.УстановитьПараметр("ВидыРасчета", ВидыРасчета);
      
      КонецПредыдущегоКвартала = НачалоКвартала(ПериодРегистрации) - 1;
      НачалоПредыдущегоКвартала = НачалоКвартала(КонецПредыдущегоКвартала);
      
      Запрос.УстановитьПараметр("НачалоПредыдущегоКвартала", НачалоПредыдущегоКвартала);
      Запрос.УстановитьПараметр("КонецПредыдущегоКвартала",  КонецПредыдущегоКвартала);
      
      Запрос.УстановитьПараметр("Ссылка", Ссылка);
      
      РезультатЗапроса = Запрос.Выполнить();
      
      Выборка = РезультатЗапроса.Выбрать();
      
      Для Каждого Движение Из Движения.ДополнительныеНачисления Цикл
        
        Если ВидыРасчета.Найти(Движение.ВидРасчета) = Неопределено Тогда
          Продолжить;			
        КонецЕсли;     
        
        Выборка.Сбросить();
        Выборка.НайтиСледующий(Движение.НомерСтроки, "НомерСтроки");
        
        Если Движение.ВидРасчета = ПланыВидовРасчета.ДополнительныеНачисления.Надбавка Тогда
          Движение.Результат = Выборка.СуммаПродаж * Движение.Параметр / 100;			
          ЗаполнитьЗначенияСвойств(Движение, Выборка, "СуммаПродаж");
        КонецЕсли;
        
      КонецЦикла;
      
      Движения.ДополнительныеНачисления.Записать();
    
      Запрос = Новый Запрос;
      Запрос.Текст = "ВЫБРАТЬ
                     |	ДополнительныеНачисленияБазаДополнительныеНачисления.НомерСтроки КАК НомерСтроки,
                     |	МАКСИМУМ(ДополнительныеНачисленияБазаДополнительныеНачисления.РезультатБаза) КАК БазаНачислений
                     |ИЗ
                     |	РегистрРасчета.ДополнительныеНачисления.БазаДополнительныеНачисления(
                     |			&Измерения,
                     |			&Измерения,
                     |			&Разрезы,
                     |			Регистратор = &Ссылка
                     |				И ВидРасчета В (&ВидыРасчета)) КАК ДополнительныеНачисленияБазаДополнительныеНачисления
                     |ГДЕ
                     |	ДополнительныеНачисленияБазаДополнительныеНачисления.Сотрудник <> ДополнительныеНачисленияБазаДополнительныеНачисления.СотрудникРазрез
                     |
                     |СГРУППИРОВАТЬ ПО
                     |	ДополнительныеНачисленияБазаДополнительныеНачисления.НомерСтроки";
      //Запрос.Текст = 
      //	"ВЫБРАТЬ
      //	|	ДополнительныеНачисления.НомерСтроки КАК НомерСтроки,
      //	|	ДополнительныеНачисления.Сотрудник КАК Сотрудник,
      //	|	ДополнительныеНачисления.Подразделение КАК Подразделение
      //	|ПОМЕСТИТЬ втДанныеНачислений
      //	|ИЗ
      //	|	РегистрРасчета.ДополнительныеНачисления КАК ДополнительныеНачисления
      //	|ГДЕ
      //	|	ДополнительныеНачисления.Регистратор = &Регистратор
      //	|	И ДополнительныеНачисления.ВидРасчета В(&ВидыРасчета)
      //	|
      //	|ИНДЕКСИРОВАТЬ ПО
      //	|	Подразделение
      //	|;
      //	|
      //	|////////////////////////////////////////////////////////////////////////////////
      //	|ВЫБРАТЬ
      //	|	ДополнительныеНачисления.Сотрудник КАК Сотрудник,
      //	|	ДополнительныеНачисления.Подразделение КАК Подразделение,
      //	|	ДополнительныеНачисления.Результат КАК Результат
      //	|ПОМЕСТИТЬ втБазаНачислений
      //	|ИЗ
      //	|	РегистрРасчета.ДополнительныеНачисления КАК ДополнительныеНачисления
      //	|ГДЕ
      //	|	ДополнительныеНачисления.ПериодРегистрации = &ПериодРегистрации
      //	|	И ДополнительныеНачисления.ВидРасчета В(&ВидыРасчетаБаза)
      //	|	И ДополнительныеНачисления.Активность
      //	|
      //	|ИНДЕКСИРОВАТЬ ПО
      //	|	Подразделение
      //	|;
      //	|
      //	|////////////////////////////////////////////////////////////////////////////////
      //	|ВЫБРАТЬ
      //	|	втДанныеНачислений.НомерСтроки КАК НомерСтроки,
      //	|	МАКСИМУМ(втБазаНачислений.Результат) КАК БазаНачислений
      //	|ИЗ
      //	|	втДанныеНачислений КАК втДанныеНачислений
      //	|		ЛЕВОЕ СОЕДИНЕНИЕ втБазаНачислений КАК втБазаНачислений
      //	|		ПО втДанныеНачислений.Подразделение = втБазаНачислений.Подразделение
      //	|ГДЕ
      //	|	втБазаНачислений.Сотрудник <> втДанныеНачислений.Сотрудник
      //	|
      //	|СГРУППИРОВАТЬ ПО
      //	|	втДанныеНачислений.НомерСтроки";
      
      Измерения = Новый Массив;
      Измерения.Добавить("Подразделение");
      Запрос.УстановитьПараметр("Измерения", Измерения);
      
      Разрезы = Новый Массив;
      Разрезы.Добавить("Сотрудник");
      Запрос.УстановитьПараметр("Разрезы", Разрезы);
      
        ВидыРасчета = Новый Массив;
      ВидыРасчета.Добавить(ПланыВидовРасчета.ДополнительныеНачисления.ПремияНачальникуБригады);
      Запрос.УстановитьПараметр("ВидыРасчета", ВидыРасчета);
                    
      Запрос.УстановитьПараметр("Ссылка", Ссылка);
      
      РезультатЗапроса = Запрос.Выполнить();
      
      Выборка = РезультатЗапроса.Выбрать();
      
      Для Каждого Движение Из Движения.ДополнительныеНачисления Цикл
        
        Если ВидыРасчета.Найти(Движение.ВидРасчета) = Неопределено Тогда
          Продолжить;			
        КонецЕсли;
        
        Выборка.Сбросить();
        Если Выборка.НайтиСледующий(Движение.НомерСтроки, "НомерСтроки") Тогда
        
          Если Движение.ВидРасчета = ПланыВидовРасчета.ДополнительныеНачисления.ПремияНачальникуБригады Тогда
            Движение.Результат = Выборка.БазаНачислений * Движение.Параметр / 100;
            ЗаполнитьЗначенияСвойств(Движение, Выборка, "БазаНачислений");
          КонецЕсли;                                                        
          
        КонецЕсли;
        
      КонецЦикла;
      
      Движения.ДополнительныеНачисления.Записать();
      
    КонецПроцедуры
    
    <p>///// Модуль объекта ЗаполнениеГрафика</p>
    Процедура ЗаполнитьГрафик(ДатаНачала, ДатаОкончания, ВыходныеДни) Экспорт 
      
      Набор = РегистрыСведений.ГрафикиРаботы.СоздатьНаборЗаписей();
      
      ЧислоСекундВСутках = 86400;
      
      Дат = ДатаНачала;	
      Пока Дат <= ДатаОкончания Цикл
        Запись = Набор.Добавить();
        Запись.Дата = Дат;
        Если Найти(ВыходныеДни, Строка(ДеньНедели(Дат))) Тогда
          Запись.Значение = 0;
        Иначе	          
          Запись.Значение = 8;
        КонецЕсли; 
        Дат = Дат + ЧислоСекундВСутках;
      КонецЦикла; 
      Набор.Записать();
    КонецПроцедуры
    
    <p>///// Модуль формы</p>
    &НаКлиенте
    Процедура Заполнить(Команда)
      ВыполнитьОбработку();
      Сообщить("Обработка завершена!");
    КонецПроцедуры
    
    &НаСервере
    Процедура ВыполнитьОбработку()
      ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
        ОбработкаОбъект.ЗаполнитьГрафик(ВыборПериода.ДатаНачала, ВыборПериода.ДатаОкончания, ВыходныеДни, ГрафикРаботы);
    КонецПроцедуры
    <embed src="pdf/ОбщееСПР13.pdf" type="application/pdf" width="1200" height="800">
      <img src="pdf/СПР13.jpg">
  </pre>
</body>

</html>
