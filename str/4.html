<!doctype html>
<html lang="ru">

<head>
  <meta charset="utf-8">
  <title>HTML Document Template</title>
  <link href="styles.css" rel="stylesheet">
</head>

<body>
  <div class="header__menu">
    <ul>
      <li><a href="str/1">1</a></li>
      <li><a href="str/2">2</a></li>
      <li><a href="str/3">3</a></li>
      <li><a href="str/4">4</a></li>
      <li><a href="str/5">5</a></li>
      <li><a href="str/6">6</a></li>
      <li><a href="str/7">7</a></li>
      <li><a href="str/8">8</a></li>
      <li><a href="str/9">9</a></li>
      <li><a href="str/10">10</a></li>
      <li><a href="str/11">11</a></li>
      <li><a href="str/12">12</a></li>
      <li><a href="str/13">13</a></li>
      <li><a href="str/14">14</a></li>
      <li><a href="str/15">15</a></li>
      <li><a href="str/16">16</a></li>
    </ul>
  </div>
  <h1>4</h1>
  <H2 class="OU">
    Оперативный учет
  </H2>
  <pre>
    <p>///// Общий модуль ОбработкаТабличнойЧасти</p>
    Процедура РассчитатьСуммуВСтроке(ТекСтрока) Экспорт
      ТекСтрока.Сумма = ТекСтрока.Цена * ТекСтрока.Количество;
    КонецПроцедуры 
    
    <p>///// Документ ПриходнаяНакладная</p>
    Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
      СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
    КонецПроцедуры
    
    Процедура ОбработкаПроведения(Отказ, Режим)
    
      // регистр ОстаткиНоменклатуры Приход
      Движения.ОстаткиНоменклатуры.Записывать = Истина;
      Для Каждого ТекСтрокаСписокНоменклатуры Из СписокНоменклатуры Цикл
        Движение = Движения.ОстаткиНоменклатуры.Добавить();
        Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
        Движение.Период = Дата;
        Движение.Номенклатура = ТекСтрокаСписокНоменклатуры.Номенклатура;
        Движение.Склад = Склад;
        Движение.Количество = ТекСтрокаСписокНоменклатуры.Количество;
        Движение.Себестоимость = ТекСтрокаСписокНоменклатуры.Сумма;
      КонецЦикла;
    
    КонецПроцедуры
    
    <p>///// Документ РасходнаяНакладная</p> 
    
    Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
      СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
    КонецПроцедуры
    
    Процедура ОбработкаПроведения(Отказ, РежимПроведения)
      
      Движения.ОстаткиНоменклатуры.Записывать = Истина;
      Движения.Записать();
        
      Движения.ОстаткиНоменклатуры.Записывать = Истина;
      
      Блокировка = Новый БлокировкаДанных;
      ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
      ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
      ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
      ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
      Блокировка.Заблокировать();
      
      Запрос = Новый Запрос;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
        |	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество
        |ПОМЕСТИТЬ втТЧТовары
        |ИЗ
        |	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
        |ГДЕ
        |	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
        |	И РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Товар)
        |
        |СГРУППИРОВАТЬ ПО
        |	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура
        |
        |ИНДЕКСИРОВАТЬ ПО
        |	Номенклатура
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	втТЧТовары.Номенклатура КАК Номенклатура,
        |	втТЧТовары.Номенклатура.Представление КАК НоменклатураПредставление,
        |	втТЧТовары.Количество КАК Количество,
        |	ОстаткиНоменклатурыОстатки.Склад КАК Склад,
        |	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
        |	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.СебестоимостьОстаток, 0) КАК СебестоимостьОстаток,
        |	ВЫБОР
        |		КОГДА ОстаткиНоменклатурыОстатки.Склад = &Склад
        |			ТОГДА -1
        |		ИНАЧЕ ЕСТЬNULL(ПриоритетыСкладовСрезПоследних.Приоритет, 100)
        |	КОНЕЦ КАК Приоритет
        |ИЗ
        |	втТЧТовары КАК втТЧТовары
        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
        |				&МоментВремени,
        |				Номенклатура В
        |					(ВЫБРАТЬ
        |						втТЧТовары.Номенклатура КАК Номенклатура
        |					ИЗ
        |						втТЧТовары КАК втТЧТовары)) КАК ОстаткиНоменклатурыОстатки
        |			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПриоритетыСкладов.СрезПоследних(&МоментВремени, ) КАК ПриоритетыСкладовСрезПоследних
        |			ПО ОстаткиНоменклатурыОстатки.Склад = ПриоритетыСкладовСрезПоследних.Склад
        |		ПО втТЧТовары.Номенклатура = ОстаткиНоменклатурыОстатки.Номенклатура
        |
        |УПОРЯДОЧИТЬ ПО
        |	Приоритет
        |ИТОГИ
        |	МАКСИМУМ(Количество),
        |	СУММА(КоличествоОстаток)
        |ПО
        |	Номенклатура";
      
      Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
      Запрос.УстановитьПараметр("Склад", Склад);
      Запрос.УстановитьПараметр("Ссылка", Ссылка);
      
      РезультатЗапроса = Запрос.Выполнить();
      
      ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
      
      Пока ВыборкаНоменклатура.Следующий() Цикл
        
        Если ВыборкаНоменклатура.Количество > ВыборкаНоменклатура.КоличествоОстаток Тогда
          Отказ = Истина;
          
          Сообщение = Новый СообщениеПользователю;
          Сообщение.Текст = СтрШаблон("Недостаточно товара %1", ВыборкаНоменклатура.НоменклатураПредставление);
          Сообщение.Сообщить();
        КонецЕсли;
        
        Если Отказ Тогда
          Продолжить;
        КонецЕсли;
        
        КоличествоСписать = ВыборкаНоменклатура.Количество;
        
        ВыборкаДетальныеЗаписи = ВыборкаНоменклатура.Выбрать();
      
        Пока ВыборкаДетальныеЗаписи.Следующий() И КоличествоСписать > 0 Цикл
          
          Количество = Мин(КоличествоСписать, ВыборкаДетальныеЗаписи.КоличествоОстаток);
          
          Если Количество = ВыборкаДетальныеЗаписи.КоличествоОстаток Тогда
            Себестоимость = ВыборкаДетальныеЗаписи.СебестоимостьОстаток;
          Иначе
            Себестоимость = ВыборкаДетальныеЗаписи.СебестоимостьОстаток / ВыборкаДетальныеЗаписи.КоличествоОстаток * Количество;
          КонецЕсли;
          
          Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
          ЗаполнитьЗначенияСвойств(Движение, ВыборкаДетальныеЗаписи);
          Движение.Период = Дата;
          Движение.Количество = Количество;
          Движение.Себестоимость = Себестоимость;
          
          КоличествоСписать = КоличествоСписать - Количество;
          
        КонецЦикла;
      КонецЦикла;
      
    КонецПроцедуры
    
    <p>///// Модуль формы</p>
    &НаКлиенте
    Процедура СписокНоменклатурыКоличествоПриИзменении(Элемент)
      ТекСтрока = Элементы.СписокНоменклатуры.ТекущиеДанные;
      ОбработкаТабличнойЧасти.РассчитатьСуммуВСтроке(ТекСтрока);
    КонецПроцедуры
    
    &НаКлиенте
    Процедура СписокНоменклатурыЦенаПриИзменении(Элемент)
      ТекСтрока = Элементы.СписокНоменклатуры.ТекущиеДанные;
      ОбработкаТабличнойЧасти.РассчитатьСуммуВСтроке(ТекСтрока);
    КонецПроцедуры    
    <embed src="pdf/ОбщееОУ4.pdf" type="application/pdf" width="1200" height="800">
      <img src="pdf/ОУ4.jpg">
    <H2 class="OU">
      Бухгалтерский учет
    </H2>
    <p>///// Общий модуль ОбработкаТабличнойЧастиКлиент</p>
    Процедура РассчитатьСуммуВСтроке(ТекСтрока) Экспорт
      ТекСтрока.Сумма = ТекСтрока.Цена * ТекСтрока.Количество;
    КонецПроцедуры 
    
    <p>///// Документ Операция</p>   
    Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
      Если Не Движения.Управленческий.Записывать Тогда
        Движения.Управленческий.Прочитать();
        Движения.Управленческий.Записывать = Истина;
      КонецЕсли;
      
      Для Каждого Движение Из Движения.Управленческий Цикл
        Движение.Период = Дата;
        Движение.Активность = Не ПометкаУдаления;
      КонецЦикла;
    КонецПроцедуры
    
    Процедура ПриКопировании(ОбъектКопирования)	
      Выборка = РегистрыБухгалтерии.Управленческий.ВыбратьПоРегистратору(ОбъектКопирования.Ссылка);
      Пока Выборка.Следующий() Цикл
        Движение = Движения.Управленческий.Добавить();
        ЗаполнитьЗначенияСвойств(Движение, Выборка);
        ЗаполнитьЗначенияСвойств(Движение.СубконтоДт, Выборка.СубконтоДт);
        ЗаполнитьЗначенияСвойств(Движение.СубконтоКт, Выборка.СубконтоКт);
      КонецЦикла;
    КонецПроцедуры
     
    <p>///// Документ ПриходнаяНакладная</p>
    Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
      СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
    КонецПроцедуры
    
    Процедура ОбработкаПроведения(Отказ, РежимПроведения)
      
      Движения.Управленческий.Записывать = Истина;
      
      Запрос = Новый Запрос;
      
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
        |	СУММА(ПриходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
        |	СУММА(ПриходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма
        |ИЗ
        |	Документ.ПриходнаяНакладная.СписокНоменклатуры КАК ПриходнаяНакладнаяСписокНоменклатуры
        |ГДЕ
        |	ПриходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
        |
        |СГРУППИРОВАТЬ ПО
        |	ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура";
      
      Запрос.УстановитьПараметр("Ссылка", Ссылка);
      
      РезультатЗапроса = Запрос.Выполнить();
      
      Выборка = РезультатЗапроса.Выбрать();
      
      Пока Выборка.Следующий() Цикл
        
        Движение = Движения.Управленческий.Добавить();
        Движение.Период = Дата;
        Движение.Организация = Организация;
        
        Движение.СчетДт = ПланыСчетов.Управленческий.Товары;
        Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = Выборка.Номенклатура;
        Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Склады] = Склад;
        Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Партии] = Ссылка;
        Движение.КоличествоДт = Выборка.Количество;
        
        Движение.СчетКт = ПланыСчетов.Управленческий.Поставщики;
        
        Движение.Сумма = Выборка.Сумма;
        
      КонецЦикла;
    
      
    КонецПроцедуры
    
    <p>///// Документ РасходнаяНакладная</p> 
    Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
      СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
    КонецПроцедуры
    
    <p>///// Документ КупляПродажа</p> 
    Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
      СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
    КонецПроцедуры
    
    Процедура ОбработкаПроведения(Отказ, РежимПроведения)
      
      Движения.Управленческий.Записывать = Истина;
      Движения.Записать();
      
      Движения.Управленческий.Записывать = Истина;
      
      Блокировка = Новый БлокировкаДанных;
      ЭлементБлокировки = Блокировка.Добавить("РегистрБухгалтерии.Управленческий");
      ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
      ЭлементБлокировки.УстановитьЗначение("Счет", ПланыСчетов.Управленческий.Товары);
      ЭлементБлокировки.УстановитьЗначение("Организация", ОрганизацияПоставщик);
      ЭлементБлокировки.УстановитьЗначение(ПланыВидовХарактеристик.ВидыСубконто.Склады, СкладПоставщика);
      ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
      ЭлементБлокировки.ИспользоватьИзИсточникаДанных(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура, "Номенклатура");
      Блокировка.Заблокировать();
      
      Запрос = Новый Запрос;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	КупляПродажаСписокНоменклатуры.Номенклатура КАК Номенклатура,
        |	СУММА(КупляПродажаСписокНоменклатуры.Количество) КАК Количество,
        |	СУММА(КупляПродажаСписокНоменклатуры.Сумма) КАК Сумма
        |ПОМЕСТИТЬ втТЧТовары
        |ИЗ
        |	Документ.КупляПродажа.СписокНоменклатуры КАК КупляПродажаСписокНоменклатуры
        |ГДЕ
        |	КупляПродажаСписокНоменклатуры.Ссылка = &Ссылка
        |
        |СГРУППИРОВАТЬ ПО
        |	КупляПродажаСписокНоменклатуры.Номенклатура
        |
        |ИНДЕКСИРОВАТЬ ПО
        |	Номенклатура
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	втТЧТовары.Номенклатура КАК Номенклатура,
        |	втТЧТовары.Номенклатура.Представление КАК НоменклатураПредставление,
        |	втТЧТовары.Количество КАК Количество,
        |	УправленческийОстатки.Субконто3 КАК Партия,
        |	ЕСТЬNULL(УправленческийОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
        |	ЕСТЬNULL(УправленческийОстатки.СуммаОстаток, 0) КАК СуммаОстаток,
        |	втТЧТовары.Сумма КАК Сумма
        |ИЗ
        |	втТЧТовары КАК втТЧТовары
        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(
        |				&МоментВремени,
        |				Счет = &СчетТовары,
        |				&ВидыСубконто,
        |				Субконто1 В
        |						(ВЫБРАТЬ
        |							втТЧТовары.Номенклатура КАК Номенклатура
        |						ИЗ
        |							втТЧТовары КАК втТЧТовары)
        |					И Субконто2 = &СкладПоставщика
        |					И Организация = &Организация) КАК УправленческийОстатки
        |		ПО втТЧТовары.Номенклатура = УправленческийОстатки.Субконто1
        |
        |УПОРЯДОЧИТЬ ПО
        |	ВЫРАЗИТЬ(УправленческийОстатки.Субконто3 КАК Документ.ПриходнаяНакладная).МоментВремени
        |ИТОГИ
        |	МАКСИМУМ(Количество),
        |	СУММА(КоличествоОстаток),
        |	СУММА(СуммаОстаток),
        |	МАКСИМУМ(Сумма)
        |ПО
        |	Номенклатура";
      
      ВидыСубконто = Новый Массив;
      ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура);
      ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконто.Склады);
      ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконто.Партии);
      
      Запрос.УстановитьПараметр("ВидыСубконто", ВидыСубконто);
      Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
      Запрос.УстановитьПараметр("Организация", ОрганизацияПоставщик);
      Запрос.УстановитьПараметр("СкладПоставщика", СкладПоставщика);
      Запрос.УстановитьПараметр("Ссылка", Ссылка);
      Запрос.УстановитьПараметр("СчетТовары", ПланыСчетов.Управленческий.Товары);
      
      РезультатЗапроса = Запрос.Выполнить();
      
      ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
      
      Пока ВыборкаНоменклатура.Следующий() Цикл
        
        Если ВыборкаНоменклатура.Количество > ВыборкаНоменклатура.КоличествоОстаток Тогда
          Отказ = Истина;
          Сообщение = Новый СообщениеПользователю;
          Сообщение.Текст = СтрШаблон("Недостаточно товара %1 в количестве %2",
          ВыборкаНоменклатура.НоменклатураПредставление,
          ВыборкаНоменклатура.Количество - ВыборкаНоменклатура.КоличествоОстаток);
          Сообщение.Сообщить();
        КонецЕсли;
        
        Если Отказ Тогда
          Продолжить;
        КонецЕсли;
        
        КоличествоСписать = ВыборкаНоменклатура.Количество;
        
        ВыборкаДетальныеЗаписи = ВыборкаНоменклатура.Выбрать();
      
        Пока ВыборкаДетальныеЗаписи.Следующий() И КоличествоСписать > 0 Цикл
          
          Количество = Мин(КоличествоСписать, ВыборкаДетальныеЗаписи.КоличествоОстаток);
          Если Количество = ВыборкаДетальныеЗаписи.КоличествоОстаток Тогда
            Себестоимость = ВыборкаДетальныеЗаписи.СуммаОстаток;
          Иначе
            Себестоимость = ВыборкаДетальныеЗаписи.СуммаОстаток / ВыборкаДетальныеЗаписи.КоличествоОстаток * Количество;
          КонецЕсли;
          
          // списание себестоимости по организации-поставщику
          Движение = Движения.Управленческий.Добавить();
          Движение.Период = Дата;
          Движение.Организация = ОрганизацияПоставщик;
          
          Движение.СчетДт = ПланыСчетов.Управленческий.ПрибылиУбытки;
          Движение.СчетКт = ПланыСчетов.Управленческий.Товары;
          Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = ВыборкаДетальныеЗаписи.Номенклатура;
          Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Склады] = СкладПоставщика;
          Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Партии] = ВыборкаДетальныеЗаписи.Партия;
          Движение.КоличествоКт = Количество;
          
          Движение.Сумма = Себестоимость;
          
          КоличествоСписать = КоличествоСписать - Количество;
          
        КонецЦикла;
        
        // закупка организацией-покупателем
        Движение = Движения.Управленческий.Добавить();
        Движение.Период = Дата;
        Движение.Организация = ОрганизацияПокупатель;
        
        Движение.СчетДт = ПланыСчетов.Управленческий.Товары;
        Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = ВыборкаНоменклатура.Номенклатура;
        Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Склады] = СкладПокупателя;
        Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Партии] = Ссылка;
        Движение.КоличествоДт = ВыборкаНоменклатура.Количество;
        
        Движение.СчетКт = ПланыСчетов.Управленческий.Поставщики;
        Движение.Сумма = ВыборкаНоменклатура.Сумма;
        
      КонецЦикла;
      
      Если Не Отказ Тогда
        // продажа по организации-поставщику
        Движение = Движения.Управленческий.Добавить();
        Движение.Период = Дата;
        Движение.Организация = ОрганизацияПоставщик;
        Движение.СчетДт = ПланыСчетов.Управленческий.Покупатели;
        Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Покупатели] = ОрганизацияПокупатель;	
        Движение.СчетКт = ПланыСчетов.Управленческий.ПрибылиУбытки;
        Движение.Сумма = СуммаПоДокументу;
      КонецЕсли;
      
    КонецПроцедуры
    
    <p>///// Модуль формы</p>
    &НаКлиенте
    Процедура СписокНоменклатурыКоличествоПриИзменении(Элемент)
      ТекСтрока = Элементы.СписокНоменклатуры.ТекущиеДанные;
      ОбработкаТабличнойЧасти.РассчитатьСуммуВСтроке(ТекСтрока);
    КонецПроцедуры
    
    &НаКлиенте
    Процедура СписокНоменклатурыЦенаПриИзменении(Элемент)
      ТекСтрока = Элементы.СписокНоменклатуры.ТекущиеДанные;
      ОбработкаТабличнойЧасти.РассчитатьСуммуВСтроке(ТекСтрока);
    КонецПроцедуры
    <embed src="pdf/ОбщееБУ4.pdf" type="application/pdf" width="1200" height="800">
      <img src="pdf/БУ4.jpg">
    <H2 class="OU">
      Расчеты
    </H2>
    <p>///// Документ НачислениеЗарплаты</p>   
    Процедура ОбработкаПроведения(Отказ, РежимПроведения)
      
      ПериодРегистрации = НачалоМесяца(Дата);
      
      Движения.ОсновныеНачисления.Записывать = Истина;
      Для Каждого Строка Из ОсновныеНачисления Цикл
        Движение = Движения.ОсновныеНачисления.Добавить();
        ЗаполнитьЗначенияСвойств(Движение, Строка);
        Движение.ПериодДействияНачало = Строка.ДатаНачала;
        Движение.ПериодДействияКонец = КонецДня(Строка.ДатаОкончания);
        Движение.ПериодРегистрации = ПериодРегистрации;
      КонецЦикла;
      
      Движения.ДополнительныеНачисления.Записывать = Истина;
      Для Каждого Строка Из ДополнительныеНачисления Цикл
        Движение = Движения.ДополнительныеНачисления.Добавить();
        ЗаполнитьЗначенияСвойств(Движение, Строка);
        Движение.ПериодРегистрации = ПериодРегистрации;
        Движение.БазовыйПериодНачало = ДобавитьМесяц(ПериодРегистрации, -1);
        Движение.БазовыйПериодКонец  = ПериодРегистрации - 1;
      КонецЦикла;
      
      Движения.Записать();
      
      Запрос = Новый Запрос;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	ОсновныеНачисления.ВидРасчета.СпособРасчета КАК СпособРасчета,
        |	ОсновныеНачисления.ВидРасчета.Приоритет КАК Приоритет
        |ИЗ
        |	РегистрРасчета.ОсновныеНачисления КАК ОсновныеНачисления
        |ГДЕ
        |	ОсновныеНачисления.Регистратор = &Регистратор
        |
        |ОБЪЕДИНИТЬ
        |
        |ВЫБРАТЬ
        |	ДополнительныеНачисления.ВидРасчета.СпособРасчета,
        |	ДополнительныеНачисления.ВидРасчета.Приоритет
        |ИЗ
        |	РегистрРасчета.ДополнительныеНачисления КАК ДополнительныеНачисления
        |ГДЕ
        |	ДополнительныеНачисления.Регистратор = &Регистратор
        |
        |УПОРЯДОЧИТЬ ПО
        |	Приоритет";
      
      Запрос.УстановитьПараметр("Регистратор", Ссылка);
      
      РезультатЗапроса = Запрос.Выполнить();
      
      ВыборкаПоПриоритетам = РезультатЗапроса.Выбрать();
      
      Пока ВыборкаПоПриоритетам.Следующий() Цикл
        
        Если ВыборкаПоПриоритетам.СпособРасчета = Перечисления.СпособыРасчета.ОплатаПоЧасовомуТарифу Тогда
          
          Запрос = Новый Запрос;
          Запрос.Текст = 
          "ВЫБРАТЬ
          |	ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
          |	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ЗначениеФактическийПериодДействия, 0) КАК ОтработаноЧасов,
          |	ОсновныеНачисленияДанныеГрафика.ГрафикРаботы КАК ГрафикРаботы,
          |	ОсновныеНачисленияДанныеГрафика.Сотрудник КАК Сотрудник
          |ПОМЕСТИТЬ втНачисления
          |ИЗ
          |	РегистрРасчета.ОсновныеНачисления.ДанныеГрафика(
          |			Регистратор = &Регистратор
          |				И ВидРасчета.СпособРасчета = &СпособРасчета) КАК ОсновныеНачисленияДанныеГрафика
          |
          |ИНДЕКСИРОВАТЬ ПО
          |	ГрафикРаботы
          |;
          |
          |////////////////////////////////////////////////////////////////////////////////
          |ВЫБРАТЬ
          |	втНачисления.НомерСтроки КАК НомерСтроки,
          |	втНачисления.ОтработаноЧасов КАК ОтработаноЧасов,
          |	ЕСТЬNULL(ТарифныеСтавкиСрезПоследних.ТарифнаяСтавка, 0) КАК ТарифнаяСтавка
          |ИЗ
          |	втНачисления КАК втНачисления
          |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТарифныеСтавки.СрезПоследних(
          |				&ПериодРегистрации,
          |				ГрафикРаботы В
          |					(ВЫБРАТЬ
          |						втНачисления.ГрафикРаботы КАК ГрафикРаботы
          |					ИЗ
          |						втНачисления КАК втНачисления)) КАК ТарифныеСтавкиСрезПоследних
          |		ПО втНачисления.ГрафикРаботы = ТарифныеСтавкиСрезПоследних.ГрафикРаботы";
          
          Запрос.УстановитьПараметр("СпособРасчета", ВыборкаПоПриоритетам.СпособРасчета);
          Запрос.УстановитьПараметр("Регистратор", Ссылка);
          Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
          
          РезультатЗапроса = Запрос.Выполнить();
          
          Выборка = РезультатЗапроса.Выбрать();
          
          Для Каждого Движение Из Движения.ОсновныеНачисления Цикл
            
            Выборка.Сбросить();
            Если Выборка.НайтиСледующий(Движение.НомерСтроки, "НомерСтроки") Тогда	
              Движение.Результат = Выборка.ОтработаноЧасов * Выборка.ТарифнаяСтавка;
              ЗаполнитьЗначенияСвойств(Движение, Выборка);
            КонецЕсли;
            
          КонецЦикла;
          
          Движения.ОсновныеНачисления.Записать(, Истина);
          
        ИначеЕсли ВыборкаПоПриоритетам.СпособРасчета = Перечисления.СпособыРасчета.ПроцентОтОплатыПодчиненныхЗаПрошлыйМесяц Тогда
          
          Запрос = Новый Запрос;
          Запрос.Текст = 
          "ВЫБРАТЬ
          |	ДополнительныеНачисленияБазаОсновныеНачисления.НомерСтроки КАК НомерСтроки,
          |	СУММА(ДополнительныеНачисленияБазаОсновныеНачисления.РезультатБаза) КАК БазаНачислений
          |ИЗ
          |	РегистрРасчета.ДополнительныеНачисления.БазаОсновныеНачисления(&Измерения, &Измерения, &Разрезы, Регистратор = &Регистратор И ВидРасчета.СпособРасчета = &СпособРасчета) КАК ДополнительныеНачисленияБазаОсновныеНачисления
          |ГДЕ
          |	ДополнительныеНачисленияБазаОсновныеНачисления.Сотрудник <> ДополнительныеНачисленияБазаОсновныеНачисления.СотрудникРазрез
          |
          |СГРУППИРОВАТЬ ПО
          |	ДополнительныеНачисленияБазаОсновныеНачисления.НомерСтроки";
          
          Измерения = Новый Массив;
          Измерения.Добавить("Подразделение");	
          Запрос.УстановитьПараметр("Измерения", Измерения);
          
          Разрезы = Новый Массив;
          Разрезы.Добавить("Сотрудник");
          Запрос.УстановитьПараметр("Разрезы", Разрезы);
          
          Запрос.УстановитьПараметр("Регистратор", Ссылка);
          Запрос.УстановитьПараметр("СпособРасчета", ВыборкаПоПриоритетам.СпособРасчета);
          
          РезультатЗапроса = Запрос.Выполнить();
          
          Выборка = РезультатЗапроса.Выбрать();
          
          Для Каждого Движение Из Движения.ДополнительныеНачисления Цикл
            
            Выборка.Сбросить();
            Если Выборка.НайтиСледующий(Движение.НомерСтроки, "НомерСтроки") Тогда
              Движение.Результат = Выборка.БазаНачислений * Движение.Параметр / 100;
              ЗаполнитьЗначенияСвойств(Движение, Выборка);
            КонецЕсли;
            
          КонецЦикла;
          
          Движения.ДополнительныеНачисления.Записать();						
          
        КонецЕсли;
        
      КонецЦикла;
      
    КонецПроцедуры
    
    Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
      Если ОсновныеНачисления.Количество() = 0 И ДополнительныеНачисления.Количество() = 0 Тогда
        Сообщение = Новый СообщениеПользователю;
        Сообщение.Текст = "Документ не заполнен!";
        Сообщение.Сообщить();
        Отказ = Истина;
      КонецЕсли;
    КонецПроцедуры
    
    <p>///// Модуль объекта ЗаполнениеГрафика</p>
    Процедура ЗаполнитьГрафик(ДатаНачала, ДатаОкончания, ВыходныеДни, ГрафикРаботы) Экспорт 
      
      Набор = РегистрыСведений.ГрафикиРаботы.СоздатьНаборЗаписей();
      
      Набор.Отбор.ГрафикРаботы.Установить(ГрафикРаботы);
      
      ЧислоСекундВСутках = 86400;
      
      Дат = ДатаНачала;
      Шаг = 1;
      Пока Дат <= ДатаОкончания Цикл
        Запись = Набор.Добавить();
        Запись.ГрафикРаботы = ГрафикРаботы;
        Запись.Дата = Дат;
        
        Если Шаг = 1 Тогда
          Запись.Значение = 24;
        Иначе
          Запись.Значение = 0;
        КонецЕсли;
        
        Если Шаг = 3 Тогда
          Шаг = 1;
        Иначе
          Шаг = Шаг + 1;
        КонецЕсли;
        
        Дат = Дат + ЧислоСекундВСутках;
      КонецЦикла;                          
      
      Набор.Записать();
    КонецПроцедуры
    
    <p>///// Модуль формы</p>
    &НаКлиенте
    Процедура Заполнить(Команда)
      ВыполнитьОбработку();
      Сообщить("Обработка завершена!");
    КонецПроцедуры
    
    &НаСервере
    Процедура ВыполнитьОбработку()
      ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
        ОбработкаОбъект.ЗаполнитьГрафик(ВыборПериода.ДатаНачала, ВыборПериода.ДатаОкончания, ВыходныеДни, ГрафикРаботы);
    КонецПроцедуры 
    <embed src="pdf/ОбщееСПР4.pdf" type="application/pdf" width="1200" height="800">
      <img src="pdf/СПР4.jpg">
  </pre>
  <H2 class="OU">
    Бизнес процесс
  </H2>
  <pre>
    <p>//// Модуль сеанса</p>
Процедура УстановкаПараметровСеанса(ТребуемыеПараметры)
	
	Имя = ПользователиИнформационнойБазы.ТекущийПользователь().Имя;
	
	ФизЛицо = Справочники.ФизическиеЛица.НайтиПоКоду(Имя);
	Если ФизЛицо.Пустая() Тогда
		ФизЛицоОбъект = Справочники.ФизическиеЛица.СоздатьЭлемент();
		ФизЛицоОбъект.Наименование = Имя;
		ФизЛицоОбъект.Код = Имя;
		ФизЛицоОбъект.Записать();
		ФизЛицо = ФизЛицоОбъект.Ссылка;
	КонецЕсли;

	ПараметрыСеанса.ТекущийПользователь = ФизЛицо;
		
КонецПроцедуры

<p>//// ОбщегоНазначенияВызовСервера</p>
Функция ТекущийПользователь() Экспорт
	
	Возврат ПараметрыСеанса.ТекущийПользователь;
		
КонецФункции

<p>//// ФизическиеЛица ФормаЭлемента</p>
&НаКлиенте
Процедура АдресКартинкиНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;	
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.Фильтр = "Картинки JPEG|*.jpg|Все файлы|*.*";
	Диалог.Заголовок = "Выберите картинку:";
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыборФайлаЗавершение", ЭтотОбъект);
	Диалог.Показать(ОписаниеОповещения);	
КонецПроцедуры

&НаКлиенте
Процедура ВыборФайлаЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт

	Если ВыбранныеФайлы <> Неопределено И ВыбранныеФайлы.Количество() > 0 Тогда
		ПолноеИмяФайла = ВыбранныеФайлы[0];		
		ОписаниеОповещения = Новый ОписаниеОповещения("ПомещениеФайлаЗавершение", ЭтотОбъект);
		НачатьПомещениеФайлаНаСервер(ОписаниеОповещения,,,, ПолноеИмяФайла, УникальныйИдентификатор);				
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПомещениеФайлаЗавершение(ОписаниеПомещенногоФайла, ДополнительныеПараметры) Экспорт	
	Если ОписаниеПомещенногоФайла <> Неопределено Тогда
		ДополнительныеПараметры = Новый Структура("ОписаниеПомещенногоФайла", ОписаниеПомещенногоФайла);
		ОписаниеОповещения = Новый ОписаниеОповещения("ВопросЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьВопрос(ОписаниеОповещения, "Вы хотите записать картинку в базу данных?", РежимДиалогаВопрос.ДаНетОтмена);
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура ВопросЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ЗаписыватьКартинкуВБазу = Истина;
		АдресКартинки = ДополнительныеПараметры.ОписаниеПомещенногоФайла.Адрес;
		Объект.ПутьККартинке = "";
	ИначеЕсли РезультатВопроса = КодВозвратаДиалога.Нет Тогда
		Объект.ПутьККартинке = ДополнительныеПараметры.ОписаниеПомещенногоФайла.СсылкаНаФайл.Файл.ПолноеИмя;
		АдресКартинки = ДополнительныеПараметры.ОписаниеПомещенногоФайла.Адрес;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Если ЗаписыватьКартинкуВБазу Тогда
		ТекущийОбъект.Картинка = Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(АдресКартинки));
		ЗаписыватьКартинкуВБазу = Ложь;	
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	Если Объект.ПутьККартинке = "" Тогда
		АдресКартинки = ПоместитьВоВременноеХранилище(ТекущийОбъект.Картинка.Получить());
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЗаписыватьКартинкуВБазу = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если Объект.ПутьККартинке <> "" Тогда
		Попытка
			НачатьПомещениеФайлаНаСервер(Новый ОписаниеОповещения("ПоказатьКартинкуПоПолномуПутиЗавершение", ЭтотОбъект),,,, Объект.ПутьККартинке, УникальныйИдентификатор);
		Исключение
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
			Сообщение.Сообщить();			
		КонецПопытки;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьКартинкуПоПолномуПутиЗавершение(ОписаниеПомещенногоФайла, ДополнительныеПараметры) Экспорт
	Если ОписаниеПомещенногоФайла <> Неопределено Тогда
		АдресКартинки = ОписаниеПомещенногоФайла.Адрес;	
	КонецЕсли;
КонецПроцедуры
  </pre>
  <embed src="pdf/ОбщееБП4.pdf" type="application/pdf" width="1200" height="800">
</body>

</html>