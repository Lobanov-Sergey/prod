<!doctype html>
<html lang="ru">

<head>
  <meta charset="utf-8">
  <title>HTML Document Template</title>
  <link href="styles.css" rel="stylesheet">
</head>

<body>
  <div class="header__menu">
    <ul>
      <li><a href="1">1</a></li>
      <li><a href="2">2</a></li>
      <li><a href="3">3</a></li>
      <li><a href="4">4</a></li>
      <li><a href="5">5</a></li>
      <li><a href="6">6</a></li>
      <li><a href="7">7</a></li>
      <li><a href="8">8</a></li>
      <li><a href="9">9</a></li>
      <li><a href="10">10</a></li>
      <li><a href="11">11</a></li>
      <li><a href="12">12</a></li>
      <li><a href="13">13</a></li>
      <li><a href="14">14</a></li>
      <li><a href="15">15</a></li>
      <li><a href="16">16</a></li>
    </ul>
  </div>
  <h1>16</h1>
  <H2 class="OU">
    Оперативный учет
  </H2>
  <pre>
    <p>///// Общий модуль ОбработкаТабличнойЧасти</p>
    Процедура РассчитатьСуммуВСтроке(ТекСтрока) Экспорт
      ТекСтрока.Сумма = ТекСтрока.Цена * ТекСтрока.Количество;
    КонецПроцедуры 
    
    <p>///// Документ ПриходДенег</p>   
    Процедура ОбработкаПроведения(Отказ, РежимПроведения)
      
      Движения.Взаиморасчеты.Записывать = Истина;
      Движения.Записать();
      
      Движения.Взаиморасчеты.Записывать = Истина;
      
      Блокировка = Новый БлокировкаДанных;
      ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.Взаиморасчеты");
      ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
      ЭлементБлокировки.УстановитьЗначение("Контрагент", Контрагент);
      Блокировка.Заблокировать();
      
      Запрос = Новый Запрос;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	ВзаиморасчетыОстатки.Контрагент КАК Контрагент,
        |	ВзаиморасчетыОстатки.Накладная КАК Накладная,
        |	ЕСТЬNULL(ВзаиморасчетыОстатки.СуммаОстаток, 0) КАК СуммаОстаток,
        |	&Период КАК Период
        |ИЗ
        |	РегистрНакопления.Взаиморасчеты.Остатки(&МоментВремени, Контрагент = &Контрагент) КАК ВзаиморасчетыОстатки
        |
        |УПОРЯДОЧИТЬ ПО
        |	ВзаиморасчетыОстатки.Накладная.МоментВремени
        |ИТОГИ
        |	СУММА(СуммаОстаток)
        |ПО
        |	ОБЩИЕ";
      
      Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
      Запрос.УстановитьПараметр("Период", Дата);
      Запрос.УстановитьПараметр("Ссылка", Ссылка);
      Запрос.УстановитьПараметр("Контрагент", Контрагент);
      
      РезультатЗапроса = Запрос.Выполнить();
          
      ВыборкаОбщийИтог = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
      
      ВыборкаОбщийИтог.Следующий();
      
      ДолгПоВсемНакладным = ВыборкаОбщийИтог.СуммаОстаток;
      ДолгПоВсемНакладным = ?(ДолгПоВсемНакладным = Неопределено, 0, ДолгПоВсемНакладным);
      
      Если СуммаОплаты > ДолгПоВсемНакладным Тогда
        Отказ = Истина;
        
        Сообщение = Новый СообщениеПользователю;
        Сообщение.Текст = СтрШаблон("Сумма оплаты не должна превышать суммарный долг по накладным (он равен %1)",
        ДолгПоВсемНакладным);
        Сообщение.Сообщить();
        
        Возврат;
      КонецЕсли;
      
      СуммаКОплате = СуммаОплаты;
      
      ВыборкаДетальныеЗаписи = ВыборкаОбщийИтог.Выбрать();
      
      Пока ВыборкаДетальныеЗаписи.Следующий() И СуммаКОплате > 0 Цикл
        
        Сумма = Мин(СуммаКОплате, ВыборкаДетальныеЗаписи.СуммаОстаток);
        
        Движение = Движения.Взаиморасчеты.ДобавитьРасход();
        ЗаполнитьЗначенияСвойств(Движение, ВыборкаДетальныеЗаписи);
        Движение.Сумма = Сумма;
        
        СуммаКОплате = СуммаКОплате - Сумма;
        
      КонецЦикла;
      
    КонецПроцедуры
    
    <p>///// Документ ПриходнаяНакладная</p>
    Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
      СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
    КонецПроцедуры
    
    Процедура ОбработкаПроведения(Отказ, Режим)
    
      Движения.ОстаткиНоменклатуры.Записывать = Истина;
      
      Запрос = Новый Запрос;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
        |	СУММА(ПриходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество
        |ИЗ
        |	Документ.ПриходнаяНакладная.СписокНоменклатуры КАК ПриходнаяНакладнаяСписокНоменклатуры
        |ГДЕ
        |	ПриходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
        |
        |СГРУППИРОВАТЬ ПО
        |	ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура";
      
      Запрос.УстановитьПараметр("Ссылка", Ссылка);
      
      РезультатЗапроса = Запрос.Выполнить();
      
      Выборка = РезультатЗапроса.Выбрать();
      
      Пока Выборка.Следующий() Цикл
        Движение = Движения.ОстаткиНоменклатуры.Добавить();
        Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
        Движение.Период = Дата;
        Движение.Номенклатура = Выборка.Номенклатура;
        Движение.Количество = Выборка.Количество;		
      КонецЦикла;
       
    КонецПроцедуры
    
    <p>///// Документ РасходнаяНакладная</p> 
    Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
      СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
    КонецПроцедуры
    
    Процедура ОбработкаПроведения(Отказ, РежимПроведения)
        
      Движения.Взаиморасчеты.Записывать = Истина;
      Движения.Записать();
      
      Движения.Взаиморасчеты.Записывать = Истина;
      
      Кредиты = РегистрыСведений.КредитыПокупателям.ПолучитьПоследнее(Дата, Новый Структура("Контрагент", Контрагент));
      
      Блокировка = Новый БлокировкаДанных;
      ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.Взаиморасчеты");
      ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
      ЭлементБлокировки.УстановитьЗначение("Контрагент", Контрагент);
      Блокировка.Заблокировать();
        
      Запрос = Новый Запрос;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	ЕСТЬNULL(СУММА(ВзаиморасчетыОстатки.СуммаОстаток), 0) КАК ДолгКлиента,
        |	ЕСТЬNULL(РАЗНОСТЬДАТ(МИНИМУМ(ВзаиморасчетыОстатки.Накладная.Дата), &Период, ДЕНЬ), 0) КАК КоличествоДнейДолга
        |ИЗ
        |	РегистрНакопления.Взаиморасчеты.Остатки(&МоментВремени, Контрагент = &Контрагент) КАК ВзаиморасчетыОстатки";
      
      Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
      Запрос.УстановитьПараметр("Контрагент", Контрагент);
      Запрос.УстановитьПараметр("Период", Дата);
      
      РезультатЗапроса = Запрос.Выполнить();
      Выборка = РезультатЗапроса.Выбрать();
      Выборка.Следующий();
      
      Если Выборка.ДолгКлиента + СуммаПоДокументу > Кредиты.Сумма Тогда
        Сообщение = Новый СообщениеПользователю;
        Сообщение.Текст = "Превышение над разрешенной суммой кредита на " + (Выборка.ДолгКлиента + СуммаПоДокументу - Кредиты.Сумма) + " рублей";
        Сообщение.Сообщить();
        
        Отказ = Истина;
      КонецЕсли;
      
      Если Выборка.КоличествоДнейДолга > Кредиты.СрокПредоставления Тогда
        Сообщение = Новый СообщениеПользователю;
        Сообщение.Текст = "Превышение срока кредита на " + (Выборка.КоличествоДнейДолга - Кредиты.СрокПредоставления) + " дней";
        Сообщение.Сообщить();
        
        Отказ = Истина;
      КонецЕсли;
      
      Если Не Отказ Тогда
        Движение = Движения.Взаиморасчеты.ДобавитьПриход();
        Движение.Период = Дата;
        Движение.Контрагент = Контрагент;
        Движение.Накладная = Ссылка;
        Движение.Сумма = СуммаПоДокументу;
      КонецЕсли;
      
    КонецПроцедуры
    
    <p>///// Модуль формы</p>
    &НаКлиенте
    Процедура СписокНоменклатурыКоличествоПриИзменении(Элемент)
      ТекСтрока = Элементы.СписокНоменклатуры.ТекущиеДанные;
      ОбработкаТабличнойЧасти.РассчитатьСуммуВСтроке(ТекСтрока);
    КонецПроцедуры
    
    &НаКлиенте
    Процедура СписокНоменклатурыЦенаПриИзменении(Элемент)
      ТекСтрока = Элементы.СписокНоменклатуры.ТекущиеДанные;
      ОбработкаТабличнойЧасти.РассчитатьСуммуВСтроке(ТекСтрока);
    КонецПроцедуры
    <embed src="pdf/ОбщееОУ16.pdf" type="application/pdf" width="1200" height="800">
      <img src="pdf/ОУ16.jpg">
    <H2 class="OU">
      Бухгалтерский учет
    </H2>
    <p>///// Общий модуль ОбработкаТабличнойЧастиКлиент</p>
    Процедура РассчитатьСуммуВСтроке(ТекСтрока) Экспорт
      ТекСтрока.Сумма = ТекСтрока.Цена * ТекСтрока.Количество;
    КонецПроцедуры 
    
    <p>///// Документ Операция</p>   
    Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
      Если Не Движения.Управленческий.Записывать Тогда
        Движения.Управленческий.Прочитать();
        Движения.Управленческий.Записывать = Истина;
      КонецЕсли;
      
      Для Каждого Движение Из Движения.Управленческий Цикл
        Движение.Период = Дата;
        Движение.Активность = Не ПометкаУдаления;
      КонецЦикла;
    КонецПроцедуры
    
    Процедура ПриКопировании(ОбъектКопирования)	
      Выборка = РегистрыБухгалтерии.Управленческий.ВыбратьПоРегистратору(ОбъектКопирования.Ссылка);
      Пока Выборка.Следующий() Цикл
        Движение = Движения.Управленческий.Добавить();
        ЗаполнитьЗначенияСвойств(Движение, Выборка);
        ЗаполнитьЗначенияСвойств(Движение.СубконтоДт, Выборка.СубконтоДт);
        ЗаполнитьЗначенияСвойств(Движение.СубконтоКт, Выборка.СубконтоКт);
      КонецЦикла;
    КонецПроцедуры
     
    <p>///// Документ ПриходнаяНакладная</p>
    Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
      СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
    КонецПроцедуры
    
    Процедура ОбработкаПроведения(Отказ, РежимПроведения)
      
      Движения.Управленческий.Записывать = Истина;
      
      Запрос = Новый Запрос;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
        |	СУММА(ПриходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
        |	СУММА(ПриходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма
        |ИЗ
        |	Документ.ПриходнаяНакладная.СписокНоменклатуры КАК ПриходнаяНакладнаяСписокНоменклатуры
        |ГДЕ
        |	ПриходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
        |
        |СГРУППИРОВАТЬ ПО
        |	ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура";
      
      Запрос.УстановитьПараметр("Ссылка", Ссылка);
      
      РезультатЗапроса = Запрос.Выполнить();
      
      Выборка = РезультатЗапроса.Выбрать();
      
      Пока Выборка.Следующий() Цикл
        
        Движение = Движения.Управленческий.Добавить();
        Движение.Период = Дата;
        
        Движение.СчетДт = ПланыСчетов.Управленческий.Товары;
        Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = Выборка.Номенклатура;
        Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Партии] = Ссылка;
        Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.СостоянияТоваров] = Перечисления.СостоянияТоваров.Неиспорченный;
        Движение.КоличествоДт = Выборка.Количество;
        
        Движение.СчетКт = ПланыСчетов.Управленческий.Поставщики;
        
        Движение.Сумма = Выборка.Сумма;
        
      КонецЦикла;	
    КонецПроцедуры
    
    <p>///// Документ РасходнаяНакладная</p> 
    Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
      СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
    КонецПроцедуры
    
    Процедура ОбработкаПроведения(Отказ, РежимПроведения)
      
      МетодСписанияСебестоимости = РегистрыСведений.УчетнаяПолитика.ПолучитьПоследнее(Дата).МетодСписанияСебестоимости;
      Если МетодСписанияСебестоимости.Пустая() Тогда
        Отказ = Истина;
        Сообщение = Новый СообщениеПользователю;
        Сообщение.Текст = "Не указана учетная политика";
        Сообщение.Сообщить();		
        Возврат;
      КонецЕсли;
      
      Движения.Управленческий.Записывать = Истина;
      Движения.Записать();
      
      Движения.Управленческий.Записывать = Истина;
      
      Блокировка = Новый БлокировкаДанных;
      ЭлементБлокировки = Блокировка.Добавить("РегистрБухгалтерии.Управленческий");
      ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
      ЭлементБлокировки.УстановитьЗначение("Счет", ПланыСчетов.Управленческий.Товары);
      ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
      ЭлементБлокировки.ИспользоватьИзИсточникаДанных(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура, "Номенклатура");
      Блокировка.Заблокировать();
      
      Запрос = Новый Запрос;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
        |	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
        |	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма
        |ПОМЕСТИТЬ втТЧТовары
        |ИЗ
        |	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
        |ГДЕ
        |	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
        |
        |СГРУППИРОВАТЬ ПО
        |	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура
        |
        |ИНДЕКСИРОВАТЬ ПО
        |	Номенклатура
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	втТЧТовары.Номенклатура КАК Номенклатура,
        |	втТЧТовары.Номенклатура.Представление КАК НоменклатураПредставление,
        |	втТЧТовары.Количество КАК Количество,
        |	УправленческийОстатки.Субконто2 КАК Партия,
        |	ЕСТЬNULL(УправленческийОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
        |	ЕСТЬNULL(УправленческийОстатки.СуммаОстаток, 0) КАК СуммаОстаток,
        |	втТЧТовары.Сумма КАК Сумма
        |ИЗ
        |	втТЧТовары КАК втТЧТовары
        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(
        |				&МоментВремени,
        |				Счет = &СчетТовары,
        |				&ВидыСубконто,
        |				Субконто1 В
        |						(ВЫБРАТЬ
        |							втТЧТовары.Номенклатура КАК Номенклатура
        |						ИЗ
        |							втТЧТовары КАК втТЧТовары)
        |					И Субконто3 = ЗНАЧЕНИЕ(Перечисление.СостоянияТоваров.Неиспорченный)) КАК УправленческийОстатки
        |		ПО втТЧТовары.Номенклатура = УправленческийОстатки.Субконто1
        |
        |УПОРЯДОЧИТЬ ПО
        |	ВЫРАЗИТЬ(УправленческийОстатки.Субконто2 КАК Документ.ПриходнаяНакладная).МоментВремени УБЫВ
        |ИТОГИ
        |	МАКСИМУМ(Количество),
        |	СУММА(КоличествоОстаток),
        |	МАКСИМУМ(Сумма)
        |ПО
        |	Номенклатура";
      
      Если МетодСписанияСебестоимости = Перечисления.УчетнаяПолитика.ФИФО Тогда
        Запрос.Текст = СтрЗаменить(Запрос.Текст, "УБЫВ", "ВОЗР");
      КонецЕсли;
      
      ВидыСубконто = Новый Массив;
      ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура);
      ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконто.Партии);
      ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконто.СостоянияТоваров);
      
      Запрос.УстановитьПараметр("ВидыСубконто", ВидыСубконто);
      Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
      Запрос.УстановитьПараметр("Ссылка", Ссылка);
      Запрос.УстановитьПараметр("СчетТовары", ПланыСчетов.Управленческий.Товары);
      
      РезультатЗапроса = Запрос.Выполнить();
      
      ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
      
      Пока ВыборкаНоменклатура.Следующий() Цикл
        
        Если ВыборкаНоменклатура.Количество > ВыборкаНоменклатура.КоличествоОстаток Тогда
          Отказ = Истина;
          
          Сообщение = Новый СообщениеПользователю;
          Сообщение.Текст = СтрШаблон("Недостаточно товара %1 в количестве %2",
          ВыборкаНоменклатура.НоменклатураПредставление, 
          ВыборкаНоменклатура.Количество - ВыборкаНоменклатура.КоличествоОстаток);
          Сообщение.Сообщить();
          
        КонецЕсли;
        
        Если Отказ Тогда
          Продолжить;
        КонецЕсли;
        
        КоличествоСписать = ВыборкаНоменклатура.Количество;
        
        ВыборкаДетальныеЗаписи = ВыборкаНоменклатура.Выбрать();
      
        Пока ВыборкаДетальныеЗаписи.Следующий() И КоличествоСписать > 0 Цикл
          
          Количество = Мин(КоличествоСписать, ВыборкаДетальныеЗаписи.КоличествоОстаток);
          
          Если Количество = ВыборкаДетальныеЗаписи.КоличествоОстаток Тогда
            Себестоимость = ВыборкаДетальныеЗаписи.СуммаОстаток;
          Иначе
            Себестоимость = ВыборкаДетальныеЗаписи.СуммаОстаток / ВыборкаДетальныеЗаписи.КоличествоОстаток * Количество;
          КонецЕсли;
          
          Движение = Движения.Управленческий.Добавить();
          Движение.Период = Дата;
          
          Движение.СчетДт = ПланыСчетов.Управленческий.ПрибылиУбытки;
          Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = ВыборкаНоменклатура.Номенклатура;
          
          Движение.СчетКт = ПланыСчетов.Управленческий.Товары;
          Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = ВыборкаДетальныеЗаписи.Номенклатура;
          Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Партии] = ВыборкаДетальныеЗаписи.Партия;
          Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.СостоянияТоваров] = Перечисления.СостоянияТоваров.Неиспорченный;
          Движение.КоличествоКт = Количество;
          
          Движение.Сумма = Себестоимость;
          
          КоличествоСписать = КоличествоСписать - Количество;
          
        КонецЦикла;
        
        Движение = Движения.Управленческий.Добавить();
        Движение.Период = Дата;	
        Движение.СчетДт = ПланыСчетов.Управленческий.Покупатели;
        Движение.СчетКт = ПланыСчетов.Управленческий.ПрибылиУбытки;
        Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = ВыборкаНоменклатура.Номенклатура;		
        Движение.Сумма = ВыборкаНоменклатура.Сумма;		
        
      КонецЦикла;
    
      
    КонецПроцедуры
    
    <p>///// Модуль формы</p>
    &НаКлиенте
    Процедура СписокНоменклатурыКоличествоПриИзменении(Элемент)
      ТекСтрока = Элементы.СписокНоменклатуры.ТекущиеДанные;
      ОбработкаТабличнойЧасти.РассчитатьСуммуВСтроке(ТекСтрока);
    КонецПроцедуры
    
    &НаКлиенте
    Процедура СписокНоменклатурыЦенаПриИзменении(Элемент)
      ТекСтрока = Элементы.СписокНоменклатуры.ТекущиеДанные;
      ОбработкаТабличнойЧасти.РассчитатьСуммуВСтроке(ТекСтрока);
    КонецПроцедуры
    <embed src="pdf/ОбщееБУ16.pdf" type="application/pdf" width="1200" height="800">
      <img src="pdf/БУ16.jpg">
    <H2 class="OU">
      Расчеты
    </H2>
    <p>///// Общий Модуль ПроведениеРасчетов</p>  
    Процедура РассчитатьОсновныеНачисления(НаборЗаписей, Регистратор) Экспорт
      
      Запрос = Новый Запрос;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
        |	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ЧасовФактическийПериодДействия, 0) КАК ОтработаноЧасов,
        |	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ДнейФактическийПериодДействия, 0) КАК ОтработаноДней,
        |	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ЧасовПериодДействия, 0) КАК НормаЧасов
        |ИЗ
        |	РегистрРасчета.ОсновныеНачисления.ДанныеГрафика(
        |			Регистратор = &Регистратор
        |				И ВидРасчета В (&ВидыРасчета)) КАК ОсновныеНачисленияДанныеГрафика";
    
      ВидыРасчета = Новый Массив;
      ВидыРасчета.Добавить(ПланыВидовРасчета.ОсновныеНачисления.Оклад);
      
      Запрос.УстановитьПараметр("ВидыРасчета", ВидыРасчета);
      
      Запрос.УстановитьПараметр("Регистратор", Регистратор);
      
      РезультатЗапроса = Запрос.Выполнить();
      
      Выборка = РезультатЗапроса.Выбрать();
      
      Для Каждого Движение Из НаборЗаписей Цикл
    
        Если ВидыРасчета.Найти(Движение.ВидРасчета) = Неопределено Тогда
          Продолжить;
        КонецЕсли;
        
        Выборка.Сбросить();
        Выборка.НайтиСледующий(Движение.НомерСтроки, "НомерСтроки");
        
        Если Движение.ВидРасчета = ПланыВидовРасчета.ОсновныеНачисления.Оклад Тогда
          Движение.Результат = ?(Выборка.НормаЧасов <> 0,
                       Движение.Параметр / Выборка.НормаЧасов * Выборка.ОтработаноЧасов,
                       0);
          ЗаполнитьЗначенияСвойств(Движение, Выборка, "НормаЧасов, ОтработаноЧасов, ОтработаноДней");
        КонецЕсли;
        
      КонецЦикла;
      
      НаборЗаписей.Записать(, Истина);	
      
    КонецПроцедуры
    
    Процедура РассчитатьДополнительныеНачисления(НаборЗаписей, Регистратор, ПериодРегистрации, ТаблицаВидовРасчета) Экспорт
    
      ЭтоПерерасчеты = ТаблицаВидовРасчета.Колонки.Найти("ТаблицаОтборов") <> Неопределено;
      
      ВидыРасчетаКомпенсации = Новый Массив;
      ВидыРасчетаКомпенсации.Добавить(ПланыВидовРасчета.ДополнительныеНачисления.КомпенсацияЗаМобильный);
      ВидыРасчетаКомпенсации.Добавить(ПланыВидовРасчета.ДополнительныеНачисления.КомпенсацияЗаСтационарный);
      
      Для Каждого СтрокаВидовРасчета Из ТаблицаВидовРасчета Цикл
        
        Если ВидыРасчетаКомпенсации.Найти(СтрокаВидовРасчета.ВидРасчета) <> Неопределено Тогда 
        
          Запрос = Новый Запрос;
          Запрос.Текст = 
            "ВЫБРАТЬ
            |	ДополнительныеНачисления.НомерСтроки КАК НомерСтроки,
            |	ЕСТЬNULL(ДополнительныеНачисленияБазаОсновныеНачисления.ОтработаноДнейБаза, 0) КАК ОтработаноДней,
            |	ЕСТЬNULL(ПоказателиРасчетаЗарплатыСрезПоследних.СтавкаКомпенсации, 0) КАК СтавкаКомпенсации
            |ИЗ
            |	РегистрРасчета.ДополнительныеНачисления КАК ДополнительныеНачисления
            |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ДополнительныеНачисления.БазаОсновныеНачисления(
            |				&Измерения,
            |				&Измерения,
            |				,
            |				Регистратор = &Регистратор
            |					И ВидРасчета = &ВидРасчета И #ДУ_База) КАК ДополнительныеНачисленияБазаОсновныеНачисления
            |		ПО ДополнительныеНачисления.НомерСтроки = ДополнительныеНачисленияБазаОсновныеНачисления.НомерСтроки
            |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПоказателиРасчетаЗарплаты.СрезПоследних(&ПериодРегистрации, ) КАК ПоказателиРасчетаЗарплатыСрезПоследних
            |		ПО (ИСТИНА)
            |ГДЕ
            |	ДополнительныеНачисления.Регистратор = &Регистратор И ДополнительныеНачисления.ВидРасчета = &ВидРасчета И #ДУ_Осн";
          
          Если ЭтоПерерасчеты Тогда
            Запрос.Текст = СтрЗаменить(Запрос.Текст, "И #ДУ_Осн", 
                     "И (ДополнительныеНачисления.Сотрудник, ДополнительныеНачисления.Подразделение) В (&ТаблицаОтборов)");
            Запрос.Текст = СтрЗаменить(Запрос.Текст, "И #ДУ_База", 
                     "И (Сотрудник, Подразделение) В (&ТаблицаОтборов)");			
            Запрос.УстановитьПараметр("ТаблицаОтборов", СтрокаВидовРасчета.ТаблицаОтборов);
          Иначе
            Запрос.Текст = СтрЗаменить(Запрос.Текст, "И #ДУ_Осн", "");		
            Запрос.Текст = СтрЗаменить(Запрос.Текст, "И #ДУ_База", "");
          КонецЕсли;
           
          Запрос.УстановитьПараметр("ВидРасчета", СтрокаВидовРасчета.ВидРасчета);
          
          Измерения = Новый Массив();
          Измерения.Добавить("Сотрудник");
          Измерения.Добавить("Подразделение");
          
          Запрос.УстановитьПараметр("Измерения", Измерения);
          
          Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
          Запрос.УстановитьПараметр("Регистратор", Регистратор);
          
          РезультатЗапроса = Запрос.Выполнить();
          
          Выборка = РезультатЗапроса.Выбрать();
          
          Для Каждого Движение Из НаборЗаписей Цикл
            
            Если Движение.ВидРасчета <> СтрокаВидовРасчета.ВидРасчета Тогда
              Продолжить;
            КонецЕсли;
              
            Если ЭтоПерерасчеты Тогда
              СтруктураПоиска = Новый Структура("Сотрудник, Подразделение", Движение.Сотрудник, Движение.Подразделение);
              НайденныеСтроки = СтрокаВидовРасчета.ТаблицаОтборов.НайтиСтроки(СтруктураПоиска);			
              Если НайденныеСтроки.Количество() = 0 Тогда
                Продолжить;
              КонецЕсли;					
            КонецЕсли;
            
            Выборка.Сбросить();
            Выборка.НайтиСледующий(Движение.НомерСтроки, "НомерСтроки");
            
            Если Движение.ВидРасчета = ПланыВидовРасчета.ДополнительныеНачисления.КомпенсацияЗаМобильный Тогда
              Движение.Результат = Выборка.СтавкаКомпенсации * Выборка.ОтработаноДней;			
              ЗаполнитьЗначенияСвойств(Движение, Выборка, "СтавкаКомпенсации, ОтработаноДней");
            КонецЕсли;
            
          КонецЦикла;                                                                           
          
          НаборЗаписей.Записать();	
          
        ИначеЕсли СтрокаВидовРасчета.ВидРасчета = ПланыВидовРасчета.ДополнительныеНачисления.Надбавка Тогда
          
          // ...
          
        КонецЕсли;
        
      КонецЦикла;
      
    КонецПроцедуры	  
        
    <p>///// Документ НачислениеЗарплаты</p>   
    Процедура ОбработкаПроведения(Отказ, РежимПроведения)
      
      ПериодРегистрации = НачалоМесяца(Дата);
      
      Движения.ОсновныеНачисления.Записывать = Истина;
      Для Каждого Строка Из ОсновныеНачисления Цикл
        Движение = Движения.ОсновныеНачисления.Добавить();
        ЗаполнитьЗначенияСвойств(Движение, Строка);
        Движение.ПериодРегистрации = ПериодРегистрации;
        Движение.ПериодДействияНачало = Строка.ДатаНачала;
        Движение.ПериодДействияКонец = КонецДня(Строка.ДатаОкончания);
      КонецЦикла;
      
      Движения.ДополнительныеНачисления.Записывать = Истина;
      Для Каждого Строка Из ДополнительныеНачисления Цикл
        Движение = Движения.ДополнительныеНачисления.Добавить();
        ЗаполнитьЗначенияСвойств(Движение, Строка);
        Движение.ПериодРегистрации = ПериодРегистрации;
        Если Движение.ВидРасчета = ПланыВидовРасчета.ДополнительныеНачисления.КомпенсацияЗаМобильный Тогда
          Движение.БазовыйПериодНачало = ПериодРегистрации;
          Движение.БазовыйПериодКонец = КонецМесяца(ПериодРегистрации);
        КонецЕсли;
      КонецЦикла;	
      
      Движения.Записать();
      
      ПроведениеРасчетов.РассчитатьОсновныеНачисления(Движения.ОсновныеНачисления, Ссылка);
      
      ТаблицаВидовРасчета = ДополнительныеНачисления.Выгрузить(, "ВидРасчета");
      ТаблицаВидовРасчета.Свернуть("ВидРасчета");
      ПроведениеРасчетов.РассчитатьДополнительныеНачисления(Движения.ДополнительныеНачисления, Ссылка, ПериодРегистрации, ТаблицаВидовРасчета);
      
    КонецПроцедуры
    
    Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
      
      Если ОсновныеНачисления.Количество() = 0 И ДополнительныеНачисления.Количество() = 0 Тогда
        Сообщение = Новый СообщениеПользователю;
        Сообщение.Текст = "Документ не заполнен";
        Сообщение.Сообщить();		           
        Отказ = Истина;
      КонецЕсли;
      
    КонецПроцедуры
    
    <p>///// Модуль формы</p>
    &НаКлиенте
    Процедура Рассчитать(Команда)
      РассчитатьНаСервере();
    КонецПроцедуры
    
    &НаСервере
    Процедура РассчитатьНаСервере()
      
      Если Объект.Ссылка.Пустая() Тогда
        Сообщение = Новый СообщениеПользователю;
        Сообщение.Текст = "Документ должен быть записан, если вы хотите рассчитать начисления";
        Сообщение.Сообщить();
        Отказ = Истина;
        Возврат;
      КонецЕсли;
      
      ПараметрыРасчета = Новый Структура("ПериодРегистрации", НачалоМесяца(Объект.Дата));
      
      ДокОбъект = РеквизитФормыВЗначение("Объект");
      
      ДокОбъект.ЗаписатьДанныеВРегистры(ПараметрыРасчета);
      ДокОбъект.РассчитатьНачисления(ПараметрыРасчета);
    
      ЗначениеВРеквизитФормы(ДокОбъект, "Объект");
      
    КонецПроцедуры
    
    <p>///// Модуль объекта ЗаполнениеГрафика</p>
    Процедура ЗаполнитьГрафик(ДатаНачала, ДатаОкончания, ВыходныеДни, ГрафикРаботы) Экспорт 
      
      Набор = РегистрыСведений.ГрафикиРаботы.СоздатьНаборЗаписей();
      
      Набор.Отбор.ГрафикРаботы.Установить(ГрафикРаботы);
        
      ЧислоСекундВСутках = 86400;
      
      Дат = ДатаНачала;	
      Пока Дат <= ДатаОкончания Цикл
        Запись = Набор.Добавить();
        Запись.ГрафикРаботы = ГрафикРаботы;
        Запись.Дата = Дат;
        Если Найти(ВыходныеДни, Строка(ДеньНедели(Дат))) Тогда
          Запись.Часов = 0;
          Запись.Дней = 0;
        Иначе	          
          Запись.Часов = 8;
          Запись.Дней = 1;
        КонецЕсли; 
        Дат = Дат + ЧислоСекундВСутках;
      КонецЦикла; 
      Набор.Записать();
    КонецПроцедуры
    
    <p>///// Модуль формы</p>
    &НаКлиенте
    Процедура Заполнить(Команда)
      ВыполнитьОбработку();
      Сообщить("Обработка завершена!");
    КонецПроцедуры
    
    &НаСервере
    Процедура ВыполнитьОбработку()
      ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
        ОбработкаОбъект.ЗаполнитьГрафик(ВыборПериода.ДатаНачала, ВыборПериода.ДатаОкончания, ВыходныеДни, ГрафикРаботы);
    КонецПроцедуры
    <embed src="pdf/ОбщееСПР16.pdf" type="application/pdf" width="1200" height="800">
      <img src="pdf/СПР16.jpg">
  </pre>
</body>

</html>
