<!doctype html>
<html lang="ru">

<head>
  <meta charset="utf-8">
  <title>HTML Document Template</title>
  <link href="styles.css" rel="stylesheet">
</head>

<body>
  <div class="header__menu">
    <ul>
      <li><a href="str/1">1</a></li>
      <li><a href="str/2">2</a></li>
      <li><a href="str/3">3</a></li>
      <li><a href="str/4">4</a></li>
      <li><a href="str/5">5</a></li>
      <li><a href="str/6">6</a></li>
      <li><a href="str/7">7</a></li>
      <li><a href="str/8">8</a></li>
      <li><a href="str/9">9</a></li>
      <li><a href="str/10">10</a></li>
      <li><a href="str/11">11</a></li>
      <li><a href="str/12">12</a></li>
      <li><a href="str/13">13</a></li>
      <li><a href="str/14">14</a></li>
      <li><a href="str/15">15</a></li>
      <li><a href="str/16">16</a></li>
    </ul>
  </div>
  <h1>15</h1>
  <H2 class="OU">
    Оперативный учет
  </H2>
  <pre>
    <p>///// Общий модуль ОбработкаТабличнойЧасти</p>
    Процедура РассчитатьСуммуВСтроке(ТекСтрока) Экспорт
      ТекСтрока.Сумма = ТекСтрока.Цена * ТекСтрока.Количество;
    КонецПроцедуры 
    
    <p>///// Документ НазначениеУчетнойПолитики</p>   
    Процедура ОбработкаПроведения(Отказ, Режим)
      // регистр УчетнаяПолитика
      Движения.УчетнаяПолитика.Записывать = Истина;
      Движение = Движения.УчетнаяПолитика.Добавить();
      Движение.Период = Дата;
      Движение.МетодСписанияСебестоимости = МетодСписанияСебестоимости;
    КонецПроцедуры
    
    Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
      Дата = НачалоГода(Дата);
    КонецПроцедуры 
    
    <p>///// Документ ПриходнаяНакладная</p>
    Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
      СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
    КонецПроцедуры
    
    Процедура ОбработкаПроведения(Отказ, Режим)
    
      Движения.ОстаткиНоменклатуры.Записывать = Истина;
      
      Запрос = Новый Запрос;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
        |	СУММА(ПриходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
        |	СУММА(ПриходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма
        |ИЗ
        |	Документ.ПриходнаяНакладная.СписокНоменклатуры КАК ПриходнаяНакладнаяСписокНоменклатуры
        |ГДЕ
        |	ПриходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
        |
        |СГРУППИРОВАТЬ ПО
        |	ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура";
      
      Запрос.УстановитьПараметр("Ссылка", Ссылка);
      
      РезультатЗапроса = Запрос.Выполнить();
      
      Выборка = РезультатЗапроса.Выбрать();
      
      Пока Выборка.Следующий() Цикл
        Движение = Движения.ОстаткиНоменклатуры.Добавить();
        Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
        Движение.Период = Дата;
        Движение.Номенклатура = Выборка.Номенклатура;
        Движение.Склад = Склад;
        Движение.Партия = Ссылка;
        Движение.Количество = Выборка.Количество;
        Движение.Стоимость = Выборка.Сумма;
      КонецЦикла;
       
    КонецПроцедуры
    
    <p>///// Документ РасходнаяНакладная</p> 
    Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
      СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
    КонецПроцедуры
    
    Процедура ОбработкаПроведения(Отказ, РежимПроведения)
      
      Запрос = Новый Запрос;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	УчетнаяПолитикаСрезПоследних.МетодСписанияСебестоимости КАК МетодСписанияСебестоимости
        |ИЗ
        |	РегистрСведений.УчетнаяПолитика.СрезПоследних(&Период, ) КАК УчетнаяПолитикаСрезПоследних";
      
      Запрос.УстановитьПараметр("Период", Дата);
      
      РезультатЗапроса = Запрос.Выполнить();
      
      Если РезультатЗапроса.Пустой() Тогда
        
        Сообщение = Новый СообщениеПользователю;
        Сообщение.Текст = "Не указан метод списания себестоимости в учетной политике";
        Сообщение.Сообщить();
        
        Отказ = Истина;
        Возврат;
        
      КонецЕсли;
      
      Выборка = РезультатЗапроса.Выбрать();
      Выборка.Следующий();
      
      МетодСписанияСебестоимости = Выборка.МетодСписанияСебестоимости;
    
      // удаление движения
      Движения.ОстаткиНоменклатуры.Записывать = Истина;
      Движения.Записать();
      
      // взвели флаг
      Движения.ОстаткиНоменклатуры.Записывать = Истина;
      Движения.Продажи.Записывать = Истина;
      
      Блокировка = Новый БлокировкаДанных;
      ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
      ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
      ЭлементБлокировки.УстановитьЗначение("Склад", Склад);
      ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
      ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
      Блокировка.Заблокировать();
      
      Запрос = Новый Запрос;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
        |	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
        |	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуры КАК ВидНоменклатуры,
        |	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма
        |ПОМЕСТИТЬ втТЧТовары
        |ИЗ
        |	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
        |ГДЕ
        |	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
        |
        |СГРУППИРОВАТЬ ПО
        |	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура,
        |	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуры
        |
        |ИНДЕКСИРОВАТЬ ПО
        |	Номенклатура
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	втТЧТовары.Номенклатура КАК Номенклатура,
        |	втТЧТовары.Номенклатура.Представление КАК НоменклатураПредставление,
        |	втТЧТовары.Количество КАК Количество,
        |	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
        |	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.СтоимостьОстаток, 0) КАК СтоимостьОстаток,
        |	ОстаткиНоменклатурыОстатки.Партия КАК Партия,
        |	втТЧТовары.ВидНоменклатуры КАК ВидНоменклатуры,
        |	втТЧТовары.Сумма КАК Сумма
        |ИЗ
        |	втТЧТовары КАК втТЧТовары
        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
        |				&МоментВремени,
        |				Номенклатура В
        |						(ВЫБРАТЬ
        |							втТЧТовары.Номенклатура КАК Номенклатура
        |						ИЗ
        |							втТЧТовары КАК втТЧТовары
        |						ГДЕ
        |							втТЧТовары.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Товар))
        |					И Склад = &Склад) КАК ОстаткиНоменклатурыОстатки
        |		ПО втТЧТовары.Номенклатура = ОстаткиНоменклатурыОстатки.Номенклатура
        |
        |УПОРЯДОЧИТЬ ПО
        |	ОстаткиНоменклатурыОстатки.Партия.МоментВремени
        |ИТОГИ
        |	МАКСИМУМ(Количество),
        |	СУММА(КоличествоОстаток),
        |	МАКСИМУМ(ВидНоменклатуры),
        |	МАКСИМУМ(Сумма)
        |ПО
        |	Номенклатура";
      
      Если МетодСписанияСебестоимости = Перечисления.УчетнаяПолитика.ЛИФО Тогда
        Запрос.Текст = СтрЗаменить(Запрос.Текст, "ОстаткиНоменклатурыОстатки.Партия.МоментВремени",
                             "ОстаткиНоменклатурыОстатки.Партия.МоментВремени УБЫВ");
      КонецЕсли;
      
      Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
      Запрос.УстановитьПараметр("Склад", Склад);
      Запрос.УстановитьПараметр("Ссылка", Ссылка);
      
      РезультатЗапроса = Запрос.Выполнить();
      
      ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
      
      Пока ВыборкаНоменклатура.Следующий() Цикл
        
        СебестоимостьИтого = 0;
        Если ВыборкаНоменклатура.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Товар Тогда
          
          Если ВыборкаНоменклатура.Количество > ВыборкаНоменклатура.КоличествоОстаток Тогда
            
            Отказ = Истина;
            
            Сообщение = Новый СообщениеПользователю;
            Сообщение.Текст = СтрШаблон("Недостаточно товара %1 в количестве %2", 
                          ВыборкаНоменклатура.НоменклатураПредставление, 
                          ВыборкаНоменклатура.Количество - ВыборкаНоменклатура.КоличествоОстаток);
            Сообщение.Сообщить();
            
          КонецЕсли;
          
          Если Отказ Тогда
            Продолжить;
          КонецЕсли;
          
          КоличествоСписать = ВыборкаНоменклатура.Количество;
          
          ВыборкаДетальныеЗаписи = ВыборкаНоменклатура.Выбрать();
        
          Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
            
            Количество = Мин(КоличествоСписать, ВыборкаДетальныеЗаписи.КоличествоОстаток);
            
            Если Количество = ВыборкаДетальныеЗаписи.КоличествоОстаток Тогда
              Себестоимость = ВыборкаДетальныеЗаписи.СтоимостьОстаток;
            Иначе
              Себестоимость = Количество / ВыборкаДетальныеЗаписи.КоличествоОстаток * ВыборкаДетальныеЗаписи.СтоимостьОстаток;
            КонецЕсли;
            
            Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
            Движение.Период = Дата;
            Движение.Номенклатура = ВыборкаДетальныеЗаписи.Номенклатура;
            Движение.Склад = Склад;
            Движение.Партия = ВыборкаДетальныеЗаписи.Партия;
            Движение.Количество = Количество;
            Движение.Стоимость = Себестоимость;
            
            СебестоимостьИтого = СебестоимостьИтого + Движение.Стоимость;
            
            КоличествоСписать = КоличествоСписать - Количество;
            
            Если КоличествоСписать <= 0 Тогда
              Прервать;
            КонецЕсли;
            
          КонецЦикла;
        КонецЕсли;
        
        Движение = Движения.Продажи.Добавить();
        Движение.Период = Дата;
        ЗаполнитьЗначенияСвойств(Движение, ВыборкаНоменклатура);
        Движение.Себестоимость = СебестоимостьИтого;
        Движение.Накладная = Ссылка;
        
      КонецЦикла;
      
    КонецПроцедуры
    
    <p>///// Документ ДополнительныеЗатраты</p> 
    Процедура ОбработкаПроведения(Отказ, РежимПроведения)
      
      Движения.Продажи.Записывать = Истина;
      
      Запрос = Новый Запрос;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	ДополнительныеЗатратыСписокНоменклатуры.Номенклатура КАК Номенклатура,
        |	ДополнительныеЗатратыСписокНоменклатуры.Сумма КАК Себестоимость
        |ПОМЕСТИТЬ втТЧТовары
        |ИЗ
        |	Документ.ДополнительныеЗатраты.СписокНоменклатуры КАК ДополнительныеЗатратыСписокНоменклатуры
        |ГДЕ
        |	ДополнительныеЗатратыСписокНоменклатуры.Ссылка = &Ссылка
        |
        |ИНДЕКСИРОВАТЬ ПО
        |	Номенклатура
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	Продажи.Номенклатура КАК Номенклатура,
        |	Продажи.Период КАК Период
        |ПОМЕСТИТЬ втДвиженияНакладной
        |ИЗ
        |	РегистрНакопления.Продажи КАК Продажи
        |ГДЕ
        |	Продажи.Регистратор = &Накладная
        |
        |ИНДЕКСИРОВАТЬ ПО
        |	Номенклатура
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	втТЧТовары.Номенклатура КАК Номенклатура,
        |	втТЧТовары.Себестоимость КАК Себестоимость,
        |	втТЧТовары.Номенклатура.Представление КАК НоменклатураПредставление,
        |	ВЫБОР
        |		КОГДА втДвиженияНакладной.Номенклатура ЕСТЬ NULL
        |			ТОГДА ЛОЖЬ
        |		ИНАЧЕ ИСТИНА
        |	КОНЕЦ КАК ЕстьСоответствие,
        |	втДвиженияНакладной.Период КАК Период,
        |	&Накладная КАК Накладная
        |ИЗ
        |	втТЧТовары КАК втТЧТовары
        |		ЛЕВОЕ СОЕДИНЕНИЕ втДвиженияНакладной КАК втДвиженияНакладной
        |		ПО втТЧТовары.Номенклатура = втДвиженияНакладной.Номенклатура";
      
      Запрос.УстановитьПараметр("Ссылка",    Ссылка);
      Запрос.УстановитьПараметр("Накладная", Накладная);
      
      РезультатЗапроса = Запрос.Выполнить();
      
      Выборка = РезультатЗапроса.Выбрать();
        
      Пока Выборка.Следующий() Цикл
        
        Если Не Выборка.ЕстьСоответствие Тогда
          Сообщение = Новый СообщениеПользователю;
          Сообщение.Текст = СтрШаблон("В накладной не найдена номенклатура %1", Выборка.НоменклатураПредставление);
          Сообщение.Сообщить();
          
          Отказ = Истина;
        КонецЕсли;
        
        Если Отказ Тогда 
          Продолжить;
        КонецЕсли;
        
        Движение = Движения.Продажи.Добавить();
        ЗаполнитьЗначенияСвойств(Движение, Выборка);
        
      КонецЦикла;
        
    КонецПроцедуры
    
    <p>///// Модуль формы</p>
    &НаКлиенте
    Процедура СписокНоменклатурыКоличествоПриИзменении(Элемент)
      ТекСтрока = Элементы.СписокНоменклатуры.ТекущиеДанные;
      ОбработкаТабличнойЧасти.РассчитатьСуммуВСтроке(ТекСтрока);
    КонецПроцедуры
    
    &НаКлиенте
    Процедура СписокНоменклатурыЦенаПриИзменении(Элемент)
      ТекСтрока = Элементы.СписокНоменклатуры.ТекущиеДанные;
      ОбработкаТабличнойЧасти.РассчитатьСуммуВСтроке(ТекСтрока);
    КонецПроцедуры
    <embed src="pdf/ОбщееОУ15.pdf" type="application/pdf" width="1200" height="800">
      <img src="pdf/ОУ15.jpg">
    <H2 class="OU">
      Бухгалтерский учет
    </H2>
    <p>///// Общий модуль ОбработкаТабличнойЧастиКлиент</p>
    Процедура РассчитатьСуммуВСтроке(ТекСтрока) Экспорт
      ТекСтрока.Сумма = ТекСтрока.Цена * ТекСтрока.Количество;
    КонецПроцедуры 
    
    <p>///// Документ Операция</p>   
    Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
      Если Не Движения.Управленческий.Записывать Тогда
        Движения.Управленческий.Прочитать();
        Движения.Управленческий.Записывать = Истина;
      КонецЕсли;
      
      Для Каждого Движение Из Движения.Управленческий Цикл
        Движение.Период = Дата;
        Движение.Активность = Не ПометкаУдаления;
      КонецЦикла;
    КонецПроцедуры
    
    Процедура ПриКопировании(ОбъектКопирования)	
      Выборка = РегистрыБухгалтерии.Управленческий.ВыбратьПоРегистратору(ОбъектКопирования.Ссылка);
      Пока Выборка.Следующий() Цикл
        Движение = Движения.Управленческий.Добавить();
        ЗаполнитьЗначенияСвойств(Движение, Выборка);
        ЗаполнитьЗначенияСвойств(Движение.СубконтоДт, Выборка.СубконтоДт);
        ЗаполнитьЗначенияСвойств(Движение.СубконтоКт, Выборка.СубконтоКт);
      КонецЦикла;
    КонецПроцедуры
     
    <p>///// Документ ПриходнаяНакладная</p>
    Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
      СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
    КонецПроцедуры
    
    Процедура ОбработкаПроведения(Отказ, РежимПроведения)
      
      Движения.Управленческий.Записывать = Истина;
      
      Запрос = Новый Запрос;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
        |	ПриходнаяНакладнаяСписокНоменклатуры.Склад КАК Склад,
        |	СУММА(ПриходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
        |	СУММА(ПриходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма
        |ИЗ
        |	Документ.ПриходнаяНакладная.СписокНоменклатуры КАК ПриходнаяНакладнаяСписокНоменклатуры
        |ГДЕ
        |	ПриходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
        |
        |СГРУППИРОВАТЬ ПО
        |	ПриходнаяНакладнаяСписокНоменклатуры.Склад,
        |	ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура";
      
      Запрос.УстановитьПараметр("Ссылка", Ссылка);
      
      РезультатЗапроса = Запрос.Выполнить();
      
      Выборка = РезультатЗапроса.Выбрать();
      
      Пока Выборка.Следующий() Цикл
        
        Движение = Движения.Управленческий.Добавить();
        Движение.Период = Дата;
        
        Движение.СчетДт = ПланыСчетов.Управленческий.Товары;
        Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = Выборка.Номенклатура;
        Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Склады] = Выборка.Склад;
        Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Партии] = Ссылка;
        Движение.КоличествоДт = Выборка.Количество;
        
        Движение.СчетКт = ПланыСчетов.Управленческий.Поставщики;
        
        Движение.Сумма = Выборка.Сумма;
        
      КонецЦикла;
      
    КонецПроцедуры
    
    <p>///// Документ РасходнаяНакладная</p> 
    Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
      СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
    КонецПроцедуры
    
    Процедура ОбработкаПроведения(Отказ, РежимПроведения)
      
      МетодСписанияСебестоимости = РегистрыСведений.УчетнаяПолитика.ПолучитьПоследнее(Дата).МетодСписанияСебестоимости;
      Если МетодСписанияСебестоимости.Пустая() Тогда
        Отказ = Истина;
        Сообщение = Новый СообщениеПользователю;
        Сообщение.Текст = "Не указана учетная политика";
        Сообщение.Сообщить();		
        Возврат;
      КонецЕсли;
      
      Движения.Управленческий.Записывать = Истина;
      Движения.Записать();
      
      Движения.Управленческий.Записывать = Истина;
      
      Блокировка = Новый БлокировкаДанных;
      ЭлементБлокировки = Блокировка.Добавить("РегистрБухгалтерии.Управленческий");
      ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
      ЭлементБлокировки.УстановитьЗначение("Счет", ПланыСчетов.Управленческий.Товары);
      ЭлементБлокировки.УстановитьЗначение(ПланыВидовХарактеристик.ВидыСубконто.Склады, Склад);
      ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
      ЭлементБлокировки.ИспользоватьИзИсточникаДанных(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура, "Номенклатура");
      Блокировка.Заблокировать();
      
      Запрос = Новый Запрос;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
        |	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
        |	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма
        |ПОМЕСТИТЬ втТЧТовары
        |ИЗ
        |	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
        |ГДЕ
        |	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
        |
        |СГРУППИРОВАТЬ ПО
        |	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура
        |
        |ИНДЕКСИРОВАТЬ ПО
        |	Номенклатура
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	втТЧТовары.Номенклатура КАК Номенклатура,
        |	втТЧТовары.Номенклатура.Представление КАК НоменклатураПредставление,
        |	втТЧТовары.Количество КАК Количество,
        |	УправленческийОстатки.Субконто3 КАК Партия,
        |	ЕСТЬNULL(УправленческийОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
        |	ЕСТЬNULL(УправленческийОстатки.СуммаОстаток, 0) КАК СуммаОстаток,
        |	втТЧТовары.Сумма КАК Сумма
        |ИЗ
        |	втТЧТовары КАК втТЧТовары
        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(
        |				&МоментВремени,
        |				Счет = &СчетТовары,
        |				&ВидыСубконто,
        |				Субконто1 В
        |						(ВЫБРАТЬ
        |							втТЧТовары.Номенклатура КАК Номенклатура
        |						ИЗ
        |							втТЧТовары КАК втТЧТовары)
        |					И Субконто2 = &Склад) КАК УправленческийОстатки
        |		ПО втТЧТовары.Номенклатура = УправленческийОстатки.Субконто1
        |
        |УПОРЯДОЧИТЬ ПО
        |	ВЫРАЗИТЬ(УправленческийОстатки.Субконто3 КАК Документ.ПриходнаяНакладная).МоментВремени УБЫВ
        |ИТОГИ
        |	МАКСИМУМ(Количество),
        |	СУММА(КоличествоОстаток),
        |	СУММА(СуммаОстаток),
        |	МАКСИМУМ(Сумма)
        |ПО
        |	Номенклатура";
      
      Если МетодСписанияСебестоимости = Перечисления.УчетнаяПолитика.ФИФО Тогда
        Запрос.Текст = СтрЗаменить(Запрос.Текст, "УБЫВ", "ВОЗР");
      КонецЕсли;
      
      ВидыСубконто = Новый Массив;
      ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура);
      ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконто.Склады);
      ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконто.Партии);
      
      Запрос.УстановитьПараметр("ВидыСубконто", ВидыСубконто);
      Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
      Запрос.УстановитьПараметр("Склад", Склад);
      Запрос.УстановитьПараметр("Ссылка", Ссылка);
      Запрос.УстановитьПараметр("СчетТовары", ПланыСчетов.Управленческий.Товары);
      
      РезультатЗапроса = Запрос.Выполнить();
      
      ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
      
      Пока ВыборкаНоменклатура.Следующий() Цикл
        
        Если ВыборкаНоменклатура.Количество > ВыборкаНоменклатура.КоличествоОстаток Тогда
          Отказ = Истина;
          
          Сообщение = Новый СообщениеПользователю;
          Сообщение.Текст = СтрШаблон("Недостаточно товара %1 в количестве %2",
          ВыборкаНоменклатура.НоменклатураПредставление, 
          ВыборкаНоменклатура.Количество - ВыборкаНоменклатура.КоличествоОстаток);
          Сообщение.Сообщить();
          
        КонецЕсли;
        
        Если Отказ Тогда
          Продолжить;
        КонецЕсли;
        
        КоличествоСписать = ВыборкаНоменклатура.Количество;
        
        ВыборкаДетальныеЗаписи = ВыборкаНоменклатура.Выбрать();
      
        Пока ВыборкаДетальныеЗаписи.Следующий() И КоличествоСписать > 0 Цикл
          
          Количество = Мин(КоличествоСписать, ВыборкаДетальныеЗаписи.КоличествоОстаток);
          
          Если Количество = ВыборкаДетальныеЗаписи.КоличествоОстаток Тогда
            Себестоимость = ВыборкаДетальныеЗаписи.СуммаОстаток;
          Иначе
            Себестоимость = ВыборкаДетальныеЗаписи.СуммаОстаток / ВыборкаДетальныеЗаписи.КоличествоОстаток * Количество;
          КонецЕсли;
          
          Движение = Движения.Управленческий.Добавить();
          Движение.Период = Дата;
          
          Движение.СчетДт = ПланыСчетов.Управленческий.ПрибылиУбытки;
          Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = ВыборкаДетальныеЗаписи.Номенклатура;
          
          Движение.СчетКт = ПланыСчетов.Управленческий.Товары;
          Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = ВыборкаДетальныеЗаписи.Номенклатура;
          Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Склады] = Склад;
          Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Партии] = ВыборкаДетальныеЗаписи.Партия;
          Движение.КоличествоКт = Количество;
          
          Движение.Сумма = Себестоимость;
          
          КоличествоСписать = КоличествоСписать - Количество;
          
        КонецЦикла;
        
        Движение = Движения.Управленческий.Добавить();
        Движение.Период = Дата;	
        Движение.СчетДт = ПланыСчетов.Управленческий.Покупатели;
        Движение.СчетКт = ПланыСчетов.Управленческий.ПрибылиУбытки;	
        Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = ВыборкаНоменклатура.Номенклатура;		
        Движение.Сумма  = ВыборкаНоменклатура.Сумма;
        
      КонецЦикла;
    
      
    КонецПроцедуры
    
    <p>///// Модуль формы</p>
    &НаКлиенте
    Процедура СписокНоменклатурыКоличествоПриИзменении(Элемент)
      ТекСтрока = Элементы.СписокНоменклатуры.ТекущиеДанные;
      ОбработкаТабличнойЧасти.РассчитатьСуммуВСтроке(ТекСтрока);
    КонецПроцедуры
    
    &НаКлиенте
    Процедура СписокНоменклатурыЦенаПриИзменении(Элемент)
      ТекСтрока = Элементы.СписокНоменклатуры.ТекущиеДанные;
      ОбработкаТабличнойЧасти.РассчитатьСуммуВСтроке(ТекСтрока);
    КонецПроцедуры
    <embed src="pdf/ОбщееБУ15.pdf" type="application/pdf" width="1200" height="800">
      <img src="pdf/БУ15.jpg">
    <H2 class="OU">
      Расчеты
    </H2>
    <p>///// Документ НачислениеЗарплаты</p>   
    Процедура ОбработкаПроведения(Отказ, РежимПроведения)
      
      ПериодРегистрации = НачалоМесяца(Дата);
      
      Движения.ОсновныеНачисления.Записывать = Истина;
      Для Каждого Строка Из ОсновныеНачисления Цикл
        Движение = Движения.ОсновныеНачисления.Добавить();
        ЗаполнитьЗначенияСвойств(Движение, Строка);
        Движение.ПериодДействияНачало = Строка.ДатаНачала;
        Движение.ПериодДействияКонец = КонецДня(Строка.ДатаОкончания);
        Движение.ПериодРегистрации = ПериодРегистрации;
        
        Если Строка.ВидРасчета = ПланыВидовРасчета.ОсновныеНачисления.Больничный Тогда
          Движение.БазовыйПериодНачало = НачалоМесяца(ДобавитьМесяц(Строка.ДатаНачала, -1));			
          Движение.БазовыйПериодКонец  = КонецМесяца(Движение.БазовыйПериодНачало);			
          //Движение.ГрафикРаботы        = Справочники.ГрафикиРаботы.Пятидневка;
        КонецЕсли;
      КонецЦикла;
      
      ТаблицаДополнение = Движения.ОсновныеНачисления.ПолучитьДополнение();
      Для Каждого Дополнение Из ТаблицаДополнение Цикл
        Движение = Движения.ОсновныеНачисления.Добавить();
        ЗаполнитьЗначенияСвойств(Движение, Дополнение, "Сотрудник, ВидРасчета, ГрафикРаботы, Оклад");
        Движение.ПериодРегистрации = Дополнение.ПериодРегистрацииСторно;
        Движение.ПериодДействияНачало = Дополнение.ПериодДействияНачалоСторно;
        Движение.ПериодДействияКонец  = Дополнение.ПериодДействияКонецСторно;
        Движение.Сторно = Истина;
      КонецЦикла;
      
      Движения.ДополнительныеНачисления.Записывать = Истина;
      Для Каждого Строка Из ДополнительныеНачисления Цикл
        Движение = Движения.ДополнительныеНачисления.Добавить();
        ЗаполнитьЗначенияСвойств(Движение, Строка);
        Движение.ПериодРегистрации = ПериодРегистрации;
      КонецЦикла;
      
      Движения.Записать();
    
      // расчет основных начислений
      Запрос = Новый Запрос;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
        |	ОсновныеНачисленияДанныеГрафика.Сотрудник КАК Сотрудник,
        |	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ЧасовПериодДействия, 0) КАК НормаЧасов,
        |	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ЧасовФактическийПериодДействия, 0) КАК ОтработаноЧасов,
        |	ОсновныеНачисленияДанныеГрафика.Сторно КАК Сторно,
        |	ОсновныеНачисленияДанныеГрафика.Оклад КАК Оклад,
        |	ОсновныеНачисленияДанныеГрафика.ДнейФактическийПериодДействия КАК ДнейФактическийПериодДействия,
        |	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ДнейПериодДействия, 0) КАК НормаДней
        |ПОМЕСТИТЬ втДанныеГрафика
        |ИЗ
        |	РегистрРасчета.ОсновныеНачисления.ДанныеГрафика(
        |			Регистратор = &Ссылка
        |				И ВидРасчета В (&ВидыРасчета)) КАК ОсновныеНачисленияДанныеГрафика
        |
        |ИНДЕКСИРОВАТЬ ПО
        |	Сотрудник
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	втДанныеГрафика.НомерСтроки КАК НомерСтроки,
        |	втДанныеГрафика.НормаЧасов КАК НормаЧасов,
        |	втДанныеГрафика.ОтработаноЧасов КАК ОтработаноЧасов,
        |	ВЫБОР
        |		КОГДА втДанныеГрафика.Сторно
        |			ТОГДА втДанныеГрафика.Оклад
        |		ИНАЧЕ ЕСТЬNULL(СведенияОСотрудникахСрезПоследних.Оклад, 0)
        |	КОНЕЦ КАК Оклад,
        |	втДанныеГрафика.НормаДней КАК НормаДней
        |ИЗ
        |	втДанныеГрафика КАК втДанныеГрафика
        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОСотрудниках.СрезПоследних(
        |				&ПериодРегистрации,
        |				Сотрудник В
        |					(ВЫБРАТЬ
        |						втДанныеГрафика.Сотрудник КАК Сотрудник
        |					ИЗ
        |						втДанныеГрафика КАК втДанныеГрафика)) КАК СведенияОСотрудникахСрезПоследних
        |		ПО втДанныеГрафика.Сотрудник = СведенияОСотрудникахСрезПоследних.Сотрудник";
      
      ВидыРасчета = Новый Массив;
      ВидыРасчета.Добавить(ПланыВидовРасчета.ОсновныеНачисления.Оклад);
      Запрос.УстановитьПараметр("ВидыРасчета", ВидыРасчета);
      
      Запрос.УстановитьПараметр("Ссылка", Ссылка);	
      Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
      
      РезультатЗапроса = Запрос.Выполнить();
      
      Выборка = РезультатЗапроса.Выбрать();
      
      Для Каждого Движение Из Движения.ОсновныеНачисления Цикл
        
        Если ВидыРасчета.Найти(Движение.ВидРасчета) = Неопределено Тогда
          Продолжить;
        КонецЕсли;
        
        Выборка.Сбросить();
        Выборка.НайтиСледующий(Движение.НомерСтроки, "НомерСтроки");
        
        Если Движение.ВидРасчета = ПланыВидовРасчета.ОсновныеНачисления.Оклад Тогда
          Движение.Результат = ?(Выборка.НормаЧасов <> 0,
                       Выборка.Оклад / Выборка.НормаЧасов * Выборка.ОтработаноЧасов,
                       0) * ?(Движение.Сторно, -1, 1);
          ЗаполнитьЗначенияСвойств(Движение, Выборка, "Оклад, НормаЧасов, ОтработаноЧасов, НормаДней");
          
          Если Движение.Сторно Тогда
            Движение.НормаДней = 0;
          КонецЕсли;
          
        КонецЕсли;
      КонецЦикла;
      
      Движения.ОсновныеНачисления.Записать(, Истина);
    
      Запрос = Новый Запрос;
      Запрос.Текст = 
        "ВЫБРАТЬ РАЗЛИЧНЫЕ
        |	ОсновныеНачисления.ПериодДействия КАК ПериодДействия
        |ИЗ
        |	РегистрРасчета.ОсновныеНачисления КАК ОсновныеНачисления
        |ГДЕ
        |	ОсновныеНачисления.Регистратор = &Регистратор
        |	И ОсновныеНачисления.ВидРасчета В(&ВидыРасчета)
        |
        |УПОРЯДОЧИТЬ ПО
        |	ПериодДействия";
      
      ВидыРасчета = Новый Массив;
      ВидыРасчета.Добавить(ПланыВидовРасчета.ОсновныеНачисления.Больничный);	
      Запрос.УстановитьПараметр("ВидыРасчета", ВидыРасчета);
      Запрос.УстановитьПараметр("Регистратор", Ссылка);
      
      РезультатЗапроса = Запрос.Выполнить();
      
      ВыборкаПД = РезультатЗапроса.Выбрать();
      
      Пока ВыборкаПД.Следующий() Цикл
        
        Запрос = Новый Запрос;
        Запрос.Текст = 
          "ВЫБРАТЬ
          |	ОсновныеНачисленияФактическийПериодДействия.НомерСтроки КАК НомерСтроки,
          |	СУММА(ЕСТЬNULL(ГрафикиРаботы.Дней, 0)) КАК ОтработаноДнейПоПятидневке
          |ПОМЕСТИТЬ втДанныеГрафикаПоПятидневке
          |ИЗ
          |	РегистрРасчета.ОсновныеНачисления.ФактическийПериодДействия(
          |			Регистратор = &Ссылка
          |				И ВидРасчета В (&ВидыРасчета)
          |				И ПериодДействия = &ПериодДействия) КАК ОсновныеНачисленияФактическийПериодДействия
          |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ГрафикиРаботы КАК ГрафикиРаботы
          |		ПО ОсновныеНачисленияФактическийПериодДействия.ПериодДействияНачало <= ГрафикиРаботы.Дата
          |			И ОсновныеНачисленияФактическийПериодДействия.ПериодДействияКонец >= ГрафикиРаботы.Дата
          |			И (ГрафикиРаботы.ГрафикРаботы = ЗНАЧЕНИЕ(Справочник.ГрафикиРаботы.Пятидневка))
          |
          |СГРУППИРОВАТЬ ПО
          |	ОсновныеНачисленияФактическийПериодДействия.НомерСтроки
          |;
          |
          |////////////////////////////////////////////////////////////////////////////////
          |ВЫБРАТЬ
          |	ОсновныеНачисленияБазаОсновныеНачисления.НомерСтроки КАК НомерСтроки,
          |	СУММА(ОсновныеНачисленияБазаОсновныеНачисления.РезультатБаза) КАК РезультатБаза,
          |	МАКСИМУМ(ОсновныеНачисленияБазаОсновныеНачисления.НормаДнейБаза) КАК НормаДнейБаза,
          |	МИНИМУМ(ОсновныеНачисленияБазаОсновныеНачисления.НомерСтрокиРазрез) КАК НомерСтрокиРазрез
          |ПОМЕСТИТЬ втТаблицаБаза
          |ИЗ
          |	РегистрРасчета.ОсновныеНачисления.БазаОсновныеНачисления(
          |			&Измерения,
          |			&Измерения,
          |			&Разрезы,
          |			Регистратор = &Ссылка
          |				И ВидРасчета В (&ВидыРасчета)
          |				И ПериодДействия = &ПериодДействия) КАК ОсновныеНачисленияБазаОсновныеНачисления
          |
          |СГРУППИРОВАТЬ ПО
          |	ОсновныеНачисленияБазаОсновныеНачисления.НомерСтроки
          |
          |ИНДЕКСИРОВАТЬ ПО
          |	НомерСтроки
          |;
          |
          |////////////////////////////////////////////////////////////////////////////////
          |ВЫБРАТЬ
          |	ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
          |	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ДнейФактическийПериодДействия, 0) КАК ОтработаноДнейПоСменномуГрафику,
          |	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ДнейБазовыйПериод, 0) КАК БазаРабочихДней_old,
          |	ЕСТЬNULL(втДанныеГрафикаПоПятидневке.ОтработаноДнейПоПятидневке, 0) КАК ОтработаноДней,
          |	ЕСТЬNULL(втТаблицаБаза.РезультатБаза, 0) КАК БазаНачислений,
          |	ЕСТЬNULL(втТаблицаБаза.НормаДнейБаза, 0) КАК БазаРабочихДней
          |ИЗ
          |	РегистрРасчета.ОсновныеНачисления.ДанныеГрафика(
          |			Регистратор = &Ссылка
          |				И ВидРасчета В (&ВидыРасчета)
          |				И ПериодДействия = &ПериодДействия) КАК ОсновныеНачисленияДанныеГрафика
          |		ЛЕВОЕ СОЕДИНЕНИЕ втДанныеГрафикаПоПятидневке КАК втДанныеГрафикаПоПятидневке
          |		ПО ОсновныеНачисленияДанныеГрафика.НомерСтроки = втДанныеГрафикаПоПятидневке.НомерСтроки
          |		ЛЕВОЕ СОЕДИНЕНИЕ втТаблицаБаза КАК втТаблицаБаза
          |		ПО ОсновныеНачисленияДанныеГрафика.НомерСтроки = втТаблицаБаза.НомерСтроки";
    
        Измерения = Новый Массив;
        Измерения.Добавить("Сотрудник");
        Запрос.УстановитьПараметр("Измерения", Измерения);
        
        Разрезы = Новый Массив;
        Разрезы.Добавить("НомерСтроки");
        Запрос.УстановитьПараметр("Разрезы", Разрезы);
        
        Запрос.УстановитьПараметр("ВидыРасчета", ВидыРасчета);
        Запрос.УстановитьПараметр("ПериодДействия", ВыборкаПД.ПериодДействия);
        Запрос.УстановитьПараметр("Ссылка", Ссылка);
          
        РезультатЗапроса = Запрос.Выполнить();
        
        Выборка = РезультатЗапроса.Выбрать();
        Для Каждого Движение Из Движения.ОсновныеНачисления Цикл
          Если ВидыРасчета.Найти(Движение.ВидРасчета) = Неопределено 
             Или Движение.ПериодДействия <> ВыборкаПД.ПериодДействия Тогда
            Продолжить;			
          КонецЕсли;     
          
          Выборка.Сбросить();
          Выборка.НайтиСледующий(Движение.НомерСтроки, "НомерСтроки");
          
          Если Движение.ВидРасчета = ПланыВидовРасчета.ОсновныеНачисления.Больничный Тогда
            Движение.Результат = ?(Выборка.БазаРабочихДней <> 0,
                         Выборка.БазаНачислений / Выборка.БазаРабочихДней * Выборка.ОтработаноДней,
                         0);
            ЗаполнитьЗначенияСвойств(Движение, Выборка, "БазаНачислений, БазаРабочихДней, ОтработаноДней");								  
          КонецЕсли;
        КонецЦикла;
        
        Движения.ОсновныеНачисления.Записать(, Истина);
    
        // расчет дополнительных начислений
        Для Каждого Движение Из Движения.ДополнительныеНачисления Цикл
          Если Движение.ВидРасчета = ПланыВидовРасчета.ДополнительныеНачисления.ПроизвольнаяСуммаДенег Тогда
            Движение.Результат = Движение.Параметр;
          КонецЕсли;
        КонецЦикла;
        
        Движения.ДополнительныеНачисления.Записать();		
        
      КонецЦикла;
        
    КонецПроцедуры
    
    Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
      
      Если ОсновныеНачисления.Количество() = 0 И ДополнительныеНачисления.Количество() = 0 Тогда
        Сообщение = Новый СообщениеПользователю;
        Сообщение.Текст = "Документ не заполнен!";
        Сообщение.Сообщить();		
      КонецЕсли;
      
    КонецПроцедуры
    
    <p>///// Модуль объекта ЗаполнениеГрафика</p>
    Процедура ЗаполнитьГрафик(ДатаНачала, ДатаОкончания, ВыходныеДни, ГрафикРаботы, ЭтоСменныйГрафик) Экспорт 
      
      Набор = РегистрыСведений.ГрафикиРаботы.СоздатьНаборЗаписей();
      
      Набор.Отбор.ГрафикРаботы.Установить(ГрафикРаботы);
        
      ЧислоСекундВСутках = 86400;
      
      Дат = ДатаНачала;	
      
      Если ЭтоСменныйГрафик Тогда
        Счетчик = 1;
        Пока Дат <= ДатаОкончания Цикл
          Запись = Набор.Добавить();
          Запись.ГрафикРаботы = ГрафикРаботы;
          Запись.Дата = Дат;
          
          Если Счетчик = 1 Тогда
            Запись.Часов = 24;
            Запись.Дней  = 1;
          Иначе
            Запись.Часов = 0;
            Запись.Дней  = 0;
          КонецЕсли;
     
          Дат = Дат + ЧислоСекундВСутках;
          
          Если Счетчик < 3 Тогда
            Счетчик = Счетчик + 1;
          Иначе
            Счетчик = 1
          КонецЕсли;
        КонецЦикла;
      Иначе		
        Пока Дат <= ДатаОкончания Цикл
          Запись = Набор.Добавить();
          Запись.ГрафикРаботы = ГрафикРаботы;
          Запись.Дата = Дат;
          Если Найти(ВыходныеДни, Строка(ДеньНедели(Дат))) Тогда
            Запись.Часов = 0;
            Запись.Дней  = 0;
          Иначе	          
            Запись.Часов = 8;
            Запись.Дней  = 1
          КонецЕсли; 
          Дат = Дат + ЧислоСекундВСутках;
        КонецЦикла;
      КонецЕсли;
      
      Набор.Записать();
    КонецПроцедуры
    
    <p>///// Модуль формы</p>
    &НаКлиенте
    Процедура Заполнить(Команда)
      ВыполнитьОбработку();
      Сообщить("Обработка завершена!");
    КонецПроцедуры
    
    &НаСервере
    Процедура ВыполнитьОбработку()
      ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
        ОбработкаОбъект.ЗаполнитьГрафик(ВыборПериода.ДатаНачала, ВыборПериода.ДатаОкончания, ВыходныеДни, ГрафикРаботы, ЭтоСменныйГрафик);
    КонецПроцедуры 
    
    &НаСервере
    Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
      ОбновитьВидимостьЭлементовФормы(ЭтотОбъект)
    КонецПроцедуры
    
    &НаКлиенте
    Процедура СменныйГрафикПриИзменении(Элемент)
      ОбновитьВидимостьЭлементовФормы(ЭтотОбъект)
    КонецПроцедуры
    
    &НаКлиентеНаСервереБезКонтекста
    Процедура ОбновитьВидимостьЭлементовФормы(ЭтаФорма)
      ЭтаФорма.Элементы.ВыходныеДни.Видимость = Не ЭтаФорма.ЭтоСменныйГрафик;	
    КонецПроцедуры
    <embed src="pdf/ОбщееСПР15.pdf" type="application/pdf" width="1200" height="800">
      <img src="pdf/СПР15.jpg">
  </pre>
</body>

</html>