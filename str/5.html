<!doctype html>
<html lang="ru">

<head>
  <meta charset="utf-8">
  <title>HTML Document Template</title>
  <link href="styles.css" rel="stylesheet">
</head>

<body>
  <div class="header__menu">
    <ul>
      <li><a href="str/1">1</a></li>
      <li><a href="str/2">2</a></li>
      <li><a href="str/3">3</a></li>
      <li><a href="str/4">4</a></li>
      <li><a href="str/5">5</a></li>
      <li><a href="str/6">6</a></li>
      <li><a href="str/7">7</a></li>
      <li><a href="str/8">8</a></li>
      <li><a href="str/9">9</a></li>
      <li><a href="str/10">10</a></li>
      <li><a href="str/11">11</a></li>
      <li><a href="str/12">12</a></li>
      <li><a href="str/13">13</a></li>
      <li><a href="str/14">14</a></li>
      <li><a href="str/15">15</a></li>
      <li><a href="str/16">16</a></li>
    </ul>
  </div>
  <h1>5</h1>
  <H2 class="OU">
    Оперативный учет
  </H2>
  <pre>
    <p>///// Общий модуль ОбработкаТабличнойЧасти</p>
    Процедура РассчитатьСуммуВСтроке(ТекСтрока) Экспорт
      ТекСтрока.Сумма = ТекСтрока.Цена * ТекСтрока.Количество;
    КонецПроцедуры     
    
    <p>///// Общий модуль ОбщегоНазначения</p>
    Функция ВалютаПроекта(Проект) Экспорт
      
      Запрос = Новый Запрос;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	Проекты.Валюта КАК Валюта
        |ИЗ
        |	Справочник.Проекты КАК Проекты
        |ГДЕ
        |	Проекты.Ссылка = &Ссылка";
      
      Запрос.УстановитьПараметр("Ссылка", Проект);
      
      РезультатЗапроса = Запрос.Выполнить();
      
      Выборка = РезультатЗапроса.Выбрать();
      
      Если Выборка.Следующий() Тогда
        Возврат Выборка.Валюта;	
      КонецЕсли;	
      
      Возврат Неопределено;
        
    КонецФункции
    
    <p>///// Документ ПриходДенег</p>   
    Процедура ОбработкаПроведения(Отказ, РежимПроведения)
      
      Движения.Взаиморасчеты.Записывать = Истина;
      Движения.Записать();
      
      Движения.Взаиморасчеты.Записывать = Истина;
      
      Блокировка = Новый БлокировкаДанных;
      ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.Взаиморасчеты");
      ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
      ЭлементБлокировки.УстановитьЗначение("Контрагент", Контрагент);
      Блокировка.Заблокировать();
    
      Запрос = Новый Запрос;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	ВзаиморасчетыОстатки.Контрагент КАК Контрагент,
        |	ВзаиморасчетыОстатки.Проект КАК Проект,
        |	ВзаиморасчетыОстатки.Валюта КАК Валюта,
        |	ВзаиморасчетыОстатки.СуммаВРубляхОстаток КАК СуммаВРубляхОстаток,
        |	ВзаиморасчетыОстатки.СуммаВВалютеОстаток КАК СуммаВВалютеОстаток
        |ПОМЕСТИТЬ втДолгиПокупателей
        |ИЗ
        |	РегистрНакопления.Взаиморасчеты.Остатки(
        |			&МоментВремени,
        |			Контрагент = &Контрагент
        |				И Проект <> ЗНАЧЕНИЕ(Справочник.Проекты.ПустаяСсылка)) КАК ВзаиморасчетыОстатки
        |
        |ИНДЕКСИРОВАТЬ ПО
        |	Валюта
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	втДолгиПокупателей.Контрагент КАК Контрагент,
        |	втДолгиПокупателей.Проект КАК Проект,
        |	втДолгиПокупателей.Валюта КАК Валюта,
        |	втДолгиПокупателей.СуммаВРубляхОстаток КАК СуммаВРубляхОстаток,
        |	втДолгиПокупателей.СуммаВВалютеОстаток КАК СуммаВВалютеОстаток,
        |	ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 1) КАК Курс,
        |	&Период КАК Период,
        |	втДолгиПокупателей.Проект.ДатаОплаты КАК ДатаОплаты
        |ИЗ
        |	втДолгиПокупателей КАК втДолгиПокупателей
        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(
        |				&Период,
        |				Валюта В
        |					(ВЫБРАТЬ
        |						втДолгиПокупателей.Валюта КАК Валюта
        |					ИЗ
        |						втДолгиПокупателей КАК втДолгиПокупателей)) КАК КурсыВалютСрезПоследних
        |		ПО втДолгиПокупателей.Валюта = КурсыВалютСрезПоследних.Валюта
        |
        |УПОРЯДОЧИТЬ ПО
        |	ДатаОплаты";
      
      Запрос.УстановитьПараметр("Контрагент", Контрагент);
      Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
      Запрос.УстановитьПараметр("Период", Дата);
      
      СуммаКПогашениюВРублях = СуммаПлатежа;
      
      РезультатЗапроса = Запрос.Выполнить();
      Если Не РезультатЗапроса.Пустой() Тогда
        
        Выборка = РезультатЗапроса.Выбрать();
        
        Пока Выборка.Следующий() И СуммаКПогашениюВРублях > 0 Цикл
          
          СуммаВРублях = Мин(СуммаКПогашениюВРублях, Выборка.СуммаВРубляхОстаток);
          
          Если СуммаВРублях = Выборка.СуммаВРубляхОстаток Тогда
            СуммаВВалюте = Выборка.СуммаВВалютеОстаток;
          Иначе
            СуммаВВалюте = Окр(СуммаВРублях / Выборка.Курс, 2);
            Если СуммаВВалюте = Выборка.СуммаВВалютеОстаток Тогда
              СуммаВВалюте = СуммаВВалюте - 0.01;
            КонецЕсли;
          КонецЕсли;
          
          Если СуммаВВалюте > 0 Тогда
            Движение = Движения.Взаиморасчеты.ДобавитьРасход();
            ЗаполнитьЗначенияСвойств(Движение, Выборка);
            Движение.СуммаВРублях = СуммаВРублях;
            Движение.СуммаВВалюте = СуммаВВалюте;
            
            СуммаКПогашениюВРублях = СуммаКПогашениюВРублях - СуммаВРублях;
          КонецЕсли;
          
        КонецЦикла;
        
      КонецЕсли;
      
      // авансы
      Если СуммаКПогашениюВРублях > 0 Тогда
        Движение = Движения.Взаиморасчеты.ДобавитьРасход();
        Движение.Период = Дата;
        Движение.Контрагент = Контрагент;
        Движение.Проект = Справочники.Проекты.ПустаяСсылка();
        Движение.Валюта = Справочники.Валюты.РоссийскийРубль;
        Движение.СуммаВВалюте = СуммаКПогашениюВРублях;
        Движение.СуммаВРублях = СуммаКПогашениюВРублях;
      КонецЕсли;
        
    КонецПроцедуры
     
    <p>///// Документ ПриходнаяНакладная</p>
    Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
      СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
    КонецПроцедуры
    
    <p>///// Документ РасходнаяНакладная</p> 
    Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
      СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
    КонецПроцедуры
    
    Процедура ОбработкаПроведения(Отказ, РежимПроведения)
      
      Движения.Взаиморасчеты.Записывать = Истина;
      Движения.Записать();
      
      Движения.Взаиморасчеты.Записывать = Истина;
      
      Движение = Движения.Взаиморасчеты.ДобавитьПриход();
      Движение.Период = Дата;
      Движение.Контрагент = Контрагент;
      Движение.Проект = Проект;
      
      ВалютаПроекта = ОбщегоНазначения.ВалютаПроекта(Проект);
      Движение.Валюта = ВалютаПроекта;
      
      Движение.СуммаВВалюте = СуммаПоДокументу;
      
      Курс = РегистрыСведений.КурсыВалют.ПолучитьПоследнее(Дата, Новый Структура("Валюта", ВалютаПроекта)).Курс;
      Курс = ?(Курс = 0, 1, Курс);
      
      Движение.СуммаВРублях = СуммаПоДокументу * Курс;
      
      Блокировка = Новый БлокировкаДанных;
      ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.Взаиморасчеты");
      ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
      ЭлементБлокировки.УстановитьЗначение("Контрагент", Контрагент);
      ЭлементБлокировки.УстановитьЗначение("Проект", Справочники.Проекты.ПустаяСсылка());
      Блокировка.Заблокировать();
      
      Запрос = Новый Запрос;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	-ВзаиморасчетыОстатки.СуммаВРубляхОстаток КАК ОстатокАвансаВРублях,
        |	ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 1) КАК Курс,
        |	ВЫРАЗИТЬ(-ВзаиморасчетыОстатки.СуммаВРубляхОстаток / ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 1) КАК ЧИСЛО(15, 2)) КАК ОстатокАвансаВВалюте
        |ИЗ
        |	РегистрНакопления.Взаиморасчеты.Остатки(
        |			&МоментВремени,
        |			Контрагент = &Контрагент
        |				И Проект = ЗНАЧЕНИЕ(Справочник.Проекты.ПустаяСсылка)) КАК ВзаиморасчетыОстатки
        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&МоментВремени, Валюта = &Валюта) КАК КурсыВалютСрезПоследних
        |		ПО (ИСТИНА)";
      
      Запрос.УстановитьПараметр("Валюта", ВалютаПроекта);
      Запрос.УстановитьПараметр("Контрагент", Контрагент);
      Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
      
      РезультатЗапроса = Запрос.Выполнить();
      
      Выборка = РезультатЗапроса.Выбрать();
      Выборка.Следующий();
      
      Если Выборка.ОстатокАвансаВВалюте > 0 Тогда
        
        СуммаПоДокументуВВалюте = СуммаПоДокументу;
        СуммаПоДокументуВРублях = СуммаПоДокументу * Выборка.Курс;
        
        Если Выборка.ОстатокАвансаВРублях >= СуммаПоДокументуВРублях Тогда
          СуммаСписанияАвансаВРублях = СуммаПоДокументуВРублях;
          СуммаСписанияАвансаВВалюте = СуммаПоДокументуВВалюте;
        Иначе
          СуммаСписанияАвансаВРублях = Выборка.ОстатокАвансаВРублях;
          СуммаСписанияАвансаВВалюте = Выборка.ОстатокАвансаВВалюте;
        КонецЕсли;
        
        Движение = Движения.Взаиморасчеты.ДобавитьПриход();
        Движение.Период = Дата;
        Движение.Контрагент = Контрагент;
        Движение.Проект = Справочники.Проекты.ПустаяСсылка();
        Движение.Валюта = Справочники.Валюты.РоссийскийРубль;
        Движение.СуммаВВалюте = СуммаСписанияАвансаВРублях;
        Движение.СуммаВРублях = СуммаСписанияАвансаВРублях;
        
        Движение = Движения.Взаиморасчеты.ДобавитьРасход();
        Движение.Период = Дата;
        Движение.Контрагент = Контрагент;
        Движение.Проект = Проект;
        Движение.Валюта = ВалютаПроекта;
        Движение.СуммаВВалюте = СуммаСписанияАвансаВВалюте;
        Движение.СуммаВРублях = СуммаСписанияАвансаВРублях;
        
      КонецЕсли;
      
    КонецПроцедуры
    
    <p>///// Модуль формы</p>
    &НаКлиенте
    Процедура СписокНоменклатурыКоличествоПриИзменении(Элемент)
      ТекСтрока = Элементы.СписокНоменклатуры.ТекущиеДанные;
      ОбработкаТабличнойЧасти.РассчитатьСуммуВСтроке(ТекСтрока);
    КонецПроцедуры
    
    &НаКлиенте
    Процедура СписокНоменклатурыЦенаПриИзменении(Элемент)
      ТекСтрока = Элементы.СписокНоменклатуры.ТекущиеДанные;
      ОбработкаТабличнойЧасти.РассчитатьСуммуВСтроке(ТекСтрока);
    КонецПроцедуры    
    <embed src="pdf/ОбщееОУ5.pdf" type="application/pdf" width="1200" height="800">
      <img src="pdf/ОУ5.jpg">
    <H2 class="OU">
      Бухгалтерский учет
    </H2>
    <p>///// Общий модуль ОбработкаТабличнойЧастиКлиент</p>
    Процедура РассчитатьСуммуВСтроке(ТекСтрока) Экспорт
      ТекСтрока.Сумма = ТекСтрока.Цена * ТекСтрока.Количество;
    КонецПроцедуры 
    
    <p>///// Документ Операция</p>   
    Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
      Если Не Движения.Управленческий.Записывать Тогда
        Движения.Управленческий.Прочитать();
        Движения.Управленческий.Записывать = Истина;
      КонецЕсли;
      
      Для Каждого Движение Из Движения.Управленческий Цикл
        Движение.Период = Дата;
        Движение.Активность = Не ПометкаУдаления;
      КонецЦикла;
    КонецПроцедуры
    
    Процедура ПриКопировании(ОбъектКопирования)	
      Выборка = РегистрыБухгалтерии.Управленческий.ВыбратьПоРегистратору(ОбъектКопирования.Ссылка);
      Пока Выборка.Следующий() Цикл
        Движение = Движения.Управленческий.Добавить();
        ЗаполнитьЗначенияСвойств(Движение, Выборка);
        ЗаполнитьЗначенияСвойств(Движение.СубконтоДт, Выборка.СубконтоДт);
        ЗаполнитьЗначенияСвойств(Движение.СубконтоКт, Выборка.СубконтоКт);
      КонецЦикла;
    КонецПроцедуры
     
    <p>///// Документ ПриходнаяНакладная</p>
    Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
      СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
    КонецПроцедуры
    
    Процедура ОбработкаПроведения(Отказ, РежимПроведения)
      
      // регистр Управленческий 
      Движения.Управленческий.Записывать = Истина;
      Для Каждого ТекСтрокаСписокНоменклатуры Из СписокНоменклатуры Цикл
        Движение = Движения.Управленческий.Добавить();
        Движение.СчетДт = ПланыСчетов.Управленческий.Товары;
        Движение.СчетКт = ПланыСчетов.Управленческий.Поставщики;
        Движение.Период = Дата;
        Движение.Сумма = ТекСтрокаСписокНоменклатуры.Сумма;
        Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = ТекСтрокаСписокНоменклатуры.Номенклатура;
        Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.ИнвентарныеНомера] = ТекСтрокаСписокНоменклатуры.ИнвентарныйНомер;
        Движение.КоличествоДт = ТекСтрокаСписокНоменклатуры.Количество;
      КонецЦикла;
      
    КонецПроцедуры
    
    <p>///// Документ РасходнаяНакладная</p> 
    Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
      СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
    КонецПроцедуры
    
    Процедура ОбработкаПроведения(Отказ, РежимПроведения)
      
      Движения.Управленческий.Записывать = Истина;
      Движения.Записать();
      
      Движения.Управленческий.Записывать = Истина;
      
      Блокировка = Новый БлокировкаДанных;
      ЭлементБлокировки = Блокировка.Добавить("РегистрБухгалтерии.Управленческий");
      ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
      ЭлементБлокировки.УстановитьЗначение("Счет", ПланыСчетов.Управленческий.Товары);
      ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
      ЭлементБлокировки.ИспользоватьИзИсточникаДанных(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура, "Номенклатура");
      Блокировка.Заблокировать();
      
      Запрос = Новый Запрос;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
        |	РасходнаяНакладнаяСписокНоменклатуры.ИнвентарныйНомер КАК ИнвентарныйНомер,
        |	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
        |	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма
        |ПОМЕСТИТЬ втТЧТовары
        |ИЗ
        |	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
        |ГДЕ
        |	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
        |
        |СГРУППИРОВАТЬ ПО
        |	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура,
        |	РасходнаяНакладнаяСписокНоменклатуры.ИнвентарныйНомер
        |
        |ИНДЕКСИРОВАТЬ ПО
        |	Номенклатура,
        |	ИнвентарныйНомер
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	втТЧТовары.Номенклатура КАК Номенклатура,
        |	втТЧТовары.Номенклатура.Представление КАК НоменклатураПредставление,
        |	втТЧТовары.ИнвентарныйНомер КАК ИнвентарныйНомер,
        |	втТЧТовары.ИнвентарныйНомер.Представление КАК ИнвентарныйНомерПредставление,
        |	втТЧТовары.Количество КАК Количество,
        |	втТЧТовары.Сумма КАК Сумма,
        |	ЕСТЬNULL(УправленческийОстаткиПоИнвентарнымНомерам.КоличествоОстаток, 0) КАК КоличествоОстатокИнвНомера,
        |	ЕСТЬNULL(УправленческийОстаткиПоТоварам.КоличествоОстаток, 0) КАК КоличествоОстатокТовара,
        |	ЕСТЬNULL(УправленческийОстаткиПоТоварам.СуммаОстаток, 0) КАК СуммаОстатокТовара
        |ИЗ
        |	втТЧТовары КАК втТЧТовары
        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(
        |				&МоментВремени,
        |				Счет = &СчетТовары,
        |				&ВидыСубконтоИнвНомера,
        |				(Субконто1, Субконто2) В
        |					(ВЫБРАТЬ
        |						втТЧТовары.Номенклатура КАК Номенклатура,
        |						втТЧТовары.ИнвентарныйНомер КАК ИнвентарныйНомер
        |					ИЗ
        |						втТЧТовары КАК втТЧТовары)) КАК УправленческийОстаткиПоИнвентарнымНомерам
        |		ПО втТЧТовары.Номенклатура = УправленческийОстаткиПоИнвентарнымНомерам.Субконто1
        |			И втТЧТовары.ИнвентарныйНомер = УправленческийОстаткиПоИнвентарнымНомерам.Субконто2
        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(
        |				&МоментВремени,
        |				Счет = &СчетТовары,
        |				&ВидСубконтоНоменклатура,
        |				Субконто1 В
        |					(ВЫБРАТЬ
        |						втТЧТовары.Номенклатура КАК Номенклатура
        |					ИЗ
        |						втТЧТовары КАК втТЧТовары)) КАК УправленческийОстаткиПоТоварам
        |		ПО втТЧТовары.Номенклатура = УправленческийОстаткиПоТоварам.Субконто1
        |ИТОГИ
        |	МАКСИМУМ(КоличествоОстатокТовара),
        |	МАКСИМУМ(СуммаОстатокТовара)
        |ПО
        |	Номенклатура";
      
      ВидыСубконтоИнвНомера = Новый Массив;
      ВидыСубконтоИнвНомера.Добавить(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура);
      ВидыСубконтоИнвНомера.Добавить(ПланыВидовХарактеристик.ВидыСубконто.ИнвентарныеНомера);
        
      Запрос.УстановитьПараметр("ВидыСубконтоИнвНомера",    ВидыСубконтоИнвНомера);
      Запрос.УстановитьПараметр("ВидСубконтоНоменклатура", ПланыВидовХарактеристик.ВидыСубконто.Номенклатура);
      Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
      Запрос.УстановитьПараметр("Ссылка", Ссылка);
      Запрос.УстановитьПараметр("СчетТовары", ПланыСчетов.Управленческий.Товары);
      
      РезультатЗапроса = Запрос.Выполнить();
      
      ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
      
      Пока ВыборкаНоменклатура.Следующий() Цикл
        
        ВыборкаДетальныеЗаписи = ВыборкаНоменклатура.Выбрать();
        
        СебестоимостьИтого = 0;
        КоличествоСписано  = 0;
        
        Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
        
          Если ВыборкаДетальныеЗаписи.Количество > ВыборкаДетальныеЗаписи.КоличествоОстатокИнвНомера Тогда
            Отказ = Истина;
            Сообщение = Новый СообщениеПользователю;
            Сообщение.Текст = СтрШаблон("Недостаточно товара %1 с инв.номером %2 в количестве %3",
            ВыборкаДетальныеЗаписи.НоменклатураПредставление, ВыборкаДетальныеЗаписи.ИнвентарныйНомерПредставление, 
            ВыборкаДетальныеЗаписи.Количество - ВыборкаДетальныеЗаписи.КоличествоОстатокИнвНомера);
            Сообщение.Сообщить();
          КонецЕсли;
          
          Если Отказ Тогда
            Продолжить;
          КонецЕсли;
          
          КоличествоСписано = КоличествоСписано + ВыборкаДетальныеЗаписи.Количество;
          
          Если КоличествоСписано = ВыборкаНоменклатура.КоличествоОстатокТовара Тогда
            Себестоимость = ВыборкаНоменклатура.СуммаОстатокТовара - СебестоимостьИтого;	
          Иначе
            // себестоимость
            Себестоимость = ВыборкаНоменклатура.СуммаОстатокТовара / 
                    ВыборкаНоменклатура.КоличествоОстатокТовара * ВыборкаДетальныеЗаписи.Количество;
          КонецЕсли;
          
          Движение = Движения.Управленческий.Добавить();
          Движение.Период = Дата;
          Движение.СчетДт = ПланыСчетов.Управленческий.ПрибылиУбытки;
          Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = ВыборкаДетальныеЗаписи.Номенклатура;
          Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.ИнвентарныеНомера] = ВыборкаДетальныеЗаписи.ИнвентарныйНомер;
          
          Движение.СчетКт = ПланыСчетов.Управленческий.Товары;
          Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = ВыборкаДетальныеЗаписи.Номенклатура;
          Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.ИнвентарныеНомера] = ВыборкаДетальныеЗаписи.ИнвентарныйНомер;		
          Движение.КоличествоКт = ВыборкаДетальныеЗаписи.Количество;
          
          Движение.Сумма = Себестоимость;
          
          СебестоимостьИтого = СебестоимостьИтого + Движение.Сумма;
          
          // продажа
          Движение = Движения.Управленческий.Добавить();
          Движение.Период = Дата;
          Движение.СчетДт = ПланыСчетов.Управленческий.Покупатели;
          Движение.СчетКт = ПланыСчетов.Управленческий.ПрибылиУбытки;
          Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = ВыборкаДетальныеЗаписи.Номенклатура;
          Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.ИнвентарныеНомера] = ВыборкаДетальныеЗаписи.ИнвентарныйНомер;
          Движение.Сумма = ВыборкаДетальныеЗаписи.Сумма;
        КонецЦикла;
      КонецЦикла;
    
      
    КонецПроцедуры
    
    <p>///// Модуль формы</p>
    &НаКлиенте
    Процедура СписокНоменклатурыКоличествоПриИзменении(Элемент)
      ТекСтрока = Элементы.СписокНоменклатуры.ТекущиеДанные;
      ОбработкаТабличнойЧасти.РассчитатьСуммуВСтроке(ТекСтрока);
    КонецПроцедуры
    
    &НаКлиенте
    Процедура СписокНоменклатурыЦенаПриИзменении(Элемент)
      ТекСтрока = Элементы.СписокНоменклатуры.ТекущиеДанные;
      ОбработкаТабличнойЧасти.РассчитатьСуммуВСтроке(ТекСтрока);
    КонецПроцедуры
    
    &НаКлиенте
    Процедура СписокНоменклатурыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
      Если НоваяСтрока Тогда
        ТекущиеДанные = Элементы.СписокНоменклатуры.ТекущиеДанные;
        ТекущиеДанные.Количество = 1;
      КонецЕсли;
    КонецПроцедуры    
    <embed src="pdf/ОбщееБУ5.pdf" type="application/pdf" width="1200" height="800">
      <img src="pdf/БУ5.jpg">
    <H2 class="OU">
      Расчеты
    </H2>
    <p>///// Документ НачислениеЗарплаты</p>  
    Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
      
      Если ОсновныеНачисления.Количество() = 0 Тогда
        Сообщение = Новый СообщениеПользователю;
        Сообщение.Текст = "Нет данных в документе";
        Сообщение.Сообщить();
        Отказ = Истина;
      КонецЕсли;
      
    КонецПроцедуры
    
    Процедура ОбработкаПроведения(Отказ, РежимПроведения)
      
      // I этап - подготовка записей
      
      ПериодРегистрации = НачалоМесяца(Дата);
      
      Движения.ОсновныеНачисления.Записывать = Истина;
      Для Каждого Строка Из ОсновныеНачисления Цикл
        Движение = Движения.ОсновныеНачисления.Добавить();
        ЗаполнитьЗначенияСвойств(Движение, Строка);
        Движение.ПериодДействияНачало = Строка.ДатаНачала;
        Движение.ПериодДействияКонец = КонецДня(Строка.ДатаОкончания);
        Движение.ПериодРегистрации = ПериодРегистрации;		
      КонецЦикла;
      
      Движения.ДополнительныеНачисления.Записывать = Истина;
      Для Каждого Строка Из ДополнительныеНачисления Цикл
        Движение = Движения.ДополнительныеНачисления.Добавить();
        ЗаполнитьЗначенияСвойств(Движение, Строка);
        Движение.ПериодРегистрации = ПериодРегистрации;
      КонецЦикла;
      
      Движения.Удержания.Записывать = Истина;
      Для Каждого Строка Из Удержания Цикл
        Движение = Движения.Удержания.Добавить();
        ЗаполнитьЗначенияСвойств(Движение, Строка);
        Движение.ПериодРегистрации = ПериодРегистрации;
      КонецЦикла;	
        
      Движения.Записать();
        
      // II этап - расчет
      
      Запрос = Новый Запрос;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
        |	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ЧасовПериодДействия, 0) КАК НормаЧасов,
        |	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ЧасовФактическийПериодДействия, 0) КАК ОтработаноЧасов,
        |	ЕСТЬNULL(ШкалаОкладов.Оклад, 0) КАК Оклад,
        |	ОсновныеНачисленияДанныеГрафика.Сотрудник КАК Сотрудник,
        |	ОсновныеНачисленияДанныеГрафика.Подразделение КАК Подразделение
        |ПОМЕСТИТЬ втНачисления
        |ИЗ
        |	РегистрРасчета.ОсновныеНачисления.ДанныеГрафика(Регистратор = &Регистратор) КАК ОсновныеНачисленияДанныеГрафика
        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШкалаОкладов КАК ШкалаОкладов
        |		ПО ОсновныеНачисленияДанныеГрафика.Подразделение = ШкалаОкладов.Подразделение
        |			И (ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ЧасовФактическийПериодДействия, 0) >= ШкалаОкладов.ЧасовОт)
        |			И (ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ЧасовФактическийПериодДействия, 0) < ШкалаОкладов.ЧасовДо)
        |
        |ИНДЕКСИРОВАТЬ ПО
        |	Сотрудник,
        |	Подразделение
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	втНачисления.НомерСтроки КАК НомерСтроки,
        |	втНачисления.НормаЧасов КАК НормаЧасов,
        |	ВЫБОР
        |		КОГДА втНачисления.ОтработаноЧасов > ЕСТЬNULL(ОпозданияСотрудниковОбороты.МинутОпозданияОборот / 60, 0)
        |			ТОГДА втНачисления.ОтработаноЧасов - ЕСТЬNULL(ОпозданияСотрудниковОбороты.МинутОпозданияОборот / 60, 0)
        |		ИНАЧЕ 0
        |	КОНЕЦ КАК ОтработаноЧасов,
        |	втНачисления.Оклад КАК Оклад
        |ИЗ
        |	втНачисления КАК втНачисления
        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОпозданияСотрудников.Обороты(
        |				&НачалоПериода,
        |				&КонецПериода,
        |				,
        |				(Сотрудник, Подразделение) В
        |					(ВЫБРАТЬ
        |						втНачисления.Сотрудник КАК Сотрудник,
        |						втНачисления.Подразделение КАК Подразделение
        |					ИЗ
        |						втНачисления КАК втНачисления)) КАК ОпозданияСотрудниковОбороты
        |		ПО втНачисления.Сотрудник = ОпозданияСотрудниковОбороты.Сотрудник
        |			И втНачисления.Подразделение = ОпозданияСотрудниковОбороты.Подразделение";
      
      Запрос.УстановитьПараметр("Регистратор", Ссылка);
      Запрос.УстановитьПараметр("НачалоПериода", ПериодРегистрации);
      Запрос.УстановитьПараметр("КонецПериода",  КонецМесяца(ПериодРегистрации));
      
      РезультатЗапроса = Запрос.Выполнить();
      
      Выборка = РезультатЗапроса.Выбрать();
      
      Для Каждого Движение Из Движения.ОсновныеНачисления Цикл
        
        Выборка.Сбросить();
        Если Выборка.НайтиСледующий(Движение.НомерСтроки, "НомерСтроки") Тогда
          Движение.Результат = ?(Выборка.НормаЧасов <> 0, 
                               Выборка.Оклад / Выборка.НормаЧасов * Выборка.ОтработаноЧасов,
                         0);
                                   
          ЗаполнитьЗначенияСвойств(Движение, Выборка);							   
        КонецЕсли;
                     
      КонецЦикла;
    
      Движения.ОсновныеНачисления.Записать(, Истина);
      
      Запрос = Новый Запрос;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	ДополнительныеНачисления.НомерСтроки КАК НомерСтроки,
        |	ДополнительныеНачисления.Подразделение КАК Подразделение
        |ПОМЕСТИТЬ втДопНачисления
        |ИЗ
        |	РегистрРасчета.ДополнительныеНачисления КАК ДополнительныеНачисления
        |ГДЕ
        |	ДополнительныеНачисления.Регистратор = &Регистратор
        |	И ДополнительныеНачисления.Активность
        |
        |ИНДЕКСИРОВАТЬ ПО
        |	Подразделение
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	втДопНачисления.НомерСтроки КАК НомерСтроки,
        |	ЕСТЬNULL(УправленческийОборотыДтКт.СуммаОборот, 0) КАК СуммаПродажЗаПрошлыйМесяц
        |ИЗ
        |	втДопНачисления КАК втДопНачисления
        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.ОборотыДтКт(
        |				&НачалоПрошлогоМесяца,
        |				&КонецПрошлогоМесяца,
        |				,
        |				СчетДт = &СчетПокупатели,
        |				,
        |				СчетКт = &СчетПрибылиУбытки,
        |				&ВидСубконтоПодразделения,
        |				СубконтоКт1 В
        |					(ВЫБРАТЬ
        |						втДопНачисления.Подразделение КАК Подразделение
        |					ИЗ
        |						втДопНачисления КАК втДопНачисления)) КАК УправленческийОборотыДтКт
        |		ПО втДопНачисления.Подразделение = УправленческийОборотыДтКт.СубконтоКт1";
    
      Запрос.УстановитьПараметр("ВидСубконтоПодразделения", ПланыВидовХарактеристик.ВидыСубконто.Подразделения);
      
      Запрос.УстановитьПараметр("НачалоПрошлогоМесяца", ДобавитьМесяц(ПериодРегистрации, -1));
      Запрос.УстановитьПараметр("КонецПрошлогоМесяца",  ПериодРегистрации - 1);
      
      Запрос.УстановитьПараметр("Регистратор", Ссылка);
      Запрос.УстановитьПараметр("СчетПокупатели", ПланыСчетов.Управленческий.Покупатели);
      Запрос.УстановитьПараметр("СчетПрибылиУбытки", ПланыСчетов.Управленческий.ПрибылиУбытки);
      
      РезультатЗапроса = Запрос.Выполнить();
      
      Выборка = РезультатЗапроса.Выбрать();
      
      Для Каждого Движение Из Движения.ДополнительныеНачисления Цикл
        
        Выборка.Сбросить();
        Если Выборка.НайтиСледующий(Движение.НомерСтроки, "НомерСтроки") Тогда
          
          Движение.Результат = Выборка.СуммаПродажЗаПрошлыйМесяц * Движение.Параметр / 100;
          
          ЗаполнитьЗначенияСвойств(Движение, Выборка);
          
        КонецЕсли;
        
      КонецЦикла;
    
      Движения.ДополнительныеНачисления.Записать();
      
      Запрос = Новый Запрос;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	Удержания.НомерСтроки КАК НомерСтроки,
        |	Удержания.Сотрудник КАК Сотрудник,
        |	Удержания.Подразделение КАК Подразделение
        |ПОМЕСТИТЬ втУдержания
        |ИЗ
        |	РегистрРасчета.Удержания КАК Удержания
        |ГДЕ
        |	Удержания.Регистратор = &Регистратор
        |	И Удержания.ВидРасчета = &ВидРасчета
        |
        |ИНДЕКСИРОВАТЬ ПО
        |	Сотрудник,
        |	Подразделение
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	втУдержания.НомерСтроки КАК НомерСтроки,
        |	ЕСТЬNULL(ОпозданияСотрудниковОбороты.МинутОпозданияОборот, 0) КАК МинутОпоздания
        |ИЗ
        |	втУдержания КАК втУдержания
        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОпозданияСотрудников.Обороты(
        |				&НачалоПериода,
        |				&КонецПериода,
        |				,
        |				(Сотрудник, Подразделение) В
        |					(ВЫБРАТЬ
        |						втУдержания.Сотрудник КАК Сотрудник,
        |						втУдержания.Подразделение КАК Подразделение
        |					ИЗ
        |						втУдержания КАК втУдержания)) КАК ОпозданияСотрудниковОбороты
        |		ПО втУдержания.Сотрудник = ОпозданияСотрудниковОбороты.Сотрудник
        |			И втУдержания.Подразделение = ОпозданияСотрудниковОбороты.Подразделение";
      
      Запрос.УстановитьПараметр("ВидРасчета",  ПланыВидовРасчета.Удержания.ШтрафЗаОпоздание);
      Запрос.УстановитьПараметр("Регистратор", Ссылка);
      Запрос.УстановитьПараметр("НачалоПериода", ПериодРегистрации);
      Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(ПериодРегистрации));
      
      РезультатЗапроса = Запрос.Выполнить();
      
      Выборка = РезультатЗапроса.Выбрать();
      
      // расчет удержаний
      Для Каждого Движение Из Движения.Удержания Цикл
        Выборка.Сбросить();
        Если Выборка.НайтиСледующий(Движение.НомерСтроки, "НомерСтроки") Тогда
          Движение.Результат = Цел(Выборка.МинутОпоздания / 15) * 100;
          ЗаполнитьЗначенияСвойств(Движение, Выборка);
        КонецЕсли;
      КонецЦикла;
      
      Движения.Удержания.Записать();
      
    КонецПроцедуры
    
    <p>///// Документ РегистрацияОпозданий</p>
    Процедура ОбработкаПроведения(Отказ, РежимПроведения)
      
      Движения.ОпозданияСотрудников.Записывать = Истина;
      
      Для Каждого Строка Из Сотрудники Цикл
        Движение = Движения.ОпозданияСотрудников.Добавить();
        Движение.Период = Дата;
        ЗаполнитьЗначенияСвойств(Движение, Строка);
      КонецЦикла;
      
    КонецПроцедуры
    
    <p>///// Модуль объекта ЗаполнениеГрафика</p>
    Процедура ЗаполнитьГрафик(ДатаНачала, ДатаОкончания, ВыходныеДни, Сотрудник, Подразделение) Экспорт 
      
      Набор = РегистрыСведений.ГрафикиРаботы.СоздатьНаборЗаписей();
      
      Набор.Отбор.Сотрудник.Установить(Сотрудник);
      Набор.Отбор.Подразделение.Установить(Подразделение);
      
      ЧислоСекундВСутках = 86400;
      
      Дат = ДатаНачала;	
      Пока Дат <= ДатаОкончания Цикл
        Запись = Набор.Добавить();
        Запись.Сотрудник = Сотрудник;
        Запись.Подразделение = Подразделение;
        Запись.Дата = Дат;
        Если Найти(ВыходныеДни, Строка(ДеньНедели(Дат))) Тогда
          Запись.Часов = 0;
        Иначе	          
          Запись.Часов = 8;
        КонецЕсли; 
        Дат = Дат + ЧислоСекундВСутках;
      КонецЦикла; 
      Набор.Записать();
    КонецПроцедуры
    
    <p>///// Модуль формы</p>
    &НаКлиенте
    Процедура Заполнить(Команда)
      ВыполнитьОбработку();
      Сообщить("Обработка завершена!");
    КонецПроцедуры
    
    &НаСервере
    Процедура ВыполнитьОбработку()
      ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
        ОбработкаОбъект.ЗаполнитьГрафик(ВыборПериода.ДатаНачала, ВыборПериода.ДатаОкончания, ВыходныеДни, ГрафикРаботы);
    КонецПроцедуры 
    <embed src="pdf/ОбщееСПР5.pdf" type="application/pdf" width="1200" height="800">
      <img src="pdf/СПР5.jpg">
  </pre>
  <H2 class="OU">
    Бизнес процесс
  </H2>
  <pre>
   <p> //// БизнесПроцессСУсловием ФормаБизнесПроцесса </p> 
&НаСервере
Процедура ОбновитьКартуМаршрута()
	БПОбъект = РеквизитФормыВЗначение("Объект");
	КартаМаршрута = БПОбъект.ПолучитьКартуМаршрута();	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)	
	ОбновитьКартуМаршрута();	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	КартаМаршрута = ТекущийОбъект.ПолучитьКартуМаршрута();
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "ЗадачаВыполнена" И Параметр = Объект.Ссылка Тогда
		Прочитать();
		ОбновитьКартуМаршрута();	
	КонецЕсли;
КонецПроцедуры

<p>//// ЗадачиИсполнителям ФормаЗадачи</p>
&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Оповестить("ЗадачаВыполнена", Объект.БизнесПроцесс);
КонецПроцедуры
  </pre>
  <embed src="pdf/ОбщееБП5.pdf" type="application/pdf" width="1200" height="800">
</body>

</html>