<!doctype html>
<html lang="ru">

<head>
  <meta charset="utf-8">
  <title>HTML Document Template</title>
  <link href="styles.css" rel="stylesheet">
</head>

<body>
  <div class="header__menu">
    <ul>
      <li><a href="str/1">1</a></li>
      <li><a href="str/2">2</a></li>
      <li><a href="str/3">3</a></li>
      <li><a href="str/4">4</a></li>
      <li><a href="str/5">5</a></li>
      <li><a href="str/6">6</a></li>
      <li><a href="str/7">7</a></li>
      <li><a href="str/8">8</a></li>
      <li><a href="str/9">9</a></li>
      <li><a href="str/10">10</a></li>
      <li><a href="str/11">11</a></li>
      <li><a href="str/12">12</a></li>
      <li><a href="str/13">13</a></li>
      <li><a href="str/14">14</a></li>
      <li><a href="str/15">15</a></li>
      <li><a href="str/16">16</a></li>
    </ul>
  </div>
  <h1>9</h1>
  <H2 class="OU">
    Оперативный учет
  </H2>
  <pre>
    <p>///// Общий модуль ОбработкаТабличнойЧасти</p>
    Процедура РассчитатьСуммуВСтроке(ТекСтрока) Экспорт
      ТекСтрока.Сумма = ТекСтрока.Цена * ТекСтрока.Количество;
    КонецПроцедуры  
    
    <p>///// Общий модуль РаботаССКД</p>    
    Функция НижнийРегистр(ИсходнаяСтрока) Экспорт
      Возврат НРег(ИсходнаяСтрока);
    КонецФункции
    
    <p>///// Документ ПриходнаяНакладная</p>
    Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
      СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
    КонецПроцедуры
    
    Процедура ОбработкаПроведения(Отказ, Режим)
    
      Движения.ОстаткиНоменклатуры.Записывать = Истина;
      
      Запрос = Новый Запрос;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
        |	ПриходнаяНакладнаяСписокНоменклатуры.Свойство КАК Свойство,
        |	СУММА(ПриходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
        |	СУММА(ПриходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма
        |ИЗ
        |	Документ.ПриходнаяНакладная.СписокНоменклатуры КАК ПриходнаяНакладнаяСписокНоменклатуры
        |ГДЕ
        |	ПриходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
        |
        |СГРУППИРОВАТЬ ПО
        |	ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура,
        |	ПриходнаяНакладнаяСписокНоменклатуры.Свойство";
      
      Запрос.УстановитьПараметр("Ссылка", Ссылка);
      
      РезультатЗапроса = Запрос.Выполнить();
      
      Выборка = РезультатЗапроса.Выбрать();
      
      Пока Выборка.Следующий() Цикл
        Движение = Движения.ОстаткиНоменклатуры.Добавить();
        Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
        Движение.Период = Дата;
        Движение.Номенклатура = Выборка.Номенклатура;
        Движение.Свойство = Выборка.Свойство;
        Движение.Количество = Выборка.Количество;
        Движение.Стоимость = Выборка.Сумма;
      КонецЦикла;
       
    КонецПроцедуры
    
    <p>///// Документ РасходнаяНакладная</p> 
    Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
      СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
    КонецПроцедуры
    
    Процедура ОбработкаПроведения(Отказ, Режим)
    
      // удаление движения
      Движения.ОстаткиНоменклатуры.Записывать = Истина;
      Движения.Записать();
      
      // взвели флаг
      Движения.ОстаткиНоменклатуры.Записывать = Истина;
      
      Блокировка = Новый БлокировкаДанных;
      ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
      ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
      ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
      ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
      ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Свойство", "Свойство");
      Блокировка.Заблокировать();
      
      // контроль остатков
      Запрос = Новый Запрос;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
        |	РасходнаяНакладнаяСписокНоменклатуры.Свойство КАК Свойство,
        |	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество
        |ПОМЕСТИТЬ втТЧТовары
        |ИЗ
        |	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
        |ГДЕ
        |	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
        |
        |СГРУППИРОВАТЬ ПО
        |	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура,
        |	РасходнаяНакладнаяСписокНоменклатуры.Свойство
        |
        |ИНДЕКСИРОВАТЬ ПО
        |	Номенклатура
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	втТЧТовары.Номенклатура КАК Номенклатура,
        |	втТЧТовары.Номенклатура.Представление КАК НоменклатураПредставление,
        |	втТЧТовары.Свойство КАК Свойство,
        |	втТЧТовары.Свойство.Представление КАК СвойствоПредставление,
        |	втТЧТовары.Количество КАК Количество,
        |	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
        |	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.СтоимостьОстаток, 0) КАК СтоимостьОстаток
        |ИЗ
        |	втТЧТовары КАК втТЧТовары
        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
        |				&МоментВремени,
        |				(Номенклатура, Свойство) В
        |					(ВЫБРАТЬ
        |						втТЧТовары.Номенклатура,
        |						втТЧТовары.Свойство КАК Номенклатура
        |					ИЗ
        |						втТЧТовары КАК втТЧТовары)) КАК ОстаткиНоменклатурыОстатки
        |		ПО втТЧТовары.Номенклатура = ОстаткиНоменклатурыОстатки.Номенклатура
        |			И втТЧТовары.Свойство = ОстаткиНоменклатурыОстатки.Свойство";
      
      Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
      Запрос.УстановитьПараметр("Ссылка", Ссылка);
      
      РезультатЗапроса = Запрос.Выполнить();
      
      Выборка = РезультатЗапроса.Выбрать();
      
      Пока Выборка.Следующий() Цикл
        Если Выборка.Количество > Выборка.КоличествоОстаток Тогда
          Сообщение = Новый СообщениеПользователю;
          Сообщение.Текст = "Не хватает товара " + Выборка.НоменклатураПредставление + 
          " со свойством " + Выборка.СвойствоПредставление +
          " в количестве " + (Выборка.Количество - Выборка.КоличествоОстаток);
          Сообщение.Сообщить();	
          
          Отказ = Истина;
          
        КонецЕсли;	
        
        Если Отказ Тогда
          Продолжить;
        КонецЕсли;
        
        Если Выборка.Количество = Выборка.КоличествоОстаток Тогда
          Себестоимость = Выборка.СтоимостьОстаток;
        Иначе
          Себестоимость = Выборка.СтоимостьОстаток / Выборка.КоличествоОстаток * Выборка.Количество;
        КонецЕсли;
                
        Движение = Движения.ОстаткиНоменклатуры.Добавить();
        Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
        Движение.Период = Дата;
        Движение.Номенклатура = Выборка.Номенклатура;
        Движение.Свойство = Выборка.Свойство;
        Движение.Количество = Выборка.Количество;
        Движение.Стоимость = Себестоимость;
        
      КонецЦикла;
      
    КонецПроцедуры
    
    <p>///// Модуль формы</p>
    &НаКлиенте
    Процедура СписокНоменклатурыКоличествоПриИзменении(Элемент)
      ТекСтрока = Элементы.СписокНоменклатуры.ТекущиеДанные;
      ОбработкаТабличнойЧасти.РассчитатьСуммуВСтроке(ТекСтрока);
    КонецПроцедуры
    
    &НаКлиенте
    Процедура СписокНоменклатурыЦенаПриИзменении(Элемент)
      ТекСтрока = Элементы.СписокНоменклатуры.ТекущиеДанные;
      ОбработкаТабличнойЧасти.РассчитатьСуммуВСтроке(ТекСтрока);
    КонецПроцедуры
    <embed src="pdf/ОбщееОУ9.pdf" type="application/pdf" width="1200" height="800">
      <img src="pdf/ОУ9.jpg">
    <H2 class="OU">
      Бухгалтерский учет
    </H2>
    <p>///// Общий модуль ОбработкаТабличнойЧастиКлиент</p>
    Процедура РассчитатьСуммуВСтроке(ТекСтрока) Экспорт
      ТекСтрока.Сумма = ТекСтрока.Цена * ТекСтрока.Количество;
    КонецПроцедуры 
    
    <p>///// Общий модуль ОбщегоНазначенияСервер</p>
    Функция ВалютаАгента(Агент) Экспорт
      
      Запрос = Новый Запрос;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	Агенты.Валюта КАК Валюта
        |ИЗ
        |	Справочник.Агенты КАК Агенты
        |ГДЕ
        |	Агенты.Ссылка = &Ссылка";
      
      Запрос.УстановитьПараметр("Ссылка", Агент);
      
      РезультатЗапроса = Запрос.Выполнить();
      
      Выборка = РезультатЗапроса.Выбрать();
      
      Если Выборка.Следующий() Тогда
        Возврат Выборка.Валюта;
      КонецЕсли;
      
      Возврат Неопределено;
      
    КонецФункции
    
    <p>///// Документ Операция</p>   
    Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
      Если Не Движения.Управленческий.Записывать Тогда
        Движения.Управленческий.Прочитать();
        Движения.Управленческий.Записывать = Истина;
      КонецЕсли;
      
      Для Каждого Движение Из Движения.Управленческий Цикл
        Движение.Период = Дата;
        Движение.Активность = Не ПометкаУдаления;
      КонецЦикла;
    КонецПроцедуры
    
    Процедура ПриКопировании(ОбъектКопирования)	
      Выборка = РегистрыБухгалтерии.Управленческий.ВыбратьПоРегистратору(ОбъектКопирования.Ссылка);
      Пока Выборка.Следующий() Цикл
        Движение = Движения.Управленческий.Добавить();
        ЗаполнитьЗначенияСвойств(Движение, Выборка);
        ЗаполнитьЗначенияСвойств(Движение.СубконтоДт, Выборка.СубконтоДт);
        ЗаполнитьЗначенияСвойств(Движение.СубконтоКт, Выборка.СубконтоКт);
      КонецЦикла;
    КонецПроцедуры
     
    <p>///// Документ ПриходнаяНакладная</p>
    Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
      СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
    КонецПроцедуры
    
    <p>///// Документ РасходнаяНакладная</p> 
    Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
      СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
    КонецПроцедуры
    
    Процедура ОбработкаПроведения(Отказ, РежимПроведения)
      
      Движения.Управленческий.Записывать = Истина;
      Движения.Записать();
      
      Движения.Управленческий.Записывать = Истина;
      
      Блокировка = Новый БлокировкаДанных;
      ЭлементБлокировки = Блокировка.Добавить("РегистрБухгалтерии.Управленческий");
      ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
      ЭлементБлокировки.УстановитьЗначение("Счет", ПланыСчетов.Управленческий.Товары);
      ЭлементБлокировки.УстановитьЗначение(ПланыВидовХарактеристик.ВидыСубконто.Склады, Склад);
      ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
      ЭлементБлокировки.ИспользоватьИзИсточникаДанных(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура, "Номенклатура");
      Блокировка.Заблокировать();
      
      Запрос = Новый Запрос;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
        |	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
        |	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма
        |ПОМЕСТИТЬ втТЧТовары
        |ИЗ
        |	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
        |ГДЕ
        |	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
        |
        |СГРУППИРОВАТЬ ПО
        |	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура
        |
        |ИНДЕКСИРОВАТЬ ПО
        |	Номенклатура
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	втТЧТовары.Номенклатура КАК Номенклатура,
        |	втТЧТовары.Номенклатура.Представление КАК НоменклатураПредставление,
        |	втТЧТовары.Количество КАК Количество,
        |	втТЧТовары.Сумма КАК Сумма,
        |	ЕСТЬNULL(УправленческийОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
        |	ЕСТЬNULL(УправленческийОстатки.СуммаОстаток, 0) КАК СуммаОстаток
        |ИЗ
        |	втТЧТовары КАК втТЧТовары
        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(
        |				&МоментВремени,
        |				Счет = &СчетТовары,
        |				&ВидыСубконто,
        |				Субконто1 В
        |						(ВЫБРАТЬ
        |							втТЧТовары.Номенклатура КАК Номенклатура
        |						ИЗ
        |							втТЧТовары КАК втТЧТовары)
        |					И Субконто2 = &Склад) КАК УправленческийОстатки
        |		ПО втТЧТовары.Номенклатура = УправленческийОстатки.Субконто1";
      
      ВидыСубконто = Новый Массив;
      ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура);
      ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконто.Склады);
      
      Запрос.УстановитьПараметр("ВидыСубконто", ВидыСубконто);
      Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
      Запрос.УстановитьПараметр("Склад", Склад);
      Запрос.УстановитьПараметр("Ссылка", Ссылка);
      Запрос.УстановитьПараметр("СчетТовары", ПланыСчетов.Управленческий.Товары);
      
      РезультатЗапроса = Запрос.Выполнить();
      
      Выборка = РезультатЗапроса.Выбрать();
      
      СебестоимостьИтого = 0;
      
      Пока Выборка.Следующий() Цикл
        
        Если Выборка.Количество > Выборка.КоличествоОстаток Тогда
          Отказ = Истина;
          Сообщение = Новый СообщениеПользователю;
          Сообщение.Текст = СтрШаблон("Недостаточно товара %1 в количестве %2", 
          Выборка.НоменклатураПредставление, Выборка.Количество - Выборка.КоличествоОстаток);
          Сообщение.Сообщить();
        КонецЕсли;
        
        Если Отказ Тогда
          Продолжить;
        КонецЕсли;
        
        // себестоимость
        Движение = Движения.Управленческий.Добавить();
        Движение.Период = Дата;
        Движение.СчетДт = ПланыСчетов.Управленческий.ПрибылиУбытки;
        Движение.СчетКт = ПланыСчетов.Управленческий.Товары;
        Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = Выборка.Номенклатура;
        Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Склады] = Склад;
        Движение.КоличествоКт = Выборка.Количество;
        Движение.Сумма = ?(Выборка.Количество = Выборка.КоличествоОстаток,
                   Выборка.СуммаОстаток,
                   Выборка.СуммаОстаток / Выборка.КоличествоОстаток * Выборка.Количество);
                   
        СебестоимостьИтого = СебестоимостьИтого + Движение.Сумма;						   
                   
      КонецЦикла;
      
      Если Отказ Тогда
        Возврат;
      КонецЕсли;
      
      Движение = Движения.Управленческий.Добавить();
      Движение.Период = Дата;
      Движение.СчетДт = ПланыСчетов.Управленческий.Покупатели;
      Движение.СчетКт = ПланыСчетов.Управленческий.ПрибылиУбытки;
      Движение.Сумма  = СуммаПоДокументу;
      
      // агентское вознаграждение
      Если Не Агент.Пустая() Тогда		
        Валюта = ОбщегоНазначения.ВалютаАгента(Агент);
        Курс = РегистрыСведений.КурсыВалют.ПолучитьПоследнее(Дата, Новый Структура("Валюта", Валюта)).Курс;
        Если Валюта <> Справочники.Валюты.РоссийскийРубль И Курс = 0 Тогда
          Отказ = Истина;
          Сообщение = Новый СообщениеПользователю;
          Сообщение.Текст = "Не указан курс валюты агента";
          Сообщение.Сообщить();
          Возврат;
        КонецЕсли;
        
        Курс = ?(Курс = 0, 1, Курс);
      
        СуммаВознаграждения = Окр(0.2*(СуммаПоДокументу - СебестоимостьИтого), 2);
        СуммаВознагражденияВВалюте =  Окр(СуммаВознаграждения / Курс, 2);
        Если СуммаВознаграждения > 0 И СуммаВознагражденияВВалюте > 0 Тогда
          Движение = Движения.Управленческий.Добавить();
          Движение.Период = Дата;
          Движение.СчетДт = ПланыСчетов.Управленческий.ПрибылиУбытки;
          Движение.СчетКт = ПланыСчетов.Управленческий.Агенты;
          Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Агенты] = Агент;
          Движение.СуммаВалКт = СуммаВознагражденияВВалюте;
          Движение.Сумма = СуммаВознаграждения;
        КонецЕсли;		
      КонецЕсли;
    
      
    КонецПроцедуры
    
    <p>///// Модуль формы</p>
    &НаКлиенте
    Процедура СписокНоменклатурыКоличествоПриИзменении(Элемент)
      ТекСтрока = Элементы.СписокНоменклатуры.ТекущиеДанные;
      ОбработкаТабличнойЧасти.РассчитатьСуммуВСтроке(ТекСтрока);
    КонецПроцедуры
    
    &НаКлиенте
    Процедура СписокНоменклатурыЦенаПриИзменении(Элемент)
      ТекСтрока = Элементы.СписокНоменклатуры.ТекущиеДанные;
      ОбработкаТабличнойЧасти.РассчитатьСуммуВСтроке(ТекСтрока);
    КонецПроцедуры
    <embed src="pdf/ОбщееБУ9.pdf" type="application/pdf" width="1200" height="800">
      <img src="pdf/БУ9.jpg">
    <H2 class="OU">
      Расчеты
    </H2>
    <p>///// Документ НачислениеЗарплаты</p>   
    Процедура ОбработкаПроведения(Отказ, РежимПроведения)
      
      ПериодРегистрации = НачалоМесяца(Дата);
      Движения.ОсновныеНачисления.Записывать = Истина;
      
      Запрос = Новый Запрос;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	НачислениеЗарплатыОсновныеНачисления.Сотрудник КАК Сотрудник,
        |	НачислениеЗарплатыОсновныеНачисления.Подразделение КАК Подразделение,
        |	НачислениеЗарплатыОсновныеНачисления.ГрафикРаботы КАК ГрафикРаботы,
        |	НачислениеЗарплатыОсновныеНачисления.ВидРасчета КАК ВидРасчета,
        |	НачислениеЗарплатыОсновныеНачисления.ДатаНачала КАК ПериодДействияНачало,
        |	КОНЕЦПЕРИОДА(НачислениеЗарплатыОсновныеНачисления.ДатаОкончания, ДЕНЬ) КАК ПериодДействияКонец,
        |	РАЗНОСТЬДАТ(НачислениеЗарплатыОсновныеНачисления.ДатаНачала, КОНЕЦПЕРИОДА(НачислениеЗарплатыОсновныеНачисления.ДатаОкончания, ДЕНЬ), МЕСЯЦ) КАК КоличествоЛишнихМесяцев,
        |	&ПериодРегистрации КАК ПериодРегистрации,
        |	ВЫБОР
        |		КОГДА НачислениеЗарплатыОсновныеНачисления.ВидРасчета = ЗНАЧЕНИЕ(ПланВидовРасчета.ОсновныеНачисления.Больничный)
        |			ТОГДА НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(НачислениеЗарплатыОсновныеНачисления.ДатаНачала, МЕСЯЦ, -1), МЕСЯЦ)
        |	КОНЕЦ КАК БазовыйПериодНачало,
        |	ВЫБОР
        |		КОГДА НачислениеЗарплатыОсновныеНачисления.ВидРасчета = ЗНАЧЕНИЕ(ПланВидовРасчета.ОсновныеНачисления.Больничный)
        |			ТОГДА КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(НачислениеЗарплатыОсновныеНачисления.ДатаНачала, МЕСЯЦ, -1), МЕСЯЦ)
        |	КОНЕЦ КАК БазовыйПериодКонец
        |ИЗ
        |	Документ.НачислениеЗарплаты.ОсновныеНачисления КАК НачислениеЗарплатыОсновныеНачисления
        |ГДЕ
        |	НачислениеЗарплатыОсновныеНачисления.Ссылка = &Ссылка";
      
      Запрос.УстановитьПараметр("Ссылка", Ссылка);
      Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
      
      РезультатЗапроса = Запрос.Выполнить();
      
      Выборка = РезультатЗапроса.Выбрать();
      
      Пока Выборка.Следующий() Цикл
        
        Если Выборка.ВидРасчета = ПланыВидовРасчета.ОсновныеНачисления.Больничный И Выборка.КоличествоЛишнихМесяцев > 0 Тогда
          Для НомерИтерации = 0 По Выборка.КоличествоЛишнихМесяцев Цикл
            МесяцНачало = ДобавитьМесяц(НачалоМесяца(Выборка.ПериодДействияНачало), НомерИтерации);
            МесяцКонец  = КонецМесяца(МесяцНачало);
            
            Движение = Движения.ОсновныеНачисления.Добавить();
            ЗаполнитьЗначенияСвойств(Движение, Выборка);
            Движение.ПериодДействияНачало = Макс(МесяцНачало, Выборка.ПериодДействияНачало);
            Движение.ПериодДействияКонец = Мин(МесяцКонец, Выборка.ПериодДействияКонец);
          КонецЦикла;
        Иначе
          Движение = Движения.ОсновныеНачисления.Добавить();
          ЗаполнитьЗначенияСвойств(Движение, Выборка);			
        КонецЕсли;
        
      КонецЦикла;
      
        ТаблицаДополнения = Движения.ОсновныеНачисления.ПолучитьДополнение();
      Для Каждого СтрокаДополнения Из ТаблицаДополнения Цикл
        Движение = Движения.ОсновныеНачисления.Добавить();
        ЗаполнитьЗначенияСвойств(Движение, СтрокаДополнения, "Сотрудник, Подразделение, ГрафикРаботы, ВидРасчета, Оклад");
        Движение.ПериодРегистрации = СтрокаДополнения.ПериодРегистрацииСторно;
        Движение.ПериодДействияНачало = СтрокаДополнения.ПериодДействияНачалоСторно;
        Движение.ПериодДействияКонец = СтрокаДополнения.ПериодДействияКонецСторно;
        Движение.Сторно = Истина;
      КонецЦикла;
        
      Движения.Записать();
      
      Запрос = Новый Запрос;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
        |	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ЧасовПериодДействия, 0) КАК НормаЧасов,
        |	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ЧасовФактическийПериодДействия, 0) КАК ОтработаноЧасов,
        |	ОсновныеНачисленияДанныеГрафика.Сотрудник КАК Сотрудник,
        |	ОсновныеНачисленияДанныеГрафика.Подразделение КАК Подразделение,
        |	ОсновныеНачисленияДанныеГрафика.Сторно КАК Сторно,
        |	ОсновныеНачисленияДанныеГрафика.Оклад КАК Оклад
        |ПОМЕСТИТЬ втДанныеГрафика
        |ИЗ
        |	РегистрРасчета.ОсновныеНачисления.ДанныеГрафика(
        |			Регистратор = &Ссылка
        |				И ВидРасчета В (&ВидыРасчета)) КАК ОсновныеНачисленияДанныеГрафика
        |
        |ИНДЕКСИРОВАТЬ ПО
        |	Сотрудник,
        |	Подразделение
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	втДанныеГрафика.НомерСтроки КАК НомерСтроки,
        |	втДанныеГрафика.НормаЧасов КАК НормаЧасов,
        |	втДанныеГрафика.ОтработаноЧасов КАК ОтработаноЧасов,
        |	ВЫБОР
        |		КОГДА НЕ втДанныеГрафика.Сторно
        |			ТОГДА ЕСТЬNULL(СведенияОСотрудникахСрезПоследних.Оклад, 0)
        |		ИНАЧЕ втДанныеГрафика.Оклад
        |	КОНЕЦ КАК Оклад
        |ИЗ
        |	втДанныеГрафика КАК втДанныеГрафика,
        |	РегистрСведений.СведенияОСотрудниках.СрезПоследних(
        |			&ПериодРегистрации,
        |			(Сотрудник, Подразделение) В
        |				(ВЫБРАТЬ
        |					втДанныеГрафика.Сотрудник КАК Сотрудник,
        |					втДанныеГрафика.Подразделение КАК Подразделение
        |				ИЗ
        |					втДанныеГрафика КАК втДанныеГрафика)) КАК СведенияОСотрудникахСрезПоследних";
    
      ВидыРасчета = Новый Массив;
      ВидыРасчета.Добавить(ПланыВидовРасчета.ОсновныеНачисления.Оклад);
      Запрос.УстановитьПараметр("ВидыРасчета", ВидыРасчета);
      
      Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
      Запрос.УстановитьПараметр("Ссылка", Ссылка);
      
      РезультатЗапроса = Запрос.Выполнить();
      
      Выборка = РезультатЗапроса.Выбрать();
      
      Для Каждого Движение Из Движения.ОсновныеНачисления Цикл
        
        Если ВидыРасчета.Найти(Движение.ВидРасчета) = Неопределено Тогда
          Продолжить;
        КонецЕсли;
        
        Выборка.Сбросить();
        Выборка.НайтиСледующий(Движение.НомерСтроки, "НомерСтроки");
        
        Если Движение.ВидРасчета = ПланыВидовРасчета.ОсновныеНачисления.Оклад Тогда
          Движение.Результат = ?(Выборка.НормаЧасов <> 0,
                         Выборка.Оклад / Выборка.НормаЧасов * Выборка.ОтработаноЧасов,
                       0) * ?(Движение.Сторно, -1, 1);
          ЗаполнитьЗначенияСвойств(Движение, Выборка, "Оклад, НормаЧасов, ОтработаноЧасов");								  
        КонецЕсли;
        
      КонецЦикла;
      
      Движения.ОсновныеНачисления.Записать(, Истина);
      
      Запрос = Новый Запрос;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
        |	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ДнейФактическийПериодДействия, 0) КАК ОтработаноДней,
        |	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ДнейБазовыйПериод, 0) КАК БазаРабочихДней,
        |	ЕСТЬNULL(ОсновныеНачисленияБазаОсновныеНачисления.РезультатБаза, 0) КАК БазаНачислений
        |ИЗ
        |	РегистрРасчета.ОсновныеНачисления.ДанныеГрафика(
        |			Регистратор = &Ссылка
        |				И ВидРасчета В (&ВидыРасчета)) КАК ОсновныеНачисленияДанныеГрафика
        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ОсновныеНачисления.БазаОсновныеНачисления(
        |				&Измерения,
        |				&Измерения,
        |				,
        |				Регистратор = &Ссылка
        |					И ВидРасчета В (&ВидыРасчета)) КАК ОсновныеНачисленияБазаОсновныеНачисления
        |		ПО ОсновныеНачисленияДанныеГрафика.НомерСтроки = ОсновныеНачисленияБазаОсновныеНачисления.НомерСтроки";
      
      ВидыРасчета = Новый Массив;
      ВидыРасчета.Добавить(ПланыВидовРасчета.ОсновныеНачисления.Больничный);
      Запрос.УстановитьПараметр("ВидыРасчета", ВидыРасчета);
      
      Измерения = Новый Массив;
      Измерения.Добавить("Сотрудник");
      Измерения.Добавить("Подразделение");
      Запрос.УстановитьПараметр("Измерения", Измерения);
      
      Запрос.УстановитьПараметр("Ссылка", Ссылка);
      
      РезультатЗапроса = Запрос.Выполнить();
      
      Выборка = РезультатЗапроса.Выбрать();
      
      Для Каждого Движение Из Движения.ОсновныеНачисления Цикл
        
        Если ВидыРасчета.Найти(Движение.ВидРасчета) = Неопределено Тогда
          Продолжить;
        КонецЕсли;
        
        Выборка.Сбросить();
        Выборка.НайтиСледующий(Движение.НомерСтроки, "НомерСтроки");
        
        Если Движение.ВидРасчета = ПланыВидовРасчета.ОсновныеНачисления.Больничный Тогда
          Движение.Результат = ?(Выборка.БазаРабочихДней <> 0,
                       Выборка.БазаНачислений / Выборка.БазаРабочихДней * Выборка.ОтработаноДней,
                       0);
          ЗаполнитьЗначенияСвойств(Движение, Выборка, "БазаНачислений, БазаРабочихДней, ОтработаноДней");								   
        КонецЕсли;
        
      КонецЦикла;
      
      Движения.ОсновныеНачисления.Записать(, Истина);
      
    КонецПроцедуры
    
    <p>///// Модуль объекта ЗаполнениеГрафика</p>
    Процедура ЗаполнитьГрафик(ДатаНачала, ДатаОкончания, ВыходныеДни, ГрафикРаботы) Экспорт 
      
      Набор = РегистрыСведений.ГрафикиРаботы.СоздатьНаборЗаписей();
      
      Набор.Отбор.ГрафикРаботы.Установить(ГрафикРаботы);
        
      ЧислоСекундВСутках = 86400;
      
      Дат = ДатаНачала;	
      Пока Дат <= ДатаОкончания Цикл
        Запись = Набор.Добавить();
        Запись.ГрафикРаботы = ГрафикРаботы;
        Запись.Дата = Дат;
        Если Найти(ВыходныеДни, Строка(ДеньНедели(Дат))) Тогда
          Запись.Часов = 0;
          Запись.Дней  = 0;
        Иначе	          
          Запись.Часов = 8;
          Запись.Дней  = 1;
        КонецЕсли; 
        Дат = Дат + ЧислоСекундВСутках;
      КонецЦикла; 
      Набор.Записать();
    КонецПроцедуры
    
    <p>///// Модуль формы</p>
    &НаКлиенте
    Процедура Заполнить(Команда)
      ВыполнитьОбработку();
      Сообщить("Обработка завершена!");
    КонецПроцедуры
    
    &НаСервере
    Процедура ВыполнитьОбработку()
      ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
        ОбработкаОбъект.ЗаполнитьГрафик(ВыборПериода.ДатаНачала, ВыборПериода.ДатаОкончания, ВыходныеДни, ГрафикРаботы);
    КонецПроцедуры 
    <embed src="pdf/ОбщееСПР9.pdf" type="application/pdf" width="1200" height="800">
      <img src="pdf/СПР9.jpg">
  </pre>
</body>

</html>