<!doctype html>
<html lang="ru">

<head>
	<meta charset="utf-8">
	<title>HTML Document Template</title>
	<link href="styles.css" rel="stylesheet">
</head>

<body>
	<div class="header__menu">
		<ul>
			<li><a href="1">1</a></li>
			<li><a href="2">2</a></li>
			<li><a href="3">3</a></li>
			<li><a href="4">4</a></li>
			<li><a href="5">5</a></li>
			<li><a href="6">6</a></li>
			<li><a href="7">7</a></li>
			<li><a href="8">8</a></li>
			<li><a href="9">9</a></li>
			<li><a href="10">10</a></li>
			<li><a href="11">11</a></li>
			<li><a href="12">12</a></li>
			<li><a href="13">13</a></li>
			<li><a href="14">14</a></li>
			<li><a href="15">15</a></li>
			<li><a href="16">16</a></li>
		</ul>
	</div>
	<h1>1</h1>
	<H2 class="OU">
		Оперативный учет
	</H2>

	<pre>
                        <p>///// Общий модуль ОбработкаТабличнойЧасти</p>
                        Процедура РассчитатьСуммуВСтроке(ТекСтрока) Экспорт
                        	ТекСтрока.Сумма = ТекСтрока.Цена * ТекСтрока.Количество;
                        КонецПроцедуры

                        <p>///// Документ НазначениеУчетнойПолитики</p>
                        Процедура ОбработкаПроведения(Отказ, Режим)
                        	// регистр УчетнаяПолитика
                        	Движения.УчетнаяПолитика.Записывать = Истина;
                        	Движение = Движения.УчетнаяПолитика.Добавить();
                        	Движение.Период = Дата;
                        	Движение.МетодСписанияСебестоимости = МетодСписанияСебестоимости;
                        КонецПроцедуры

                        Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
                        	Дата = НачалоГода(Дата);
                        КонецПроцедуры

                        <p>///// Документ ПриходнаяНакладная</p>
                        Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
                        	СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
                        КонецПроцедуры

                        Процедура ОбработкаПроведения(Отказ, РежимПроведения)

                        	Движения.ОстаткиНоменклатуры.Записывать = Истина;
                        	Движения.СебестоимостьТоваров.Записывать = Истина;

                        	Запрос = Новый Запрос;
                        	Запрос.Текст =
                        		"ВЫБРАТЬ
                        		|	ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
                        		|	&Склад КАК Склад,
                        		|	ПриходнаяНакладнаяСписокНоменклатуры.Ссылка КАК Партия,
                        		|	СУММА(ПриходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
                        		|	СУММА(ПриходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Стоимость,
                        		|	&Период КАК Период
                        		|ИЗ
                        		|	Документ.ПриходнаяНакладная.СписокНоменклатуры КАК ПриходнаяНакладнаяСписокНоменклатуры
                        		|ГДЕ
                        		|	ПриходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
                        		|
                        		|СГРУППИРОВАТЬ ПО
                        		|	ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура,
                        		|	ПриходнаяНакладнаяСписокНоменклатуры.Ссылка";

                        	Запрос.УстановитьПараметр("Ссылка", Ссылка);
                        	Запрос.УстановитьПараметр("Склад", Склад);
                        	Запрос.УстановитьПараметр("Период", Дата);

                        	РезультатЗапроса = Запрос.Выполнить();

                        	Выборка = РезультатЗапроса.Выбрать();

                        	Пока Выборка.Следующий() Цикл
                        		Движение = Движения.ОстаткиНоменклатуры.ДобавитьПриход();
                        		ЗаполнитьЗначенияСвойств(Движение, Выборка);

                        		Движение = Движения.СебестоимостьТоваров.ДобавитьПриход();
                        		ЗаполнитьЗначенияСвойств(Движение, Выборка);
                        	КонецЦикла;

                        КонецПроцедуры

                        <p>///// Документ РасходнаяНакладная</p>
                        Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
                        	СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
                        КонецПроцедуры

                        Процедура ОбработкаПроведения(Отказ, РежимПроведения)

                        	Результат = РегистрыСведений.УчетнаяПолитика.ПолучитьПоследнее(Дата);
                        	Если Результат.МетодСписанияСебестоимости = Перечисления.УчетнаяПолитика.ПустаяСсылка() Тогда
                        		Отказ = Истина;

                        		Сообщение = Новый СообщениеПользователю;
                        		Сообщение.Текст = "Не указан метод списания себестоимости";
                        		Сообщение.Сообщить();

                        		Возврат;
                        	КонецЕсли;

                        	МетодСписанияСебестоимости = Результат.МетодСписанияСебестоимости;

                        	// регистр ОстаткиНоменклатуры
                        	Запрос = Новый Запрос;
                        	МенеджерВТ = Новый МенеджерВременныхТаблиц;
                        	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
                        	Запрос.Текст =
                        		"ВЫБРАТЬ
                        		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
                        		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество
                        		|ПОМЕСТИТЬ втТЧТовары
                        		|ИЗ
                        		|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
                        		|ГДЕ
                        		|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
                        		|	И РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Товар)
                        		|
                        		|СГРУППИРОВАТЬ ПО
                        		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура
                        		|
                        		|ИНДЕКСИРОВАТЬ ПО
                        		|	Номенклатура
                        		|;
                        		|
                        		|////////////////////////////////////////////////////////////////////////////////
                        		|ВЫБРАТЬ
                        		|	втТЧТовары.Номенклатура КАК Номенклатура,
                        		|	втТЧТовары.Количество КАК Количество,
                        		|	&Период КАК Период,
                        		|	&Склад КАК Склад,
                        		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения
                        		|ИЗ
                        		|	втТЧТовары КАК втТЧТовары";

                        	Запрос.УстановитьПараметр("Период", Дата);
                        	Запрос.УстановитьПараметр("Склад",  Склад);
                        	Запрос.УстановитьПараметр("Ссылка", Ссылка);

                        	РезультатЗапроса = Запрос.Выполнить();

                        	Выборка = РезультатЗапроса.Выбрать();

                        	Пока Выборка.Следующий() Цикл
                        		Движение = Движения.ОстаткиНоменклатуры.Добавить();
                        		ЗаполнитьЗначенияСвойств(Движение, Выборка);
                        	КонецЦикла;

                        	Движения.ОстаткиНоменклатуры.Записывать = Истина;
                        	Движения.ОстаткиНоменклатуры.БлокироватьДляИзменения = Истина;
                        	Движения.Записать();

                        	Запрос = Новый Запрос;
                        	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
                        	Запрос.Текст =
                        		"ВЫБРАТЬ
                        		|	ОстаткиНоменклатурыОстатки.Номенклатура.Представление КАК НоменклатураПредставление,
                        		|	ОстаткиНоменклатурыОстатки.КоличествоОстаток КАК КоличествоОстаток
                        		|ИЗ
                        		|	РегистрНакопления.ОстаткиНоменклатуры.Остатки(
                        		|			&Граница,
                        		|			Склад = &Склад
                        		|				И Номенклатура В
                        		|					(ВЫБРАТЬ
                        		|						Т.Номенклатура
                        		|					ИЗ
                        		|						втТЧТовары КАК Т)) КАК ОстаткиНоменклатурыОстатки
                        		|ГДЕ
                        		|	ОстаткиНоменклатурыОстатки.КоличествоОстаток < 0";

                        	Запрос.УстановитьПараметр("Граница", Новый Граница(МоментВремени(), ВидГраницы.Включая));
                        	Запрос.УстановитьПараметр("Склад",   Склад);

                        	РезультатЗапроса = Запрос.Выполнить();

                        	Если Не РезультатЗапроса.Пустой() Тогда
                        		Отказ = Истина;

                        		Выборка = РезультатЗапроса.Выбрать();
                        		Пока Выборка.Следующий() Цикл
                        			Сообщение = Новый СообщениеПользователю;
                        			Сообщение.Текст = СтрШаблон("Недостаточно товара %1 в количестве %2",
                        			                  Выборка.НоменклатураПредставление, -Выборка.КоличествоОстаток);
                        			Сообщение.Сообщить();

                        		КонецЦикла;
                        	КонецЕсли;

                        	Если Отказ Тогда
                        		Возврат;
                        	КонецЕсли;

                        	// регистр СебестоимостьТоваров
                        	Движения.СебестоимостьТоваров.Записывать = Истина;
                        	Движения.Записать();

                        	Движения.СебестоимостьТоваров.Записывать = Истина;

                        	Блокировка = Новый БлокировкаДанных;
                        	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.СебестоимостьТоваров");
                        	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
                        	ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
                        	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
                        	Блокировка.Заблокировать();

                        	Запрос = Новый Запрос;
                        	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
                        	Запрос.Текст =
                        		"ВЫБРАТЬ
                        		|	втТЧТовары.Номенклатура КАК Номенклатура,
                        		|	втТЧТовары.Номенклатура.Представление КАК НоменклатураПредставление,
                        		|	втТЧТовары.Количество КАК Количество,
                        		|	СебестоимостьТоваровОстатки.Партия КАК Партия,
                        		|	ЕСТЬNULL(СебестоимостьТоваровОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
                        		|	ЕСТЬNULL(СебестоимостьТоваровОстатки.СтоимостьОстаток, 0) КАК СтоимостьОстаток,
                        		|	СебестоимостьТоваровОстатки.Партия.Представление КАК ПартияПредставление
                        		|ИЗ
                        		|	втТЧТовары КАК втТЧТовары
                        		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.СебестоимостьТоваров.Остатки(
                        		|				&МоментВремени,
                        		|				Номенклатура В
                        		|					(ВЫБРАТЬ
                        		|						втТЧТовары.Номенклатура КАК Номенклатура
                        		|					ИЗ
                        		|						втТЧТовары КАК втТЧТовары)) КАК СебестоимостьТоваровОстатки
                        		|		ПО втТЧТовары.Номенклатура = СебестоимостьТоваровОстатки.Номенклатура
                        		|
                        		|УПОРЯДОЧИТЬ ПО
                        		|	СебестоимостьТоваровОстатки.Партия.МоментВремени
                        		|ИТОГИ
                        		|	МАКСИМУМ(Количество),
                        		|	СУММА(КоличествоОстаток)
                        		|ПО
                        		|	Номенклатура";

                        	Если МетодСписанияСебестоимости = Перечисления.УчетнаяПолитика.ЛИФО Тогда
                        		Запрос.Текст = СтрЗаменить(Запрос.Текст, "СебестоимостьТоваровОстатки.Партия.МоментВремени",
                        		                                         "СебестоимостьТоваровОстатки.Партия.МоментВремени УБЫВ");
                        	КонецЕсли;

                        	Запрос.УстановитьПараметр("Ссылка", Ссылка);
                        	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());

                        	РезультатЗапроса = Запрос.Выполнить();

                        	ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

                        	Пока ВыборкаНоменклатура.Следующий() Цикл

                        		Если ВыборкаНоменклатура.Количество > ВыборкаНоменклатура.КоличествоОстаток Тогда

                        			Отказ = Истина;

                        			Сообщение = Новый СообщениеПользователю;
                        			Сообщение.Текст = СтрШаблон("Недостаточно товара %1 в количестве %2",
                        			                  ВыборкаНоменклатура.НоменклатураПредставление,
                        							  ВыборкаНоменклатура.Количество - ВыборкаНоменклатура.КоличествоОстаток);
                        			Сообщение.Сообщить();

                        		КонецЕсли;

                        		Если Отказ Тогда
                        			Продолжить;
                        		КонецЕсли;

                        		КоличествоСписать = ВыборкаНоменклатура.Количество;

                        		ВыборкаДетальныеЗаписи = ВыборкаНоменклатура.Выбрать();

                        		Пока ВыборкаДетальныеЗаписи.Следующий() И КоличествоСписать > 0 Цикл

                        			Количество = Мин(КоличествоСписать, ВыборкаДетальныеЗаписи.КоличествоОстаток);

                        			Если Количество = ВыборкаДетальныеЗаписи.КоличествоОстаток Тогда
                        				Себестоимость = ВыборкаДетальныеЗаписи.СтоимостьОстаток;
                        			Иначе
                        				Если ВыборкаДетальныеЗаписи.КоличествоОстаток = 0 Тогда
                        					Отказ = Истина;

                        					Сообщение = Новый СообщениеПользователю;
                        					Сообщение.Текст = СтрШаблон("Обнаружен нулевой остаток по партии %1", ВыборкаДетальныеЗаписи.ПартияПредставление);
                        					Сообщение.Сообщить();

                        					Прервать;
                        				КонецЕсли;

                        				Себестоимость = ВыборкаДетальныеЗаписи.СтоимостьОстаток / ВыборкаДетальныеЗаписи.КоличествоОстаток * Количество;
                        			КонецЕсли;

                        			Движение = Движения.СебестоимостьТоваров.ДобавитьРасход();
                        			Движение.Период = Дата;
                        			Движение.Номенклатура = ВыборкаДетальныеЗаписи.Номенклатура;
                        			Движение.Партия = ВыборкаДетальныеЗаписи.Партия;
                        			Движение.Количество = Количество;
                        			Движение.Стоимость = Себестоимость;

                        			КоличествоСписать = КоличествоСписать - Количество;

                        		КонецЦикла;
                        	КонецЦикла;

                        КонецПроцедуры

                        <p>///// Модуль формы</p>
                        &НаКлиенте
                        Процедура СписокНоменклатурыКоличествоПриИзменении(Элемент)
                        	ТекСтрока = Элементы.СписокНоменклатуры.ТекущиеДанные;
                        	ОбработкаТабличнойЧасти.РассчитатьСуммуВСтроке(ТекСтрока);
                        КонецПроцедуры

                        &НаКлиенте
                        Процедура СписокНоменклатурыЦенаПриИзменении(Элемент)
                        	ТекСтрока = Элементы.СписокНоменклатуры.ТекущиеДанные;
                        	ОбработкаТабличнойЧасти.РассчитатьСуммуВСтроке(ТекСтрока);
                        КонецПроцедуры
                        </pre>
	<embed src="pdf/ОбщееОУ1.pdf" type="application/pdf" width="1200" height="800">
	<img src="pdf/ОУ1.jpg">
	<H2 class="OU">
		Бухгалтерский учет
	</H2>
	<pre>
                        <p>///// Общий модуль ОбработкаТабличнойЧастиКлиент</p>
                        Процедура РассчитатьСуммуВСтроке(ТекСтрока) Экспорт
                        	ТекСтрока.Сумма = ТекСтрока.Цена * ТекСтрока.Количество;
                        КонецПроцедуры

                        <p>///// Документ Операция</p>
                        Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
                        	Если Не Движения.Управленческий.Записывать Тогда
                        		Движения.Управленческий.Прочитать();
                        		Движения.Управленческий.Записывать = Истина;
                        	КонецЕсли;

                        	Для Каждого Движение Из Движения.Управленческий Цикл
                        		Движение.Период = Дата;
                        		Движение.Активность = Не ПометкаУдаления;
                        	КонецЦикла;
                        КонецПроцедуры

                        Процедура ПриКопировании(ОбъектКопирования)
                        	Выборка = РегистрыБухгалтерии.Управленческий.ВыбратьПоРегистратору(ОбъектКопирования.Ссылка);
                        	Пока Выборка.Следующий() Цикл
                        		Движение = Движения.Управленческий.Добавить();
                        		ЗаполнитьЗначенияСвойств(Движение, Выборка);
                        		ЗаполнитьЗначенияСвойств(Движение.СубконтоДт, Выборка.СубконтоДт);
                        		ЗаполнитьЗначенияСвойств(Движение.СубконтоКт, Выборка.СубконтоКт);
                        	КонецЦикла;
                        КонецПроцедуры

                        <p>///// Документ ПриходнаяНакладная</p>
                        Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
                        	СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
                        КонецПроцедуры

                        Процедура ОбработкаПроведения(Отказ, РежимПроведения)

                        	Движения.Управленческий.Записывать = Истина;

                        	Запрос = Новый Запрос;
                        	Запрос.Текст =
                        		"ВЫБРАТЬ
                        		|	ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
                        		|	СУММА(ПриходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
                        		|	СУММА(ПриходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма,
                        		|	ПриходнаяНакладнаяСписокНоменклатуры.СрокГодности КАК СрокГодности
                        		|ИЗ
                        		|	Документ.ПриходнаяНакладная.СписокНоменклатуры КАК ПриходнаяНакладнаяСписокНоменклатуры
                        		|ГДЕ
                        		|	ПриходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
                        		|
                        		|СГРУППИРОВАТЬ ПО
                        		|	ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура,
                        		|	ПриходнаяНакладнаяСписокНоменклатуры.СрокГодности";

                        	Запрос.УстановитьПараметр("Ссылка", Ссылка);

                        	РезультатЗапроса = Запрос.Выполнить();

                        	Выборка = РезультатЗапроса.Выбрать();

                        	Пока Выборка.Следующий() Цикл
                        		Движение = Движения.Управленческий.Добавить();
                        		Движение.Период = Дата;

                        		Движение.СчетДт = ПланыСчетов.Управленческий.Товары;
                        		Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = Выборка.Номенклатура;
                        		Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.СрокиГодности] = Выборка.СрокГодности;
                        		Движение.КоличествоДт = Выборка.Количество;

                        		Движение.СчетКт = ПланыСчетов.Управленческий.Поставщики;

                        		Движение.Сумма = Выборка.Сумма;
                        	КонецЦикла;

                        КонецПроцедуры

                        <p>///// Документ РасходнаяНакладная</p>
                        Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
                        	СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
                        КонецПроцедуры

                        Процедура ОбработкаПроведения(Отказ, РежимПроведения)

                        	Движения.Управленческий.Записывать = Истина;
                        	Движения.Записать();

                        	Движения.Управленческий.Записывать = Истина;

                        	Блокировка = Новый БлокировкаДанных;
                        	ЭлементБлокировки = Блокировка.Добавить("РегистрБухгалтерии.Управленческий");
                        	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
                        	ЭлементБлокировки.УстановитьЗначение("Счет", ПланыСчетов.Управленческий.Товары);
                        	ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
                        	ЭлементБлокировки.ИспользоватьИзИсточникаДанных(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура, "Номенклатура");
                        	Блокировка.Заблокировать();

                        	Запрос = Новый Запрос;
                        	Запрос.Текст =
                        		"ВЫБРАТЬ
                        		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
                        		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество
                        		|ПОМЕСТИТЬ втТЧТовары
                        		|ИЗ
                        		|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
                        		|ГДЕ
                        		|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
                        		|
                        		|СГРУППИРОВАТЬ ПО
                        		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура
                        		|
                        		|ИНДЕКСИРОВАТЬ ПО
                        		|	Номенклатура
                        		|;
                        		|
                        		|////////////////////////////////////////////////////////////////////////////////
                        		|ВЫБРАТЬ
                        		|	втТЧТовары.Номенклатура КАК Номенклатура,
                        		|	втТЧТовары.Номенклатура.Представление КАК НоменклатураПредставление,
                        		|	втТЧТовары.Количество КАК Количество,
                        		|	УправленческийОстаткиПоСрокамГодности.Субконто2 КАК СрокГодности,
                        		|	ЕСТЬNULL(УправленческийОстаткиПоСрокамГодности.КоличествоОстаток, 0) КАК КоличествоОстатокПоСрокуГодности,
                        		|	УправленческийОстаткиПоТоварам.КоличествоОстаток КАК КоличествоОстатокПоТовару,
                        		|	УправленческийОстаткиПоТоварам.СуммаОстаток КАК СуммаОстатокПоТовару
                        		|ИЗ
                        		|	втТЧТовары КАК втТЧТовары
                        		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(
                        		|				&МоментВремени,
                        		|				Счет = &СчетТовары,
                        		|				&ВидыСубконтоСрокиГодности,
                        		|				Субконто1 В
                        		|					(ВЫБРАТЬ
                        		|						втТЧТовары.Номенклатура КАК Номенклатура
                        		|					ИЗ
                        		|						втТЧТовары КАК втТЧТовары)) КАК УправленческийОстаткиПоСрокамГодности
                        		|		ПО втТЧТовары.Номенклатура = УправленческийОстаткиПоСрокамГодности.Субконто1
                        		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(
                        		|				&МоментВремени,
                        		|				Счет = &СчетТовары,
                        		|				&ВидыСубконтоНоменклатура,
                        		|				Субконто1 В
                        		|					(ВЫБРАТЬ
                        		|						втТЧТовары.Номенклатура КАК Номенклатура
                        		|					ИЗ
                        		|						втТЧТовары КАК втТЧТовары)) КАК УправленческийОстаткиПоТоварам
                        		|		ПО втТЧТовары.Номенклатура = УправленческийОстаткиПоТоварам.Субконто1
                        		|
                        		|УПОРЯДОЧИТЬ ПО
                        		|	СрокГодности
                        		|ИТОГИ
                        		|	МАКСИМУМ(Количество),
                        		|	СУММА(КоличествоОстатокПоСрокуГодности),
                        		|	МАКСИМУМ(КоличествоОстатокПоТовару),
                        		|	МАКСИМУМ(СуммаОстатокПоТовару)
                        		|ПО
                        		|	Номенклатура";

                        	ВидыСубконтоСрокиГодности = Новый Массив;
                        	ВидыСубконтоСрокиГодности.Добавить(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура);
                        	ВидыСубконтоСрокиГодности.Добавить(ПланыВидовХарактеристик.ВидыСубконто.СрокиГодности);
                        	Запрос.УстановитьПараметр("ВидыСубконтоСрокиГодности", ВидыСубконтоСрокиГодности);

                        	ВидыСубконтоНоменклатура = Новый Массив;
                        	ВидыСубконтоНоменклатура.Добавить(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура);
                        	Запрос.УстановитьПараметр("ВидыСубконтоНоменклатура",  ВидыСубконтоНоменклатура);

                        	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
                        	Запрос.УстановитьПараметр("Ссылка", Ссылка);
                        	Запрос.УстановитьПараметр("СчетТовары", ПланыСчетов.Управленческий.Товары);

                        	РезультатЗапроса = Запрос.Выполнить();

                        	ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

                        	Пока ВыборкаНоменклатура.Следующий() Цикл

                        		Если ВыборкаНоменклатура.Количество > ВыборкаНоменклатура.КоличествоОстатокПоТовару Тогда
                        			Отказ = Истина;

                        			Сообщение = Новый СообщениеПользователю;
                        			Сообщение.Текст = СтрШаблон("Недостаточно товара %1 в количестве %2",
                        			ВыборкаНоменклатура.НоменклатураПредставление,
                        			ВыборкаНоменклатура.Количество - ВыборкаНоменклатура.КоличествоОстатокПоТовару);
                        			Сообщение.Сообщить();
                        		КонецЕсли;

                        		Если Отказ Тогда
                        			Продолжить;
                        		КонецЕсли;

                        		КоличествоСписать = ВыборкаНоменклатура.Количество;

                        		СебестоимостьИтого = 0;

                        		ВыборкаДетальныеЗаписи = ВыборкаНоменклатура.Выбрать();

                        		Пока ВыборкаДетальныеЗаписи.Следующий() И КоличествоСписать > 0 Цикл

                        			Количество = Мин(КоличествоСписать, ВыборкаДетальныеЗаписи.КоличествоОстатокПоСрокуГодности);

                        			Если ВыборкаНоменклатура.Количество = ВыборкаНоменклатура.КоличествоОстатокПоТовару И
                        				 Количество = КоличествоСписать Тогда
                        				Себестоимость = ВыборкаНоменклатура.СуммаОстатокПоТовару - СебестоимостьИтого;
                        			Иначе
                        				Себестоимость = ВыборкаНоменклатура.СуммаОстатокПоТовару /
                        								ВыборкаНоменклатура.КоличествоОстатокПоТовару * Количество;
                        			КонецЕсли;

                        			Движение = Движения.Управленческий.Добавить();
                        			Движение.Период = Дата;

                        			Движение.СчетДт = ПланыСчетов.Управленческий.ПрибылиУбытки;

                        			Движение.СчетКт = ПланыСчетов.Управленческий.Товары;
                        			Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = ВыборкаДетальныеЗаписи.Номенклатура;
                        			Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.СрокиГодности] = ВыборкаДетальныеЗаписи.СрокГодности;
                        			Движение.КоличествоКт = Количество;

                        			Движение.Сумма = Себестоимость;

                        			КоличествоСписать = КоличествоСписать - Количество;

                        			СебестоимостьИтого = СебестоимостьИтого + Движение.Сумма;

                        		КонецЦикла;

                        		//Движение.Сумма = Движение.Сумма + (ВыборкаНоменклатура.СуммаОстатокПоТовару - СебестоимостьИтого)
                        	КонецЦикла;

                        	Движение = Движения.Управленческий.Добавить();
                        	Движение.Период = Дата;
                        	Движение.СчетДт = ПланыСчетов.Управленческий.Покупатели;
                        	Движение.СчетКт = ПланыСчетов.Управленческий.ПрибылиУбытки;
                        	Движение.Сумма  = СуммаПоДокументу;

                        КонецПроцедуры

                        <p>///// Модуль формы</p>
                        &НаКлиенте
                        Процедура СписокНоменклатурыКоличествоПриИзменении(Элемент)
                        	ТекСтрока = Элементы.СписокНоменклатуры.ТекущиеДанные;
                        	ОбработкаТабличнойЧасти.РассчитатьСуммуВСтроке(ТекСтрока);
                        КонецПроцедуры

                        &НаКлиенте
                        Процедура СписокНоменклатурыЦенаПриИзменении(Элемент)
                        	ТекСтрока = Элементы.СписокНоменклатуры.ТекущиеДанные;
                        	ОбработкаТабличнойЧасти.РассчитатьСуммуВСтроке(ТекСтрока);
                        КонецПроцедуры
                        </pre>
	<embed src="pdf/ОбщееБУ1.pdf" type="application/pdf" width="1200" height="800">
	<img src="pdf/БУ1.jpg">
	<H2 class="OU">
		Расчеты
	</H2>
	<pre>
                        <p>///// Документ НачислениеЗарплаты</p>
                        Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)

                        	Если ОсновныеНачисления.Количество() = 0 И ДополнительныеНачисления.Количество() = 0 Тогда
                        		Сообщение = Новый СообщениеПользователю;
                        		Сообщение.Текст = "Нет данных в документе";
                        		Сообщение.Сообщить();
                        		Отказ = Истина;
                        	КонецЕсли;

                        КонецПроцедуры

                        Процедура ОбработкаПроведения(Отказ, РежимПроведения)

                        	ПараметрыРасчета = Новый Структура("ПериодРегистрации", НачалоМесяца(Дата));
                        	ЗаписатьДанныеВРегистры(ПараметрыРасчета);

                        КонецПроцедуры

                        Процедура ЗаписатьДанныеВРегистры(ПараметрыРасчета) Экспорт

                        	ПериодРегистрации = ПараметрыРасчета.ПериодРегистрации;

                        	Движения.ОсновныеНачисления.Записывать = Истина;
                        	Для Каждого Строка Из ОсновныеНачисления Цикл
                        		Движение = Движения.ОсновныеНачисления.Добавить();
                        		ЗаполнитьЗначенияСвойств(Движение, Строка);
                        		Движение.ПериодДействияНачало = Строка.ДатаНачала;
                        		Движение.ПериодДействияКонец = КонецДня(Строка.ДатаОкончания);
                        		Движение.ПериодРегистрации = ПериодРегистрации;
                        	КонецЦикла;

                        	Движения.ДополнительныеНачисления.Записывать = Истина;
                        	Для Каждого Строка Из ДополнительныеНачисления Цикл
                        		Движение = Движения.ДополнительныеНачисления.Добавить();
                        		ЗаполнитьЗначенияСвойств(Движение, Строка);
                        		Движение.ПериодРегистрации = ПериодРегистрации;
                        		Движение.БазовыйПериодНачало = ПериодРегистрации;
                        		Движение.БазовыйПериодКонец = КонецМесяца(ПериодРегистрации);
                        	КонецЦикла;

                        	Движения.Записать();

                        КонецПроцедуры

                        Процедура РассчитатьНачисления(ПараметрыРасчета) Экспорт

                        	// II этап - расчет
                        	ПериодРегистрации = ПараметрыРасчета.ПериодРегистрации;

                        	Запрос = Новый Запрос;
                        	Запрос.Текст =
                        		"ВЫБРАТЬ
                        		|	ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
                        		|	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ЗначениеПериодДействия, 0) КАК РабочихДней,
                        		|	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ЗначениеФактическийПериодДействия, 0) КАК ОтработаноДней,
                        		|	ОсновныеНачисленияДанныеГрафика.Сотрудник КАК Сотрудник
                        		|ПОМЕСТИТЬ втДанныеГрафика
                        		|ИЗ
                        		|	РегистрРасчета.ОсновныеНачисления.ДанныеГрафика(Регистратор = &Регистратор) КАК ОсновныеНачисленияДанныеГрафика
                        		|
                        		|ИНДЕКСИРОВАТЬ ПО
                        		|	Сотрудник
                        		|;
                        		|
                        		|////////////////////////////////////////////////////////////////////////////////
                        		|ВЫБРАТЬ
                        		|	втДанныеГрафика.НомерСтроки КАК НомерСтроки,
                        		|	втДанныеГрафика.РабочихДней КАК РабочихДней,
                        		|	втДанныеГрафика.ОтработаноДней КАК ОтработаноДней,
                        		|	ЕСТЬNULL(СведенияОСотрудникахСрезПоследних.Оклад, 0) КАК Оклад
                        		|ИЗ
                        		|	втДанныеГрафика КАК втДанныеГрафика
                        		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОСотрудниках.СрезПоследних(
                        		|				&ПериодРегистрации,
                        		|				Сотрудник В
                        		|					(ВЫБРАТЬ
                        		|						втДанныеГрафика.Сотрудник КАК Сотрудник
                        		|					ИЗ
                        		|						втДанныеГрафика КАК втДанныеГрафика)) КАК СведенияОСотрудникахСрезПоследних
                        		|		ПО втДанныеГрафика.Сотрудник = СведенияОСотрудникахСрезПоследних.Сотрудник";

                        	Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
                        	Запрос.УстановитьПараметр("Регистратор", Ссылка);

                        	РезультатЗапроса = Запрос.Выполнить();

                        	Выборка = РезультатЗапроса.Выбрать();

                        	Для Каждого Движение Из Движения.ОсновныеНачисления Цикл

                        		Выборка.Сбросить();
                        		Выборка.НайтиСледующий(Движение.НомерСтроки, "НомерСтроки");

                        		Движение.Результат = ?(Выборка.РабочихДней <> 0,
                        		                       Выборка.Оклад / Выборка.РабочихДней * Выборка.ОтработаноДней,
                        							   0);

                        		ЗаполнитьЗначенияСвойств(Движение, Выборка);

                        		СтрокаТаблицы = ОсновныеНачисления[Движение.НомерСтроки - 1];
                        		СтрокаТаблицы.Результат = Движение.Результат;
                        		ЗаполнитьЗначенияСвойств(СтрокаТаблицы, Выборка);

                        	КонецЦикла;

                        	Движения.ОсновныеНачисления.Записать(, Истина);

                        	Запрос = Новый Запрос;
                        	Запрос.Текст =
                        		"ВЫБРАТЬ
                        		|	ДополнительныеНачисленияБазаОсновныеНачисления.НомерСтроки КАК НомерСтроки,
                        		|	ДополнительныеНачисленияБазаОсновныеНачисления.РезультатБаза КАК БазаНачислений,
                        		|	(РАЗНОСТЬДАТ(ДополнительныеНачисленияБазаОсновныеНачисления.Сотрудник.ДатаПриема, &ПериодРегистрации, ДЕНЬ) + ДополнительныеНачисленияБазаОсновныеНачисления.Сотрудник.НачальныйСтажВДнях) / 365 КАК Стаж,
                        		|	ЕСТЬNULL(ШкалаПроцентовПремииОтСтажа.Процент, 0) КАК Процент
                        		|ИЗ
                        		|	РегистрРасчета.ДополнительныеНачисления.БазаОсновныеНачисления(&Измерения, &Измерения, , Регистратор = &Регистратор) КАК ДополнительныеНачисленияБазаОсновныеНачисления
                        		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШкалаПроцентовПремииОтСтажа КАК ШкалаПроцентовПремииОтСтажа
                        		|		ПО ((РАЗНОСТЬДАТ(ДополнительныеНачисленияБазаОсновныеНачисления.Сотрудник.ДатаПриема, &ПериодРегистрации, ДЕНЬ) + ДополнительныеНачисленияБазаОсновныеНачисления.Сотрудник.НачальныйСтажВДнях) / 365 >= ШкалаПроцентовПремииОтСтажа.СтажОт)
                        		|			И ((РАЗНОСТЬДАТ(ДополнительныеНачисленияБазаОсновныеНачисления.Сотрудник.ДатаПриема, &ПериодРегистрации, ДЕНЬ) + ДополнительныеНачисленияБазаОсновныеНачисления.Сотрудник.НачальныйСтажВДнях) / 365 < ШкалаПроцентовПремииОтСтажа.СтажДо)";

                        	Измерения = Новый Массив;
                        	Измерения.Добавить("Сотрудник");

                        	Запрос.УстановитьПараметр("Измерения", Измерения);
                        	Запрос.УстановитьПараметр("Регистратор", Ссылка);
                        	Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);

                        	РезультатЗапроса = Запрос.Выполнить();

                        	Выборка = РезультатЗапроса.Выбрать();

                        	Для Каждого Движение Из Движения.ДополнительныеНачисления Цикл

                        		Выборка.Сбросить();
                        		Если Выборка.НайтиСледующий(Движение.НомерСтроки, "НомерСтроки") Тогда
                        			СтрокаТаблицы = ДополнительныеНачисления[Движение.НомерСтроки - 1];
                        			СтрокаТаблицы.Результат = Выборка.БазаНачислений * Выборка.Процент / 100;
                        			ЗаполнитьЗначенияСвойств(СтрокаТаблицы, Выборка);
                        		КонецЕсли;

                        	КонецЦикла;

                        	Движения.ОсновныеНачисления.Очистить();
                        	Движения.ОсновныеНачисления.Записать();

                        	Движения.ДополнительныеНачисления.Очистить();
                        	Движения.ДополнительныеНачисления.Записать();

                        КонецПроцедуры

                        <p>///// Модуль формы</p>
                        &НаКлиенте
                        Процедура Рассчитать(Команда)
                        	РассчитатьНаСервере();
                        КонецПроцедуры

                        &НаСервере
                        Процедура РассчитатьНаСервере()

                        	Если Объект.Ссылка.Пустая() Тогда
                        		Сообщение = Новый СообщениеПользователю;
                        		Сообщение.Текст = "Документ должен быть записан, если вы хотите рассчитать начисления";
                        		Сообщение.Сообщить();
                        		Отказ = Истина;
                        		Возврат;
                        	КонецЕсли;

                        	ПараметрыРасчета = Новый Структура("ПериодРегистрации", НачалоМесяца(Объект.Дата));

                        	ДокОбъект = РеквизитФормыВЗначение("Объект");

                        	ДокОбъект.ЗаписатьДанныеВРегистры(ПараметрыРасчета);
                        	ДокОбъект.РассчитатьНачисления(ПараметрыРасчета);

                        	ЗначениеВРеквизитФормы(ДокОбъект, "Объект");

                        КонецПроцедуры

                        <p>///// Модуль объекта ЗаполнениеГрафика</p>
                        Процедура ЗаполнитьГрафик(ДатаНачала, ДатаОкончания, ВыходныеДни, ГрафикРаботы) Экспорт

                        	Набор = РегистрыСведений.ГрафикиРаботы.СоздатьНаборЗаписей();

                        	Набор.Отбор.ГрафикРаботы.Установить(ГрафикРаботы);

                        	ЧислоСекундВСутках = 86400;

                        	Дат = ДатаНачала;
                        	Пока Дат <= ДатаОкончания Цикл
                        		Запись = Набор.Добавить();
                        		Запись.ГрафикРаботы = ГрафикРаботы;
                        		Запись.Дата = Дат;
                        		Если Найти(ВыходныеДни, Строка(ДеньНедели(Дат))) Тогда
                        			Запись.Значение = 0;
                        		Иначе
                        			Запись.Значение = 1;
                        		КонецЕсли;
                        		Дат = Дат + ЧислоСекундВСутках;
                        	КонецЦикла;
                        	Набор.Записать();
                        КонецПроцедуры

                        <p>///// Модуль формы</p>
                        &НаКлиенте
                        Процедура Заполнить(Команда)
                        	ВыполнитьОбработку();
                        	Сообщить("Обработка завершена!");
                        КонецПроцедуры

                        &НаСервере
                        Процедура ВыполнитьОбработку()
                        	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
                            ОбработкаОбъект.ЗаполнитьГрафик(ВыборПериода.ДатаНачала, ВыборПериода.ДатаОкончания, ВыходныеДни, ГрафикРаботы);
                        КонецПроцедуры
                        </pre>
	<embed src="pdf/ОбщееСПР1.pdf" type="application/pdf" width="1200" height="800">
	<img src="pdf/СПР1.jpg">
	<H2 class="OU">
		Бизнес процесс
	</H2>
	<pre>
		<p>//// БизнесПроцессСУсловием</p> 
Процедура ОплатаНаличнымиПроверкаУсловия(ТочкаМаршрутаБизнесПроцесса, Результат)
	Результат = ОплатаНаличными;
КонецПроцедуры

<p>//// БизнесПроцессСУсловием ФормаБизнесПроцесса</p>
&НаСервере
Процедура ОбновитьКартуМаршрута()
	БПОбъект = РеквизитФормыВЗначение("Объект");
	КартаМаршрута = БПОбъект.ПолучитьКартуМаршрута();	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)	
	ОбновитьКартуМаршрута();	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	КартаМаршрута = ТекущийОбъект.ПолучитьКартуМаршрута();
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "ЗадачаВыполнена" И Параметр = Объект.Ссылка Тогда
		Прочитать();
		ОбновитьКартуМаршрута();	
	КонецЕсли;
КонецПроцедуры   

<p>//// ЗадачиИсполнителям ФормаЗадачи</p>
&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Оповестить("ЗадачаВыполнена", Объект.БизнесПроцесс);
КонецПроцедуры
	</pre>
	<embed src="pdf/ОбщееБП1.pdf" type="application/pdf" width="1200" height="800">
</body>

</html>
