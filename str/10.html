<!doctype html>
<html lang="ru">

<head>
  <meta charset="utf-8">
  <title>HTML Document Template</title>
  <link href="styles.css" rel="stylesheet">
</head>

<body>
  <div class="header__menu">
    <ul>
      <li><a href="str/1">1</a></li>
      <li><a href="str/2">2</a></li>
      <li><a href="str/3">3</a></li>
      <li><a href="str/4">4</a></li>
      <li><a href="str/5">5</a></li>
      <li><a href="str/6">6</a></li>
      <li><a href="str/7">7</a></li>
      <li><a href="str/8">8</a></li>
      <li><a href="str/9">9</a></li>
      <li><a href="str/10">10</a></li>
      <li><a href="str/11">11</a></li>
      <li><a href="str/12">12</a></li>
      <li><a href="str/13">13</a></li>
      <li><a href="str/14">14</a></li>
      <li><a href="str/15">15</a></li>
      <li><a href="str/16">16</a></li>
    </ul>
  </div>
  <h1>10</h1>
  <H2 class="OU">
    Оперативный учет
  </H2>
  <pre>
    <p>///// Общий модуль ОбработкаТабличнойЧасти</p>
    Процедура РассчитатьСуммуВСтроке(ТекСтрока) Экспорт
      ТекСтрока.Сумма = ТекСтрока.Цена * ТекСтрока.Количество;
    КонецПроцедуры 
    
    <p>///// Документ ПриходнаяНакладная</p>
    Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
      СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
    КонецПроцедуры
    
    Процедура ОбработкаПроведения(Отказ, Режим)
    
      Движения.ОстаткиНоменклатуры.Записывать = Истина;
      
      Запрос = Новый Запрос;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
        |	СУММА(ПриходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество
        |ИЗ
        |	Документ.ПриходнаяНакладная.СписокНоменклатуры КАК ПриходнаяНакладнаяСписокНоменклатуры
        |ГДЕ
        |	ПриходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
        |
        |СГРУППИРОВАТЬ ПО
        |	ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура";
      
      Запрос.УстановитьПараметр("Ссылка", Ссылка);
      
      РезультатЗапроса = Запрос.Выполнить();
      
      Выборка = РезультатЗапроса.Выбрать();
      
      Пока Выборка.Следующий() Цикл
        Движение = Движения.ОстаткиНоменклатуры.Добавить();
        Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
        Движение.Период = Дата;
        Движение.Номенклатура = Выборка.Номенклатура;
        Движение.Количество = Выборка.Количество;		
      КонецЦикла;
       
    КонецПроцедуры
    
    <p>///// Документ РасходнаяНакладная</p> 
    Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
      СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
    КонецПроцедуры
    
    Процедура ОбработкаПроведения(Отказ, Режим)
    
      Запрос = Новый Запрос;
      МенеджерВТ = Новый МенеджерВременныхТаблиц;
      Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
        |	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуры КАК ВидНоменклатуры,
        |	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество
        |ПОМЕСТИТЬ втТЧТовары
        |ИЗ
        |	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
        |ГДЕ
        |	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
        |
        |СГРУППИРОВАТЬ ПО
        |	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура,
        |	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуры
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	втТЧТовары.Номенклатура КАК Номенклатура,
        |	втТЧТовары.Количество КАК Количество
        |ПОМЕСТИТЬ втНаборыПродуктов
        |ИЗ
        |	втТЧТовары КАК втТЧТовары
        |ГДЕ
        |	втТЧТовары.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.НаборПродуктов)
        |
        |ИНДЕКСИРОВАТЬ ПО
        |	Номенклатура
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	СоставНаборовПродуктов.НаборПродуктов КАК НаборПродуктов,
        |	МАКСИМУМ(СоставНаборовПродуктов.Период) КАК Период
        |ПОМЕСТИТЬ втДатыПоследнихИзмененийНаборов
        |ИЗ
        |	РегистрСведений.СоставНаборовПродуктов КАК СоставНаборовПродуктов
        |ГДЕ
        |	СоставНаборовПродуктов.Период <= &Период
        |	И СоставНаборовПродуктов.НаборПродуктов В
        |			(ВЫБРАТЬ
        |				Т.Номенклатура
        |			ИЗ
        |				втНаборыПродуктов КАК Т)
        |
        |СГРУППИРОВАТЬ ПО
        |	СоставНаборовПродуктов.НаборПродуктов
        |
        |ИНДЕКСИРОВАТЬ ПО
        |	Период,
        |	НаборПродуктов
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	СоставНаборовПродуктов.НаборПродуктов КАК НаборПродуктов,
        |	СоставНаборовПродуктов.Продукт КАК Продукт,
        |	СоставНаборовПродуктов.Количество КАК Количество
        |ПОМЕСТИТЬ втСоставНаборовПродуктов
        |ИЗ
        |	РегистрСведений.СоставНаборовПродуктов КАК СоставНаборовПродуктов
        |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДатыПоследнихИзмененийНаборов КАК втДатыПоследнихИзмененийНаборов
        |		ПО СоставНаборовПродуктов.НаборПродуктов = втДатыПоследнихИзмененийНаборов.НаборПродуктов
        |			И СоставНаборовПродуктов.Период = втДатыПоследнихИзмененийНаборов.Период
        |
        |ИНДЕКСИРОВАТЬ ПО
        |	НаборПродуктов
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	втТЧТовары.Номенклатура КАК НоменклатураПродажи,
        |	ЕСТЬNULL(втСоставНаборовПродуктов.Продукт, ВЫБОР
        |			КОГДА втТЧТовары.ВидНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.НаборПродуктов)
        |				ТОГДА втТЧТовары.Номенклатура
        |		КОНЕЦ) КАК Номенклатура,
        |	втТЧТовары.Количество * ЕСТЬNULL(втСоставНаборовПродуктов.Количество, ВЫБОР
        |			КОГДА втТЧТовары.ВидНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.НаборПродуктов)
        |				ТОГДА 1
        |		КОНЕЦ) КАК Количество
        |ПОМЕСТИТЬ втПродуктыИБлюда
        |ИЗ
        |	втТЧТовары КАК втТЧТовары
        |		ЛЕВОЕ СОЕДИНЕНИЕ втСоставНаборовПродуктов КАК втСоставНаборовПродуктов
        |		ПО втТЧТовары.Номенклатура = втСоставНаборовПродуктов.НаборПродуктов
        |
        |ИНДЕКСИРОВАТЬ ПО
        |	Номенклатура
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	втПродуктыИБлюда.НоменклатураПродажи.Представление КАК НоменклатураПредставление
        |ИЗ
        |	втПродуктыИБлюда КАК втПродуктыИБлюда
        |ГДЕ
        |	втПродуктыИБлюда.Номенклатура ЕСТЬ NULL
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	втПродуктыИБлюда.Номенклатура КАК Номенклатура,
        |	СУММА(втПродуктыИБлюда.Количество) КАК Количество,
        |	&Период КАК Период
        |ИЗ
        |	втПродуктыИБлюда КАК втПродуктыИБлюда
        |ГДЕ
        |	НЕ втПродуктыИБлюда.Номенклатура ЕСТЬ NULL
        |
        |СГРУППИРОВАТЬ ПО
        |	втПродуктыИБлюда.Номенклатура";
      
      Запрос.УстановитьПараметр("Период", Дата);
      Запрос.УстановитьПараметр("Ссылка", Ссылка);
      
      РезультатыЗапросов = Запрос.ВыполнитьПакет();
      
      РезультатПроверка = РезультатыЗапросов[5];
      Если Не РезультатПроверка.Пустой() Тогда
        
        Отказ = Истина;
        
        Выборка = РезультатПроверка.Выбрать();
        Пока Выборка.Следующий() Цикл
          Сообщение = Новый СообщениеПользователю;
          Сообщение.Текст = "Не определен состав для набора " + Выборка.НоменклатураПредставление;
          Сообщение.Сообщить();
        КонецЦикла;
        
        Возврат;
        
      КонецЕсли;
      
      Результат = РезультатыЗапросов[6];
      Выборка = Результат.Выбрать();
      Пока Выборка.Следующий() Цикл
        Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
        ЗаполнитьЗначенияСвойств(Движение, Выборка);
      КонецЦикла;
      
      Движения.ОстаткиНоменклатуры.Записывать = Истина;
      Движения.ОстаткиНоменклатуры.БлокироватьДляИзменения = Истина;
      Движения.Записать();
      
      Запрос = Новый Запрос;
      Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	ОстаткиНоменклатурыОстатки.Номенклатура.Представление КАК НоменклатураПредставление,
        |	ОстаткиНоменклатурыОстатки.Номенклатура.ВидНоменклатуры КАК ВидНоменклатуры,
        |	-ОстаткиНоменклатурыОстатки.КоличествоОстаток КАК КоличествоНехватки
        |ИЗ
        |	РегистрНакопления.ОстаткиНоменклатуры.Остатки(
        |			&Граница,
        |			Номенклатура В
        |				(ВЫБРАТЬ
        |					Т.Номенклатура
        |				ИЗ
        |					втПродуктыИБлюда КАК Т)) КАК ОстаткиНоменклатурыОстатки
        |ГДЕ
        |	ОстаткиНоменклатурыОстатки.КоличествоОстаток < 0";
      
      Запрос.УстановитьПараметр("Граница", Новый Граница(МоментВремени(), ВидГраницы.Включая));
      
      РезультатЗапроса = Запрос.Выполнить();
      Если Не РезультатЗапроса.Пустой() Тогда	
        
        Отказ = Истина;
        
        Выборка = РезультатЗапроса.Выбрать();		
        Пока Выборка.Следующий() Цикл
          Сообщение = Новый СообщениеПользователю;
          Если Выборка.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Продукт Тогда
            Сообщение.Текст = СтрШаблон("Недостаточно продукта %1 в количестве %2 (он может входить в состав набора)",
            Выборка.НоменклатураПредставление, Выборка.КоличествоНехватки);
          ИначеЕсли Выборка.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Блюдо Тогда
            Сообщение.Текст = СтрШаблон("Недостаточно блюда %1 в количестве %2",
            Выборка.НоменклатураПредставление, Выборка.КоличествоНехватки);
          Иначе
            Сообщение.Текст = СтрШаблон("Недостаточно товара %1 в количестве %2",
            Выборка.НоменклатураПредставление, Выборка.КоличествоНехватки);				
          КонецЕсли;
          Сообщение.Сообщить();	
        КонецЦикла;
      КонецЕсли;
        
    КонецПроцедуры
    
    <p>///// Документ Комплектация</p> 
    Процедура ОбработкаПроведения(Отказ, РежимПроведения)
      
      Запрос = Новый Запрос;
      МенеджерВТ = Новый МенеджерВременныхТаблиц;
      Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	КомплектацияПродукты.Продукт КАК Продукт,
        |	СУММА(КомплектацияПродукты.Количество) КАК Количество
        |ПОМЕСТИТЬ втТЧТовары
        |ИЗ
        |	Документ.Комплектация.Продукты КАК КомплектацияПродукты
        |ГДЕ
        |	КомплектацияПродукты.Ссылка = &Ссылка
        |
        |СГРУППИРОВАТЬ ПО
        |	КомплектацияПродукты.Продукт
        |
        |ИНДЕКСИРОВАТЬ ПО
        |	Продукт
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	втТЧТовары.Продукт КАК Номенклатура,
        |	втТЧТовары.Количество КАК Количество,
        |	&Период КАК Период
        |ИЗ
        |	втТЧТовары КАК втТЧТовары";
      
      Запрос.УстановитьПараметр("Период", Дата);
      Запрос.УстановитьПараметр("Ссылка", Ссылка);
      
      РезультатЗапроса = Запрос.Выполнить();
      
      Выборка = РезультатЗапроса.Выбрать();
      
      Пока Выборка.Следующий() Цикл
        Движение  = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
        ЗаполнитьЗначенияСвойств(Движение, Выборка);
      КонецЦикла;
      
      Движение = Движения.ОстаткиНоменклатуры.ДобавитьПриход();
      Движение.Период = Дата;
      Движение.Номенклатура = Блюдо;
      Движение.Количество = Количество;
      
      Движения.ОстаткиНоменклатуры.Записывать = Истина;
      Движения.ОстаткиНоменклатуры.БлокироватьДляИзменения = Истина;
      Движения.Записать();
      
      Запрос = Новый Запрос;
      Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	ОстаткиНоменклатурыОстатки.Номенклатура.Представление КАК НоменклатураПредставление,
        |	-ОстаткиНоменклатурыОстатки.КоличествоОстаток КАК КоличествоНехватки
        |ИЗ
        |	РегистрНакопления.ОстаткиНоменклатуры.Остатки(
        |			&Граница,
        |			Номенклатура В
        |				(ВЫБРАТЬ
        |					Т.Продукт
        |				ИЗ
        |					втТЧТовары КАК Т)) КАК ОстаткиНоменклатурыОстатки
        |ГДЕ
        |	ОстаткиНоменклатурыОстатки.КоличествоОстаток < 0";
      
      Запрос.УстановитьПараметр("Граница", Новый Граница(МоментВремени(), ВидГраницы.Включая));
      
      РезультатЗапроса = Запрос.Выполнить();
      
      Если Не РезультатЗапроса.Пустой() Тогда
        Отказ = Истина;
        
        Выборка = РезультатЗапроса.Выбрать();	
        Пока Выборка.Следующий() Цикл
          Сообщение = Новый СообщениеПользователю;
          Сообщение.Текст = СтрШаблон("Недостаточно продукта %1 в количестве %2",
          Выборка.НоменклатураПредставление, Выборка.КоличествоНехватки);
          Сообщение.Сообщить();
        КонецЦикла;
      КонецЕсли;
      
    КонецПроцедуры
    
    <p>///// Документ РучнаяКомплектация</p>
    Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
      Если Не Движения.ОстаткиНоменклатуры.Записывать Тогда
        Движения.ОстаткиНоменклатуры.Прочитать();	
      КонецЕсли;
      
      Для Каждого Движение Из Движения.ОстаткиНоменклатуры Цикл
        Движение.Период = Дата;
        Движение.Активность = Не ПометкаУдаления;
      КонецЦикла;
      
      Движения.ОстаткиНоменклатуры.Записывать = Истина;
    КонецПроцедуры
    
    Процедура ПриКопировании(ОбъектКопирования)
      Выборка = РегистрыНакопления.ОстаткиНоменклатуры.ВыбратьПоРегистратору(ОбъектКопирования.Ссылка);
      Пока Выборка.Следующий() Цикл
        Движение = Движения.ОстаткиНоменклатуры.Добавить();
        ЗаполнитьЗначенияСвойств(Движение, Выборка);
      КонецЦикла;
    КонецПроцедуры
    
    <p>///// Модуль формы</p>
    &НаКлиенте
    Процедура СписокНоменклатурыКоличествоПриИзменении(Элемент)
      ТекСтрока = Элементы.СписокНоменклатуры.ТекущиеДанные;
      ОбработкаТабличнойЧасти.РассчитатьСуммуВСтроке(ТекСтрока);
    КонецПроцедуры
    
    &НаКлиенте
    Процедура СписокНоменклатурыЦенаПриИзменении(Элемент)
      ТекСтрока = Элементы.СписокНоменклатуры.ТекущиеДанные;
      ОбработкаТабличнойЧасти.РассчитатьСуммуВСтроке(ТекСтрока);
    КонецПроцедуры
    <embed src="pdf/ОбщееОУ10.pdf" type="application/pdf" width="1200" height="800">
      <img src="pdf/ОУ10.jpg">
    <H2 class="OU">
      Бухгалтерский учет
    </H2>
    <p>///// Общий модуль ОбработкаТабличнойЧастиКлиент</p>
    Процедура РассчитатьСуммуВСтроке(ТекСтрока) Экспорт
      ТекСтрока.Сумма = ТекСтрока.Цена * ТекСтрока.Количество;
    КонецПроцедуры 
    
    <p>///// Документ Операция</p>   
    Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
      Если Не Движения.Управленческий.Записывать Тогда
        Движения.Управленческий.Прочитать();
        Движения.Управленческий.Записывать = Истина;
      КонецЕсли;
      
      Для Каждого Движение Из Движения.Управленческий Цикл
        Движение.Период = Дата;
        Движение.Активность = Не ПометкаУдаления;
      КонецЦикла;
    КонецПроцедуры
    
    Процедура ПриКопировании(ОбъектКопирования)	
      Выборка = РегистрыБухгалтерии.Управленческий.ВыбратьПоРегистратору(ОбъектКопирования.Ссылка);
      Пока Выборка.Следующий() Цикл
        Движение = Движения.Управленческий.Добавить();
        ЗаполнитьЗначенияСвойств(Движение, Выборка);
        ЗаполнитьЗначенияСвойств(Движение.СубконтоДт, Выборка.СубконтоДт);
        ЗаполнитьЗначенияСвойств(Движение.СубконтоКт, Выборка.СубконтоКт);
      КонецЦикла;
    КонецПроцедуры
     
    <p>///// Документ ПриходнаяНакладная</p>
    Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
      СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
    КонецПроцедуры
    
    <p>///// Документ РасходнаяНакладная</p> 
    Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
      СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
    КонецПроцедуры
    
    <p>///// Документ Отправление</p>
    Процедура ОбработкаПроведения(Отказ, РежимПроведения)
      
      Движения.Управленческий.Записывать = Истина;
      Движения.Записать();
      
      Движения.Управленческий.Записывать = Истина;
      
      Блокировка = Новый БлокировкаДанных;
      ЭлементБлокировки = Блокировка.Добавить("РегистрБухгалтерии.Управленческий");
      ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
      ЭлементБлокировки.УстановитьЗначение("Счет", ПланыСчетов.Управленческий.Товары);
      ЭлементБлокировки.УстановитьЗначение(ПланыВидовХарактеристик.ВидыСубконто.Склады, Склад);
      ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
      ЭлементБлокировки.ИспользоватьИзИсточникаДанных(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура, "Номенклатура");
      Блокировка.Заблокировать();
      
      Запрос = Новый Запрос;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	ОтправлениеСписокНоменклатуры.Номенклатура КАК Номенклатура,
        |	СУММА(ОтправлениеСписокНоменклатуры.Количество) КАК Количество
        |ПОМЕСТИТЬ втТЧТовары
        |ИЗ
        |	Документ.Отправление.СписокНоменклатуры КАК ОтправлениеСписокНоменклатуры
        |ГДЕ
        |	ОтправлениеСписокНоменклатуры.Ссылка = &Ссылка
        |
        |СГРУППИРОВАТЬ ПО
        |	ОтправлениеСписокНоменклатуры.Номенклатура
        |
        |ИНДЕКСИРОВАТЬ ПО
        |	Номенклатура
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	втТЧТовары.Номенклатура КАК Номенклатура,
        |	втТЧТовары.Номенклатура.Представление КАК НоменклатураПредставление,
        |	втТЧТовары.Количество КАК Количество,
        |	ЕСТЬNULL(УправленческийОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
        |	ЕСТЬNULL(УправленческийОстатки.СуммаОстаток, 0) КАК СуммаОстаток
        |ИЗ
        |	втТЧТовары КАК втТЧТовары
        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(
        |				&МоментВремени,
        |				Счет = &СчетТовары,
        |				&ВидыСубконто,
        |				Субконто1 В
        |						(ВЫБРАТЬ
        |							втТЧТовары.Номенклатура КАК Номенклатура
        |						ИЗ
        |							втТЧТовары КАК втТЧТовары)
        |					И Субконто2 = &Склад) КАК УправленческийОстатки
        |		ПО втТЧТовары.Номенклатура = УправленческийОстатки.Субконто1";
      
      ВидыСубконто = Новый Массив;
      ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура);
      ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконто.Склады);
      
      Запрос.УстановитьПараметр("ВидыСубконто", ВидыСубконто);
      Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
      Запрос.УстановитьПараметр("Склад", Склад);
      Запрос.УстановитьПараметр("Ссылка", Ссылка);
      Запрос.УстановитьПараметр("СчетТовары", ПланыСчетов.Управленческий.Товары);
      
      РезультатЗапроса = Запрос.Выполнить();
      
      Выборка = РезультатЗапроса.Выбрать();
      
      Пока Выборка.Следующий() Цикл
        
        Если Выборка.Количество > Выборка.КоличествоОстаток Тогда
          Отказ = Истина;
          Сообщение = Новый СообщениеПользователю;
          Сообщение.Текст = СтрШаблон("Недостаточно товара %1 в количестве %2",
          Выборка.НоменклатураПредставление, Выборка.Количество - Выборка.КоличествоОстаток);
          Сообщение.Сообщить();
        КонецЕсли;
        
        Если Отказ Тогда
          Продолжить;
        КонецЕсли;
        
        Если Выборка.Количество = Выборка.КоличествоОстаток Тогда
          Себестоимость = Выборка.СуммаОстаток;
        Иначе
          Себестоимость = Выборка.СуммаОстаток / Выборка.КоличествоОстаток * Выборка.Количество;
        КонецЕсли;
        
        Движение = Движения.Управленческий.Добавить();
        Движение.Период = Дата;
        Движение.СчетДт = ПланыСчетов.Управленческий.ТоварыВПути;
        Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = Выборка.Номенклатура;
        Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.ДокументыОтправления] = Ссылка;
        Движение.КоличествоДт = Выборка.Количество;
        
        Движение.СчетКт = ПланыСчетов.Управленческий.Товары;
        Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = Выборка.Номенклатура;
        Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Склады] = Склад;
        Движение.КоличествоКт = Выборка.Количество;
        Движение.Сумма = Себестоимость;
        
      КонецЦикла;
    
    КонецПроцедуры
    
    <p>///// Документ Прибытие</p> 
    Процедура ОбработкаПроведения(Отказ, РежимПроведения)
      
      Движения.Управленческий.Записывать = Истина;
      Движения.Записать();
      
      Движения.Управленческий.Записывать = Истина;
      
      Блокировка = Новый БлокировкаДанных;
      ЭлементБлокировки = Блокировка.Добавить("РегистрБухгалтерии.Управленческий");
      ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
      ЭлементБлокировки.УстановитьЗначение("Счет", ПланыСчетов.Управленческий.ТоварыВПути);
      ЭлементБлокировки.УстановитьЗначение(ПланыВидовХарактеристик.ВидыСубконто.ДокументыОтправления, ДокументОтправления);
      Блокировка.Заблокировать();
      
      Запрос = Новый Запрос;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	УправленческийОстатки.Субконто1 КАК Номенклатура,
        |	УправленческийОстатки.КоличествоОстаток КАК КоличествоОстаток,
        |	УправленческийОстатки.СуммаОстаток КАК СуммаОстаток
        |ИЗ
        |	РегистрБухгалтерии.Управленческий.Остатки(&МоментВремени, Счет = &СчетТоварыВПути, &ВидыСубконто, Субконто2 = &ДокументОтправления) КАК УправленческийОстатки";
      
      ВидыСубконто = Новый Массив;
      ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура);
      ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконто.ДокументыОтправления);
      
      Запрос.УстановитьПараметр("ВидыСубконто", ВидыСубконто);
      
      Запрос.УстановитьПараметр("ДокументОтправления", ДокументОтправления);
      Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
      Запрос.УстановитьПараметр("СчетТоварыВПути", ПланыСчетов.Управленческий.ТоварыВПути);
      
      РезультатЗапроса = Запрос.Выполнить();
      
      Если РезультатЗапроса.Пустой() Тогда
        Отказ = Истина;
        
        Сообщение = Новый СообщениеПользователю;
        Сообщение.Текст = "Нет данных для формирования документа";
        Сообщение.Сообщить();
        
        Возврат;
      КонецЕсли;
      
      Выборка = РезультатЗапроса.Выбрать();
      
      Пока Выборка.Следующий() Цикл		
        Движение = Движения.Управленческий.Добавить();
        Движение.Период = Дата;
        Движение.СчетДт = ПланыСчетов.Управленческий.Товары;
        Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = Выборка.Номенклатура;
        Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Склады] = Склад;
        Движение.КоличествоДт = Выборка.КоличествоОстаток;
        
        Движение.СчетКт = ПланыСчетов.Управленческий.ТоварыВПути;
        Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = Выборка.Номенклатура;
        Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.ДокументыОтправления] = ДокументОтправления;
        Движение.КоличествоКт = Выборка.КоличествоОстаток;
        
        Движение.Сумма = Выборка.СуммаОстаток;		
      КонецЦикла;
        
    КонецПроцедуры
    
    Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
      Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.Отправление") Тогда
        ДокументОтправления = ДанныеЗаполнения;
      КонецЕсли;
    КонецПроцедуры
    
    <p>///// Модуль формы Прибытие</p>
    &НаСервере
    Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
      
      Если Не ЗначениеЗаполнено(Объект.ДокументОтправления) Тогда
        Сообщение = Новый СообщениеПользователю;
        Сообщение.Текст = "Этот документ можно создать только на основании документа ""Отправление""";
        Сообщение.Сообщить();	
        Отказ = Истина;
      КонецЕсли;
      
    КонецПроцедуры
    <embed src="pdf/ОбщееБУ10.pdf" type="application/pdf" width="1200" height="800">
      <img src="pdf/БУ10.jpg">
    <H2 class="OU">
      Расчеты
    </H2>
    <p>///// Документ НачислениеЗарплаты</p>   
    Процедура ОбработкаПроведения(Отказ, РежимПроведения)
      
      ПериодРегистрации = НачалоМесяца(Дата);
      КонецПериодаРегистрации = КонецМесяца(Дата);
      
      Движения.ОсновныеНачисления.Записывать = Истина;
      Для Каждого Строка Из ОсновныеНачисления Цикл
        Движение = Движения.ОсновныеНачисления.Добавить();
        ЗаполнитьЗначенияСвойств(Движение, Строка);
        Движение.ПериодРегистрации = ПериодРегистрации;
        Движение.ПериодДействияНачало = Строка.ДатаНачала;
        Движение.ПериодДействияКонец = КонецДня(Строка.ДатаОкончания);
      КонецЦикла;
      
      ТаблицаДополнения = Движения.ОсновныеНачисления.ПолучитьДополнение();
      Для Каждого СтрокаДополнения Из ТаблицаДополнения Цикл
        Движение = Движения.ОсновныеНачисления.Добавить();
        ЗаполнитьЗначенияСвойств(Движение, СтрокаДополнения, "Сотрудник, Подразделение, ГрафикРаботы, ВидРасчета, ТарифнаяСтавка");
        Движение.ПериодРегистрации = СтрокаДополнения.ПериодРегистрацииСторно;
        Движение.ПериодДействияНачало = СтрокаДополнения.ПериодДействияНачалоСторно;
        Движение.ПериодДействияКонец = СтрокаДополнения.ПериодДействияКонецСторно;
        Движение.Сторно = Истина;
      КонецЦикла;
      
      Движения.ДополнительныеНачисления.Записывать = Истина;
      Для Каждого Строка Из ДополнительныеНачисления Цикл
        Движение = Движения.ДополнительныеНачисления.Добавить();
        ЗаполнитьЗначенияСвойств(Движение, Строка);
        Движение.ПериодРегистрации = ПериодРегистрации;
        Если Строка.ВидРасчета = ПланыВидовРасчета.ДополнительныеНачисления.КомпенсацияЗатратНаОбеды Тогда
          Движение.БазовыйПериодНачало = ПериодРегистрации;
          Движение.БазовыйПериодКонец  = КонецПериодаРегистрации;
        КонецЕсли;
      КонецЦикла;	
        
      Движения.Записать();
      
      Запрос = Новый Запрос;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
        |	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ЧасовФактическийПериодДействия, 0) КАК ОтработаноЧасов,
        |	ОсновныеНачисленияДанныеГрафика.Сотрудник КАК Сотрудник,
        |	ОсновныеНачисленияДанныеГрафика.Подразделение КАК Подразделение,
        |	ОсновныеНачисленияДанныеГрафика.Сторно КАК Сторно,
        |	ОсновныеНачисленияДанныеГрафика.ТарифнаяСтавка КАК ТарифнаяСтавка,
        |	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ДнейПериодДействия, 0) КАК НормаДней,
        |	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ДнейФактическийПериодДействия, 0) КАК ОтработаноДней
        |ПОМЕСТИТЬ втДанныеГрафика
        |ИЗ
        |	РегистрРасчета.ОсновныеНачисления.ДанныеГрафика(
        |			Регистратор = &Ссылка
        |				И ВидРасчета В (&ВидыРасчета)) КАК ОсновныеНачисленияДанныеГрафика
        |
        |ИНДЕКСИРОВАТЬ ПО
        |	Сотрудник,
        |	Подразделение
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	втДанныеГрафика.НомерСтроки КАК НомерСтроки,
        |	втДанныеГрафика.ОтработаноЧасов КАК ОтработаноЧасов,
        |	ВЫБОР
        |		КОГДА НЕ втДанныеГрафика.Сторно
        |			ТОГДА ЕСТЬNULL(СведенияОСотрудникахСрезПоследних.ТарифнаяСтавка, 0)
        |		ИНАЧЕ втДанныеГрафика.ТарифнаяСтавка
        |	КОНЕЦ КАК ТарифнаяСтавка,
        |	втДанныеГрафика.ОтработаноДней КАК ОтработаноДней,
        |	втДанныеГрафика.НормаДней КАК НормаДней
        |ИЗ
        |	втДанныеГрафика КАК втДанныеГрафика
        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОСотрудниках.СрезПоследних(
        |				&ПериодРегистрации,
        |				(Сотрудник, Подразделение) В
        |					(ВЫБРАТЬ
        |						втДанныеГрафика.Сотрудник КАК Сотрудник,
        |						втДанныеГрафика.Подразделение КАК Подразделение
        |					ИЗ
        |						втДанныеГрафика КАК втДанныеГрафика)) КАК СведенияОСотрудникахСрезПоследних
        |		ПО втДанныеГрафика.Сотрудник = СведенияОСотрудникахСрезПоследних.Сотрудник
        |			И втДанныеГрафика.Подразделение = СведенияОСотрудникахСрезПоследних.Подразделение";
      
      ВидыРасчета = Новый Массив;
      ВидыРасчета.Добавить(ПланыВидовРасчета.ОсновныеНачисления.ОплатаПоЧасовомуТарифу);
      Запрос.УстановитьПараметр("ВидыРасчета", ВидыРасчета);
      Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
      Запрос.УстановитьПараметр("Ссылка", Ссылка);
      
      РезультатЗапроса = Запрос.Выполнить();
      
      Выборка = РезультатЗапроса.Выбрать();
      
      Для Каждого Движение Из Движения.ОсновныеНачисления Цикл
        
        Если ВидыРасчета.Найти(Движение.ВидРасчета) = Неопределено Тогда
          Продолжить;
        КонецЕсли;
        
        Выборка.Сбросить();
        Выборка.НайтиСледующий(Движение.НомерСтроки , "НомерСтроки");
        
        Если Движение.ВидРасчета = ПланыВидовРасчета.ОсновныеНачисления.ОплатаПоЧасовомуТарифу Тогда
          Движение.Результат = Выборка.ТарифнаяСтавка * Выборка.ОтработаноЧасов * ?(Движение.Сторно, -1, 1);
          ЗаполнитьЗначенияСвойств(Движение, Выборка, "ТарифнаяСтавка, ОтработаноЧасов, НормаДней, ОтработаноДней");
          
          Если Движение.Сторно Тогда
            Движение.НормаДней = 0;
          КонецЕсли;
          
        КонецЕсли;
        
      КонецЦикла;
      
      Движения.ОсновныеНачисления.Записать(, Истина);
    
      Запрос = Новый Запрос;
      //Запрос.Текст = 
      //	"ВЫБРАТЬ
      //	|	ДополнительныеНачисленияБазаОсновныеНачисления.НомерСтроки КАК НомерСтроки,
      //	|	СУММА(ДополнительныеНачисленияБазаОсновныеНачисления.РезультатБаза) КАК БазаНачислений,
      //	|	СУММА(ДополнительныеНачисленияБазаОсновныеНачисления.ОтработаноДнейБаза) КАК ОтработаноДней,
      //	|	МАКСИМУМ(ДополнительныеНачисленияБазаОсновныеНачисления.НормаДнейБаза) КАК БазаРабочихДней,
      //	|	МИНИМУМ(ДополнительныеНачисленияБазаОсновныеНачисления.НомерСтрокиРазрез) КАК СлужебноеПоле
      //	|ПОМЕСТИТЬ втТаблицаБаза
      //	|ИЗ
      //	|	РегистрРасчета.ДополнительныеНачисления.БазаОсновныеНачисления(
      //	|			&Измерения,
      //	|			&Измерения,
      //	|			&Разрезы,
      //	|			Регистратор = &Ссылка
      //	|				И ВидРасчета В (&ВидыРасчета)) КАК ДополнительныеНачисленияБазаОсновныеНачисления
      //	|
      //	|СГРУППИРОВАТЬ ПО
      //	|	ДополнительныеНачисленияБазаОсновныеНачисления.НомерСтроки
      //	|
      //	|ИНДЕКСИРОВАТЬ ПО
      //	|	НомерСтроки
      //	|;
      //	|
      //	|////////////////////////////////////////////////////////////////////////////////
      //	|ВЫБРАТЬ
      //	|	ДополнительныеНачисления.НомерСтроки КАК НомерСтроки,
      //	|	ЕСТЬNULL(втТаблицаБаза.БазаНачислений, 0) КАК БазаНачислений,
      //	|	ЕСТЬNULL(втТаблицаБаза.ОтработаноДней, 0) КАК ОтработаноДней,
      //	|	ЕСТЬNULL(втТаблицаБаза.БазаРабочихДней, 0) КАК БазаРабочихДней,
      //	|	ПроцентКомпенсацииЗатрат.Значение КАК ПроцентКомпенсацииЗатратНаОбед
      //	|ИЗ
      //	|	РегистрРасчета.ДополнительныеНачисления КАК ДополнительныеНачисления
      //	|		ЛЕВОЕ СОЕДИНЕНИЕ втТаблицаБаза КАК втТаблицаБаза
      //	|		ПО ДополнительныеНачисления.НомерСтроки = втТаблицаБаза.НомерСтроки,
      //	|	Константа.ПроцентКомпенсацииЗатратНаОбед КАК ПроцентКомпенсацииЗатрат
      //	|ГДЕ
      //	|	ДополнительныеНачисления.Регистратор = &Ссылка
      //	|	И ДополнительныеНачисления.ВидРасчета В(&ВидыРасчета)";
      
      Запрос.Текст =  "ВЫБРАТЬ
                      |	ДополнительныеНачисления.НомерСтроки КАК НомерСтроки,
                      |	ДополнительныеНачисления.ГрафикРаботы КАК ГрафикРаботы,
                      |	ДополнительныеНачисления.ПериодРегистрации КАК ПериодРегистрации
                      |ПОМЕСТИТЬ втОсновнаяТаблица
                      |ИЗ
                      |	РегистрРасчета.ДополнительныеНачисления КАК ДополнительныеНачисления
                      |ГДЕ
                      |	ДополнительныеНачисления.Регистратор = &Ссылка
                      |	И ДополнительныеНачисления.ВидРасчета В(&ВидыРасчета)
                      |
                      |ИНДЕКСИРОВАТЬ ПО
                      |	НомерСтроки
                      |;
                      |
                      |////////////////////////////////////////////////////////////////////////////////
                      |ВЫБРАТЬ
                      |	ГрафикиРаботы.ГрафикРаботы КАК ГрафикРаботы,
                      |	СУММА(ЕСТЬNULL(ГрафикиРаботы.Дней, 0)) КАК РабочихДней
                      |ПОМЕСТИТЬ втДопДанныеГрафика
                      |ИЗ
                      |	РегистрСведений.ГрафикиРаботы КАК ГрафикиРаботы
                      |ГДЕ
                      |	ГрафикиРаботы.ГрафикРаботы В
                      |			(ВЫБРАТЬ
                      |				втОсновнаяТаблица.ГрафикРаботы КАК ГрафикРаботы
                      |			ИЗ
                      |				втОсновнаяТаблица КАК втОсновнаяТаблица)
                      |	И ГрафикиРаботы.Дата МЕЖДУ &НачалоПериодаРегистрации И &КонецПериодаРегистрации
                      |
                      |СГРУППИРОВАТЬ ПО
                      |	ГрафикиРаботы.ГрафикРаботы
                      |
                      |ИНДЕКСИРОВАТЬ ПО
                      |	ГрафикРаботы
                      |;
                      |
                      |////////////////////////////////////////////////////////////////////////////////
                      |ВЫБРАТЬ
                      |	втОсновнаяТаблица.НомерСтроки КАК НомерСтроки,
                      |	ЕСТЬNULL(ДополнительныеНачисленияБазаОсновныеНачисления.РезультатБаза, 0) КАК БазаНачислений,
                      |	ЕСТЬNULL(ДополнительныеНачисленияБазаОсновныеНачисления.ОтработаноДнейБаза, 0) КАК ОтработаноДней,
                      |	ПроцентКомпенсации.Значение КАК ПроцентКомпенсацииЗатратНаОбед,
                      |	ЕСТЬNULL(втДопДанныеГрафика.РабочихДней, 0) КАК БазаРабочихДней
                      |ИЗ
                      |	втОсновнаяТаблица КАК втОсновнаяТаблица
                      |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ДополнительныеНачисления.БазаОсновныеНачисления(
                      |				&Измерения,
                      |				&Измерения,
                      |				,
                      |				Регистратор = &Ссылка
                      |					И ВидРасчета В (&ВидыРасчета)) КАК ДополнительныеНачисленияБазаОсновныеНачисления
                      |		ПО втОсновнаяТаблица.НомерСтроки = ДополнительныеНачисленияБазаОсновныеНачисления.НомерСтроки
                      |		ЛЕВОЕ СОЕДИНЕНИЕ втДопДанныеГрафика КАК втДопДанныеГрафика
                      |		ПО втОсновнаяТаблица.ГрафикРаботы = втДопДанныеГрафика.ГрафикРаботы,
                      |	Константа.ПроцентКомпенсацииЗатратНаОбед КАК ПроцентКомпенсации";
      
      Запрос.УстановитьПараметр("НачалоПериодаРегистрации", ПериодРегистрации);
      Запрос.УстановитьПараметр("КонецПериодаРегистрации", КонецПериодаРегистрации);
      
      ВидыРасчета = Новый Массив;
      ВидыРасчета.Добавить(ПланыВидовРасчета.ДополнительныеНачисления.КомпенсацияЗатратНаОбеды);
      Запрос.УстановитьПараметр("ВидыРасчета", ВидыРасчета);                                   
      
      Измерения = Новый Массив;
      Измерения.Добавить("Сотрудник");
      Измерения.Добавить("Подразделение");
      Запрос.УстановитьПараметр("Измерения", Измерения);
      
      Разрезы = Новый Массив;
      Разрезы.Добавить("НомерСтроки");
      Запрос.УстановитьПараметр("Разрезы", Разрезы);
      Запрос.УстановитьПараметр("Ссылка", Ссылка);
      
      РезультатЗапроса = Запрос.Выполнить();
      
      Выборка = РезультатЗапроса.Выбрать();
      
      Для Каждого Движение Из Движения.ДополнительныеНачисления Цикл
        
        Если ВидыРасчета.Найти(Движение.ВидРасчета) = Неопределено Тогда
          Продолжить;
        КонецЕсли;     
        
        Выборка.Сбросить();
        Выборка.НайтиСледующий(Движение.НомерСтроки, "НомерСтроки");
        
        Если Движение.ВидРасчета = ПланыВидовРасчета.ДополнительныеНачисления.КомпенсацияЗатратНаОбеды Тогда
          Движение.Результат = ?(Выборка.БазаРабочихДней <> 0,
                       Выборка.ОтработаноДней * Выборка.БазаНачислений / Выборка.БазаРабочихДней 
                       * Выборка.ПроцентКомпенсацииЗатратНаОбед / 100,
                       0);
          ЗаполнитьЗначенияСвойств(Движение, Выборка, 
                       "ОтработаноДней, БазаНачислений, БазаРабочихДней, ПроцентКомпенсацииЗатратНаОбед");								   
        КонецЕсли;
        
      КонецЦикла;
      
      Движения.ДополнительныеНачисления.Записать();
      
    КонецПроцедуры
    
    Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
      Если ОсновныеНачисления.Количество() = 0 И ДополнительныеНачисления.Количество() = 0 Тогда
        Сообщение = Новый СообщениеПользователю;
        Сообщение.Текст = "Документ не заполнен";
        Сообщение.Сообщить();		           
        Отказ = Истина;
      КонецЕсли;
    КонецПроцедуры
    
    <p>///// Модуль объекта ЗаполнениеГрафика</p>
    Процедура ЗаполнитьГрафик(ДатаНачала, ДатаОкончания, ВыходныеДни, ГрафикРаботы) Экспорт 
      
      Набор = РегистрыСведений.ГрафикиРаботы.СоздатьНаборЗаписей();
      
      Набор.Отбор.ГрафикРаботы.Установить(ГрафикРаботы);
        
      ЧислоСекундВСутках = 86400;
      
      Дат = ДатаНачала;	
      Пока Дат <= ДатаОкончания Цикл
        Запись = Набор.Добавить();
        Запись.ГрафикРаботы = ГрафикРаботы;
        Запись.Дата = Дат;
        Если Найти(ВыходныеДни, Строка(ДеньНедели(Дат))) Тогда
          Запись.Часов = 0;
          Запись.Дней = 0;
        Иначе	          
          Запись.Часов = 8;
          Запись.Дней = 1;			
        КонецЕсли; 
        Дат = Дат + ЧислоСекундВСутках;
      КонецЦикла; 
      Набор.Записать();
    КонецПроцедуры
    
    <p>///// Модуль формы</p>
    &НаКлиенте
    Процедура Заполнить(Команда)
      ВыполнитьОбработку();
      Сообщить("Обработка завершена!");
    КонецПроцедуры
    
    &НаСервере
    Процедура ВыполнитьОбработку()
      ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
        ОбработкаОбъект.ЗаполнитьГрафик(ВыборПериода.ДатаНачала, ВыборПериода.ДатаОкончания, ВыходныеДни, ГрафикРаботы);
    КонецПроцедуры
    <embed src="pdf/ОбщееСПР10.pdf" type="application/pdf" width="1200" height="800">
      <img src="pdf/СПР10.jpg">
  </pre>
</body>

</html>