<!doctype html>
<html lang="ru">

<head>
  <meta charset="utf-8">
  <title>HTML Document Template</title>
  <link href="styles.css" rel="stylesheet">
</head>

<body>
  <div class="header__menu">
    <ul>
      <li><a href="str/1">1</a></li>
      <li><a href="str/2">2</a></li>
      <li><a href="str/3">3</a></li>
      <li><a href="str/4">4</a></li>
      <li><a href="str/5">5</a></li>
      <li><a href="str/6">6</a></li>
      <li><a href="str/7">7</a></li>
      <li><a href="str/8">8</a></li>
      <li><a href="str/9">9</a></li>
      <li><a href="str/10">10</a></li>
      <li><a href="str/11">11</a></li>
      <li><a href="str/12">12</a></li>
      <li><a href="str/13">13</a></li>
      <li><a href="str/14">14</a></li>
      <li><a href="str/15">15</a></li>
      <li><a href="str/16">16</a></li>
    </ul>
  </div>
  <h1>12</h1>
  <H2 class="OU">
    Оперативный учет
  </H2>
  <pre>
    <p>///// Общий модуль ОбработкаТабличнойЧасти</p>
    Процедура РассчитатьСуммуВСтроке(ТекСтрока) Экспорт
      ТекСтрока.Сумма = ТекСтрока.Цена * ТекСтрока.Количество;
    КонецПроцедуры   
    
    <p>///// Общий модуль ПрефиксацияОбъектов</p>
    Функция НомерНаПечать(Знач НомерОбъекта) Экспорт
      Пока Лев(НомерОбъекта, 1) = "0" Цикл
        НомерОбъекта = Сред(НомерОбъекта, 2);
      КонецЦикла;
      Возврат НомерОбъекта;
    КонецФункции
    
    <p>///// Документ ЗаказКлиента</p>   
    Процедура ОбработкаПроведения(Отказ, РежимПроведения)
      
      Движения.ТоварыКЗакупке.Записывать = Истина;
      
      Запрос = Новый Запрос;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	ЗаказКлиентаСписокНоменклатуры.Номенклатура КАК Номенклатура,
        |	СУММА(ЗаказКлиентаСписокНоменклатуры.Количество) КАК Количество,
        |	&Ссылка КАК ЗаказКлиента,
        |	&Период КАК Период
        |ИЗ
        |	Документ.ЗаказКлиента.СписокНоменклатуры КАК ЗаказКлиентаСписокНоменклатуры
        |ГДЕ
        |	ЗаказКлиентаСписокНоменклатуры.Ссылка = &Ссылка
        |
        |СГРУППИРОВАТЬ ПО
        |	ЗаказКлиентаСписокНоменклатуры.Номенклатура";
      
      Запрос.УстановитьПараметр("Ссылка", Ссылка);
      Запрос.УстановитьПараметр("Период", Дата);
      
      РезультатЗапроса = Запрос.Выполнить();
      
      Выборка = РезультатЗапроса.Выбрать();
      
      Пока Выборка.Следующий() Цикл
        Движение = Движения.ТоварыКЗакупке.ДобавитьПриход();
        ЗаполнитьЗначенияСвойств(Движение, Выборка);
      КонецЦикла;
      
    КонецПроцедуры
    
    <p>///// Документ ПриходнаяНакладная</p>
    Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
      СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
    КонецПроцедуры
    
    Процедура ОбработкаПроведения(Отказ, РежимПроведения)
      
      Запрос = Новый Запрос;
      МенеджерВТ = Новый МенеджерВременныхТаблиц;
      Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
        |	СУММА(ПриходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
        |	ПриходнаяНакладнаяСписокНоменклатуры.ЗаказКлиента КАК ЗаказКлиента,
        |	СУММА(ПриходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма
        |ПОМЕСТИТЬ втТЧТовары
        |ИЗ
        |	Документ.ПриходнаяНакладная.СписокНоменклатуры КАК ПриходнаяНакладнаяСписокНоменклатуры
        |ГДЕ
        |	ПриходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
        |
        |СГРУППИРОВАТЬ ПО
        |	ПриходнаяНакладнаяСписокНоменклатуры.ЗаказКлиента,
        |	ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура
        |
        |ИНДЕКСИРОВАТЬ ПО
        |	Номенклатура,
        |	ЗаказКлиента
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	втТЧТовары.Номенклатура КАК Номенклатура,
        |	втТЧТовары.ЗаказКлиента КАК ЗаказКлиента,
        |	втТЧТовары.Количество КАК Количество,
        |	&Период КАК Период,
        |	втТЧТовары.Сумма КАК Стоимость
        |ИЗ
        |	втТЧТовары КАК втТЧТовары";
      
      Запрос.УстановитьПараметр("Период", Дата);
      Запрос.УстановитьПараметр("Ссылка", Ссылка);
      
      РезультатЗапроса = Запрос.Выполнить();
      
      Выборка = РезультатЗапроса.Выбрать();
      
      Пока Выборка.Следующий() Цикл
        Движение = Движения.ТоварыКЗакупке.ДобавитьРасход();
        ЗаполнитьЗначенияСвойств(Движение, Выборка);
        
        Движение = Движения.ОстаткиНоменклатуры.ДобавитьПриход();
        ЗаполнитьЗначенияСвойств(Движение, Выборка);
      КонецЦикла;
      
      Движения.ТоварыКЗакупке.Записывать = Истина;
      Движения.ТоварыКЗакупке.БлокироватьДляИзменения = Истина;
      Движения.Записать();
      
      Запрос = Новый Запрос;
      Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	ТоварыКЗакупкеОстатки.Номенклатура.Представление КАК НоменклатураПредставление,
        |	ТоварыКЗакупкеОстатки.ЗаказКлиента.Представление КАК ЗаказКлиентаПредставление,
        |	-ТоварыКЗакупкеОстатки.КоличествоОстаток КАК КоличествоНехватки
        |ИЗ
        |	РегистрНакопления.ТоварыКЗакупке.Остатки(
        |			&Граница,
        |			(Номенклатура, ЗаказКлиента) В
        |				(ВЫБРАТЬ
        |					Т.Номенклатура,
        |					Т.ЗаказКлиента
        |				ИЗ
        |					втТЧТовары КАК Т)) КАК ТоварыКЗакупкеОстатки
        |ГДЕ
        |	ТоварыКЗакупкеОстатки.КоличествоОстаток < 0";
      
      Запрос.УстановитьПараметр("Граница", Новый Граница(МоментВремени(), ВидГраницы.Включая));
      
      РезультатЗапроса = Запрос.Выполнить();
      
      Если Не РезультатЗапроса.Пустой() Тогда
        
        Отказ = Истина;
        
        Выборка = РезультатЗапроса.Выбрать();
        
        Пока Выборка.Следующий() Цикл
          Сообщение = Новый СообщениеПользователю;
          Сообщение.Текст = СтрШаблон("Недостаточно товара %1 по заказу %2 в количестве %3",
          Выборка.НоменклатураПредставление, Выборка.ЗаказКлиентаПредставление, Выборка.КоличествоНехватки);
          Сообщение.Сообщить();
        КонецЦикла;
        
        Возврат;
      КонецЕсли;
      
      Движения.ОстаткиНоменклатуры.Записывать = Истина;
    
    КонецПроцедуры
    
    <p>///// Документ РасходнаяНакладная</p> 
    Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
      СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
    КонецПроцедуры
    
    Процедура ОбработкаПроведения(Отказ, Режим)
    
      // удаление движения
      Движения.ОстаткиНоменклатуры.Записывать = Истина;
      Движения.Записать();
      
      // взвели флаг
      Движения.ОстаткиНоменклатуры.Записывать = Истина;
      
      Блокировка = Новый БлокировкаДанных;
      ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
      ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
      ЭлементБлокировки.УстановитьЗначение("ЗаказКлиента", ЗаказКлиента);
      ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
      ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
      Блокировка.Заблокировать();
      
      // контроль остатков
      Запрос = Новый Запрос;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
        |	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество
        |ПОМЕСТИТЬ втТЧТовары
        |ИЗ
        |	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
        |ГДЕ
        |	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
        |
        |СГРУППИРОВАТЬ ПО
        |	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура
        |
        |ИНДЕКСИРОВАТЬ ПО
        |	Номенклатура
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	втТЧТовары.Номенклатура КАК Номенклатура,
        |	втТЧТовары.Номенклатура.Представление КАК НоменклатураПредставление,
        |	втТЧТовары.Количество КАК Количество,
        |	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
        |	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.СтоимостьОстаток, 0) КАК СтоимостьОстаток
        |ИЗ
        |	втТЧТовары КАК втТЧТовары
        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
        |				&МоментВремени,
        |				ЗаказКлиента = &ЗаказКлиента
        |					И Номенклатура В
        |						(ВЫБРАТЬ
        |							втТЧТовары.Номенклатура КАК Номенклатура
        |						ИЗ
        |							втТЧТовары КАК втТЧТовары)) КАК ОстаткиНоменклатурыОстатки
        |		ПО втТЧТовары.Номенклатура = ОстаткиНоменклатурыОстатки.Номенклатура";
      
      Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
      Запрос.УстановитьПараметр("Ссылка", Ссылка);
      Запрос.УстановитьПараметр("ЗаказКлиента", ЗаказКлиента);
      
      РезультатЗапроса = Запрос.Выполнить();
      
      Выборка = РезультатЗапроса.Выбрать();
      
      Пока Выборка.Следующий() Цикл
        Если Выборка.Количество > Выборка.КоличествоОстаток Тогда
          Сообщение = Новый СообщениеПользователю;
          Сообщение.Текст = "Не хватает товара " + Выборка.НоменклатураПредставление + " в количестве " + (Выборка.Количество - Выборка.КоличествоОстаток);
          Сообщение.Сообщить();	
          
          Отказ = Истина;
          
        КонецЕсли;	
        
        Если Отказ Тогда
          Продолжить;
        КонецЕсли;
        
        Если Выборка.Количество = Выборка.КоличествоОстаток Тогда
          Себестоимость = Выборка.СтоимостьОстаток;
        Иначе
          Себестоимость = Выборка.СтоимостьОстаток / Выборка.КоличествоОстаток * Выборка.Количество;
        КонецЕсли;
                
        Движение = Движения.ОстаткиНоменклатуры.Добавить();
        Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
        Движение.Период = Дата;
        Движение.Номенклатура = Выборка.Номенклатура;
        Движение.ЗаказКлиента = ЗаказКлиента;
        Движение.Количество = Выборка.Количество;
        Движение.Стоимость = Себестоимость;
        
      КонецЦикла;
      
    КонецПроцедуры
    
    Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
      Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаказКлиента") Тогда
        // Заполнение шапки
        ЗаказКлиента = ДанныеЗаполнения;
        Для Каждого ТекСтрокаСписокНоменклатуры Из ДанныеЗаполнения.СписокНоменклатуры Цикл
          НоваяСтрока = СписокНоменклатуры.Добавить();
          НоваяСтрока.Номенклатура = ТекСтрокаСписокНоменклатуры.Номенклатура;
          НоваяСтрока.Количество = ТекСтрокаСписокНоменклатуры.Количество;			
        КонецЦикла;
      КонецЕсли;
    КонецПроцедуры
    
    <p>///// Модуль формы</p>
    &НаКлиенте
    Процедура СписокНоменклатурыКоличествоПриИзменении(Элемент)
      ТекСтрока = Элементы.СписокНоменклатуры.ТекущиеДанные;
      ОбработкаТабличнойЧасти.РассчитатьСуммуВСтроке(ТекСтрока);
    КонецПроцедуры
    
    &НаКлиенте
    Процедура СписокНоменклатурыЦенаПриИзменении(Элемент)
      ТекСтрока = Элементы.СписокНоменклатуры.ТекущиеДанные;
      ОбработкаТабличнойЧасти.РассчитатьСуммуВСтроке(ТекСтрока);
    КонецПроцедуры
    <embed src="pdf/ОбщееОУ12.pdf" type="application/pdf" width="1200" height="800">
      <img src="pdf/ОУ12.jpg">
    <H2 class="OU">
      Бухгалтерский учет
    </H2>
    <p>///// Общий модуль ОбработкаТабличнойЧастиКлиент</p>
    Процедура РассчитатьСуммуВСтроке(ТекСтрока) Экспорт
      ТекСтрока.Сумма = ТекСтрока.Цена * ТекСтрока.Количество;
    КонецПроцедуры 
    
    <p>///// Документ Операция</p>   
    Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
      Если Не Движения.Управленческий.Записывать Тогда
        Движения.Управленческий.Прочитать();
        Движения.Управленческий.Записывать = Истина;
      КонецЕсли;
      
      Для Каждого Движение Из Движения.Управленческий Цикл
        Движение.Период = Дата;
        Движение.Активность = Не ПометкаУдаления;
      КонецЦикла;
    КонецПроцедуры
    
    Процедура ПриКопировании(ОбъектКопирования)	
      Выборка = РегистрыБухгалтерии.Управленческий.ВыбратьПоРегистратору(ОбъектКопирования.Ссылка);
      Пока Выборка.Следующий() Цикл
        Движение = Движения.Управленческий.Добавить();
        ЗаполнитьЗначенияСвойств(Движение, Выборка);
        ЗаполнитьЗначенияСвойств(Движение.СубконтоДт, Выборка.СубконтоДт);
        ЗаполнитьЗначенияСвойств(Движение.СубконтоКт, Выборка.СубконтоКт);
      КонецЦикла;
    КонецПроцедуры
     
    <p>///// Документ ПриходнаяНакладная</p>
    Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
      СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
    КонецПроцедуры
    
    <p>///// Документ РасходнаяНакладная</p> 
    Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
      СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
    КонецПроцедуры
    
    Процедура ОбработкаПроведения(Отказ, РежимПроведения)
      
      Движения.Управленческий.Записывать = Истина;
      Движения.Записать();
      
      Движения.Управленческий.Записывать = Истина;
      
      Блокировка = Новый БлокировкаДанных;
      
      ЭлементБлокировки = Блокировка.Добавить("РегистрБухгалтерии.Управленческий");
      ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
      ЭлементБлокировки.УстановитьЗначение("Счет", ПланыСчетов.Управленческий.Товары);
      ЭлементБлокировки.УстановитьЗначение(ПланыВидовХарактеристик.ВидыСубконто.Склады, Склад);
      ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
      ЭлементБлокировки.ИспользоватьИзИсточникаДанных(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура, "Номенклатура");
      
      ЭлементБлокировки = Блокировка.Добавить("РегистрБухгалтерии.Управленческий");
      ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
      ЭлементБлокировки.УстановитьЗначение("Счет", ПланыСчетов.Управленческий.Материалы);
      ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
      ЭлементБлокировки.ИспользоватьИзИсточникаДанных(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура, "Номенклатура");
      Блокировка.Заблокировать();	
      
      Запрос = Новый Запрос;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
        |	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
        |	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма,
        |	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуры КАК ВидНоменклатуры
        |ПОМЕСТИТЬ втТЧТовары
        |ИЗ
        |	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
        |ГДЕ
        |	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
        |
        |СГРУППИРОВАТЬ ПО
        |	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура,
        |	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуры
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	втТЧТовары.Номенклатура КАК Номенклатура,
        |	втТЧТовары.Количество КАК Количество,
        |	втТЧТовары.Сумма КАК Сумма
        |ПОМЕСТИТЬ втТовары
        |ИЗ
        |	втТЧТовары КАК втТЧТовары
        |ГДЕ
        |	втТЧТовары.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Товар)
        |
        |ИНДЕКСИРОВАТЬ ПО
        |	Номенклатура
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	втТЧТовары.Номенклатура КАК Номенклатура,
        |	втТЧТовары.Количество КАК Количество,
        |	втТЧТовары.Сумма КАК Сумма
        |ПОМЕСТИТЬ втМатериалы
        |ИЗ
        |	втТЧТовары КАК втТЧТовары
        |ГДЕ
        |	втТЧТовары.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Материал)
        |
        |ИНДЕКСИРОВАТЬ ПО
        |	Номенклатура
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	втТовары.Номенклатура КАК Номенклатура,
        |	втТовары.Номенклатура.Представление КАК НоменклатураПредставление,
        |	ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Товар) КАК ВидНоменклатуры,
        |	втТовары.Количество КАК Количество,
        |	втТовары.Сумма КАК Сумма,
        |	ЕСТЬNULL(УправленческийОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
        |	ЕСТЬNULL(УправленческийОстатки.СуммаОстаток, 0) КАК СуммаОстаток
        |ИЗ
        |	втТовары КАК втТовары
        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(
        |				&МоментВремени,
        |				Счет = &СчетТовары,
        |				&ВидыСубконтоТовары,
        |				Субконто1 В
        |						(ВЫБРАТЬ
        |							втТовары.Номенклатура КАК Номенклатура
        |						ИЗ
        |							втТовары КАК втТовары)
        |					И Субконто2 = &Склад) КАК УправленческийОстатки
        |		ПО втТовары.Номенклатура = УправленческийОстатки.Субконто1
        |
        |ОБЪЕДИНИТЬ ВСЕ
        |
        |ВЫБРАТЬ
        |	втМатериалы.Номенклатура,
        |	втМатериалы.Номенклатура.Представление,
        |	ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Материал),
        |	втМатериалы.Количество,
        |	втМатериалы.Сумма,
        |	ЕСТЬNULL(УправленческийОстатки.КоличествоОстаток, 0),
        |	ЕСТЬNULL(УправленческийОстатки.СуммаОстаток, 0)
        |ИЗ
        |	втМатериалы КАК втМатериалы
        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(
        |				&МоментВремени,
        |				Счет = &СчетМатериалы,
        |				&ВидСубконтоНоменклатура,
        |				Субконто1 В
        |					(ВЫБРАТЬ
        |						втМатериалы.Номенклатура КАК Номенклатура
        |					ИЗ
        |						втМатериалы КАК втМатериалы)) КАК УправленческийОстатки
        |		ПО втМатериалы.Номенклатура = УправленческийОстатки.Субконто1";
      
      Запрос.УстановитьПараметр("ВидСубконтоНоменклатура", ПланыВидовХарактеристик.ВидыСубконто.Номенклатура);
      
      ВидыСубконтоТовары = Новый Массив;
      ВидыСубконтоТовары.Добавить(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура);
      ВидыСубконтоТовары.Добавить(ПланыВидовХарактеристик.ВидыСубконто.Склады);
      
      Запрос.УстановитьПараметр("ВидыСубконтоТовары", ВидыСубконтоТовары);
      Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
      Запрос.УстановитьПараметр("Склад", Склад);
      Запрос.УстановитьПараметр("Ссылка", Ссылка);
      Запрос.УстановитьПараметр("СчетМатериалы", ПланыСчетов.Управленческий.Материалы);
      Запрос.УстановитьПараметр("СчетТовары", ПланыСчетов.Управленческий.Товары);
      
      РезультатЗапроса = Запрос.Выполнить();
      
      Выборка = РезультатЗапроса.Выбрать();
      
      Пока Выборка.Следующий() Цикл
        
        Если Выборка.Количество > Выборка.КоличествоОстаток Тогда
          Отказ = Истина;
          Сообщение = Новый СообщениеПользователю;
          Сообщение.Текст = СтрШаблон("Недостаточно позиции %1 в количестве %2",
          Выборка.НоменклатураПредставление, Выборка.Количество - Выборка.КоличествоОстаток);
          Сообщение.Сообщить();
        КонецЕсли;
        
        Если Отказ Тогда
          Продолжить;
        КонецЕсли;
        
        // себестоимость
        Если Выборка.Количество = Выборка.КоличествоОстаток Тогда
          Себестоимость = Выборка.СуммаОстаток;
        Иначе
          Себестоимость = Выборка.СуммаОстаток / Выборка.КоличествоОстаток * Выборка.Количество;
        КонецЕсли;
        
        Движение = Движения.Управленческий.Добавить();
        Движение.Период = Дата;
        Движение.СчетДт = ПланыСчетов.Управленческий.ПрибылиУбытки;
        Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = Выборка.Номенклатура;
        
        Если Выборка.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Товар Тогда
          Движение.СчетКт = ПланыСчетов.Управленческий.Товары;
          Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = Выборка.Номенклатура;
          Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Склады] = Склад;
        Иначе	
          Движение.СчетКт = ПланыСчетов.Управленческий.Материалы;
          Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = Выборка.Номенклатура;
        КонецЕсли;	
        Движение.КоличествоКт = Выборка.Количество;
        
        Движение.Сумма = Себестоимость;
        
        // продажная проводка
        Движение = Движения.Управленческий.Добавить();
        Движение.Период = Дата;
        Движение.СчетДт = ПланыСчетов.Управленческий.Покупатели;
        Движение.СчетКт = ПланыСчетов.Управленческий.ПрибылиУбытки;
        Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = Выборка.Номенклатура;
        Движение.Сумма = Выборка.Сумма;
        
      КонецЦикла;
    
      
    КонецПроцедуры
    
    <p>///// Документ Сборка</p>
    Процедура ОбработкаПроведения(Отказ, РежимПроведения)
      
      Движения.Управленческий.Записывать = Истина;
      Движения.Записать();
      
      Движения.Управленческий.Записывать = Истина;
      
      Блокировка = Новый БлокировкаДанных;
      ЭлементБлокировки = Блокировка.Добавить("РегистрБухгалтерии.Управленческий");
      ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
      ЭлементБлокировки.УстановитьЗначение("Счет", ПланыСчетов.Управленческий.Материалы);
      ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
      ЭлементБлокировки.ИспользоватьИзИсточникаДанных(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура, "Номенклатура");
      Блокировка.Заблокировать();
      
      Запрос = Новый Запрос;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	СборкаСписокНоменклатуры.Номенклатура КАК Номенклатура,
        |	СУММА(СборкаСписокНоменклатуры.Количество) КАК Количество
        |ПОМЕСТИТЬ втТЧТовары
        |ИЗ
        |	Документ.Сборка.СписокНоменклатуры КАК СборкаСписокНоменклатуры
        |ГДЕ
        |	СборкаСписокНоменклатуры.Ссылка = &Ссылка
        |
        |СГРУППИРОВАТЬ ПО
        |	СборкаСписокНоменклатуры.Номенклатура
        |
        |ИНДЕКСИРОВАТЬ ПО
        |	Номенклатура
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	втТЧТовары.Номенклатура КАК Номенклатура,
        |	втТЧТовары.Номенклатура.Представление КАК НоменклатураПредставление,
        |	втТЧТовары.Количество КАК Количество,
        |	ЕСТЬNULL(УправленческийОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
        |	ЕСТЬNULL(УправленческийОстатки.СуммаОстаток, 0) КАК СуммаОстаток
        |ИЗ
        |	втТЧТовары КАК втТЧТовары
        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(
        |				&МоментВремени,
        |				Счет = &СчетМатериалы,
        |				&ВидСубконтоНоменклатура,
        |				Субконто1 В
        |					(ВЫБРАТЬ
        |						втТЧТовары.Номенклатура КАК Номенклатура
        |					ИЗ
        |						втТЧТовары КАК втТЧТовары)) КАК УправленческийОстатки
        |		ПО втТЧТовары.Номенклатура = УправленческийОстатки.Субконто1";
      
      Запрос.УстановитьПараметр("ВидСубконтоНоменклатура", ПланыВидовХарактеристик.ВидыСубконто.Номенклатура);
      Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
      Запрос.УстановитьПараметр("Ссылка", Ссылка);
      Запрос.УстановитьПараметр("СчетМатериалы", ПланыСчетов.Управленческий.Материалы);
      
      КоличествоВсехКомплектующих = СписокНоменклатуры.Итог("Количество");
      
      КоличествоКомплектаИтого = 0;
      
      КоличествоКомплектующихСписано = 0;
      
      РезультатЗапроса = Запрос.Выполнить();
      
      Выборка = РезультатЗапроса.Выбрать();
      
      Пока Выборка.Следующий() Цикл
        
        Если Выборка.Количество > Выборка.КоличествоОстаток Тогда
          Отказ = Истина;
          Сообщение = Новый СообщениеПользователю;
          Сообщение.Текст = СтрШаблон("Недостаточно товара %1 в количестве %2",
          Выборка.НоменклатураПредставление, Выборка.Количество - Выборка.КоличествоОстаток);
          Сообщение.Сообщить();
        КонецЕсли;
        
        Если Отказ Тогда
          Продолжить;
        КонецЕсли;
        
        Если Выборка.Количество = Выборка.КоличествоОстаток Тогда
          Себестоимость = Выборка.СуммаОстаток;
        Иначе
          Себестоимость = Выборка.СуммаОстаток / Выборка.КоличествоОстаток * Выборка.Количество;
        КонецЕсли;
        
        КоличествоКомплектующихСписано = КоличествоКомплектующихСписано + Выборка.Количество;
        
        Если КоличествоКомплектующихСписано = КоличествоВсехКомплектующих Тогда
          КоличествоКомплекта = Количество - КоличествоКомплектаИтого;
        Иначе
          КоличествоКомплекта = Выборка.Количество / КоличествоВсехКомплектующих * Количество;
        КонецЕсли;
        
        Движение = Движения.Управленческий.Добавить();
        Движение.Период = Дата;
        Движение.СчетДт = ПланыСчетов.Управленческий.Товары;
        Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = Номенклатура;
        Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Склады] = Склад;
        Движение.КоличествоДт = КоличествоКомплекта;
        
        КоличествоКомплектаИтого = КоличествоКомплектаИтого + Движение.КоличествоДт;
        
        Движение.СчетКт = ПланыСчетов.Управленческий.Материалы;
        Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = Выборка.Номенклатура;
        Движение.КоличествоКт = Выборка.Количество;
        
        Движение.Сумма = Себестоимость;
        
      КонецЦикла;
    
    КонецПроцедуры
    
    <p>///// Модуль формы</p>
    &НаКлиенте
    Процедура СписокНоменклатурыКоличествоПриИзменении(Элемент)
      ТекСтрока = Элементы.СписокНоменклатуры.ТекущиеДанные;
      ОбработкаТабличнойЧасти.РассчитатьСуммуВСтроке(ТекСтрока);
    КонецПроцедуры
    
    &НаКлиенте
    Процедура СписокНоменклатурыЦенаПриИзменении(Элемент)
      ТекСтрока = Элементы.СписокНоменклатуры.ТекущиеДанные;
      ОбработкаТабличнойЧасти.РассчитатьСуммуВСтроке(ТекСтрока);
    КонецПроцедуры
    <embed src="pdf/ОбщееБУ12.pdf" type="application/pdf" width="1200" height="800">
      <img src="pdf/БУ12.jpg">
    <H2 class="OU">
      Расчеты
    </H2>
    <p>///// Документ НачислениеЗарплаты</p>   
    Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
      Если ОсновныеНачисления.Количество() = 0 И ДополнительныеНачисления.Количество() = 0 Тогда
        Сообщение = Новый СообщениеПользователю;
        Сообщение.Текст = "Документ не заполнен";
        Сообщение.Сообщить();		           
        Отказ = Истина;
      КонецЕсли;
    КонецПроцедуры
    
    Процедура ОбработкаПроведения(Отказ, РежимПроведения)
      
      ПериодРегистрации = НачалоМесяца(Дата);
      КонецРасчетногоПериода = КонецМесяца(ПериодРегистрации);
      
      Движения.ОсновныеНачисления.Записывать = Истина;
      Для Каждого Строка Из ОсновныеНачисления Цикл
        Движение = Движения.ОсновныеНачисления.Добавить();
        ЗаполнитьЗначенияСвойств(Движение, Строка);
        Движение.ПериодРегистрации = ПериодРегистрации;
        Движение.ПериодДействияНачало = Строка.ДатаНачала;
        Движение.ПериодДействияКонец = КонецДня(Строка.ДатаОкончания);
      КонецЦикла;
      
      Движения.ДополнительныеНачисления.Записывать = Истина;
      Для Каждого Строка Из ДополнительныеНачисления Цикл
        Движение = Движения.ДополнительныеНачисления.Добавить();
        ЗаполнитьЗначенияСвойств(Движение, Строка);
        Движение.ПериодРегистрации = ПериодРегистрации;
        Если Строка.ВидРасчета = ПланыВидовРасчета.ДополнительныеНачисления.КомпенсацияНаРемонт Тогда
          Движение.БазовыйПериодНачало = ПериодРегистрации;
          Движение.БазовыйПериодКонец  = КонецРасчетногоПериода;
        КонецЕсли;
      КонецЦикла;	
      
      Движения.Записать();
      
      Запрос = Новый Запрос;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
        |	ОсновныеНачисленияДанныеГрафика.Сотрудник КАК Сотрудник,
        |	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ЧасовФактическийПериодДействия, 0) КАК ОтработаноЧасов,
        |	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ДнейФактическийПериодДействия, 0) КАК ОтработаноДней
        |ПОМЕСТИТЬ втНачисления
        |ИЗ
        |	РегистрРасчета.ОсновныеНачисления.ДанныеГрафика(
        |			Регистратор = &Регистратор
        |				И ВидРасчета В (&ВидыРасчета)) КАК ОсновныеНачисленияДанныеГрафика
        |
        |ИНДЕКСИРОВАТЬ ПО
        |	Сотрудник
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	втНачисления.НомерСтроки КАК НомерСтроки,
        |	втНачисления.ОтработаноЧасов КАК ОтработаноЧасов,
        |	втНачисления.ОтработаноДней КАК ОтработаноДней,
        |	ЕСТЬNULL(СведенияОСотрудникахСрезПоследних.ТарифнаяСтавка, 0) КАК ТарифнаяСтавка,
        |	ШтрафЗаПрогул.Значение КАК ШтрафЗаПрогул
        |ИЗ
        |	втНачисления КАК втНачисления
        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОСотрудниках.СрезПоследних(
        |				&ПериодРегистрации,
        |				Сотрудник В
        |					(ВЫБРАТЬ
        |						втНачисления.Сотрудник КАК Сотрудник
        |					ИЗ
        |						втНачисления КАК втНачисления)) КАК СведенияОСотрудникахСрезПоследних
        |		ПО втНачисления.Сотрудник = СведенияОСотрудникахСрезПоследних.Сотрудник
        |		ЛЕВОЕ СОЕДИНЕНИЕ Константа.ШтрафЗаПрогул КАК ШтрафЗаПрогул
        |		ПО (ИСТИНА)";
      
      ВидыРасчета = Новый Массив;
      ВидыРасчета.Добавить(ПланыВидовРасчета.ОсновныеНачисления.ОплатаПоЧасовомуТарифу);
      ВидыРасчета.Добавить(ПланыВидовРасчета.ОсновныеНачисления.Прогул);
      
      Запрос.УстановитьПараметр("ВидыРасчета", ВидыРасчета);
      Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
      Запрос.УстановитьПараметр("Регистратор", Ссылка);
      
      РезультатЗапроса = Запрос.Выполнить();
      
      Выборка = РезультатЗапроса.Выбрать();
      
      Для Каждого Движение Из Движения.ОсновныеНачисления Цикл
        
        Если ВидыРасчета.Найти(Движение.ВидРасчета) = Неопределено Тогда
          Продолжить;
        КонецЕсли;
        
        Выборка.Сбросить();
        Выборка.НайтиСледующий(Движение.НомерСтроки, "НомерСтроки");
        
        Если Движение.ВидРасчета = ПланыВидовРасчета.ОсновныеНачисления.ОплатаПоЧасовомуТарифу Тогда
          Движение.Результат = Выборка.ТарифнаяСтавка * Выборка.ОтработаноЧасов;
          ЗаполнитьЗначенияСвойств(Движение, Выборка, "ТарифнаяСтавка, ОтработаноЧасов");
        ИначеЕсли Движение.ВидРасчета = ПланыВидовРасчета.ОсновныеНачисления.Прогул Тогда
          Движение.Результат = Выборка.ОтработаноДней * Выборка.ШтрафЗаПрогул;
          ЗаполнитьЗначенияСвойств(Движение, Выборка, "ОтработаноДней, ШтрафЗаПрогул");
        КонецЕсли;
        
      КонецЦикла;
      
      Движения.ОсновныеНачисления.Записать(, Истина);
    
      Движения.НаезженныеЧасы.Записать();
      
      //Блокировка = Новый БлокировкаДанных;
      //ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.НаезженныеЧасы");
      //ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
      //
      //Запрос = Новый Запрос;
      //Запрос.Текст = 
      //	"ВЫБРАТЬ РАЗЛИЧНЫЕ
      //	|	ДополнительныеНачисления.Сотрудник КАК Сотрудник
      //	|ИЗ
      //	|	РегистрРасчета.ДополнительныеНачисления КАК ДополнительныеНачисления
      //	|ГДЕ
      //	|	ДополнительныеНачисления.Регистратор = &Регистратор
      //	|	И ДополнительныеНачисления.ВидРасчета = &ВидРасчетаКомпенсация";
      //
      //Запрос.УстановитьПараметр("ВидРасчетаКомпенсация", ПланыВидовРасчета.ДополнительныеНачисления.КомпенсацияНаРемонт);
      //Запрос.УстановитьПараметр("Регистратор", Ссылка);
      //
      //РезультатЗапроса = Запрос.Выполнить();
      //
      //ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
      //ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Сотрудник", "Сотрудник");
      //Блокировка.Заблокировать();
        
      Запрос = Новый Запрос;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	ДополнительныеНачисления.НомерСтроки КАК НомерСтроки,
        |	ДополнительныеНачисления.Сотрудник КАК Сотрудник,
        |	ЕСТЬNULL(ДополнительныеНачисленияБазаОсновныеНачисления.РезультатБаза, 0) КАК БазаНачислений
        |ПОМЕСТИТЬ втНачисления
        |ИЗ
        |	РегистрРасчета.ДополнительныеНачисления КАК ДополнительныеНачисления
        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ДополнительныеНачисления.БазаОсновныеНачисления(
        |				&Измерения,
        |				&Измерения,
        |				,
        |				Регистратор = &Регистратор
        |					И ВидРасчета В (&ВидыРасчета)) КАК ДополнительныеНачисленияБазаОсновныеНачисления
        |		ПО ДополнительныеНачисления.НомерСтроки = ДополнительныеНачисленияБазаОсновныеНачисления.НомерСтроки
        |ГДЕ
        |	ДополнительныеНачисления.Регистратор = &Регистратор
        |
        |ИНДЕКСИРОВАТЬ ПО
        |	Сотрудник
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	втНачисления.НомерСтроки КАК НомерСтроки,
        |	втНачисления.БазаНачислений КАК БазаНачислений,
        |	ЕСТЬNULL(НаезженныеЧасыОстатки.ЧасовОстаток, 0) КАК НаезженоЧасов,
        |	ЕСТЬNULL(ПоказателиРасчетаЗарплатыСрезПоследних.ПроцентКомпенсации, 0) КАК ПроцентКомпенсации,
        |	ПределНаезженныхЧасов.Значение КАК ПределНаезженныхЧасов
        |ИЗ
        |	втНачисления КАК втНачисления
        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НаезженныеЧасы.Остатки(
        |				&НачалоСледующегоМесяца,
        |				Сотрудник В
        |					(ВЫБРАТЬ
        |						втНачисления.Сотрудник КАК Сотрудник
        |					ИЗ
        |						втНачисления КАК втНачисления)) КАК НаезженныеЧасыОстатки
        |		ПО втНачисления.Сотрудник = НаезженныеЧасыОстатки.Сотрудник
        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПоказателиРасчетаЗарплаты.СрезПоследних(&ПериодРегистрации) КАК ПоказателиРасчетаЗарплатыСрезПоследних
        |		ПО (ИСТИНА)
        |		ЛЕВОЕ СОЕДИНЕНИЕ Константа.ПределНаезженныхЧасов КАК ПределНаезженныхЧасов
        |		ПО (ИСТИНА)";
    
      ВидыРасчета = Новый Массив;
      ВидыРасчета.Добавить(ПланыВидовРасчета.ДополнительныеНачисления.КомпенсацияНаРемонт);
      Запрос.УстановитьПараметр("ВидыРасчета", ВидыРасчета);                             
      
      Измерения = Новый Массив;
      Измерения.Добавить("Сотрудник");
      Запрос.УстановитьПараметр("Измерения", Измерения);
      
      Запрос.УстановитьПараметр("НачалоСледующегоМесяца", ДобавитьМесяц(ПериодРегистрации, 1));
      Запрос.УстановитьПараметр("Регистратор", Ссылка);
      
      Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);	
        
      РезультатЗапроса = Запрос.Выполнить();
        
      Выборка = РезультатЗапроса.Выбрать();
      
      Для Каждого Движение Из Движения.ДополнительныеНачисления Цикл
        
        Если ВидыРасчета.Найти(Движение.ВидРасчета) = Неопределено Тогда
          Продолжить;			
        КонецЕсли;     
        
        Выборка.Сбросить();
        Выборка.НайтиСледующий(Движение.НомерСтроки, "НомерСтроки");
        Если Движение.ВидРасчета = ПланыВидовРасчета.ДополнительныеНачисления.КомпенсацияНаРемонт Тогда
          Если Выборка.НаезженоЧасов > Выборка.ПределНаезженныхЧасов Тогда
            Движение.Результат = Выборка.БазаНачислений * Выборка.ПроцентКомпенсации / 100;
            
            ДвижениеЧасы = Движения.НаезженныеЧасы.ДобавитьРасход();
            ДвижениеЧасы.Период = КонецРасчетногоПериода;
            ДвижениеЧасы.Сотрудник = Движение.Сотрудник;
            ДвижениеЧасы.Часов = Выборка.ПределНаезженныхЧасов;
            
            Движения.НаезженныеЧасы.Записывать = Истина;
            
          КонецЕсли;
          ЗаполнитьЗначенияСвойств(Движение, Выборка, "БазаНачислений, ПроцентКомпенсации, НаезженоЧасов, ПределНаезженныхЧасов");
        КонецЕсли;
        
      КонецЦикла;
      
      Движения.ДополнительныеНачисления.Записать();
      
    КонецПроцедуры
    
    <p>///// Документ РегистрацияНаезженныхЧасов</p>
    Процедура ОбработкаПроведения(Отказ, Режим)
    
      // регистр НаезженныеЧасы Приход
      Движения.НаезженныеЧасы.Записывать = Истина;
      Для Каждого ТекСтрокаСотрудники Из Сотрудники Цикл
        Движение = Движения.НаезженныеЧасы.Добавить();
        Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
        Движение.Период = Дата;
        Движение.Сотрудник = ТекСтрокаСотрудники.Сотрудник;
        Движение.Часов = ТекСтрокаСотрудники.Часов;
      КонецЦикла;
    
    КонецПроцедуры
    
    <p>///// Модуль объекта ЗаполнениеГрафика</p>
    Процедура ЗаполнитьГрафик(ДатаНачала, ДатаОкончания, ВыходныеДни) Экспорт 
      
      Набор = РегистрыСведений.ГрафикиРаботы.СоздатьНаборЗаписей();
      
      ЧислоСекундВСутках = 86400;
      
      Дат = ДатаНачала;	
      Пока Дат <= ДатаОкончания Цикл
        Запись = Набор.Добавить();
        Запись.Дата = Дат;
        Если Найти(ВыходныеДни, Строка(ДеньНедели(Дат))) Тогда
          Запись.Часов = 0;
          Запись.Дней = 0;
        Иначе	          
          Запись.Часов = 8;
          Запись.Дней = 1;
        КонецЕсли; 
        Дат = Дат + ЧислоСекундВСутках;
      КонецЦикла; 
      Набор.Записать();
      
    КонецПроцедуры
    
    <p>///// Модуль формы</p>
    &НаКлиенте
    Процедура Заполнить(Команда)
      ВыполнитьОбработку();
      Сообщить("Обработка завершена!");
    КонецПроцедуры
    
    &НаСервере
    Процедура ВыполнитьОбработку()
      ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
        ОбработкаОбъект.ЗаполнитьГрафик(ВыборПериода.ДатаНачала, ВыборПериода.ДатаОкончания, ВыходныеДни, ГрафикРаботы);
    КонецПроцедуры
    <embed src="pdf/ОбщееСПР12.pdf" type="application/pdf" width="1200" height="800">
      <img src="pdf/СПР12.jpg">
      <img src="pdf/СПР12.1.jpg">
  </pre>
</body>

</html>