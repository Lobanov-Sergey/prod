<!doctype html>
<html lang="ru">

<head>
	<meta charset="utf-8">
	<title>HTML Document Template</title>
	<link href="styles.css" rel="stylesheet">
</head>

<body>
	<div class="header__menu">
		<ul>
			<li><a href="str/1">1</a></li>
			<li><a href="str/2">2</a></li>
			<li><a href="str/3">3</a></li>
			<li><a href="str/4">4</a></li>
			<li><a href="str/5">5</a></li>
			<li><a href="str/6">6</a></li>
			<li><a href="str/7">7</a></li>
			<li><a href="str/8">8</a></li>
			<li><a href="str/9">9</a></li>
			<li><a href="str/10">10</a></li>
			<li><a href="str/11">11</a></li>
			<li><a href="str/12">12</a></li>
			<li><a href="str/13">13</a></li>
			<li><a href="str/14">14</a></li>
			<li><a href="str/15">15</a></li>
			<li><a href="str/16">16</a></li>
		</ul>
	</div>
	<h1>2</h1>
	<H2 class="OU">
		Оперативный учет
	</H2>
	<pre>
    <p>///// Общий модуль ОбработкаТабличнойЧасти</p>
Процедура РассчитатьСуммуВСтроке(ТекСтрока) Экспорт
	ТекСтрока.Сумма = ТекСтрока.Цена * ТекСтрока.Количество;
КонецПроцедуры 

<p>///// Документ НазначениеУчетнойПолитики</p>   
Процедура ОбработкаПроведения(Отказ, Режим)
	// регистр УчетнаяПолитика
	Движения.УчетнаяПолитика.Записывать = Истина;
	Движение = Движения.УчетнаяПолитика.Добавить();
	Движение.Период = Дата;
	Движение.МетодСписанияСебестоимости = МетодСписанияСебестоимости;
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Дата = НачалоГода(Дата);
КонецПроцедуры 

<p>///// Документ ПриходнаяНакладная</p> 
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)

	// регистр ОстаткиНоменклатуры Приход
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Для Каждого ТекСтрокаСписокНоменклатуры Из СписокНоменклатуры Цикл
		Движение = Движения.ОстаткиНоменклатуры.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = Дата;
		Движение.Номенклатура = ТекСтрокаСписокНоменклатуры.Номенклатура;
		Движение.Партия = Ссылка;
		Движение.Количество = ТекСтрокаСписокНоменклатуры.Количество;
		Движение.Стоимость = ТекСтрокаСписокНоменклатуры.Сумма;
	КонецЦикла;

КонецПроцедуры

<p>///// Документ РасходнаяНакладная</p>
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Движения.Записать();
	
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Движения.Продажи.Записывать = Истина;
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Партия",       "Партия");
	Блокировка.Заблокировать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
		|	РасходнаяНакладнаяСписокНоменклатуры.Партия КАК Партия,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма,
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуры КАК ВидНоменклатуры
		|ПОМЕСТИТЬ втТЧТовары
		|ИЗ
		|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
		|ГДЕ
		|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	РасходнаяНакладнаяСписокНоменклатуры.Партия,
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура,
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуры
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура,
		|	Партия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	&Период КАК Период,
		|	втТЧТовары.Номенклатура КАК Номенклатура,
		|	втТЧТовары.Номенклатура.Представление КАК НоменклатураПредставление,
		|	втТЧТовары.ВидНоменклатуры КАК ВидНоменклатуры,
		|	втТЧТовары.Партия КАК Партия,
		|	втТЧТовары.Партия.Представление КАК ПартияПредставление,
		|	втТЧТовары.Количество КАК Количество,
		|	втТЧТовары.Сумма КАК СуммаПродажи,
		|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
		|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.СтоимостьОстаток, 0) КАК СтоимостьОстаток
		|ИЗ
		|	втТЧТовары КАК втТЧТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
		|				&МоментВремени,
		|				(Номенклатура, Партия) В
		|					(ВЫБРАТЬ
		|						втТЧТовары.Номенклатура КАК Номенклатура,
		|						втТЧТовары.Партия КАК Партия
		|					ИЗ
		|						втТЧТовары КАК втТЧТовары
		|					ГДЕ
		|						втТЧТовары.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Товар))) КАК ОстаткиНоменклатурыОстатки
		|		ПО втТЧТовары.Номенклатура = ОстаткиНоменклатурыОстатки.Номенклатура
		|			И втТЧТовары.Партия = ОстаткиНоменклатурыОстатки.Партия
		|ИТОГИ
		|	СУММА(Количество),
		|	СУММА(СуммаПродажи)
		|ПО
		|	Номенклатура";
	
	Запрос.УстановитьПараметр("Период", Дата);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаНоменклатура.Следующий() Цикл
		
		ВыборкаДетальныеЗаписи = ВыборкаНоменклатура.Выбрать();
		
		ИтогоСебестоимость = 0;
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			Если ВыборкаДетальныеЗаписи.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Товар Тогда
			
				Если ВыборкаДетальныеЗаписи.Количество > ВыборкаДетальныеЗаписи.КоличествоОстаток Тогда
					Отказ = Истина;
					
					Сообщение = Новый СообщениеПользователю;
					Сообщение.Текст = СтрШаблон("Недостаточно товара %1 по партии %2 в количестве %3",
									  ВыборкаДетальныеЗаписи.НоменклатураПредставление, ВыборкаДетальныеЗаписи.ПартияПредставление,
					 				  ВыборкаДетальныеЗаписи.Количество - ВыборкаДетальныеЗаписи.КоличествоОстаток);
					Сообщение.Сообщить();
				КонецЕсли;
				
				Если Отказ Тогда
					Продолжить;
				КонецЕсли;
				
				Если ВыборкаДетальныеЗаписи.Количество = ВыборкаДетальныеЗаписи.КоличествоОстаток Тогда
					Себестоимость = ВыборкаДетальныеЗаписи.СтоимостьОстаток;
				Иначе
					Себестоимость = ВыборкаДетальныеЗаписи.Количество / ВыборкаДетальныеЗаписи.КоличествоОстаток * ВыборкаДетальныеЗаписи.СтоимостьОстаток;
				КонецЕсли;
				
				Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
				ЗаполнитьЗначенияСвойств(Движение, ВыборкаДетальныеЗаписи);
				Движение.Стоимость = Себестоимость;
				
				ИтогоСебестоимость = ИтогоСебестоимость + Движение.Стоимость;
			КонецЕсли;
		КонецЦикла;
		
		Движение = Движения.Продажи.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, ВыборкаНоменклатура);
		Движение.Период = Дата;
		Движение.Себестоимость = ИтогоСебестоимость;
		
	КонецЦикла;
	
	
КонецПроцедуры

<p>///// Модуль формы</p>
&НаКлиенте
Процедура СписокНоменклатурыКоличествоПриИзменении(Элемент)
	ТекСтрока = Элементы.СписокНоменклатуры.ТекущиеДанные;
	ОбработкаТабличнойЧасти.РассчитатьСуммуВСтроке(ТекСтрока);
КонецПроцедуры

&НаКлиенте
Процедура СписокНоменклатурыЦенаПриИзменении(Элемент)
	ТекСтрока = Элементы.СписокНоменклатуры.ТекущиеДанные;
	ОбработкаТабличнойЧасти.РассчитатьСуммуВСтроке(ТекСтрока);
КонецПроцедуры
<embed src="pdf/ОбщееОУ2.pdf" type="application/pdf" width="1200" height="800">
	<img src="pdf/ОУ2.jpg">
<H2 class="OU">
  Бухгалтерский учет
</H2>
<p>///// Общий модуль ОбработкаТабличнойЧастиКлиент</p>
Процедура РассчитатьСуммуВСтроке(ТекСтрока) Экспорт
	ТекСтрока.Сумма = ТекСтрока.Цена * ТекСтрока.Количество;
КонецПроцедуры 

<p>///// Документ Операция</p>   
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если Не Движения.Управленческий.Записывать Тогда
		Движения.Управленческий.Прочитать();
		Движения.Управленческий.Записывать = Истина;
	КонецЕсли;
	
	Для Каждого Движение Из Движения.Управленческий Цикл
		Движение.Период = Дата;
		Движение.Активность = Не ПометкаУдаления;
	КонецЦикла;
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)	
	Выборка = РегистрыБухгалтерии.Управленческий.ВыбратьПоРегистратору(ОбъектКопирования.Ссылка);
	Пока Выборка.Следующий() Цикл
		Движение = Движения.Управленческий.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, Выборка);
		ЗаполнитьЗначенияСвойств(Движение.СубконтоДт, Выборка.СубконтоДт);
		ЗаполнитьЗначенияСвойств(Движение.СубконтоКт, Выборка.СубконтоКт);
	КонецЦикла;
КонецПроцедуры
 
<p>///// Документ ПриходнаяНакладная</p>
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
КонецПроцедуры

<p>///// Документ РасходнаяНакладная</p> 
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.Управленческий.Записывать = Истина;
	Движения.Записать();
	
	Движения.Управленческий.Записывать = Истина;
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрБухгалтерии.Управленческий");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Счет", ПланыСчетов.Управленческий.Товары);
	ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура, "Номенклатура");
	Блокировка.Заблокировать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма
		|ПОМЕСТИТЬ втТЧТовары
		|ИЗ
		|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
		|ГДЕ
		|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втТЧТовары.Номенклатура КАК Номенклатура,
		|	втТЧТовары.Номенклатура.Представление КАК НоменклатураПредставление,
		|	втТЧТовары.Количество КАК Количество,
		|	втТЧТовары.Сумма КАК Сумма,
		|	ЕСТЬNULL(УправленческийОстаткиПоСкладу.КоличествоОстаток, 0) КАК КоличествоОстатокНаСкладе,
		|	ЕСТЬNULL(УправленческийОстаткиПоКомпании.КоличествоОстаток, 0) КАК КоличествоОстатокВКомпании,
		|	ЕСТЬNULL(УправленческийОстаткиПоКомпании.СуммаОстаток, 0) КАК СуммаОстатокВКомпании
		|ИЗ
		|	втТЧТовары КАК втТЧТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(
		|				&МоментВремени,
		|				Счет = &СчетТовары,
		|				&ВидыСубконтоНоменклатураСклад,
		|				Субконто1 В
		|						(ВЫБРАТЬ
		|							втТЧТовары.Номенклатура КАК Номенклатура
		|						ИЗ
		|							втТЧТовары КАК втТЧТовары)
		|					И Субконто2 = &Склад) КАК УправленческийОстаткиПоСкладу
		|		ПО втТЧТовары.Номенклатура = УправленческийОстаткиПоСкладу.Субконто1
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(
		|				&МоментВремени,
		|				Счет = &СчетТовары,
		|				&ВидСубконтоНоменклатура,
		|				Субконто1 В
		|					(ВЫБРАТЬ
		|						втТЧТовары.Номенклатура КАК Номенклатура
		|					ИЗ
		|						втТЧТовары КАК втТЧТовары)) КАК УправленческийОстаткиПоКомпании
		|		ПО втТЧТовары.Номенклатура = УправленческийОстаткиПоКомпании.Субконто1";
	
	ВидыСубконтоНоменклатураСклад = Новый Массив;
	ВидыСубконтоНоменклатураСклад.Добавить(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура);
	ВидыСубконтоНоменклатураСклад.Добавить(ПланыВидовХарактеристик.ВидыСубконто.Склады);
	
	Запрос.УстановитьПараметр("ВидыСубконтоНоменклатураСклад", ВидыСубконтоНоменклатураСклад);
	Запрос.УстановитьПараметр("ВидСубконтоНоменклатура", ПланыВидовХарактеристик.ВидыСубконто.Номенклатура);
	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("СчетТовары", ПланыСчетов.Управленческий.Товары);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.Количество > Выборка.КоличествоОстатокНаСкладе Тогда
			Отказ = Истина;
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("Недостаточно товара %1 в количестве %2",
			Выборка.НоменклатураПредставление, Выборка.Количество - Выборка.КоличествоОстатокНаСкладе);
			Сообщение.Сообщить();
		КонецЕсли;
		
		Если Отказ Тогда
			Продолжить;
		КонецЕсли;
		
		// себестоимость
		Если Выборка.Количество = Выборка.КоличествоОстатокВКомпании Тогда
			Себестоимость = Выборка.СуммаОстатокВКомпании;
		Иначе
			Себестоимость = Выборка.СуммаОстатокВКомпании / Выборка.КоличествоОстатокВКомпании * Выборка.Количество;
		КонецЕсли;	
			
		Движение = Движения.Управленческий.Добавить();
		Движение.Период = Дата;
		Движение.СчетДт = ПланыСчетов.Управленческий.ПрибылиУбытки;
		Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = Выборка.Номенклатура;
		Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Проекты] = Проект;
		
		Движение.СчетКт = ПланыСчетов.Управленческий.Товары;
		Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = Выборка.Номенклатура;
		Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Склады] = Склад;
		Движение.КоличествоКт = Выборка.Количество;
		
		Движение.Сумма = Себестоимость;
	
		// продажа
		Движение = Движения.Управленческий.Добавить();
		Движение.Период = Дата;
		Движение.СчетДт = ПланыСчетов.Управленческий.Покупатели;
		Движение.СчетКт = ПланыСчетов.Управленческий.ПрибылиУбытки;
		Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = Выборка.Номенклатура;
		Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Проекты] = Проект;		
		Движение.Сумма = Выборка.Сумма;
	КонецЦикла;
	
КонецПроцедуры

<p>///// Документ Затраты</p>
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.Управленческий.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗатратыСписокЗатрат.Проект КАК Проект,
		|	СУММА(ЗатратыСписокЗатрат.Сумма) КАК Сумма
		|ИЗ
		|	Документ.Затраты.СписокЗатрат КАК ЗатратыСписокЗатрат
		|ГДЕ
		|	ЗатратыСписокЗатрат.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗатратыСписокЗатрат.Проект";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Движение = Движения.Управленческий.Добавить();
		Движение.Период = Дата;
		Движение.СчетДт = ПланыСчетов.Управленческий.ПрибылиУбытки;
		Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Проекты] = Выборка.Проект;
		Движение.СчетКт = ПланыСчетов.Управленческий.ОбщехозяйственныеЗатраты;
		Движение.Сумма = Выборка.Сумма;
		
	КонецЦикла;
	
КонецПроцедуры

<p>///// Модуль формы</p>
&НаКлиенте
Процедура СписокНоменклатурыКоличествоПриИзменении(Элемент)
	ТекСтрока = Элементы.СписокНоменклатуры.ТекущиеДанные;
	ОбработкаТабличнойЧасти.РассчитатьСуммуВСтроке(ТекСтрока);
КонецПроцедуры

&НаКлиенте
Процедура СписокНоменклатурыЦенаПриИзменении(Элемент)
	ТекСтрока = Элементы.СписокНоменклатуры.ТекущиеДанные;
	ОбработкаТабличнойЧасти.РассчитатьСуммуВСтроке(ТекСтрока);
КонецПроцедуры
<embed src="pdf/ОбщееБУ2.pdf" type="application/pdf" width="1200" height="800">
	<img src="pdf/БУ2.jpg">
<H2 class="OU">
  Расчеты
</H2>
<p>///// Документ НачислениеЗарплаты</p>   
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПериодРегистрации = НачалоМесяца(Дата);

	Движения.ОсновныеНачисления.Записывать = Истина;

	#Область Запрос
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НачислениеЗарплатыОсновныеНачисления.НомерСтроки КАК НомерСтроки,
		|	НачислениеЗарплатыОсновныеНачисления.Сотрудник КАК Сотрудник,
		|	НачислениеЗарплатыОсновныеНачисления.Подразделение КАК Подразделение,
		|	НачислениеЗарплатыОсновныеНачисления.ВидРасчета КАК ВидРасчета,
		|	НачислениеЗарплатыОсновныеНачисления.ДатаНачала КАК ДатаНачала,
		|	КОНЕЦПЕРИОДА(НачислениеЗарплатыОсновныеНачисления.ДатаОкончания, ДЕНЬ) КАК ДатаОкончания,
		|	НАЧАЛОПЕРИОДА(НачислениеЗарплатыОсновныеНачисления.Ссылка.Дата, МЕСЯЦ) КАК ПериодРегистрации,
		|	НачислениеЗарплатыОсновныеНачисления.Параметр КАК Параметр
		|ПОМЕСТИТЬ втНачисления
		|ИЗ
		|	Документ.НачислениеЗарплаты.ОсновныеНачисления КАК НачислениеЗарплатыОсновныеНачисления
		|ГДЕ
		|	НачислениеЗарплатыОсновныеНачисления.Ссылка = &Ссылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Сотрудник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втНачисления.НомерСтроки КАК НомерСтроки,
		|	втНачисления.Сотрудник КАК Сотрудник,
		|	втНачисления.Подразделение КАК Подразделение,
		|	втНачисления.ВидРасчета КАК ВидРасчета,
		|	втНачисления.ДатаНачала КАК ДатаНачала,
		|	втНачисления.ДатаОкончания КАК ДатаОкончания,
		|	втНачисления.ПериодРегистрации КАК ПериодРегистрации,
		|	ЕСТЬNULL(СведенияОСотрудниках.Период, ДАТАВРЕМЯ(1, 1, 1)) КАК Период,
		|	ЕСТЬNULL(СведенияОСотрудниках.Оклад, 0) КАК Оклад,
		|	втНачисления.Параметр КАК Параметр
		|ПОМЕСТИТЬ втНачисленияСОкладом
		|ИЗ
		|	втНачисления КАК втНачисления
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОСотрудниках КАК СведенияОСотрудниках
		|		ПО втНачисления.Сотрудник = СведенияОСотрудниках.Сотрудник
		|			И (втНачисления.ДатаОкончания >= ЕСТЬNULL(СведенияОСотрудниках.Период, ДАТАВРЕМЯ(1, 1, 1)))
		|ГДЕ
		|	втНачисления.ВидРасчета = &Оклад
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	НомерСтроки,
		|	Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втНачисленияСОкладом.НомерСтроки КАК НомерСтроки,
		|	МАКСИМУМ(втНачисленияСОкладом.Период) КАК Период
		|ПОМЕСТИТЬ втДатыСреза
		|ИЗ
		|	втНачисленияСОкладом КАК втНачисленияСОкладом
		|ГДЕ
		|	втНачисленияСОкладом.Период <= втНачисленияСОкладом.ДатаНачала
		|
		|СГРУППИРОВАТЬ ПО
		|	втНачисленияСОкладом.НомерСтроки
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	НомерСтроки,
		|	Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втНачисленияСОкладом.Сотрудник.Представление КАК СотрудникПредставление,
		|	втНачисленияСОкладом.ДатаНачала КАК ДатаНачала
		|ИЗ
		|	втНачисленияСОкладом КАК втНачисленияСОкладом
		|ГДЕ
		|	НЕ втНачисленияСОкладом.НомерСтроки В
		|				(ВЫБРАТЬ
		|					втДатыСреза.НомерСтроки КАК НомерСтроки
		|				ИЗ
		|					втДатыСреза КАК втДатыСреза)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втНачисленияСОкладом.НомерСтроки КАК НомерСтроки,
		|	втНачисленияСОкладом.Сотрудник КАК Сотрудник,
		|	втНачисленияСОкладом.Подразделение КАК Подразделение,
		|	втНачисленияСОкладом.ВидРасчета КАК ВидРасчета,
		|	втНачисленияСОкладом.ДатаНачала КАК ПериодДействияНачало,
		|	втНачисленияСОкладом.ДатаОкончания КАК ПериодДействияКонец,
		|	втНачисленияСОкладом.ПериодРегистрации КАК ПериодРегистрации,
		|	втНачисленияСОкладом.Оклад КАК Оклад,
		|	втНачисленияСОкладом.Параметр КАК Параметр
		|ИЗ
		|	втНачисленияСОкладом КАК втНачисленияСОкладом
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДатыСреза КАК втДатыСреза
		|		ПО втНачисленияСОкладом.НомерСтроки = втДатыСреза.НомерСтроки
		|			И втНачисленияСОкладом.Период = втДатыСреза.Период
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	втНачисленияСОкладом.НомерСтроки,
		|	втНачисленияСОкладом.Сотрудник,
		|	втНачисленияСОкладом.Подразделение,
		|	втНачисленияСОкладом.ВидРасчета,
		|	втНачисленияСОкладом.Период,
		|	втНачисленияСОкладом.ДатаОкончания,
		|	втНачисленияСОкладом.ПериодРегистрации,
		|	втНачисленияСОкладом.Оклад,
		|	втНачисленияСОкладом.Параметр
		|ИЗ
		|	втНачисленияСОкладом КАК втНачисленияСОкладом
		|ГДЕ
		|	втНачисленияСОкладом.Период > втНачисленияСОкладом.ДатаНачала
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	втНачисления.НомерСтроки,
		|	втНачисления.Сотрудник,
		|	втНачисления.Подразделение,
		|	втНачисления.ВидРасчета,
		|	втНачисления.ДатаНачала,
		|	втНачисления.ДатаОкончания,
		|	втНачисления.ПериодРегистрации,
		|	0,
		|	втНачисления.Параметр
		|ИЗ
		|	втНачисления КАК втНачисления
		|ГДЕ
		|	втНачисления.ВидРасчета <> &Оклад
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПериодДействияНачало
		|ИТОГИ ПО
		|	НомерСтроки";
	
	#КонецОбласти
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Оклад", ПланыВидовРасчета.ОсновныеНачисления.Оклад);
	
	РезультатыЗапросов = Запрос.ВыполнитьПакет();

	Если Не РезультатыЗапросов[3].Пустой() Тогда
		ВыборкаКонтроль = РезультатыЗапросов[3].Выбрать();    	
		Пока ВыборкаКонтроль.Следующий() Цикл
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "По сотруднику " + ВыборкаКонтроль.СотрудникПредставление 
			                + " не указана ставка оклада на дату " + Формат(ВыборкаКонтроль.ДатаНачала, "ДФ=dd.MM.yyyy");
			Сообщение.Сообщить();
			Отказ = Истина;
			Возврат;
		КонецЦикла;
	КонецЕсли;
			
	ВыборкаНомерСтроки = РезультатыЗапросов[4].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаНомерСтроки.Следующий() Цикл
	
		Выборка = ВыборкаНомерСтроки.Выбрать();
		
		ДвижениеПред = Неопределено;
		
		Пока Выборка.Следующий() Цикл
			Движение = Движения.ОсновныеНачисления.Добавить();
			ЗаполнитьЗначенияСвойств(Движение, Выборка);
			
			Если ДвижениеПред <> Неопределено Тогда
				ДвижениеПред.ПериодДействияКонец = Выборка.ПериодДействияНачало - 1;					
			КонецЕсли;
			
			ДвижениеПред = Движение;			
		КонецЦикла;
	КонецЦикла;
	
	Движения.Записать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
		|	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ЧасовПериодДействия, 0) КАК НормаЧасов,
		|	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ЧасовФактическийПериодДействия, 0) КАК ОтработаноЧасов
		|ИЗ
		|	РегистрРасчета.ОсновныеНачисления.ДанныеГрафика(
		|			Регистратор = &Ссылка
		|				И ВидРасчета В (&ВидыРасчета)) КАК ОсновныеНачисленияДанныеГрафика";
	
	ВидыРасчета = Новый Массив;
	ВидыРасчета.Добавить(ПланыВидовРасчета.ОсновныеНачисления.Оклад);
	Запрос.УстановитьПараметр("ВидыРасчета", ВидыРасчета);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	// оклад
	Для Каждого Движение Из Движения.ОсновныеНачисления Цикл
		
		Если ВидыРасчета.Найти(Движение.ВидРасчета) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Выборка.Сбросить();
		Выборка.НайтиСледующий(Движение.НомерСтроки, "НомерСтроки");
		Если Движение.ВидРасчета = ПланыВидовРасчета.ОсновныеНачисления.Оклад Тогда
			Движение.Результат = ?(Выборка.НормаЧасов <> 0,
								   Движение.Оклад / Выборка.НормаЧасов * Выборка.ОтработаноЧасов,
								   0);
			ЗаполнитьЗначенияСвойств(Движение, Выборка, "НормаЧасов, ОтработаноЧасов");
		КонецЕсли;
		
	КонецЦикла;
	
	Движения.ОсновныеНачисления.Записать(, Истина);

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОсновныеНачисления.НомерСтроки КАК НомерСтроки,
		|	ЕСТЬNULL(ОсновныеНачисленияБазаОсновныеНачисления.РезультатБаза, 0) КАК БазаНачислений
		|ИЗ
		|	РегистрРасчета.ОсновныеНачисления КАК ОсновныеНачисления
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ОсновныеНачисления.БазаОсновныеНачисления(
		|				&Измерения,
		|				&Измерения,
		|				,
		|				Регистратор = &Ссылка
		|					И ВидРасчета В (&ВидыРасчета)) КАК ОсновныеНачисленияБазаОсновныеНачисления
		|		ПО ОсновныеНачисления.НомерСтроки = ОсновныеНачисленияБазаОсновныеНачисления.НомерСтроки
		|ГДЕ
		|	ОсновныеНачисления.Регистратор = &Ссылка
		|	И ОсновныеНачисления.ВидРасчета В(&ВидыРасчета)";
	
	ВидыРасчетаДляПолученияБазы = Новый Массив;
	ВидыРасчетаДляПолученияБазы.Добавить(ПланыВидовРасчета.ОсновныеНачисления.Премия);
	Запрос.УстановитьПараметр("ВидыРасчета", ВидыРасчетаДляПолученияБазы);            
	
	Измерения = Новый Массив;
	Измерения.Добавить("Сотрудник");
	Запрос.УстановитьПараметр("Измерения", Измерения);
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();

	// премия
	Для Каждого Движение Из Движения.ОсновныеНачисления Цикл
		
		Если ВидыРасчетаДляПолученияБазы.Найти(Движение.ВидРасчета) <> Неопределено Тогда
			
			Выборка.Сбросить();
			Выборка.НайтиСледующий(Движение.НомерСтроки, "НомерСтроки");
			
			Если Движение.ВидРасчета = ПланыВидовРасчета.ОсновныеНачисления.Премия Тогда
				Движение.Результат = Выборка.БазаНачислений * Движение.Параметр / 100;			
				ЗаполнитьЗначенияСвойств(Движение, Выборка, "БазаНачислений");
			КонецЕсли;
			
		ИначеЕсли Движение.ВидРасчета = ПланыВидовРасчета.ОсновныеНачисления.ПроизвольнаяСуммаДенег Тогда
			
			Движение.Результат = Движение.Параметр;
			
		КонецЕсли;
			
	КонецЦикла;
	
	Движения.ОсновныеНачисления.Записать(, Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОсновныеНачисления.Сотрудник КАК Сотрудник,
		|	ОсновныеНачисления.Подразделение КАК Подразделение,
		|	ОсновныеНачисления.ВидРасчета КАК ВидРасчета,
		|	СУММА(ОсновныеНачисления.Результат) КАК Сумма
		|ИЗ
		|	РегистрРасчета.ОсновныеНачисления КАК ОсновныеНачисления
		|ГДЕ
		|	ОсновныеНачисления.Регистратор = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ОсновныеНачисления.Подразделение,
		|	ОсновныеНачисления.ВидРасчета,
		|	ОсновныеНачисления.Сотрудник";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Движение = Движения.ВзаиморасчетыССотрудниками.ДобавитьПриход();
		ЗаполнитьЗначенияСвойств(Движение, Выборка);
		Движение.Период = ПериодРегистрации;
	КонецЦикла;                                     
	Движения.ВзаиморасчетыССотрудниками.Записывать = Истина;
	
КонецПроцедуры

<p>///// Документ ЗарплатаКВыплате</p>
Процедура ОбработкаПроведения(Отказ, Режим)

	Движения.ВзаиморасчетыССотрудниками.Записывать = Истина;
	Для Каждого ТекСтрокаСотрудники Из Сотрудники Цикл
		Движение = Движения.ВзаиморасчетыССотрудниками.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Подразделение = Подразделение;
		ЗаполнитьЗначенияСвойств(Движение, ТекСтрокаСотрудники);
	КонецЦикла;

КонецПроцедуры

<p>///// Модуль формы ЗарплатаКВыплате</p>
&НаКлиенте
Процедура ЗаполнитьОстатками(Команда)
	
	Если Объект.Проведен Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Необходимо снять с проведения документ";
		Сообщение.Сообщить();		                             
		Возврат;
	КонецЕсли;
	
	ЗаполнитьОстаткамиНаСервере();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОстаткамиНаСервере()
	
	Объект.Сотрудники.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВзаиморасчетыССотрудникамиОстатки.Сотрудник КАК Сотрудник,
		|	ВзаиморасчетыССотрудникамиОстатки.СуммаОстаток КАК Сумма
		|ИЗ
		|	РегистрНакопления.ВзаиморасчетыССотрудниками.Остатки(&ДатаОстатков, Подразделение = &Подразделение) КАК ВзаиморасчетыССотрудникамиОстатки";
	
	Запрос.УстановитьПараметр("ДатаОстатков",  Новый Граница(КонецМесяца(Объект.Дата), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Подразделение", Объект.Подразделение);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = Объект.Сотрудники.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
	КонецЦикла;
	
КонецПроцедуры

<p>///// Модуль объекта ЗаполнениеГрафика</p>
Процедура ЗаполнитьГрафик(ДатаНачала, ДатаОкончания, ВыходныеДни, Подразделение) Экспорт 
	
	Набор = РегистрыСведений.ГрафикиРаботы.СоздатьНаборЗаписей();
	
	Набор.Отбор.Подразделение.Установить(Подразделение);
		
	ЧислоСекундВСутках = 86400;
	
	Дат = ДатаНачала;	
	Пока Дат <= ДатаОкончания Цикл
		Запись = Набор.Добавить();
		Запись.Подразделение = Подразделение;
		Запись.Дата = Дат;
		Если Найти(ВыходныеДни, Строка(ДеньНедели(Дат))) Тогда
			Запись.Часов = 0;
		Иначе	          
			Запись.Часов = 8;
		КонецЕсли; 
		Дат = Дат + ЧислоСекундВСутках;
	КонецЦикла; 
	Набор.Записать();
КонецПроцедуры

<p>///// Модуль формы</p>
&НаКлиенте
Процедура Заполнить(Команда)
	ВыполнитьОбработку();
	Сообщить("Обработка завершена!");
КонецПроцедуры

&НаСервере
Процедура ВыполнитьОбработку()
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
    ОбработкаОбъект.ЗаполнитьГрафик(ВыборПериода.ДатаНачала, ВыборПериода.ДатаОкончания, ВыходныеДни, ГрафикРаботы);
КонецПроцедуры
<embed src="pdf/ОбщееСПР2.pdf" type="application/pdf" width="1200" height="800">
	<img src="pdf/СПР2.jpg">
  </pre>
	<H2 class="OU">
		Бизнес процесс
	</H2>
	<pre>
		<p>//// Модуль приложения</p>
Процедура ПриНачалеРаботыСистемы()
	
	ПодключитьОбработчикОжидания("ПоказатьФормыЗадач", 300, Ложь);
	
КонецПроцедуры

Процедура ПоказатьФормыЗадач() Экспорт

	Задачи = ОбщегоНазначенияВызовСервера.СписокЗадачТекущегоПользователя();
	Для Каждого Задача Из Задачи Цикл
		Окна = ПолучитьОкна();
		УИДЗадачи = Задача.УникальныйИдентификатор();
		ОкноНайдено = Ложь;
		Для Каждого Окно Из Окна Цикл
			Если Окно.Основное Тогда
				Продолжить;
			КонецЕсли;
			
			Если Окно.Содержимое.Количество() > 0 Тогда
				Если ТипЗнч(Окно.Содержимое[0]) = Тип("ФормаКлиентскогоПриложения") Тогда
					
					Ключ = Неопределено;
					Окно.Содержимое[0].Параметры.Свойство("Ключ", Ключ);
					Если Ключ = Неопределено Тогда
						Продолжить;						
					КонецЕсли;
					
					Если Ключ = Задача Тогда
						ОкноНайдено = Истина;						
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		Если Не ОкноНайдено Тогда
			ПараметрыФормы = Новый Структура("Ключ", Задача);
			ОткрытьФорму("Задача.ЗадачиИсполнителям.ФормаОбъекта", ПараметрыФормы,,УИДЗадачи);		
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

<p>//// Модуль сеанса</p>
Процедура УстановкаПараметровСеанса(ТребуемыеПараметры)
	
	Имя = ПользователиИнформационнойБазы.ТекущийПользователь().Имя;
	
	ФизЛицо = Справочники.ФизическиеЛица.НайтиПоКоду(Имя);
	Если ФизЛицо.Пустая() Тогда
		ФизЛицоОбъект = Справочники.ФизическиеЛица.СоздатьЭлемент();
		ФизЛицоОбъект.Наименование = Имя;
		ФизЛицоОбъект.Код = Имя;
		ФизЛицоОбъект.Записать();
		ФизЛицо = ФизЛицоОбъект.Ссылка;
	КонецЕсли;

	ПараметрыСеанса.ТекущийПользователь = ФизЛицо;
		
КонецПроцедуры

<p>//// ОбщегоНазначенияВызовСервера</p> 
Функция ТекущийПользователь() Экспорт
	
	Возврат ПараметрыСеанса.ТекущийПользователь;
		
КонецФункции

Функция СписокЗадачТекущегоПользователя() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗадачиИсполнителям.Ссылка КАК Ссылка
		|ИЗ
		|	Задача.ЗадачиИсполнителям КАК ЗадачиИсполнителям
		|ГДЕ
		|	ЗадачиИсполнителям.Исполнитель = &Исполнитель
		|	И НЕ ЗадачиИсполнителям.Выполнена
		|	И НЕ ЗадачиИсполнителям.ПометкаУдаления
		|	И ЗадачиИсполнителям.ЭтоОтчетОПроделаннойРаботе";
	
	Запрос.УстановитьПараметр("Исполнитель", ПараметрыСеанса.ТекущийПользователь);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	МассивЗадач = Новый Массив;
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		МассивЗадач.Добавить(Выборка.Ссылка);
	КонецЦикла;                            
	
	Возврат МассивЗадач;
	
КонецФункции

<p>//// РеглментныеЗаданияСервер</p> 
Процедура СоздатьЗадачиНаСервере() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗадачиИсполнителям.Исполнитель КАК Исполнитель
		|ПОМЕСТИТЬ втЗадачи
		|ИЗ
		|	Задача.ЗадачиИсполнителям КАК ЗадачиИсполнителям
		|ГДЕ
		|	НАЧАЛОПЕРИОДА(ЗадачиИсполнителям.Дата, ДЕНЬ) = &НачалоДня
		|	И ЗадачиИсполнителям.ЭтоОтчетОПроделаннойРаботе
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Исполнитель
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СведенияОСотрудникахСрезПоследних.Сотрудник КАК Сотрудник
		|ИЗ
		|	РегистрСведений.СведенияОСотрудниках.СрезПоследних(&Период, ) КАК СведенияОСотрудникахСрезПоследних
		|		ЛЕВОЕ СОЕДИНЕНИЕ втЗадачи КАК втЗадачи
		|		ПО СведенияОСотрудникахСрезПоследних.Сотрудник = втЗадачи.Исполнитель
		|ГДЕ
		|	втЗадачи.Исполнитель ЕСТЬ NULL";
	
	Запрос.УстановитьПараметр("Период", ТекущаяДата());
	Запрос.УстановитьПараметр("НачалоДня", НачалоДня(ТекущаяДата()));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ЗадачаОбъект = Задачи.ЗадачиИсполнителям.СоздатьЗадачу();
		ЗадачаОбъект.Дата = ТекущаяДата();
		ЗадачаОбъект.Наименование = "Отчет о проделанной работе";
		ЗадачаОбъект.Исполнитель = Выборка.Сотрудник;
		ЗадачаОбъект.ЭтоОтчетОПроделаннойРаботе = Истина;
		ЗадачаОбъект.Записать();
	КонецЦикла;
	
КонецПроцедуры
</pre>
	<embed src="pdf/ОбщееБП2.pdf" type="application/pdf" width="1200" height="800">
</body>

</html>