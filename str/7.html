<!doctype html>
<html lang="ru">

<head>
  <meta charset="utf-8">
  <title>HTML Document Template</title>
  <link href="styles.css" rel="stylesheet">
</head>

<body>
  <div class="header__menu">
    <ul>
      <li><a href="1">1</a></li>
      <li><a href="2">2</a></li>
      <li><a href="3">3</a></li>
      <li><a href="4">4</a></li>
      <li><a href="5">5</a></li>
      <li><a href="6">6</a></li>
      <li><a href="7">7</a></li>
      <li><a href="8">8</a></li>
      <li><a href="9">9</a></li>
      <li><a href="10">10</a></li>
      <li><a href="11">11</a></li>
      <li><a href="12">12</a></li>
      <li><a href="13">13</a></li>
      <li><a href="14">14</a></li>
      <li><a href="15">15</a></li>
      <li><a href="16">16</a></li>
    </ul>
  </div>
  <h1>7</h1>
  <H2 class="OU">
    Оперативный учет
  </H2>
  <pre>
    <p>///// Общий модуль ОбработкаТабличнойЧасти</p>
    Процедура РассчитатьСуммуВСтроке(ТекСтрока) Экспорт
      ТекСтрока.Сумма = ТекСтрока.Цена * ТекСтрока.Количество;
    КонецПроцедуры 
    
    <p>///// Документ НазначениеУчетнойПолитики</p>   
    Процедура ОбработкаПроведения(Отказ, Режим)
      // регистр УчетнаяПолитика
      Движения.УчетнаяПолитика.Записывать = Истина;
      Движение = Движения.УчетнаяПолитика.Добавить();
      Движение.Период = Дата;
      Движение.МетодСписанияСебестоимости = МетодСписанияСебестоимости;
    КонецПроцедуры
    
    Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
      Дата = НачалоГода(Дата);
    КонецПроцедуры
    
    <p>///// Документ ПриходнаяНакладная</p>
    Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
      СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
    КонецПроцедуры
    
    Процедура ОбработкаПроведения(Отказ, Режим)
      
      Движения.ОстаткиНоменклатуры.Записывать = Истина;
      
      Запрос = Новый Запрос;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
        |	СУММА(ПриходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
        |	СУММА(ПриходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма
        |ИЗ
        |	Документ.ПриходнаяНакладная.СписокНоменклатуры КАК ПриходнаяНакладнаяСписокНоменклатуры
        |ГДЕ
        |	ПриходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
        |
        |СГРУППИРОВАТЬ ПО
        |	ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура";
      
      Запрос.УстановитьПараметр("Ссылка", Ссылка);
      
      РезультатЗапроса = Запрос.Выполнить();
      
      Выборка = РезультатЗапроса.Выбрать();
      
      Пока Выборка.Следующий() Цикл
        Движение = Движения.ОстаткиНоменклатуры.Добавить();
        Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
        Движение.Период = Дата;
        Движение.Номенклатура = Выборка.Номенклатура;
        Движение.Склад = Склад;
        Движение.Партия = Ссылка;
        Движение.Количество = Выборка.Количество;
        Движение.СтоимостьВРублях = Выборка.Сумма;
        Движение.СтоимостьВДолларах = Выборка.Сумма / КурсДоллара;
      КонецЦикла;
       
    КонецПроцедуры
    
    <p>///// Документ РасходнаяНакладная</p> 
    
    Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
      СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
    КонецПроцедуры
    
    Процедура ОбработкаПроведения(Отказ, РежимПроведения)
      
      Запрос = Новый Запрос;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	УчетнаяПолитикаСрезПоследних.МетодСписанияСебестоимости КАК МетодСписанияСебестоимости
        |ИЗ
        |	РегистрСведений.УчетнаяПолитика.СрезПоследних(&Период, ) КАК УчетнаяПолитикаСрезПоследних";
      
      Запрос.УстановитьПараметр("Период", Дата);
      
      РезультатЗапроса = Запрос.Выполнить();
      
      Если РезультатЗапроса.Пустой() Тогда
        
        Сообщение = Новый СообщениеПользователю;
        Сообщение.Текст = "Не указан метод списания себестоимости в учетной политике";
        Сообщение.Сообщить();
        
        Отказ = Истина;
        Возврат;
        
      КонецЕсли;
      
      Выборка = РезультатЗапроса.Выбрать();
      Выборка.Следующий();
      
      МетодСписанияСебестоимости = Выборка.МетодСписанияСебестоимости;
    
      // удаление движения
      Движения.ОстаткиНоменклатуры.Записывать = Истина;
      Движения.Записать();
      
      // взвели флаг
      Движения.ОстаткиНоменклатуры.Записывать = Истина;
      Движения.Продажи.Записывать = Истина;
      
      Блокировка = Новый БлокировкаДанных;
      ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
      ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
      ЭлементБлокировки.УстановитьЗначение("Склад", Склад);
      ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
      ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
      Блокировка.Заблокировать();
      
      Запрос = Новый Запрос;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
        |	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
        |	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуры КАК ВидНоменклатуры,
        |	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма
        |ПОМЕСТИТЬ втТЧТовары
        |ИЗ
        |	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
        |ГДЕ
        |	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
        |
        |СГРУППИРОВАТЬ ПО
        |	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура,
        |	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуры
        |
        |ИНДЕКСИРОВАТЬ ПО
        |	Номенклатура
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	втТЧТовары.Номенклатура КАК Номенклатура,
        |	втТЧТовары.Номенклатура.Представление КАК НоменклатураПредставление,
        |	втТЧТовары.Количество КАК Количество,
        |	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
        |	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.СтоимостьВРубляхОстаток, 0) КАК СтоимостьВРубляхОстаток,
        |	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.СтоимостьВДолларахОстаток, 0) КАК СтоимостьВДолларахОстаток,
        |	ОстаткиНоменклатурыОстатки.Партия КАК Партия,
        |	втТЧТовары.ВидНоменклатуры КАК ВидНоменклатуры,
        |	втТЧТовары.Сумма КАК СуммаВРублях
        |ИЗ
        |	втТЧТовары КАК втТЧТовары
        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
        |				&МоментВремени,
        |				Номенклатура В
        |						(ВЫБРАТЬ
        |							втТЧТовары.Номенклатура КАК Номенклатура
        |						ИЗ
        |							втТЧТовары КАК втТЧТовары
        |						ГДЕ
        |							втТЧТовары.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Товар))
        |					И Склад = &Склад) КАК ОстаткиНоменклатурыОстатки
        |		ПО втТЧТовары.Номенклатура = ОстаткиНоменклатурыОстатки.Номенклатура
        |
        |УПОРЯДОЧИТЬ ПО
        |	ОстаткиНоменклатурыОстатки.Партия.МоментВремени
        |ИТОГИ
        |	МАКСИМУМ(Количество),
        |	СУММА(КоличествоОстаток),
        |	МАКСИМУМ(ВидНоменклатуры),
        |	МАКСИМУМ(Сумма)
        |ПО
        |	Номенклатура";
      
      Если МетодСписанияСебестоимости = Перечисления.УчетнаяПолитика.ЛИФО Тогда
        Запрос.Текст = СтрЗаменить(Запрос.Текст, "ОстаткиНоменклатурыОстатки.Партия.МоментВремени",
                             "ОстаткиНоменклатурыОстатки.Партия.МоментВремени УБЫВ");
      КонецЕсли;
      
      Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
      Запрос.УстановитьПараметр("Склад", Склад);
      Запрос.УстановитьПараметр("Ссылка", Ссылка);
      
      РезультатЗапроса = Запрос.Выполнить();
      
      ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
      
      Пока ВыборкаНоменклатура.Следующий() Цикл
        
        СебестоимостьВРубляхИтого = 0;
        СебестоимостьВДолларахИтого = 0;
        Если ВыборкаНоменклатура.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Товар Тогда
          
          Если ВыборкаНоменклатура.Количество > ВыборкаНоменклатура.КоличествоОстаток Тогда
            
            Отказ = Истина;
            
            Сообщение = Новый СообщениеПользователю;
            Сообщение.Текст = СтрШаблон("Недостаточно товара %1 в количестве %2", 
                          ВыборкаНоменклатура.НоменклатураПредставление, 
                          ВыборкаНоменклатура.Количество - ВыборкаНоменклатура.КоличествоОстаток);
            Сообщение.Сообщить();
            
          КонецЕсли;
          
          Если Отказ Тогда
            Продолжить;
          КонецЕсли;
          
          КоличествоСписать = ВыборкаНоменклатура.Количество;
          
          ВыборкаДетальныеЗаписи = ВыборкаНоменклатура.Выбрать();
        
          Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
            
            Количество = Мин(КоличествоСписать, ВыборкаДетальныеЗаписи.КоличествоОстаток);
            
            Если Количество = ВыборкаДетальныеЗаписи.КоличествоОстаток Тогда
              СебестоимостьВРублях = ВыборкаДетальныеЗаписи.СтоимостьВРубляхОстаток;
              СебестоимостьВДолларах = ВыборкаДетальныеЗаписи.СтоимостьВДолларахОстаток;
            Иначе
              СебестоимостьВРублях = Количество / ВыборкаДетальныеЗаписи.КоличествоОстаток * ВыборкаДетальныеЗаписи.СтоимостьВРубляхОстаток;
              СебестоимостьВДолларах = Количество / ВыборкаДетальныеЗаписи.КоличествоОстаток * ВыборкаДетальныеЗаписи.СтоимостьВДолларахОстаток;
            КонецЕсли;
            
            Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
            Движение.Период = Дата;
            Движение.Номенклатура = ВыборкаДетальныеЗаписи.Номенклатура;
            Движение.Склад = Склад;
            Движение.Партия = ВыборкаДетальныеЗаписи.Партия;
            Движение.Количество = Количество;
            Движение.СтоимостьВРублях = СебестоимостьВРублях;
            Движение.СтоимостьВДолларах = СебестоимостьВДолларах;
            
            СебестоимостьВРубляхИтого = СебестоимостьВРубляхИтого + Движение.СтоимостьВРублях;
            СебестоимостьВДолларахИтого = СебестоимостьВДолларахИтого + Движение.СтоимостьВДолларах;
            
            КоличествоСписать = КоличествоСписать - Количество;
            
            Если КоличествоСписать <= 0 Тогда
              Прервать;
            КонецЕсли;
            
          КонецЦикла;
        КонецЕсли;
        
        Движение = Движения.Продажи.Добавить();
        Движение.Период = Дата;
        ЗаполнитьЗначенияСвойств(Движение, ВыборкаНоменклатура);
        Движение.СуммаВДолларах = Движение.СуммаВРублях / КурсДоллара;
        Движение.СебестоимостьВРублях = СебестоимостьВРубляхИтого;
        Движение.СебестоимостьВДолларах = СебестоимостьВДолларахИтого;
        
      КонецЦикла;
      
    КонецПроцедуры
    
    <p>///// Модуль формы</p>
    &НаКлиенте
    Процедура СписокНоменклатурыКоличествоПриИзменении(Элемент)
      ТекСтрока = Элементы.СписокНоменклатуры.ТекущиеДанные;
      ОбработкаТабличнойЧасти.РассчитатьСуммуВСтроке(ТекСтрока);
    КонецПроцедуры
    
    &НаКлиенте
    Процедура СписокНоменклатурыЦенаПриИзменении(Элемент)
      ТекСтрока = Элементы.СписокНоменклатуры.ТекущиеДанные;
      ОбработкаТабличнойЧасти.РассчитатьСуммуВСтроке(ТекСтрока);
    КонецПроцедуры
    <embed src="pdf/ОбщееОУ7.pdf" type="application/pdf" width="1200" height="800">
      <img src="pdf/ОУ7.jpg">
    <H2 class="OU">
      Бухгалтерский учет
    </H2>
    <p>///// Общий модуль ОбработкаТабличнойЧастиКлиент</p>
    Процедура РассчитатьСуммуВСтроке(ТекСтрока) Экспорт
      ТекСтрока.Сумма = ТекСтрока.Цена * ТекСтрока.Количество;
    КонецПроцедуры 
    
    <p>///// Документ Операция</p>   
    Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
      Если Не Движения.Управленческий.Записывать Тогда
        Движения.Управленческий.Прочитать();
        Движения.Управленческий.Записывать = Истина;
      КонецЕсли;
      
      Для Каждого Движение Из Движения.Управленческий Цикл
        Движение.Период = Дата;
        Движение.Активность = Не ПометкаУдаления;
      КонецЦикла;
    КонецПроцедуры
    
    Процедура ПриКопировании(ОбъектКопирования)	
      Выборка = РегистрыБухгалтерии.Управленческий.ВыбратьПоРегистратору(ОбъектКопирования.Ссылка);
      Пока Выборка.Следующий() Цикл
        Движение = Движения.Управленческий.Добавить();
        ЗаполнитьЗначенияСвойств(Движение, Выборка);
        ЗаполнитьЗначенияСвойств(Движение.СубконтоДт, Выборка.СубконтоДт);
        ЗаполнитьЗначенияСвойств(Движение.СубконтоКт, Выборка.СубконтоКт);
      КонецЦикла;
    КонецПроцедуры
     
    <p>///// Документ ПриходнаяНакладная</p>
    Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
      СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
    КонецПроцедуры
    
    <p>///// Документ РасходнаяНакладная</p> 
    Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
      СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
    КонецПроцедуры
    
    Процедура ОбработкаПроведения(Отказ, РежимПроведения)
      
      Движения.Управленческий.Записывать = Истина;
      Движение = Движения.Управленческий.Добавить();
      Движение.Период = Дата;
      Движение.СчетДт = ПланыСчетов.Управленческий.Покупатели;
      Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Покупатели] = Контрагент;
      Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Договоры]   = Договор;
      Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Накладные]  = Ссылка;
      Движение.СуммаВЕвроДт = СуммаПоДокументу / КурсЕвро;
      Движение.СчетКт = ПланыСчетов.Управленческий.ПрибылиУбытки;
      Движение.Сумма = СуммаПоДокументу;
    
      
    КонецПроцедуры
    
    <p>///// Документ ПриходДенег</p>
    Процедура ОбработкаПроведения(Отказ, РежимПроведения)
      
      Движения.Управленческий.Записывать = Истина;
      Движения.Записать();
      
      Движения.Управленческий.Записывать = Истина;
      
      Блокировка = Новый БлокировкаДанных;
      ЭлементБлокировки = Блокировка.Добавить("РегистрБухгалтерии.Управленческий");
      ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
      ЭлементБлокировки.УстановитьЗначение("Счет", ПланыСчетов.Управленческий.Покупатели);
      ЭлементБлокировки.УстановитьЗначение(ПланыВидовХарактеристик.ВидыСубконто.Покупатели, Контрагент);
      Блокировка.Заблокировать();
      
      Запрос = Новый Запрос;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	УправленческийОстатки.Субконто2 КАК Договор,
        |	УправленческийОстатки.Субконто3 КАК Накладная,
        |	УправленческийОстатки.СуммаОстаток КАК СуммаОстаток
        |ИЗ
        |	РегистрБухгалтерии.Управленческий.Остатки(&МоментВремени, Счет = &СчетПокупатели, , Субконто1 = &Контрагент) КАК УправленческийОстатки
        |
        |УПОРЯДОЧИТЬ ПО
        |	ВЫРАЗИТЬ(УправленческийОстатки.Субконто2 КАК Справочник.Договоры).ДатаОкончания,
        |	ВЫРАЗИТЬ(УправленческийОстатки.Субконто3 КАК Документ.РасходнаяНакладная).МоментВремени
        |ИТОГИ
        |	СУММА(СуммаОстаток)
        |ПО
        |	ОБЩИЕ";
      
      Запрос.УстановитьПараметр("Контрагент", Контрагент);
      Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
      Запрос.УстановитьПараметр("СчетПокупатели", ПланыСчетов.Управленческий.Покупатели);
      
      РезультатЗапроса = Запрос.Выполнить();
      Если РезультатЗапроса.Пустой() Тогда
        Отказ = Истина;
        Сообщение = Новый СообщениеПользователю;
        Сообщение.Текст = "Нет задолженности по контрагенту";
        Сообщение.Сообщить();
        Возврат;
      КонецЕсли;
      
      ВыборкаОбщийИтог = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
      ВыборкаОбщийИтог.Следующий();
      
      Если СуммаПоДокументу > ВыборкаОбщийИтог.СуммаОстаток Тогда
        Отказ = Истина;
        Сообщение = Новый СообщениеПользователю;
        Сообщение.Текст = "Попытка оформить переплату (долг клиента составляет всего " + ВыборкаОбщийИтог.СуммаОстаток;
        Сообщение.Сообщить();
        Возврат;
      КонецЕсли;
        
      СуммаКПогашению = СуммаПоДокументу;
      
      Выборка = ВыборкаОбщийИтог.Выбрать();	
      Пока Выборка.Следующий() И СуммаКПогашению > 0 Цикл
        
        СуммаВРублях = Мин(СуммаКПогашению, Выборка.СуммаОстаток);
        
        Движение = Движения.Управленческий.Добавить();
        Движение.Период = Дата;
        Движение.СчетДт = ПланыСчетов.Управленческий.Касса;
        Движение.СчетКт = ПланыСчетов.Управленческий.Покупатели;
        Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Покупатели] = Контрагент;
        Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Договоры]  = Выборка.Договор;
        Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Накладные] = Выборка.Накладная;
        Движение.СуммаВЕвроКт = СуммаВРублях / КурсЕвро;
        Движение.Сумма = СуммаВРублях;
        
        СуммаКПогашению = СуммаКПогашению - СуммаВРублях;
        
      КонецЦикла;
      
      
    КонецПроцедуры
    
    <p>///// Модуль формы</p>
    &НаКлиенте
    Процедура СписокНоменклатурыКоличествоПриИзменении(Элемент)
      ТекСтрока = Элементы.СписокНоменклатуры.ТекущиеДанные;
      ОбработкаТабличнойЧасти.РассчитатьСуммуВСтроке(ТекСтрока);
    КонецПроцедуры
    
    &НаКлиенте
    Процедура СписокНоменклатурыЦенаПриИзменении(Элемент)
      ТекСтрока = Элементы.СписокНоменклатуры.ТекущиеДанные;
      ОбработкаТабличнойЧасти.РассчитатьСуммуВСтроке(ТекСтрока);
    КонецПроцедуры
    <embed src="pdf/ОбщееБУ7.pdf" type="application/pdf" width="1200" height="800">
      <img src="pdf/БУ7.jpg">
    <H2 class="OU">
      Расчеты
    </H2>
    <p>///// Документ НачислениеЗарплаты</p>   
    Процедура ОбработкаПроведения(Отказ, РежимПроведения)
      
      ПериодРегистрации = НачалоМесяца(Дата);
      
      БазовыйПериодНачало = ДобавитьМесяц(ПериодРегистрации, -2);
      БазовыйПериодКонец  = ПериодРегистрации - 1;
      
      Движения.ОсновныеНачисления.Записывать = Истина;
      Для Каждого Строка Из ОсновныеНачисления Цикл
        Движение = Движения.ОсновныеНачисления.Добавить();
        ЗаполнитьЗначенияСвойств(Движение, Строка);
        Движение.ПериодРегистрации = ПериодРегистрации;
        Движение.ПериодДействияНачало = Строка.ДатаНачала;
        Движение.ПериодДействияКонец = КонецДня(Строка.ДатаОкончания);
        
        Если Строка.ВидРасчета = ПланыВидовРасчета.ОсновныеНачисления.Командировка Тогда
          Движение.БазовыйПериодНачало = БазовыйПериодНачало;
          Движение.БазовыйПериодКонец  = БазовыйПериодКонец;
        КонецЕсли;
      КонецЦикла;                                                       
      Движения.Записать();
      
      Запрос = Новый Запрос;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
        |	ОсновныеНачисленияДанныеГрафика.Сотрудник КАК Сотрудник,
        |	ОсновныеНачисленияДанныеГрафика.Подразделение КАК Подразделение,
        |	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ЗначениеПериодДействия, 0) КАК НормаЧасов,
        |	ОсновныеНачисленияДанныеГрафика.ПериодДействияНачало КАК ПериодДействияНачало,
        |	ОсновныеНачисленияДанныеГрафика.ПериодДействияКонец КАК ПериодДействияКонец,
        |	ОсновныеНачисленияДанныеГрафика.ВидРасчета КАК ВидРасчета,
        |	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ЗначениеБазовыйПериод, 0) КАК БазаРабочихЧасов
        |ПОМЕСТИТЬ втДанныеГрафика
        |ИЗ
        |	РегистрРасчета.ОсновныеНачисления.ДанныеГрафика(
        |			Регистратор = &Ссылка
        |				И ВидРасчета В (&ВидыРасчета)) КАК ОсновныеНачисленияДанныеГрафика
        |
        |ИНДЕКСИРОВАТЬ ПО
        |	Сотрудник,
        |	Подразделение
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	втДанныеГрафика.НомерСтроки КАК НомерСтроки,
        |	СУММА(ЕСТЬNULL(ДанныеТабеля.Часов, 0)) КАК ОтработаноЧасов
        |ПОМЕСТИТЬ втДанныеЯвки
        |ИЗ
        |	втДанныеГрафика КАК втДанныеГрафика
        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ДанныеТабеля КАК ДанныеТабеля
        |		ПО втДанныеГрафика.Сотрудник = ДанныеТабеля.Сотрудник
        |			И втДанныеГрафика.Подразделение = ДанныеТабеля.Подразделение
        |			И втДанныеГрафика.ПериодДействияНачало <= ДанныеТабеля.Период
        |			И втДанныеГрафика.ПериодДействияКонец >= ДанныеТабеля.Период
        |			И (ДанныеТабеля.Активность)
        |			И (ДанныеТабеля.ВидВремени = ЗНАЧЕНИЕ(Справочник.ВидыВремени.Явка))
        |ГДЕ
        |	втДанныеГрафика.ВидРасчета В(&ВидыРасчетаЯвки)
        |
        |СГРУППИРОВАТЬ ПО
        |	втДанныеГрафика.НомерСтроки
        |
        |ИНДЕКСИРОВАТЬ ПО
        |	НомерСтроки
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	втДанныеГрафика.НомерСтроки КАК НомерСтроки,
        |	СУММА(ЕСТЬNULL(ДанныеТабеля.Часов, 0)) КАК ОтработаноЧасов
        |ПОМЕСТИТЬ втДанныеКомандировки
        |ИЗ
        |	втДанныеГрафика КАК втДанныеГрафика
        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ДанныеТабеля КАК ДанныеТабеля
        |		ПО втДанныеГрафика.Сотрудник = ДанныеТабеля.Сотрудник
        |			И втДанныеГрафика.Подразделение = ДанныеТабеля.Подразделение
        |			И втДанныеГрафика.ПериодДействияНачало <= ДанныеТабеля.Период
        |			И втДанныеГрафика.ПериодДействияКонец >= ДанныеТабеля.Период
        |			И (ДанныеТабеля.Активность)
        |			И (ДанныеТабеля.ВидВремени = ЗНАЧЕНИЕ(Справочник.ВидыВремени.Командировка))
        |ГДЕ
        |	втДанныеГрафика.ВидРасчета В(&ВидыРасчетаКомандировки)
        |
        |СГРУППИРОВАТЬ ПО
        |	втДанныеГрафика.НомерСтроки
        |
        |ИНДЕКСИРОВАТЬ ПО
        |	НомерСтроки
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	втДанныеГрафика.НомерСтроки КАК НомерСтроки,
        |	втДанныеГрафика.НормаЧасов КАК НормаЧасов,
        |	ЕСТЬNULL(СведенияОСотрудникахСрезПоследних.Оклад, 0) КАК Оклад,
        |	ВЫБОР
        |		КОГДА втДанныеГрафика.ВидРасчета В (&ВидыРасчетаЯвки)
        |			ТОГДА ЕСТЬNULL(втДанныеЯвки.ОтработаноЧасов, 0)
        |		КОГДА втДанныеГрафика.ВидРасчета В (&ВидыРасчетаКомандировки)
        |			ТОГДА ЕСТЬNULL(втДанныеКомандировки.ОтработаноЧасов, 0)
        |	КОНЕЦ КАК ОтработаноЧасов,
        |	ЕСТЬNULL(ОсновныеНачисленияБазаОсновныеНачисления.РезультатБаза, 0) КАК БазаНачислений,
        |	втДанныеГрафика.БазаРабочихЧасов КАК БазаРабочихЧасов
        |ИЗ
        |	втДанныеГрафика КАК втДанныеГрафика
        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОСотрудниках.СрезПоследних(
        |				&ПериодРегистрации,
        |				(Сотрудник, Подразделение) В
        |					(ВЫБРАТЬ
        |						втДанныеГрафика.Сотрудник КАК Сотрудник,
        |						втДанныеГрафика.Подразделение КАК Подразделение
        |					ИЗ
        |						втДанныеГрафика КАК втДанныеГрафика)) КАК СведенияОСотрудникахСрезПоследних
        |		ПО втДанныеГрафика.Сотрудник = СведенияОСотрудникахСрезПоследних.Сотрудник
        |			И втДанныеГрафика.Подразделение = СведенияОСотрудникахСрезПоследних.Подразделение
        |		ЛЕВОЕ СОЕДИНЕНИЕ втДанныеЯвки КАК втДанныеЯвки
        |		ПО втДанныеГрафика.НомерСтроки = втДанныеЯвки.НомерСтроки
        |		ЛЕВОЕ СОЕДИНЕНИЕ втДанныеКомандировки КАК втДанныеКомандировки
        |		ПО втДанныеГрафика.НомерСтроки = втДанныеКомандировки.НомерСтроки
        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ОсновныеНачисления.БазаОсновныеНачисления(
        |				&Измерения,
        |				&Измерения,
        |				,
        |				Регистратор = &Ссылка
        |					И ВидРасчета В (&ВидыРасчетаБаза)) КАК ОсновныеНачисленияБазаОсновныеНачисления
        |		ПО втДанныеГрафика.НомерСтроки = ОсновныеНачисленияБазаОсновныеНачисления.НомерСтроки";
      
      ВидыРасчетаЯвки = Новый Массив;
      ВидыРасчетаЯвки.Добавить(ПланыВидовРасчета.ОсновныеНачисления.Оклад);
      Запрос.УстановитьПараметр("ВидыРасчетаЯвки", ВидыРасчетаЯвки);
      
      ВидыРасчета = Новый Массив;
      ВидыРасчета.Добавить(ПланыВидовРасчета.ОсновныеНачисления.Оклад);
      ВидыРасчета.Добавить(ПланыВидовРасчета.ОсновныеНачисления.Командировка);
      Запрос.УстановитьПараметр("ВидыРасчета", ВидыРасчета);
    
      ВидыРасчетаБаза = Новый Массив;
      ВидыРасчетаБаза.Добавить(ПланыВидовРасчета.ОсновныеНачисления.Командировка);
      Запрос.УстановитьПараметр("ВидыРасчетаБаза", ВидыРасчетаБаза);
      
      ВидыРасчетаКомандировки = Новый Массив;
      ВидыРасчетаКомандировки.Добавить(ПланыВидовРасчета.ОсновныеНачисления.Командировка);
      Запрос.УстановитьПараметр("ВидыРасчетаКомандировки", ВидыРасчетаКомандировки);	
      
      Измерения = Новый Массив;
      Измерения.Добавить("Сотрудник");
      Измерения.Добавить("Подразделение");
      Запрос.УстановитьПараметр("Измерения", Измерения);
        
      Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
      Запрос.УстановитьПараметр("Ссылка", Ссылка);
      
      РезультатЗапроса = Запрос.Выполнить();
      
      Выборка = РезультатЗапроса.Выбрать();
      
      Для Каждого Движение Из Движения.ОсновныеНачисления Цикл
        
        Если ВидыРасчета.Найти(Движение.ВидРасчета) = Неопределено Тогда
          Продолжить;			
        КонецЕсли;     
        
        Выборка.Сбросить();
        Выборка.НайтиСледующий(Движение.НомерСтроки, "НомерСтроки");
        
        Если Движение.ВидРасчета = ПланыВидовРасчета.ОсновныеНачисления.Оклад Тогда
          Движение.Результат = ?(Выборка.НормаЧасов <> 0,
                       Выборка.Оклад / Выборка.НормаЧасов * Выборка.ОтработаноЧасов,
                       0);
          ЗаполнитьЗначенияСвойств(Движение, Выборка, "Оклад, НормаЧасов, ОтработаноЧасов");								  	
        ИначеЕсли Движение.ВидРасчета = ПланыВидовРасчета.ОсновныеНачисления.Командировка Тогда
          Движение.Результат = ?(Выборка.БазаРабочихЧасов <> 0,
                       Выборка.ОтработаноЧасов * Выборка.БазаНачислений / Выборка.БазаРабочихЧасов,
                       0);
          ЗаполнитьЗначенияСвойств(Движение, Выборка, "ОтработаноЧасов, БазаНачислений, БазаРабочихЧасов");								   
        КонецЕсли;
        
      КонецЦикла;
      
      Движения.ОсновныеНачисления.Записать(, Истина);
      
    КонецПроцедуры
    
    <p>///// Документ Табель</p>
    Процедура ОбработкаПроведения(Отказ, РежимПроведения)
    
      ПериодРегистрации = НачалоМесяца(Дата);
      
      Движения.ДанныеТабеля.Записывать = Истина;
    
      Запрос = Новый Запрос;
      Запрос.Текст = 
        "ВЫБРАТЬ
        |	ГрафикиРаботы.Дата КАК Дата,
        |	ГрафикиРаботы.Значение КАК Значение
        |ИЗ
        |	РегистрСведений.ГрафикиРаботы КАК ГрафикиРаботы
        |ГДЕ
        |	ГрафикиРаботы.ГрафикРаботы = &ГрафикРаботы
        |	И ГрафикиРаботы.Дата МЕЖДУ &НачалоПериода И &КонецПериода";
      
      Запрос.УстановитьПараметр("ГрафикРаботы", Справочники.ГрафикиРаботы.Пятидневка);
      Запрос.УстановитьПараметр("НачалоПериода", ПериодРегистрации);
      Запрос.УстановитьПараметр("КонецПериода",  КонецМесяца(ПериодРегистрации));
      
      РезультатЗапроса = Запрос.Выполнить();
      
      Выборка = РезультатЗапроса.Выбрать();
      
      Для Каждого Строка Из ДанныеТабеля Цикл
        
        ПоследнийДеньМесяца = День(КонецМесяца(Дата));
        
        Для НомерДня = 1 По ПоследнийДеньМесяца Цикл
          
          ЗначениеЯчейки = ВРег(СокрЛП(Строка["День" + НомерДня]));
          
          Если ЗначениеЯчейки <> "" Тогда
            
            Период = ПериодРегистрации + (НомерДня - 1)*24*3600;
            
            Попытка
              Если ЗначениеЯчейки = "К" Тогда
                ВидВремени = Справочники.ВидыВремени.Командировка;	
                
                Выборка.Сбросить();
                Если Выборка.НайтиСледующий(Период, "Дата") Тогда
                  Часов = Выборка.Значение;							
                Иначе
                  Часов = 0;
                КонецЕсли;
                
                //Часов = Число(Лев(ЗначениеЯчейки, СтрДлина(ЗначениеЯчейки) - 1));
              Иначе	
                ВидВремени = Справочники.ВидыВремени.Явка;
                Часов = Число(ЗначениеЯчейки);
              КонецЕсли;
            Исключение
              Сообщение = Новый СообщениеПользователю;
              Сообщение.Текст = СтрШаблон("Некорректный ввод данных (строка %1, день %2)", Строка.НомерСтроки, НомерДня);
              Сообщение.Сообщить();
              Продолжить;
              Отказ = Истина;
            КонецПопытки;
            
            Движение = Движения.ДанныеТабеля.Добавить();
            Движение.Период = Период;
            Движение.Сотрудник = Строка.Сотрудник;
            Движение.Подразделение = Подразделение;
            Движение.ВидВремени = ВидВремени;
            Движение.Часов = Часов;
        
          КонецЕсли;
          
        КонецЦикла;
        
      КонецЦикла;
      
    КонецПроцедуры
    
    <p>///// Модуль объекта ЗаполнениеГрафика</p>
    Процедура ЗаполнитьГрафик(ДатаНачала, ДатаОкончания, ВыходныеДни, ГрафикРаботы) Экспорт 
      
      Набор = РегистрыСведений.ГрафикиРаботы.СоздатьНаборЗаписей();
      
      Набор.Отбор.ГрафикРаботы.Установить(ГрафикРаботы);
        
      ЧислоСекундВСутках = 86400;
      
      Дат = ДатаНачала;	
      Пока Дат <= ДатаОкончания Цикл
        Запись = Набор.Добавить();
        Запись.ГрафикРаботы = ГрафикРаботы;
        Запись.Дата = Дат;
        Если Найти(ВыходныеДни, Строка(ДеньНедели(Дат))) Тогда
          Запись.Значение = 0;
        Иначе	          
          Запись.Значение = 8;
        КонецЕсли; 
        Дат = Дат + ЧислоСекундВСутках;
      КонецЦикла; 
      Набор.Записать();
    КонецПроцедуры
    
    <p>///// Модуль формы</p>
    &НаКлиенте
    Процедура Заполнить(Команда)
      ВыполнитьОбработку();
      Сообщить("Обработка завершена!");
    КонецПроцедуры
    
    &НаСервере
    Процедура ВыполнитьОбработку()
      ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
        ОбработкаОбъект.ЗаполнитьГрафик(ВыборПериода.ДатаНачала, ВыборПериода.ДатаОкончания, ВыходныеДни, ГрафикРаботы);
    КонецПроцедуры
    <embed src="pdf/ОбщееСПР7.pdf" type="application/pdf" width="1200" height="800">
      <img src="pdf/СПР7.jpg">
  </pre>
</body>

</html>
